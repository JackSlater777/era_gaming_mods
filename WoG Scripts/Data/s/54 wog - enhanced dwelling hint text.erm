ZVSE2

** ENHANCED DWELLING HINT TEXT
** ПОДРОБНЫЕ ОПИСАНИЯ ПО ВНЕШНИМ ЖИЛИЩАМ

** Option 024 by Timothy Pulver
** Rewritten by igrik (16.10.2020)
** updated by daemon_n (04.08.2022)

** Special thanks to Berserker!

** This script enhances the information you see when you right-click on a
** dwelling. If you or an allied player own the dwelling, you'll be shown how
** many creatures are still available to be hired there. If the dwelling is
** unowned or owned by another non-allied player, you'll just be shown the
** standard information.


!#DC(WOG_OPT_DWELLING_HINT_TEXT) = 24;


!?FU(OnAdventureMapRightMouseClick)&i^key_ctrl^=(FALSE)/i^key_shift^=(FALSE)/i^key_alt^=(FALSE)/i^mouse_action^=(MOUSE_RMB_PRESSED)/i^mouse_item^=37;
!!UN:P(WOG_OPT_DWELLING_HINT_TEXT)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;

; get click coordinates
!!CM:P?(x:y)/?(y:y)/?(z:y);

; get object params
!!OB(x)/(y)/(z):T?(objectType:y) U?(dwellingType:y);

; exit if not a dwelling
!!FU&(objectType)<>(OBJ_CREATURE_GENERATOR_1)/(objectType)<>(OBJ_CREATURE_GENERATOR_4):E;

; get object entrance coordinates
!!SN:O?(x)/?(y)/?(z);

; get dwelling owner
!!DW(x)/(y)/(z):O?(owner:y)/1;
!!FU&(owner)=(NO_OWNER):E;

; get me player id
!!FU(WOG_GameMgr_GetPlayer_Me):P?(mePlayerId:y);

; get my team id
!!FU(WOG_GameMgr_GetPlayer_Team):P(mePlayerId)/?(myTeam:y);

; get dwelling owner team id
!!FU(WOG_GameMgr_GetPlayer_Team):P(owner)/?(ownerTeam:y);

; check is this my team dwelling
!!FU&(myTeam)<>(ownerTeam):E;

; disable standard action
!!CM:R(FALSE);

; get dwelling name
!!FU(WOG_GameMgr_GetDwellingName):P(objectType)/(dwellingType);
!!VR(dwellingName:z):Ss^result^;

; get owner text
!!FU(WOG_GameMgr_GetObjOwnerStr):P(owner);
!!VR(ownerText:z):Ss^result^;

; init local vars
!#VA(msgType[4]:y) (msgMonType[4]:y);
!!VR(msgType[0]):C(NO_MON)/(NO_MON)/(NO_MON)/(NO_MON); 
!!VR(msgMonType[0]):C(NO_MON)/(NO_MON)/(NO_MON)/(NO_MON); 
!!FU(NewIntArray):P8/(NO_PIC_TYPE)/?(monsters:y);
!!VRf:S0;
!!VRj:S-1;
!!re i/0/3/1;
  !!DW(x)/(y)/(z):Mi/?(monType:y)/?(monCount:y);

  !!if&(monType)<>(NO_MON);
    !!if&f=0;
      !!VR(singleMonType:y):S(monType);
      !!VR(singleMonCount:y):S(monCount);
    !!en;
    !!SN:M(monsters)/f/(PIC_TYPE_MONSTER);
    !!VRf:+1;
    !!VR(monCount):Sd<<16 |(monType);
    !!SN:M(monsters)/f/(monCount);
    !!VRf:+1;
  !!en;
!!en;

!!if&f=2;
  !!SN:E5577600/(CALLCONV_FASTCALL)/(singleMonType)/?(singleMonCount);
!!el;
  !!FU(PrepareMultiPicDialog):P(monsters);
  !!IF:N(MSG_TYPE_POPUP)/^{%(dwellingName)}%T(wog.endl)%T(wog.endl)%(ownerText)^;
!!en;
