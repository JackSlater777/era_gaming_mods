ZVSE2

**   Enhanced Hint Text Script Version 1.3 created by Timothy E. Pulver
**   Updated: June 16, 2004
**   Rewritten by Archer30 on Apr 2022

** This script enhances the information you see when you right-click on a
** dwelling. If you or an allied player own the dwelling, you'll be shown how
** many creatures are still available to be hired there. If the dwelling is
** unowned or owned by another non-allied player, you'll just be shown the
** standard information.

** To install this script in a map, simply copy & paste the code into a global
** (timed) event. Copy the entire code, from ZVSE to the end. Make sure
** that you disable ALL colours from receiving the event and/or change
** the event day to something like 500 or later to avoid the ERM script
** popping up as if it were a regular event message. The title of the event
** is unimportant, but I would suggest something like "Enhanced Hint Text".
** Happy right-clicking! :-)

** Version 1.1 adds code to work with the new WoGify Options screen and
** no longer uses flag 58.

** Version 1.2 no longer sets the hint text and instead simply displays
** the extended dialogue box on the screen; this resolves a problem with
** incorrect text being shown when moving the mouse over other dwellings.
** Additionally, this update displays pictures of the creatures if there's
** fewer than 4 types available at the dwelling.

** Version 1.3 is updated to work with the new 3.58 creature dwellings.

** Version 2.0 is rewritten by Archer30 in order to improve the compatibility
** with multiple scripts and mods.

-----------------------------------------------------------------------------------------

!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^=37;
!!UN:P24/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit if not clicking on a dwelling
!!CM:P?(x:y)/?(y:y)/?(z:y);
!!OB(x)/(y)/(z):T?(type:y) U?(subtype:y);
!!FU&(type)<>(OBJ_CREATURE_GENERATOR_1)/(type)<>(OBJ_CREATURE_GENERATOR_4):E;

; Exit if the interacting player is not the owner of the dwelling
!!DW(x)/(y)/(z):O?(owner:y);
!!OW:C?(player:y)/?(interactPlayer:y);
!!FU&(owner)<>(interactPlayer):E;

!!CM:R0;

; Set up the dialogue with monster and numbers
!!FU(NewIntArray):P?(availMons:y);

!!re (slot:y)/0/3;
  !!DW(x)/(y)/(z):M(slot)/?(mon:y)/?(num:y);

  !!if&(mon)>(NO_MON);
    !!VR(num):F0/32767;
    !!VR(monAndNum:y):S65536 *(num) +(mon);
    !!FU(Array_Push):P(availMons)/(PIC_TYPE_MONSTER)/(monAndNum);
  !!en;
!!en;

!!FU(WOG_SetupMultiPicDlg):P(availMons);

; Get the string for the title
!!if&(type)=(OBJ_CREATURE_GENERATOR_1);
  !!FU(WOG_GetStringByFilename):P^crgen1^/(subtype)/?(dwellName:z);
!!el&(type)=(OBJ_CREATURE_GENERATOR_4);
  !!FU(WOG_GetStringByFilename):P^crgen4^/(subtype)/?(dwellName);
!!en;

!!FU(WOG_GetStringByFilename):P^plcolors^/(owner)/?(colourStr:z);
!!FU(StrToLower):P(colourStr)/?(lowerColourStr:z);
!!SN:T^wog.owner^/?(ownerStr:z)/^colour^/(lowerColourStr);

; Open the dialogue
!!IF:N(MSG_TYPE_POPUP)/^%(dwellName)
%(ownerStr)^;

** End of Script **
