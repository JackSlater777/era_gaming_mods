ZVSE2

** All My Scripts (2004 September 19)                         (for WoG v3.58)
** by Tobyn
** with big thanks to Fnord and the team for all their advice
**
** Each scriptlet is fully documented and contains a separate variables list.
** Vars v2361-v2367 are script activity "flags" for my new scriptlets here.
** To enable a script, set respective var to 1 (normally done via Wogify Options).
**
** Some scriptlets use my Universal Timer (which automatically runs with Wogify
** script00.erm). If you want to use them without wogify, e.g. by copying the code,
** enable UniTimer by setting v2370 to 1 in the following line (change S0 to S1 there).

!#VRv2370:S0;              disabled Universal Timer setup and calculations (because already done by Wogify script00.erm)

!#UN:P188/?v2366;          check if (Conflux)Rampart Monster Change is enabled in WoGify Options
!#UN:P189/?v2367;          check if Conflux(Rampart) Monster Change is enabled in WoGify Options
!#UN:P190/?i^wog_190_enabled^; check if First Aid Enhanced is enabled in WoGify Options
!#UN:P191/?v2363;          check if Estates Enhanced is enabled in WoGify Options
!#UN:P193/?v2365;          check if Warfare is enabled in WoGify Options
!#UN:P194/?v2361;          check if Advanced Witch Huts are enabled in WoGify Options

!#UN:P203/?v2368;          check if Estates I (script48.erm, Arstahd) is enabled
!#UN:P36/?v2369;           check if Mithril (script36.erm, Anders) is enabled

******************************************************************************************

** TOBYN LIBRARY FUNCTIONS (2004 September 19)                (for WoG v3.58)
** by Tobyn
**
**
** some common functions (may also be used from outside with a fct call)
**
** USES FU2333,(WOG_GetSkillInfoByClick)


  LUCK FUNCTION (get current luck of a hero)

!?FU2333;                            in: x1=hero number, out: x2=current luck
!!VRx2:S0;                             init output var x2
!!HEx1:S9/?y1 A2/45/d/?y2 A2/46/d/?y3 A2/47/d/?y4 A2/48/d/?y5 A2/108/d/?y6 R1/?y7 R6/?y8 A2/85/d/?y9;
;       Luck  StillEyeDragon  Clover   Cards       Ladybird    PendCourage  temp   cheat  Hourglass
!!re i/2/6;
  !!VRyi&yi>1:S1;
!!en;

!!VRy6:*3;                             set 3 bonus for Pendant of Courage
!!VRy8:*33;                            set 33 bonus if "maximum luck cheat" flag is set
!!VRx2&y9=0:+y1 +y2 +y3 +y4 +y5 +y6 +y7 +y8; current luck (x2) if no Hourglass (still no Spirit Guardian included)
!!VRx2&y9=1/y7<0:Sy7 +y8;              current negative luck (if Hourglass really negates luck *bonus* only)

******************************************************************************************

** ADVANCED WITCH HUTS (2004 September 19)                    (for WoG v3.58)
** by Tobyn
**
** This script enhances Witch Huts with an option not to learn the
** offered skill, get that skill advanced (for 3000 gold) or even
** forget all about the respective skill.
**
** AI always learns advanced skill (if enough gold, only needs 500 gold)
** or basic skill otherwise (standard object behaviour), but
** AI never forgets that skill here.
**
** Witch Huts won't give their skill if hero already has 8 other skills.
** careful: to have standard no-more-than-8-skills behaviour, it is
**   needed to check pre-visit skill expertise, then operate standard
**   Witch Hut and check post-visit skill expertise. Compare, and if
**   both are "none", Witch Hut gives out standard msg for full skills.
**
** IDEA:
** - if player has more than 9 Mithril and hero expert skill, let the
**   witch "offer" to hide that skill for 10 Mithril. If rejected,
**   either do nothing or call full wrath (= unlearn that skill)
**
** USES function FU2335, vars v2328-v2330
** ONLY ACTIVE if v2361=1 (wogify option 194)


!?OB113&v2361=1;           any Witch Hut visited
!!WHv998/v999/v1000:S?v2328; get Witch Hut skill
!!HE-1:Sv2328/?v2329;        get hero skill expertise
!!OBv998/v999/v1000:M-1/1/0; disable next std msg of this Witch Hut

!$OB113&v2361=1;           post-visit trigger for any Witch Hut
!!UN:N4/z-1/v2328;           using built-in skill name lookup
!!HE-1:Sv2328/?v2330;        get hero skill expertise after visit
!!HE-1&v2329=0/v2330>0:Sv2328/0; reset hero skill expertise if different
!!FU2335&v2330>0:P;          call dialogue function if different
!!IF&v2330=0/1000:M1/z164000; msg if not given (= no free skill slot)

!?FU2335;                  dialogue function
!!IF:V1/0;                   init flag 1
!!WHv998/v999/v1000:S?y1;    get Witch Hut skill
!!HE-1:Sy1/?y2;              get hero expertise with this skill
!!OW:R-1/6/?y3;              get owner's gold
!!VRy4:Sy1*3;                (calculations for display skill pic)
!!VRy4:+2+y2;                 .
!!VRy4&y2<2:+1;               .
!!VRy5|y1=10/y1=20/y1=27:S1; set y5=1 if one of the Warfare skills
!!VRy5&v2365=0:S0;           set y5=0 if Warfare inactive

!!HE-1&y2=0/y3<500/-1000:Sy1/1; AI gets basic if not enough gold
!!HE-1&y2<2/y3>499/-1000:Sy1/2; AI gets advanced if enough gold
!!OW&y3>499/-1000:R-1/6/d-500;  AI pays 500 gold if advanced

!!IF&y2>1/1000:Q1/20/y4/2/z164001;     msg if already adv or expert

!!HE-1&y2>1/-1/1000/y5=0:Sy1/0; remove adv/exp skill at player's choice (AI never forgets) and end function (because y2 gladly not actualized)
!!HE-1&y2>1/-1/1000/v2365=1/y5=1:S10/0 S20/0 S27/0; unlearn all three Warfare skills if appropriate and end fct

!!IF&y2>1/-1/1000:M1/z164002;          msg if "unlearn"

!!IF&y2=0/1000:Q1/20/y4/2/z164003; std msg if no skill
!!HE-1&y2=0/1:Sy1/1;         set skill to basic if agreed to learn
!!VRy4&y2=0/1:+1;            (inc display pic)
!!VRy2&y2=0/1:S1;            set skill expertise var to basic
!!IF:V1/0;                   reset flag 1

!!IF&y2=1/y3<3000/1000:Q1/20/y4/6/3000/1/z164004; msg if bas and not enough gold
!!IF&y2=1/y3>2999/1000:Q1/20/y4/6/3000/2/z164005; msg if bas and enough gold (offer to advance)
!!OW&y2=1/1:R-1/6/d-3000;    pay 3000 gold if agreed
!!HE-1&y2=1/1:Sy1/2;         set skill to advanced then
!!IF:W-1;                    set hero var reference to current hero
!!VRw48&y2=1/y5=1/v2365=1:Sy1; set hero var w48 (Warfare) to appropriate Warfare skill

******************************************************************************************

** ESTATES ENHANCED (2004 September 19)                       (for WoG v3.58)
** by Tobyn
**
** This script enhances Estates secondary skill with further resources.
** Estates will give additional 25/50/100 Gold (Basic/Advanced/Expert)
** for every 10,000 experience each day (adjustable via v2333, will be *0.25/0.5/1)
** Max +2000 Gold/day (from hero lvl 25 on), but Estates specialists have max +5000
** Gold/day (lvl 30) in addition to their standard +5% per level (no cap there)
** Also generates additional resources, depending on hero level and weekday:
** - For lvl 1 to 9, each hero level gives one resource per week (i.e 1-7
**   resources per week in total, lvl 7-9 will give you one resource each day)
**   Resource type randomly chosen at first time and always stays the same:
**   1 (Wood+Ore|Mercury|Sulfur|Crystal|Gem|350 Gold)
** - For lvl 10+, Estates change resource type every day, cycling through
**   all types within one week: 1 Wood+Ore (monday), 1 Mercury (tuesday), ...
**   350 Gold (saturday) and 1 Mithril (sunday)
** - Humans may choose resource with left-clicking Expert Estates icon in heroes
**   screen if hero is lvl 10+. The hero will provide this resource every day
**   until player interferes again. Choose none to reactivate weekly cycle.
**   If Mithril chosen, there's a random factor whether player gets Mithril or
**   the weekday's resource, modified by hero's current luck. Player will always
**   get Mithril on sundays (or the 350 gold if Mithril disabled)
**
** - Heroes' normal resource specialties are not affected by this script.
**
** IDEAS to improve this script
** - if possible to change skill pic, display last or chosen resource pic there
** - or once the "choose from many pictures" dialogue is available, use it to
**   replace the successive msgboxes for resource selection
**
** USES hero var w47 (resource choice), var v2369 (Mithril)
**      FU2342-FU2344
** calls library fcts FU2333 (luck), FU(WOG_GetSkillInfoByClick) (skill click)
** uses flag 236, uses v2333 (modifier for ExpEst gold/day), v2334 (lvl 10 msg status)
** temp uses vars (v2331,v2332)
** ONLY ACTIVE if v2363=1 (wogify option 191)

;  resource (Wood+Ore|Mercury|Sulphur|Crystal|Gem|350 Gold)  Mithril   cycling
;    w47  =      1       2       3       4     5     6         (7)        8
;                9      10      11      12    13    14         15        16
;
; 0 = nothing, 7 = (Mithril, but unused), 17+ not used
; 8 = cycling: automatically from lvl 10 on, each weekday gets corrspondant resource, unless chosen manually
; 9..14 = 1 Wood/Ore .. 1 Gem each day (chosen)
;    15 = 1 Mithril each day was chosen, but if you get it depends on random factor, modified by hero's current luck
;    16 = Mithril chosen, but unlucky that day (give weekday resource then if Estates) => always 1 Mithril on sunday
;
;  following are statistics about Mithril threshold
;  r9 means random 0..9 (exception here: will be random -1..8 if luck is 0, so 0 luck is treated as 1 luck)
;
;  luck    -1    0    1    2    3    4    5    6    7    8    9   10
;  r9<luck  0   10   10   20   30   40   50   60   70   80   90  100  % each day (10% base, 100% max)
;  Mithril  1    1    1    1    1    1    1    1    1    1    1    7   worst/week
;  Mithril  1   1.6  1.6  2.2  2.8  3.4   4   4.6  5.2  5.8  6.4   7   avg/week
;  Mithril  1    7    7    7    7    7    7    7    7    7    7    7   best/week


!#VRv2333&v2363=1:S100; set daily additional Expert Estates gold per 10,000 experience (adv=half, basic=quarter), may be modified by map maker, e.g. =0 if only resource bonus

  DAILY ADDED RESOURCES FOR ESTATES HEROES (or heroes with manually ERM set w47)

;   check w47 for resources/day irrespective of Estates,
;   only take Estates into account when *changing* w47

!?FU(OnEveryDay)&v2363=1;                       daily timer for every color (if script active)
!!VRv2334&$day$=1/$once$=1:S0;         init lvl 10 msg status at first day
!!UN:B0/?v2369;                        check Mithril status (1=active)
!!OW:H-1/2331/0;                       store current color's number of heroes in v2331
!!DO2343/1/v2331/1:P;                  loop through every hero a player has

!?FU2343;                            hero loop function for Estates
!!OW:H-1/2332/x16;                     store x16th hero of that player in v2332
!!IF:V236/1 Wv2332;                    set w var reference to current hero, set flag 236
!!HEv2332:S13/?y1 E?y2/?y3/1 X?y14/?y13/d/d/d/d/d; get Estates expertise y1, hero experience y2 and level y3, hero skill spec (y14=0 and y13=13 if Estates)
!!FU&y1=0/w47=0:E;                     exit if that hero has no Estates and no w47
!!VRy10:S1;                            full modifier for Expert Estates and in general to avoid division by zero
!!VRy10&y1=2:S2;                       half for Advanced Estates
!!VRy10&y1=1:S4;                       quarter for Basic Estates
!!VRy4&y1>0:Sy2 :10000 *v2333 :y10;    calculate gold according to expertise and experience
!!VRy4&y4>2000/y13<>13:S2000;          max of +2000 Gold/day (from lvl 25 on) unless Estates specialist
!!VRy4&y4>5000:S5000;                  max of +5000 Gold/day (around lvl 30) even if Estates specialist
!!OW&y1>0:R-1/6/dy4;                   give/remove that gold
!!OW&y1>0:R-1/6/?y17;                  check current treasure (player's current gold)
!!OW&y1>0/y17<0:R-1/6/0;               no negative treasure allowed

  change w47 state automatically (init w47 for lvl 1..9 or change w47 for lvl 10+ or reset unlucky Mithril)
*!VRw47&y1=0:S0;                      (if no Estates then reset w47 because hero may have unlearned Estates)
!!VRw47&w47=0/y1>0/y3<10:R5 +1;        lvl 1..9 inits w47 with random 1..6, meaning +1 (Wood+Ore|Mercury|Sulfur|Crystal|Gem|350 Gold)
*!VRw47&w47<8/y13=13/y14=0:S8;         Estates specialty allows cycling/choosing from the beginning
!!VRw47&w47<8/y13=13/y14=0:+8;         Estates specialty sets random resource to daily (even for low-level), Expert allows chosing as if lvl10+
!!VRy5:S41 +y1;                        set correct Estates pic for msg below
!!VRy6:S-1;                            init pic to none
!!VRy6&y1=3/y3>9/v2369=1:S7;           set Mithril pic if Expert Estates and lvl10+ and Mithril active
!!VRy12:S1 +$color$;                   get color + 1 (CAREFUL: VR:S$color$ +1 would ignore the +1)
!!FU$bit$:Py12;                        bit conversion, CAREFUL: result is returned in y-100
!!VRy12:Sv2334 &y-100;

!!VRz-1&v2369=1:Sz164014;              Mithril selection line added to lvl 10 msg if Mithril active

!!IF&w47<8/y1>0/y3>9/1000/y12=0:Q236/20/y5/y6/0/2/z164015; human only msg once hero has reached lvl 10
!!VRv2334&-236:|y-100;                 that color won't receive the message any more if Cancel was clicked
!!VRy11&w47<8/y1>0/y3>9:S1;            indicator: no cycling/choice yet, Estates present, lvl10+
!!VRw47&y11=1:S8;                      init w47 resource cycling
!!VRw47&y11=1/y1=3/v2369=1:S15;        init w47 Mithril cycling if Mithril script active and Exp Estates and lvl10+
!!VRw47&w47=16:S15;                    reset w47 Mithril choice unlucky flag (from the day before)

  prepare resources according to w47 state
!!VRy7:S0;                             init resource var (y7)
!!VRy7&w47>0/w47<8/y3>=$weekday$:Sw47; lvl<10, check for weekday if to give resource type w47 at all that day
!!VRy7&w47=8:S$weekday$;               lvl 10+  cycling, give respective weekday resource
!!VRy7&w47>8/w47<15:Sw47 -8;           lvl 10+  give chosen resource type (w47 -8)
!!FU2333&w47=15:Pv2332/?y-1;           Mithril section: check current luck for that hero
!!VRy8&w47=15:R9;                      .. set daily chance threshold (9)
!!VRy8&w47=15/y-1=0:-1;                .. treat 0 luck as 1 luck (10% base chance)
!!VRy9&y8<y-1:S1;                      .. compare, set (y9=1) if lucky
!!VRy7&w47=15/y9=1:S7;                 .. set y7 to Mithril if successful
!!VRy7&w47=15/y9=0/y1>0:S$weekday$;    .. set y7 to weekday if unsuccessful but Estates
!!VRw47&w47=15/y9=0:S16;               .. set w47 to 16 if unsuccessful

  give resources according to above conditions
!!OW&y7=1:R-1/0/d1 R-1/2/d1;           give 1 Wood and 1 Ore
!!OW&y7=2:R-1/1/d1;                    give 1 Mercury
!!OW&y7>2/y7<6:R-1/y7/d1;              give 1 Sulphur, Crystal or Gem
!!OW&y7=6:R-1/y7/d350;                 give 350 Gold
!!OW&y7=7/v2369=1:R-1/7/d1;            give 1 Mithril if active
!!OW&y7=7/v2369=0:R-1/6/d350;          give 350 Gold if Mithril inactive (weekday cycle reached sunday)

    --------------------

  HERO SCREEN CLICK

!?FU(OnHeroScreenMouseClick)|i^mouse_action^=(MOUSE_LMB_PRESSED)/i^mouse_action^=(MOUSE_RMB_PRESSED);

  !!if|v2363=1/v2365=1;                         [estates II option/ WarFare option]


    !!FU(WOG_GetSkillInfoByClick):Pi^mouse_item^/-1/?(skill:y)/?(skillArea:y); [call skill click function (in: click number and hero number, out: skill (y-3), area (y-4) and slot (y-5))]


    !!if&(skill)=(SKILL_ESTATES)/v2363=1;
      !!UN:P203/?(isEstates:y);          check if Estates I (script48.erm, Arstahd) is enabled
      !!UN:P75/?(isShortDescr:y);           check if Short Skill descriptions (script75.erm, Hermann the Weird) is enabled


      !!if&(isEstates)=(FALSE)/(isShortDescr)=(FALSE);
        !!CM:R0;     [if clicked skill was Estates, disable standard skill click reaction         [NOTE to get std Estates descr on name click: y-1<87 instead of y-1<103]
        !!FU2342:P(skillArea)/i^mouse_action^; [.. and call enhEstates display function (in: skill area, left/right-click)  [NOTE to get std Estates descr on name click: add /y-1<87]

      !!el&(skillArea)=1;
        !!CM:R0;      [same but for Estates I or short skill descr, then only a click on skill icons will trigger the below msgboxes]
        !!FU2342:P(skillArea)/i^mouse_action^; [.. same but for Estates I or short skill descr]
      !!en;


    !!en;

    !!if|(skill)=(SKILL_BALLISTICS)/(skill)=(SKILL_ARTILLERY)/(skill)=(SKILL_FIRST_AID);
      !!if&(skillArea)>1/v2365=1;
        !!CM:R0;
        !!FU(WOG_44_Msg_SkillView):P(skillArea)/i^mouse_action^/(skill);           .. and call Warfare display function (in: skill area, left/right-click, skill)
      !!en;
    !!en;

  !!en;

  ENHANCED ESTATES DISPLAY FUNCTION

!?FU2342;                            display function
!#VA(skillArea:x) (mouseAction:x);
;   x1 = skill area (= 1 if icon, = 2 if name, = 3 if expertise clicked)
;   x2 = click type (= 12 left click, = 14 right click)

!!VR(msgType:y):S(mouseAction) -10;                      if left-click  then init y16:=2 (OK/Cancel buttons)
*!VRy16&x2=14:S4;                      if right-click then init y16:=4 (no button)
*!FU&y16=0:E;      BUG COUNTERMEASURE: clicking non-functional-areas after Estates (with disabled std reaction) erroneously gives another msgbox and nasty side effects
!!IF:W-1;                              set w var reference to current hero
!!HE-1:S(SKILL_ESTATES)/?(skillLvl:y) E?(heroExp:y)/?(heroLvl:y)/1 X?(specType:y)/?(specSkill:y)/d/d/d/d/d; get Estates expertise (y1), hero experience (y2) and level (y3), skill specialty (y20=0 and y19=13 is Estates)


!!if&(skillLvl);
  !!VRw47&w47=0/(heroLvl)<10:S1 R5;             init w47 with random 1..6 for lvl 1..9 and no init
  !!VRw47&w47<8/(heroLvl)>9:S8;                 init w47 with cycling (no lvl 10 msg displayed then)
  !!VR(stringNum:y):S164015 +i^timerWeekDay^;
  !!VRz-3:Sz(stringNum);

  !!if&w47<8;
*!IF:L^%^;
    !!VR(msgType)&(mouseAction)=(MOUSE_LMB_PRESSED):S1;            lvl 1..9 left-click has OK button only
    !!VRz-1&(heroLvl)=1:Sz164023;       lvl 1 text
    !!VRv2:S(heroLvl);
    !!VRz-1&(heroLvl)>1/(heroLvl)<7:Sz164024;  lvl 2..6 text
    !!VRz-1&(heroLvl)>6/(heroLvl)<10:Sz164025; lvl 7..9 text
    
  !!en;

  !!if&w47=8;
    !!if&(mouseAction)=(MOUSE_LMB_PRESSED);
      !!VR(msgType)&(skillLvl)<3:S1;           lvl 10+ cycling left-click Bas/Adv Estates OK button only
      !!VRz-1&(skillLvl)<3:Sz164026;     lvl 10+ cycling left-click Bas/Adv Estates
      !!VRz-1&(skillLvl)=3:Sz164027;     lvl 10+ cycling left-click Expert Estates
    !!el;
      !!VRz-1:Sz164028;          lvl 10+ cycling right-click
    !!en;
  !!en;

  !!if&w47>8/w47<15;
    !!VRz-1&x2=12:Sz164029;   lvl 10+ chosen left-click
    !!VRz-1&x2=14:Sz164030;   lvl 10+ chosen right-click

  !!en;

  lvl 10+ and Mithril chosen
!!FU2333&w47>14:P-1/?(luckBonus:y);             check current luck of current hero
!!VR(chance:y):S(luckBonus) *10;                      set daily chance

!!if&(chance)>100;
  !!VR(chance):S100;                    [set daily chance 100% max if luck 10+]
!!el&(chance)=0;
  !!VR(chance):S10;                     [set daily chance 10% if zero luck]
!!el&(chance)<0;
  !!VR(chance):S0;                      [set daily chance 0% if negative luck]
!!en;

!!if&w47>14;
  !!VRv2:S(chance);
  !!VRv3:S(luckBonus);
  !!VRz-1&x2=12:Sz164031;         lvl 10+ Mithril chosen left-click
  !!VRz-1&x2=14:Sz164032;         lvl 10+ Mithril chosen right-click
!!en;
                      
  calculate correct resource pics

!!VR(pic:y):S(PIC_TYPE_SEC_SKILL);                           set first pic as skill pic
!!VR(picSub:y):S41 +(skillLvl);                       calculate correct Estates pic (according to expertise)
!#VA(resource:y);
!!VR(resource)&w47<8:Sw47;                    set resource display for lvl 1..9
!!VR(resource)|w47=8/w47=16:Si^timerWeekDay^;       if cycling (=8), or unlucky Mithril (=16), set pic to weekday resource
!!VR(resource)&w47>8/w47<16:Sw47 -8;          set resource display for lvl 10+ chosen

!!if&(resource)=1;
  !!VR(pic):S(RES_WOOD);                      if Wood/Ore then set first pic to Wood instead of Estates
  !!VR(picSub):S1;                      .. set Wood amount
  !!VR(resource):S(RES_ORE);                      .. set Ore  
!!en;


!!VR(resource)&(resource)=2/(picSub)>1:S(RES_MERCURY);                set Mercury resource
!!VR(resource)&(resource)=(RES_MITHRIL)/v2369=0:S(RES_GOLD);              set gold resource instead of Mithril if Mithril script disabled (needed for weekday=sunday)
!!VR(resourceNum:y):S1;                            resource amount
!!VR(resourceNum)&(resource)=(RES_GOLD):S350;                    gold amount 350 instead of 1

  calculate correct additional amount of gold text (dep on hero experience and Estates Expertise) [%gold, %x and %y]

!!VR(goldBonus:y):S(heroExp) :10000;                     calculate additional gold bonus for display

!!if&(skillLvl)=1;
  !!VR(goldMult:y):Sv2333 :4;                 set basic bonus factor
  !!VR(baseBonus:y):S125;                      set basic standard value
  !!VRz-2:Sz164033;                 set basic skill descriptor
  
!!el&(skillLvl)=2;
  !!VR(goldMult:y):Sv2333 :2;                  .. adv
  !!VR(baseBonus:y):S250;                       .. adv
  !!VRz-2:Sz164034;                  .. adv
!!el;
  !!VR(goldMult:y):Sv2333;                     ... exp
  !!VR(baseBonus:y):S500;                       ... exp
  !!VRz-2:Sz164035;                  ... exp
!!en;


!!VR(goldBonus):*(goldMult);                            set bonus

!!VR(goldBonus)&(goldBonus)>2000/(specSkill)<>(SKILL_ESTATES):S2000;          max of +2000 Gold/day (from lvl 25 on) unless Estates specialist
!!VR(goldBonus)&(goldBonus)>5000:S5000;                  max of +5000 Gold/day (around lvl 30) even if Estates specialist
!!VR(specBonus:y):S(baseBonus);                           standard gold bonus
!!VR(baseBonus)&(specSkill)=(SKILL_ESTATES)/(specType)=0::20 *(heroLvl) +(specBonus);      Estates display incl. specialty +5% per level bonus (y21)
!!VRv4:S(goldBonus);                           display additional gold (given by this script)
!!VRv5:S(heroExp) :10000 *10;                calculate experience step for additional gold
!!VRv5&(specSkill)<>(SKILL_ESTATES)/v5>200:S200;          display max 200,000 exp step if no Estates specialist
!!VRv5&(specSkill)=(SKILL_ESTATES)/v5>500:S500;           display max 500,000 exp step if Estates specialist
!!VR(goldBonus):+(baseBonus);                            total gold/day (incl. Estates spec, excl. Estates I)

  if Estates I also active
!!VRv6:S5 *(skillLvl);                        check if 5, 10 or 15 gold
!!VR(heroLvlBonus:y):Sv6 *(heroLvl);                      set 5/10/15 gold per level (from Estates I)
!!VR(goldBonus)&v2368=1:+(heroLvlBonus);                   total if Estates I also active
!!VRz-4&v2368=0:S^)^;                  if Estates I inactive
!!VRz-4&v2368=1:Sz164036;              if Estates I active

  finally display msgbox (incl z var, pics and button state from above)
!!VR(maxRes:y):S(RES_GOLD) +v2369;                     init upper resource bound (Gold or Mithril if enabled)
!!VRv2:S(goldBonus);
!!VRv3:S(baseBonus);
!!IF:Q1/(pic)/(picSub)/(resource)/(resourceNum)/(msgType)/z164037; main display

!!if&-1/(skillLvl)=3/(msgType)=(MSG_TYPE_QUESTION);

  !!IF:Q1/(RES_WOOD)/1/(RES_ORE)/1/(msgType)/z164038; main display
  !!VRw47:S1 +8;            set w47=8 (cycling) if final resource also rejected

  !!if&-1;
    
    !!VR(picSub):S44;

    !!re i/2/(maxRes);
      !!if&i=(RES_GOLD);
        !!VR(resourceNum):S350;
      !!el;
        !!VR(resourceNum):S1;
      !!en;

      !!VR(resource):Si;
      !!VR(resource)&i=(RES_ORE):S(RES_MERCURY);
      !!VR(resource)&i=(RES_MERCURY):S(RES_ORE);

      !!IF:Q1/(PIC_TYPE_SEC_SKILL)/(picSub)/(resource)/(resourceNum)/(msgType)/z164038; main display

      !!if&1;
        !!VRw47:Si +8;            set w47=8 (cycling) if final resource also rejected
        !!br;
      !!en;
    !!en;
  !!en;

  !!VRw47&-1:S8;            set w47=8 (cycling) if final resource also rejected
!!en;

!!FU:E;                           exit if no Estates present

!!FU&y1=0:E;                           exit if no Estates present

lvl 10+ and cycling

#1: flag
#2: type pic 1    (=20 for secondary skill)
#3: subtype pic 1 (=skill incl expertise, here =41+EstatesExpertise)
#4: type pic 2    (=1..7 for resource, -1 for none)
#5: subtype pic 2 (=amount resource, 350 if gold (resource=6), else 1)
#6: type message  (=1 OK button, =2 OK/Cancel buttons, =4 no button)


******************************************************************************************

** FIRST AID ENHANCED (2004 September 19)                     (for WoG v3.58)
** by Tobyn
**
** This script greatly enhances First Aid skill and hero specialty.
** - First Aid skill summons and gives control over additional First Aid
**   Tents (# of tents = hero level).
**   The one warmachine Tent and Hierophant Commander Tents are added.
** - Casts a Cure spell each healing, based on First Aid expertise (Expert
**   First Aid will give Mass Cure) and spellpower equal to number of tents.
**   Tent Healing Cursor upon two-hex-creatures gives Cure only if the left
**   hex (position) is targetted, the right hex only gives standard healing.
** - First Aid Specialty gives 2 Tents per hero level and temporary knowledge
**   of three spells during that combat (Cure, Animate Dead and Resurrection)
**   First Aid Specialty will also restore First Aid Tent Warmachine *if*
**   hero had one before battle began, even if it was destroyed in battle.
** - If Enhanced Warmachines II is eanbled, the additional tents would be
**   reduced to 2. One for learning First Aid and one for First Aid specialty.
**
** uses vars v2301..23
** ONLY ACTIVE if i^wog_190_enabled^ (wogify option 190)


  INIT SKILL DESCRIPTION moved to script75.erm (skill descriptions)

  BATTLE AFFAIRS BELOW

  v2301=hero level, v2302=spellpower, v2303=1 if specialty, v2304=First Aid expertise,
  v2305=1 if Tent warmachine present, v2306 amount of warmachine/Hierophant Tents
  v2307=Cure, v2308=Resurrection and v2309=Animate Dead (=1 each if known by hero)
  v2311..v2319 same for defender (if present)


  BEFORE BATTLE

!?FU(OnBeforeBattleUniversal)&i^wog_190_enabled^; [start combat trigger for both sides (if script active)]
!!VRv2301:C0/0/0/0/0/0/0/0/0/0/0;       [init v2301..11]
!!VRv2312:C0/0/0/0/0/0/0/0/0/0/0/0;     [init v2312..23]
!!HEi^battle_hero_0^:Ed/?v2301/1 S(SKILL_FIRST_AID)/?v2304 A2/6/?v2305/d M37/?v2307 M38/?v2308 M39/?v2309;
!!HEi^battle_hero_1^&i^battle_hero_1^>(NO_HERO):Ed/?v2311/1 S(SKILL_FIRST_AID)/?v2314 A2/6/?v2315/d M37/?v2317 M38/?v2318 M39/?v2319;

; Compatibility with Enhanced Warmachines II, reduce the additional tents to 1 ~ 2
!!if&i^wog_55_enabled^;
  !!VRv2301:S1;
  !!VRv2311&i^battle_hero_1^>(NO_HERO):S1;
; Tents = 1/4 hero level + 1 if Enhanced Warmachines II is disabled
!!el;
  !!VRv2301::4 +1;
  !!VRv2311&i^battle_hero_1^>(NO_HERO)::4 +1;
!!en;

  First Aid Specialty
!!HEi^battle_hero_0^:X?y-1/?y-2/d/d/d/d/d;                     [get Attacker skill specialty]
!!VRv2303&y-1=0/y-2=27:S1;                                     [set indicator if First Aid spec]
!!HEi^battle_hero_0^&v2303=1:M37/1;                            [hero temp gets Cure spell during battle if specialist (even w/o First Aid skill)]
!!HEi^battle_hero_0^&v2303=1/v2304>1:M38/1;                    [hero temp gets Animate Dead if Advanced or Expert specialist]
!!HEi^battle_hero_0^&v2303=1/v2304>2:M39/1;                    [hero temp gets Resurrection if Expert specialist]
!!VRv2301&v2303=1:*2;                                          [specialty gives 2 Tents per hero level]
!!HEi^battle_hero_1^&i^battle_hero_1^>-1:X?y-3/?y-4/d/d/d/d/d; [get Defender skill specialty]
!!VRv2313&i^battle_hero_1^>-1/y-3=0/y-4=27:S1;                 [set indicator if First Aid spec]
!!HEi^battle_hero_1^&v2313=1:M37/1;                            [temp Cure spell during battle]
!!HEi^battle_hero_1^&v2313=1/v2314>1:M38/1;                    [temp Animate Dead if Advanced or Expert]
!!HEi^battle_hero_1^&v2313=1/v2314>2:M39/1;                    [temp Resurrection if Expert]
!!VRv2311&v2313=1:*2;                                          [specialty gives 2 Tents per hero level]


  IN BATTLE (summon Tents and apply Cure spell)

!?FU(OnSetupBattlefield)&i^wog_190_enabled^/i^wog_isCBBattle^=(FALSE); [start battlefield trigger (if script active)]
!!if&v2304>(SKILL_NOT_LEARNED);
  !!BU:E153/?(tentStack:y);                     [check if attacker already has tents]

  !!if&(tentStack:y)>(NO_STACK);
    !!BM(tentStack:y):Ndv2301;                  [add Tents for attacker]
  !!el;
    !!BU:S(MON_FIRST_AID_TENT)/v2301/153/(BATTLE_LEFT)/-1/0; [summons Tents for attacker]
  !!en;
!!en;

!!if&v2314>(SKILL_NOT_LEARNED);
  !!BU:E168/?(tentStack:y);                     [check if defender already has tents]

  !!if&(tentStack:y)>(NO_STACK);
    !!BM(tentStack:y):Ndv2311;                  [add Tents for defender]
  !!el;
    !!BU:S(MON_FIRST_AID_TENT)/v2311/168/(BATTLE_RIGHT)/-1/0; [summons Tents for defender]
  !!en;
!!en;

!?FU(OnBeforeBattleAction)&i^wog_190_enabled^; [battleground action (useable by both sides) (if script active)]
!!BG:A?(action:y);                      [get current action (wait for Tent Healing)]

!!if&(action)=(BATTLE_ACTION_TENT_HEAL);
  !!BG:N?(tentStack:y) D?(healPos:y) Q?(side:y); [get Tent number, destination position, owner]

  !!BU:E(healPos)/?(stack:y);
  !!BM(stack):P?(stackPos:y);

  !!BM(tentStack):N?(tents:y);          [get current amount of Tents]
  !!BM(tentStack)&(side)=(BATTLE_LEFT)/v2304>(SKILL_NOT_LEARNED):C(SPELL_CURE)/(stackPos)/v2304/(tents)/1; [cast Cure on destination (left side)]
  !!BM(tentStack)&(side)=(BATTLE_RIGHT)/v2314>(SKILL_NOT_LEARNED):C(SPELL_CURE)/(stackPos)/v2314/(tents)/1; [cast Cure on destination (right side)]
!!en;


  AFTER BATTLE (restore values given by First Aid Specialty)

!?FU(OnAfterBattleAction)&i^wog_190_enabled^; [post-action trigger, if script active]
!!BU:C?(battleIsEnded:y);               [check if combat has ended this turn]
!!FU&(battleIsEnded)<>(TRUE):E;         [exit if it hasn't]

  attacker
!!HEi^battle_hero_0^&v2303=1/v2305>0:A1/(ART_FIRST_AID_TENT)/15; [restore Tent if given and hero specialty]
!!HEi^battle_hero_0^:M37/v2307 M38/v2308 M39/v2309; [remove Cure, Res, AnDead spells if only given by Specialty]
  defender
!!HEi^battle_hero_1^&v2313=1/v2315>0:A1/(ART_FIRST_AID_TENT)/15; [restore Tent if given and hero specialty]
!!HEi^battle_hero_1^&i^battle_hero_1^>-1:M37/v2317 M38/v2318 M39/v2319; [remove Cure, Res, AnDead spells if only given by Specialty]

******************************************************************************************

** WARFARE (2004 September 19)                                (for WoG v3.58)
** by Tobyn
**
** This script combines the three war machine skills Artillery, Ballistics
** and First Aid and treats them as one skill. This new Warfare "skill"
** gives you control over all three War Machines and standard SoD abilities
** from all three skills.
**
** First Aid Tents get 750 HPs (only if no other First Aid enhancements active).
**
** Whenever a hero has one or more of the skills and a battle starts,
** the hero will get all three skills at the highest expertise available.
** Similar if clicked on any warfare skill in hero screen, but:
** - if clicked on icon of any Warfare skill, you get standard skill description
** - if clicked on text (name or expertise), you get another msgbox instead
**   (left-click lets you choose, right-click cycles the displayed Warfare skill)
**
** Hero might temporarily have diverging expertises in the three Warfare
** skills while on adventure map (e.g. via Witch Huts or through other ERM),
** but this and display will adjust with the next click, levelup or battle.
**
** NOTE:
** - side effect with Arstahd's Enhanced Commander Artifacts (give hero skills)
**   As soon as an artifact with one of the three skills is obtained, player may
**   get instant Expert Warfare with just a few mouse clicks and without further
**   need for the Artifact thereafter
**   (by toggling the Warfare skills while repeatedly equipping the artifact).
**   Three free Expert skills for just temporarily having one artifact equipped. WOW!
**   And you can always give it to another hero or to a commander afterwards...
**
** uses fcts FU(WOG_193_UpdateWarFareLevel)
** uses strings z237..239 for skill names
** ONLY ACTIVE if v2365=1 (wogify option 193)


  INIT SKILL NAMES (leaves descriptions unchanged)

!?FU(OnEveryDay)&i^timerDay^=1/i^timerOnce^=1/v2365=1;      triggers for first active color on day 1 only (if script active)
;                                      (will trigger after script03.erm skill enhancement)
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_I)/?y-1;                         check if War Machines I is active (Overlord, script54, war machine levels)
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_II)/?y-2;                         check if War Machines II is active (Arstahd, script55, Tent mid-battle Resurrection)
!!UN:P(WOG_OPT_FIRST_AID_I)/?y-3;                        check if First Aid I is active (Arstahd, script48, Tent post-battle Resurrection)
!!UN:P(WOG_OPT_FIRST_AID_II)/?y-4;                        check if First Aid II is active (Tobyn, script64 see above, Tent Cure spell)
!!UN:P(WOG_OPT_ARTILLERY)/?y-5;                        check if Artillery I is active (Arstahd, script48, pre-battle damage)
!!UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_III)/?y-6;

*!MA&y-1=0/y-2=0/y-3=0/y-4=0/y-6=0:P147/750; removed by angry @daemon_n all First Aid Tents will have 750 HPs (if neither First Aid skill nor the Tent war machine itself are enhanced)
!!VR(balName:z):Sz164039;
!!VR(balName)|y-1=1/y-2=1:S^{%(balName)}^;
!!VR(artName:z):Sz164040;
!!VR(artName)&y-5=1:S^{%(artName)}^;
!!VR(faName:z):Sz164041;
!!VR(faName)|y-1/y-3=1/y-4=1:S^{%(faName)}^;

!!VRz237:Sz164039;                     if no warfare relevant skills were enhanced
!!VRz238:Sz164040;                      .
!!VRz239:Sz164041; 


                     .
!!VRz237|y-1=1/y-2=1:Sz164042;         if Ballistics enhanced by script 54 or 55
!!VRz238|y-5=1:Sz164043;               if Artillery enhanced by script 48
!!VRz239|y-3=1/y-4=1:Sz164044;         if First Aid enhanced by script 48 or 64

!!SN:H^secskill^/(SKILL_BALLISTICS)/0/(balName) H^secskill^/(SKILL_ARTILLERY)/0/(artName) H^secskill^/(SKILL_FIRST_AID)/0/(faName);
*!UN:G0/10/0/237 G0/20/0/238 G0/27/0/239;  set skill names to Warfare
*!IF:M^%(balName)^;

  MANUALLY SET WARFARE MAIN SKILL


!?FU(WOG_44_Msg_SkillView);                            Warfare skill display and determination (x1=skill area, x2=click type, x3=skill)
!#VA(skillArea:x) (mouseAction:x) (skill:x);
!#VA(usedY[8]:y);

!!if&(mouseAction)=(MOUSE_RMB_PRESSED);
  !!VR(msgType:y):S(MSG_TYPE_POPUP);
!!el;
  !!VR(msgType:y):S(MSG_TYPE_CHOOSE_PIC_OR_CANCEL);
!!en;

!!IF:W(CURRENT_HERO);                  set w var reference to current hero
!!VRy5:S0;                             init y5

; set cycling "flag" y5 if right-clicked on text field (name or expertise) - disabled by Archer30 as when the hero has the secondary scrolling bar activated, this is really confusing
*!VRy5&(skillArea)>1/(mouseAction)=(MOUSE_RMB_PRESSED):S1;

!!FU(WOG_193_UpdateWarFareLevel):P-1/(skill)/y5;                    determine and set Warfare expertise (highest of all three skills) on current hero (-1)
!!HE-1:S(skill)/?(skillLvl:y);                        get Warfare expertise

!#VA(skills[3]:y);
!#VA(pics[3]:y);
!!VR(skills[0]):S(SKILL_BALLISTICS);
!!VR(skills[1]):S(SKILL_ARTILLERY);
!!VR(skills[2]):S(SKILL_FIRST_AID);

  !!re i/0/2;
    !!if&(skills[i])=(skill);
      !!if&i=0;
        !!VR(pics[0]):S(skills[1]);
        !!VR(pics[1]):S(skills[2]);
      !!el&i=1;
        !!VR(pics[0]):S(skills[2]);
        !!VR(pics[1]):S(skills[0]);
      !!el;
        !!VR(pics[0]):S(skills[0]);
        !!VR(pics[1]):S(skills[1]);
      !!en;

      !!VR(pics[2]):S(skill); *3 +2 +(skillLvl);
      !!br;
    !!en;
  !!en;

  !!re i/0/2;
    !!VR(pics[i]):*3 +2 +(skillLvl);
  !!en;  

!!VRv2:S0;                           init v2

!!UN:P75/?(wogOption:y);           check if Short Skill descriptions (script75.erm, Hermann the Weird) is enabled

!!if&(wogOption);
  !!VRz-1:Sz164045;              msgbox text if short skill description (script75) active
!!el;
  !!VRz-1:Sz164046;              if standard skill description (script75 inactive)
!!en;

!!IF&(skillArea)>1:Q2/20/(pics[0])/20/(pics[1])/20/(pics[2])/(msgType)^%Z-1^;  msg each time Warfare skill text field (name or expertise) is clicked

!!if&v2>0;
  !!VRi:Sv2 -1;
  !!VR(answer:y):S(pics[i]) -(skillLvl) - 2 :3;
  !!FU(WOG_193_UpdateWarFareLevel):P-1/(answer)/0;              determine and set Warfare expertise (highest of all three skills) on current hero (-1)
!!en;

!!SN:D;                            redraw hero screen


  GET WARFARE MAIN SKILL AND EXPERTISE

!?FU(WOG_193_UpdateWarFareLevel);                  set Warfare main skill and expertise (x1=hero, x2= w48 or clicked Warfare skill, x3=if cycling)
!!FU&x2<>0/x2<>10/x2<>20/x2<>27:E; exit if skill parameter invalid (neither none, Bal, Art nor FA)

!!FU:A?y30;
!!VRx3&y30<=0:S0;

!!IF:Wx1;                    set w var reference to that hero
!!HEx1:S10/?y1 S20/?y2 S27/?y3; get expertise of all three Warfare skills (y1, y2, y3)

!!VRy4:Sy1;                  (calculate highest Warfare expertise (y4))
!!VRy4&y2>y4:Sy2;             .
!!VRy4&y3>y4:Sy3;             .

*!HEx1:S10/0;
*!HEx1:S20/0;
*!HEx1:S27/0;    remove any Warfare skill (to later guarantee they are the last displayed skills) - Archer30: HE:S27/0 results in unknown erm bug, disabling  seem to be fine

!!if&x3=1/x2;
  !!VRy6&x2=10:S20;       cycle to display next skill (if skill text field (name or expertise) was right-clicked)
  !!VRy6&x2=20:S27;        .
  !!VRy6&x2=27:S10;        .
  !!VRx2:Sy6;         .
!!en;

!!if&x2=0;
  !!if&y1>=y2/y1>=y3;
    !!VRx2:S10;

  !!el&y2>=y1/y2>=y3;
    !!VRx2:S20;

  !!el&y3>=y1/y3>=y2;
    !!VRx2:S27;

  !!en;
!!en;

!!HEx1&y4>0/x2>0:Sx2/y4;     set max expertise to clicked/selected Warfare skill
!!VRw48&y4>0/x2>0:Sx2;       set hero var w48 to respective Warfare display skill
!!HEx1:S?y5;                 get number of skills shown (y5)
!!HEx1:S10/y4 S20/y4 S27/y4; give the other two skills at same expertise, but hide them afterwards
!!HEx1&x2=10:S0/20/1 S0/27/1; if Bal then hide Art and FA

!!HEx1&x2=20:S0/10/1 S0/27/1; if Art then hide Bal and FA
!!HEx1&x2=27:S0/10/1 S0/20/1; if FA  then hide Bal and Art
!!HEx1:Sy5;                  set back number of skills shown to avoid erroneous display


  TRIGGERS TO SET CORRECT EXPERTISE FOR THE OTHER TWO WARFARE SKILLS

!?FU(OnAfterErmInstructions)&v2365=1;
!!re i/0/(HERO_LAST_WOG);
  !!IF:Wi;                    set w var reference to current hero
  !!FU(WOG_193_UpdateWarFareLevel):Pi/w48;            update Warfare skill levels and displayed skill  
!!en;

!?FU(OnAfterHeroGainLevel)&v2365=1; hero levelup lets hero advance the other warfare skills to pre-levelup expertise [post-levelup trigger would be nice (now it is Post Lvl UP @daemon_n)]
!!IF:W-1;                    set w var reference to current hero
!!FU(WOG_193_UpdateWarFareLevel):P-1/w48;            update Warfare skill levels and displayed skill

!?OB104&v2365=1;           University pre-visit trigger
!!IF:W-1;                    set w var reference to current hero
!!FU(WOG_193_UpdateWarFareLevel):P-1/w48;            update Warfare skill levels and displayed skill

!$OB104&v2365=1;           University post-visit trigger
!!IF:W-1;                    set w var reference to current hero
!!FU(WOG_193_UpdateWarFareLevel):P-1/w48;            update Warfare skill levels and displayed skill

**OB63/51&v2365=1;         Market of Time pre/post-visit trigger [NOT RECOMMENDED]
; pre-visit would still take place after script09 (Market of Time) effects
; -> problem now adressed and rectified in Market of Time script09

!$OB113&v2365=1;           Witch Hut post-visit trigger [pre-visit trigger might interfere with Advanced Witch Huts]
!!IF:W-1;                    set w var reference to current hero
!!FU(WOG_193_UpdateWarFareLevel):P-1/w48;            update Warfare skill levels and displayed skill

!?BA52&v2365=1;            battle has started (trigger for both sides)
!!IF:Wi^battle_hero_0^;                 set w var reference to attacking hero
!!FU(WOG_193_UpdateWarFareLevel):Pi^battle_hero_0^/w48;         check attacking hero
!!IF&i^battle_hero_1^>-1:Wi^battle_hero_1^;          set w var reference to defending hero (if any)
!!FU(WOG_193_UpdateWarFareLevel)&i^battle_hero_1^>-1:Pi^battle_hero_1^/w48;  check defending hero (if any)

******************************************************************************************

** CONFLUX RAMPART MONSTER CHANGE (2004 September 19)         (for WoG v3.58)
** by Tobyn
**
** Rampart gives one Faerie Dragon instead of two Green Dragons per week
** Rampart gives one Diamond Dragon instead of two Gold Dragons per week
** Conflux gives 3 Firebirds/Phoenix instead of 4 per week (if Neutral Town script67 inactive)
**
** NOTE: Faerie Dragon resource will be *displayed* as Crystal, but it *is* Gems.
** Can't display different resources for upg and std in same hire dialogue box.
**
** BTW: how to change building cost without using \data\building.txt file?
**
** USES functions FU2334, var (v2324..2327), (flag 1)
** ONLY ACTIVE if v2366=1 or v2367=1 (wogify options 188 and 189)

!#UN&v2366=1:T1/6/0/134 T1/6/1/151;  replace Green->FaerieDrag, Gold->DiaDrag
!#MA&v2366=1:C151/6/12000 O134/1;    DiaDrag 8000->12000 Gold, FaerieDrag neutral->Rampart
!#MA&v2366=1:U26/27;                 GreenDrag still upgrades to GoldDrag at Hill Fort

!?FU(OnEveryDay)&i^timerWeekDay^=1/i^timerOnce^=1;          get town coords and change every Rampart/Conflux ONCE every monday
!!UN&v2367=1:P67/?y-1;                 check if Neutral Town script67 is active
!!VRv2367&y-1=1:S0;                    if so, disable reduction in Neutral "Conflux" Town
!!UN|v2366=1/v2367=1:U98/-1/?v2324;    store how many towns there are
!!VRv2325|v2366=1/v2367=1:S-1;         set index to -1 for new quick UN:U search
!!DO2334/1/v2324/1|v2366=1/v2367=1:P;  call Rampart/Conflux mod loop

!?FU2334;                            each Rampart/Conflux with Castle gets one lvl 7 creature less
!!IF:V1/0;                             init flag 1
!!UN:U98/-1/-1/2325;                   get coordinates (v2325..2327) (uses new quick UN:U index -1 instead of x16)
!!OBv2325/v2326/v2327:U?y1;            check town type (1=Rampart, 8=Conflux)
!!CAv2325/v2326/v2327:B3/9;            check if Castle built in that town
!!VRy1&-1:S-10;                        forget about that town if no Castle
!!CAv2325/v2326/v2327&y1=1/v2366=1:M1/6/d-1/d-1; subtract one lvl 7 if OK and Rampart options active
!!CAv2325/v2326/v2327&y1=8/v2367=1:M1/6/d-1/d-1; subtract one lvl 7 if OK and Conflux options active

**================ END OF SCRIPT ==================**
