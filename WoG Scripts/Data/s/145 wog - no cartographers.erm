ZVSE2

** CARTOGRAPHERS ARE REPLACED
** ЗАМЕНА КАРТОГРАФОВ ВУАЛЯМИ ТЬМЫ

** Option 145 by Timothy Pulver
** Rewritten by igrik (24.11.2020)

* All cartographers are removed from the map and replaced by Covers of Darkness.


!#DC(WOG_OPT_NO_CARTOGRAPHERS) = 145;

; function for passing through all map objects
!?FU(WOG_OnIterateAllMapObjects);
!#VA(x:x) (y:x) (z:x);
!#VA(objType:x);
!#VA(objSubtype:x);
!#VA(isYellowSquare:x); boolean.
!#VA(isPassable:x);     boolean.

; check: is entry to the object
!!FU&(isYellowSquare)<>(TRUE):E;

!!UN:P(WOG_OPT_NO_CARTOGRAPHERS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

; check: object type
!!FU&(objType)<>(OBJ_CARTOGRAPHER):E;

; set a new object type
!!VR(objType):S(OBJ_COVER_OF_DARKNESS);

; delete current and create a new object
!!UN:O(x)/(y)/(z)/1/(ERM_REPLACE_NO_REDRAW); 
!!UN:I(x)/(y)/(z)/(objType)/0/(ERM_REPLACE_NO_REDRAW);


** End of Script **
