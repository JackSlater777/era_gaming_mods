ZVSE2

** SOME DWELLINGS HAVE UPGRADED CREATURES
** УЛУЧШЕННЫЕ ВНЕШНИЕ ЖИЛИЩА

** Option 133 by Donald X. Vaccarino
** Rewritten by igrik (24.11.2020)

* Approximately 25% of Dwellings will sell upgraded creatures.


!#DC(WOG_OPT_DWELLINGS_UPGRADED_CREATURES) = 133;


; function for passing through all map objects
!?FU(WOG_OnIterateAllMapObjects);
!#VA(x:x) (y:x) (z:x);
!#VA(objType:x);
!#VA(objSubtype:x);
!#VA(isYellowSquare:x); boolean.
!#VA(isPassable:x);     boolean.

; check: is entry to the object
!!FU&(isYellowSquare)<>(TRUE):E;

; check option is enabled
!!UN:P(WOG_OPT_DWELLINGS_UPGRADED_CREATURES)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;

; check: object type
!!FU&(objType)<>(OBJ_CREATURE_GENERATOR_1):E;

; generate 20% chance
!!VR(random:y):R0/0/4; 
!!FU&(random)<>0:E;

; get type creature
!!DW(x)/(y)/(z):M0/?(typeMon:y)/d; 
!!FU&(typeMon)>(MON_PHOENIX):E;

; get creature upgrade
!!FU(GetUpgradedMonster):P(typeMon)/?(typeUpgrade:y);
!!FU&(typeUpgrade)<=(NO_MON):E;

; Exit if the upgraded monster is stronger than Blood Dragon
!!MA:F(typeUpgrade)/?(upgFv:y) F(MON_BLOOD_DRAGON)/?(bloodDragonFv:y);
!!FU&(upgFv)>=(bloodDragonFv):E;

; get creature lvl
!!MA:L(typeUpgrade)/?(level:y);

; set upgrade type guards, if level >= 4
!!DW(x)/(y)/(z)&(level)>=4:G0/(typeUpgrade)/d;

; set upgrade type creature
!!DW(x)/(y)/(z):M0/(typeUpgrade)/d;

** End of Script **
