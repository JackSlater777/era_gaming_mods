ZVSE2

Mysticism Enhancement v1.3 by Anders Jonsson
Updated July 14, 2004
Updated September 15, 2004 by Timothy Pulver for local y variable correction.
Updated August 1, 2022 by daemon_n

This script enhances the Mysticism Skill levels as:
Basic- hero can predict what next "Week of" will be. This only works if Timothy Pulver's Week of Monsters
Script v1.3 or higher is running at the same time.
Advanced- hero can predict what spells a Mage Guild will contain. This will drain the hero of all spell
points and movement for that turn.
Expert- if the player right-clicks on an enemy hero whithin a hero with Mysticism's scouting radius, and
the hero with Mysticism is selected, the hero may pay 5 Spell Points to learn all enemy hero skills,
spells, equipped artifacts, creatures, and how much experience, primary skills, luck, morale and spell
points that hero has.
It also lets heroes regenerate 5%, 10% or 15% of their normal maximum spell points each day, depending
on their level of Mysticism, double amount for AI heroes.

Variables used:
v1-v10
(z1-z9), [z135000-z135177]
Flags:
(1-10), 56, 172, 412
Timers:
TM30;

!#UN:P35/?v1; (check for WoG option)
!#TM30&v1=1/56:S1/999/1/255; (setup timer)


*************
*** Basic ***
*************

!?FU(WOG_OnNewDay);
!!UN:P35/?y1;
!!FU&y1<>1:E;

!!VRy1:Sc0 %7;       (Get day of week)

!!if&56/y1=2;       (Week of Monsters script is enabled, on tuesday)
  !!re y2/0/7;      (loop for all players)
    !!SN:W^WOG43_HeroID_%Y2^/-1; (reset var)
  !!en;
!!en;

!!DO(WOG43_LoopAllHeroes_AddSpellPoints)/0/155/1:P;

!?FU(WOG43_LoopAllHeroes_AddSpellPoints);
; x16 - hero id
!!HEx16:O?y1 S8/?y2; (Check hero owner and level of Mysticism)
!!FU|y1=-1/y2<1:E;   (Abort if not the same)

!!OW:Iy1/?y4;        (owner is AI?)
!!VRy3:Sc0 %7;       (Get day of week)
!!FU(WOG43_GetHeroID_KnowWeekOfMonstre)&56/y3=2/y4=0:Px16/y1; (Week of Monsters script is enabled, on tuesday and not AI)

!!FU(WOG_Hero_GetFullSpellPoints):Px16/?y3;
!!HEx16:I?y5/1;      (get current spell points)
!!FU&y5>=y3:E;       (exit if current spell points >= maximum)

!!VRy6:Sy3 *y2 :20 -1 -y2; (Spell points are mysticism level/10 of normal, -level of mysticism -1, for normal regeneration)
!!HEx16:X?y8/?y9/d/d/d/d/d;

!!if&y8=0/y9=8; (if Hero is a spec Mysticism)
  !!HEx16:Ed/?y11/1;
  !!VRy10:Sy3 *y11 :207; (spec mysticism: +0.5% for lvl of max SP hero)
  !!VRy6:+y10;
!!en;

!!VRy6&y4=1:*2;      (double bonus for AI)
!!VRy5:+y6;          (add current spell points)
!!VRy5&y5<1:S1;      (check for negative SP)
!!VRy5&y5>y3:Sy3;    (check for add SP > max)
!!HEx16:Iy5/1;       (set hero spell points)


!?FU(WOG43_GetHeroID_KnowWeekOfMonstre);
; x1 - hero id
; x2 - player color
!!SN:W^WOG43_HeroID_%X2^/?y1; (check player hero id)
!!SN&y1=-1:W^WOG43_HeroID_%X2^/x1; (save player hero id)


!?TM30&56;
*** Linked to Week of Monster Script ***
!!UN:P35/?y1;
!!FU&y1<>1:E;

!!VRy2:Sc0 %7;       (Get day of week)
!!FU&y2<>2:E;

!!OW:C?y1 Iy1/?y2;   (get active player)
!!SN:W^WOG43_HeroID_%Y1^/?y16; (check player hero id)
!!SN:W^WOG43_HeroID_%Y1^/-1;   (reset player hero id)
!!FU|y16=-1/y2=1:E;

!!HEy16:O?y1 S8/?y2;   (Check hero owner and level of Mysticism)
!!FU|y1=-1/y2<1:E;     (Abort if not the same)

!!HEy16:B0/?z1; (get hero name)
!!HEy16:R2/?y2;
!!VRz7&y2=0:Sz135000;
!!VRz7&y2=1:Sz135001;
!!VRz2&y2=0:Sz135002;
!!VRz2&y2=1:Sz135003; (Get hero sex)
!!FU(WOG43_DrawCreaturesDesc):P1/y16; (get creature with description and way to predict)
!!FU181:P; (call Function 181 from Week of Monsters to see if it's "Week of the Imp" at start of game)
!!UN&v180>=0/-172:N3/z3/v180/0; (get monster Type)
!!VRz3&v180=-1:Sz135004;
!!VRz3&v180=-2:Sz135005;
!!VRz3&v180=-3:Sz135006;
!!VRz3&v180=-4:Sz135007;
!!VRz3&v180=-5:Sz135008;
!!VRz3&v180=-6:Sz135009;
!!VRz3&v180=-7:Sz135010;
!!VRz-4:S^ ^;
!!VRz-4&v180>=0:Sz135011;
*!VRz1:S^%Z1's^;
~~VRz1:+z135012; [This doesn't work properly, it adds an extra space]
!!IF:M1/z135013;
!!IF:V9/1; (Don't display this again if more heroes have mysticism)

****************
*** Advanced ***
****************

!?CM1; (tigger on right-click in town screen)
*!UN:P35/?y1;
*!FU(WOG43_MistAdv)&y1=1:P;
!?FU(OnTownMouseClick)&999/i^mouse_action^=(MOUSE_RMB_PRESSED);
!!UN:P35/?y1;
!!FU(WOG43_MistAdv)&y1=1:P;

!?FU(WOG43_MistAdv);
*!UN:P35/?y1;
*!FU&y1<>1:E;
*!IF:V10/0; (Flag 10 is on/off switch)

*!IF&-1000:V10/1; (Disable if AI(fat chance it rightclicks though))
!!VR(heroId:y):S(NO_HERO);
!!CA-1:H0/?(harHero:y) H1/?(visHero:y) G?(guildLevel:y) T?(townType:y);
!!VR(maxGm:y):S4;

!!if|(townType)=(TOWN_STRONGHOLD)/(townType)=(TOWN_FORTRESS);
  !!VR(maxGm:y):S2;
!!el&(townType)=(TOWN_CASTLE);
  !!VR(maxGm:y):S3;
!!en;

!!VR(guildId:y):S(guildLevel) -1;

!!if&(visHero)<>(NO_HERO);
  !!HE(visHero):S(SKILL_MYSTICISM)/?(lvl:y);
  !!VR(heroId)&(lvl)>1:S(visHero);

!!el&(harHero)<>(NO_HERO);
  !!HE(harHero):S(SKILL_MYSTICISM)/?(lvl:y);
  !!VR(heroId)&(lvl)>1:S(harHero);
!!en;
*!IF:L^%(guildId)^;

!!if&(heroId)<>(NO_HERO)/i^mouse_item^=(guildId)/(guildId)<>(maxGm);

  !!CA-1:N?z1;               (get town name)
  !!HE(heroId):R2/?y29 I?(sPoints:y)/1;
  !!VRz2&y29=0:Sz135014;
  !!VRz2&y29=1:Sz135015;
  !!VRz3&y29=0:Sz135000;
  !!VRz3&y29=1:Sz135001;       (get hero sex)
  !!HE(heroId):B0/?z7 B0/?z5;             (Get hero name)
  !!VRz1:+z135012;
  !!VR(sPoints):*-1;
  !!VR(picType:y):S(PIC_TYPE_FIRST_TOWN_BUILDING) +(townType);
  !!VR(picId:y):S(lvl) +26;
  !!IF:Q1/(PIC_TYPE_SEC_SKILL)/(picId)/(picType)/(maxGm)/(PIC_TYPE_SPELL_POINTS)/(sPoints)/(MSG_TYPE_QUESTION)/z135016;
  !!if&1;
    !!HE(heroId):I0/1 W0/1;

    !!FU(NewIntArray):P42/(PIC_TYPE_SPELL)/?(spells:y); [set array of pic spells]
    !!VR(hasLibrary:y):S(FALSE);

    !!VR(hasLibrary)&(townType)=(TOWN_TOWER):S(TRUE);

    !!VRf:S1;

    !!re i/0/(maxGm); loop all gm's
      !!VR(maxScrollsOnLevel:y):S5 -i +(hasLibrary);
      !!re j/0/(maxScrollsOnLevel)/1/-1;
        !!CA-1:Gi/j/?(spellId:y);
        !!if&(spellId)<>(NO_SPELL);
          !!SN:M(spells)/f/(spellId);
          !!VRf:+2;
        !!en;
      !!en;
    !!en;

    !!VR(size:y):Sf -1;
    !!SN:M(spells)/(size);
    !!VR(dlgs:y):S(size):16;
    !!VR(it:y):S0;
    !!VRv1:S(heroId);
    !!FU(WOG43_DrawCreaturesDesc):P2;                              (get creature with description and way to predict)
    !!VRz3:S^^;
    !!VRz1:Sz135017;
    !!re i/0/(dlgs);
      !!FU(Array_Slice):P(spells)/(it)/16/?(spellsC:y);
      !!VR(it:y):+16; Fd/(size);
      !!FU(PrepareMultiPicDialog):P(spellsC);
      !!IF:N1/z135020;    
    !!en;

    !!CM:R0;                   (Disable normal popup)

  !!en;

!!en;


!?FU(WOG43_DrawCreaturesDesc);                    (Draw creature with description and way to predict)
!!VRy3:T4;               (Draw to decide what kind of method is used)
!!VRy4:R160;             (Draw to get creature)
!!VRy4&y4=>122:+1;
!!VRy4&y4=>124:+1;
!!VRy4&y4=>126:+1;
!!VRy4&y4=>128:+1;
!!VRy4&y4=>145:+1;
!!VRy4&y4=>146:+1;
!!VRy4&y4=>147:+1;
!!VRy4&y4=>148:+1;
!!VRy4&y4=>149:+1;
!!VRy4&y4=>160:+1;
!!VRy4&y4=>161:+1;
!!VRy4&y4=>162:+1;
!!VRy4&y4=>163:+1;       (Exclude certain creatures)
!!UN:N3/z6/y4/0;         (Get creature name)

!!VRy5:S135021 R32;              (Draw description)
!!VRz4:Szy5; (Get description straight from ert file)

!!VRz5&y3=0:Sz135154;
!!VRz5&y3=1:Sz135155;
!!HEv1&x1=2:R2/?y1;     (Get hero sex)
!!HEx2&x1=1:R2/?y1;     (Get hero sex) (Different methods depending on what called this function)
!!VRz8&y1=0/y3=2/x1=1:Sz135000;
!!VRz8&y1=1/y3=2/x1=1:Sz135001;
!!VRz8&x1=1:Sz7;
!!VRz8&y3=3/y1=0:Sz135014;
!!VRz8&y3=3/y1=1:Sz135015;
!!VRz5&y3=2:Sz135156;
!!VRz5&y3=3:Sz135157;
!!VRz5&y3=4:Sz135158;

**************
*** Expert ***
**************




// cut hero dlg
!?FU(OnPreHeroScreen)&i^WOG_35_DisableHeroClick^=1;

  !!UN:C6916808/4/?(dlg:y);
  !!re i/122/137;(end_value);
    !!SN:E6288816/2/(dlg)/i;
    !!if&v1>0;
      !!SN:E6286720/2/v1/6/6;/0/0/0/(dlg);
      !!VRi^WOG_35_DisableHeroClick^:S2;
    !!en;
  !!en;

!?FU(OnAdventureMapRightMouseClick)&999/i^mouse_item^=(ITEM_ADVMAP_ADVENTURE_MAP);
!!UN:P35/?y1;
!!FU(WOG_35_MistExp)&y1=1:P;

!?FU(WOG_35_MistExp);

!!OW:A(CURRENT_PLAYER)/?(activeHero:y);

!!if&(activeHero)<>(NO_HERO);
  !!HE(activeHero):S(SKILL_MYSTICISM)/?(mist:y) I?(sPoints:y)/1;

  !!if&(mist)>2/(sPoints)>=5;
    !!OBi^mouse_mapX^/i^mouse_mapY^/i^mouse_mapZ^:T?t;
    !!if&t=(OBJ_HERO);
      !!HEi^mouse_mapX^/i^mouse_mapY^/i^mouse_mapZ^:N?(heroId:y) O?(owner:y);
      !!OW:Ti^timerOwner^/?(myTeam:y) T(owner)/?(targetTeam:y);
      !!if&(owner)<>i^timerOwner^/(myTeam)<>(targetTeam);

        !!FU(WOG_Hero_GetObjectRange):P(activeHero)/i^mouse_mapX^/i^mouse_mapY^/i^mouse_mapZ^/?(range:y);
        !!FU(WOG_Hero_GetScoutingRadius):P(activeHero)/?(scouting:y);
        !!VR(scouting):+1 Sd*(scouting);
        !!if&(scouting)>(range);
          

          !!FU(WOG_35_ShowOfferMessage):P(activeHero)/(heroId)/?(result:y);

          !!if&(result); // show hero dlg
            !!HE(activeHero):Id-5;
            !!CM:R0;
            !!VRi^WOG_35_DisableHeroClick^:S(TRUE); // tell to disable actions
            !!UN:R5/1/0; // set std cursor
            //show cut hero dlg
            !!SN:E5118576/(CALLCONV_FASTCALL)/(heroId)/1/1/0;i(int hero_id, int is_DelButtn, int a3, char is_RightClickDlg)
            !!VRi^WOG_35_DisableHeroClick^:S(FALSE);
          !!en;
        !!en;

      !!en;
      
    !!en;
  !!en;
!!en;

!?FU(WOG_35_ShowOfferMessage);
!#VA(heroId:x) (targetHero:x) (result:x);
!!IF:V10/0;   (Check if Hero has Expert Mysticism)

!!HE(heroId:x):B0/?z1 R2/?y1;          (Get Hero name)

!!VRz3&y1=0:Sz135014;
!!VRz3&y1=1:Sz135015;
!!VRz4&y1=0:Sz135000;
!!VRz4&y1=1:Sz135001;
!!HEx2:B0/?z2 R2/?y1;           (Get Hero name)
!!VRz6&y1=0:Sz135002;
!!VRz6&y1=1:Sz135003;      (Get Hero Sex)
!!VRz-1:Sz2;
!!VRz-1:+z135012;
!!IF:Q1/(PIC_TYPE_SEC_SKILL)/29/(PIC_TYPE_SPELL_POINTS)/-5/(MSG_TYPE_QUESTION)/z135159;
!!VR(result)&1:S(TRUE);