ZVSE2

** NEW BATTLEFIELDS
** НОВЫЕ ПОЛЯ БИТВ

** Option 068 by Donald X. Vaccarino, Overlord, Dutch, 
** Jonas Gustafsson, and Timothy Pulver 

** Rewritten by igrik
** Updated by Archer30 to support external iamges

** New Battlefield pictures will often be used in combat. 
** This has no affect on the battles but adds some visual variety.


!#DC(WOG_OPT_NEW_BATTLE_FIELDS) = 68;


!?FU(WOG_OPT_SetNewBattleFields)&i^battle_isVisible^; [Executes after OnBeforeBattleUniversal]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                      

!!FU(WOG_Battle_GetMapItem):P?(mapItem:y);       
!!FU&(mapItem)=(FALSE):E;

!!FU(WOG_MapItem_GetRealType):P(mapItem)/?(meetType:y);
!!FU&(meetType)=(OBJ_TOWN):E;                    [Exit on battling towns (even without walls)]

!!BA:P?(x:y)/?(y:y)/?(z:y); 
!!OB(x)/(y)/(z):U?(meetSubType:y);

!!BA:H(BATTLE_RIGHT)/?(rightHero:y);

!!if&(rightHero)>(NO_HERO);
  !!HE(rightHero):P?(x)/?(y)/?(z);
  !!VR(meetType):S(OBJ_HERO);
!!en;

; Get terrain type
!!TR(x)/(y)/(z):T?(terrType:y)/?(terrSubType:y);
!!FU&(terrType)>=9:E;                   [Exit if the battle initiates on rocks]
!!FU&(terrType)=8/i^battle_hero_vs_hero^:E; [Exit if it's a battle between two boats]

; Deal with Creature Banks and Mines
!!VR(name:z):S^^;

!!if&(meetType)=(OBJ_DRAGON_UTOPIA); 
  !!VR(name):S^BnUt^;

!!el&(meetType)=(OBJ_CREATURE_BANK);
  !!VR(name):S^BnNg^;

!!el&(meetType)=(OBJ_CRYPT);            [N/A by default]
  !!VR(name):S^BnCr^;
      
!!el&(meetType)=(OBJ_MINE)/(meetSubType)=7;
  !!VR(name):S^GrMn^;
!!el;
  !!BU:G?(bonuses:y);                   [есть ли бонусы от спец.земли в битве]

  !!if&(bonuses)=-1;                    [если не модифицируемая битва от типа земли]
    !!VR(name):S^Gr%(terrType)^;
  !!el;                                 [N/A by default]
    !!if&(bonuses)=21;
      !!VR(name):S^Curs^;
    !!el&(bonuses)=46;
      !!VR(name):S^Magi^;
    !!el&(bonuses)=227;
      !!VR(name):S^Holy^;
    !!el&(bonuses)=224;
      !!VR(name):S^Evil^;
    !!el&(bonuses)=222;
      !!VR(name):S^Clov^;
    !!el&(bonuses)=228;
      !!VR(name):S^Luci^;
    !!el&(bonuses)=226;
      !!VR(name):S^Fier^;
    !!el&(bonuses)=229;
      !!VR(name):S^Clou^;
    !!en;
  !!en;
!!en;

; Exit if no definition for the name
!!FU&(name)=^^:E;

; Check out the max available background image index (must be consecutive, can be empty)
!!VR(availNum:y):S0;
!!SN:F^PcxPngExists^/^Bk%(name).pcx^;
!!VR(availNum)&v1:+1;

!!re i/0/23;                            [23 - max subtype of Sand, meaning max of 25 images available for one terrain type]
  !!SN:F^PcxPngExists^/^Bk%(name)%i.pcx^;

  !!br&v1<>(TRUE);
!!en;

!!VR(availNum):+i;
!!FU&(availNum)=0:E;                    [Exit if no image]

!!VR(backImgIndex:y):S(terrSubType) %(availNum);[вычисляем 1 из (availNum) фонов по остатку от деления]
!!BA:B^BkGr%(terrType)%(backImgIndex).pcx^; [установить новый фон битвы]

!?FU(WOG_CreateERMHook);
*!UN:C4603972/1/235;                             [нет проверки на в подземелье (Z=1)]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                       

!!SN:Ex1/1/4601891/(WOG_OPT_SetNewBattleFields); 

** End of Script **
