ZVSE2

** NEW BATTLEFIELDS
** НОВЫЕ ПОЛЯ БИТВ

** Option 068 by Donald X. Vaccarino, Overlord, Dutch, 
** Jonas Gustafsson, and Timothy Pulver 

** Rewritten by igrik

** New Battlefield pictures will often be used in combat. 
** This has no affect on the battles but adds some visual variety.


!#DC(WOG_OPT_NEW_BATTLE_FIELDS) = 68;


!?FU(WOG_OPT_SetNewBattleFields);                [Executes before OnBeforeBattleUniversal, global vars from EEF are not initialised yet!]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                      

!!FU(WOG_Battle_GetMapItem):P?(mapItem:y);       
!!FU&(mapItem)=(FALSE):E;                        

!!FU(WOG_MapItem_GetRealType):P(mapItem)/?(meetType:y);
!!FU&(meetType)=(OBJ_TOWN):E;                    [Exit on battling towns (even without walls)]

!!BA:P?(x:y)/?(y:y)/?(z:y); 
!!OB(x)/(y)/(z):U?(meetSubType:y);

!!BA:H(BATTLE_RIGHT)/?(rightHero:y);

!!if&(rightHero)>(NO_HERO);
  !!HE(rightHero):P?(x)/?(y)/?(z);
  !!VR(meetType):S(OBJ_HERO);
!!en;

!!if&(meetType)=(OBJ_DRAGON_UTOPIA); 
  !!BA:B^BkBnUt.pcx^;  !!FU:E;   
!!en;

!!if&(meetType)=(OBJ_CREATURE_BANK);
 !!BA:B^BkBnNg.pcx^;   !!FU:E;           
!!en;

!!if&(meetType)=(OBJ_MINE)/(meetSubType)=7;
  !!BA:B^BkGr60.pcx^;  !!FU:E;  
!!en;

!!TR(x)/(y)/(z):T?(terrType:y)/?(terrSubType:y); 

!!if&(terrType)>=0/(terrType)<=7;                [если обычный тип почвы]
  !!BU:G?(bonuses:y);                            [есть ли бонусы от спец.земли в битве]

  !!if&(bonuses)=-1;                             [если не модифицируемая битва от типа земли]
    !!VR(backImgIndex:y):S(terrSubType) %4;      [вычисляем 1 из 4 фонов по остатку от деления]
    !!VR(backImgIndex)&(terrType)=7/v1000=1:+4;  [инферно под землей (есть доп.фоны)]
    !!BA:B^BkGr%(terrType)%(backImgIndex).pcx^;  [установить новый фон битвы]
  !!en;                                          [конец условия_2]
!!en;                                            [конец условия_1]

!?FU(WOG_CreateERMHook);                         [функция создания хука (см.1000 wog - base)]
*!UN:C4603972/1/235;                             [нет проверки на в подземелье (Z=1)]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                       

!!SN:Ex1/1/4601891/(WOG_OPT_SetNewBattleFields); 

** End of Script **
