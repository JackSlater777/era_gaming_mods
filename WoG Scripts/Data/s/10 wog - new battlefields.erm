ZVSE2

** NEW BATTLEFIELDS
** НОВЫЕ ПОЛЯ БИТВ

** Option 068 by Donald X. Vaccarino, Overlord, Dutch, 
** Jonas Gustafsson, and Timothy Pulver 

** Rewritten by igrik

** New Battlefield pictures will often be used in combat. 
** This has no affect on the battles but adds some visual variety.


!#DC(WOG_OPT_NEW_BATTLE_FIELDS) = 68;


!?FU(WOG_OPT_SetNewBattleFields);                 
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                      

!!FU(WOG_Battle_GetMapItem):P?(mapItem:y);       
!!FU&(mapItem)=(FALSE):E;                        

!!FU(WOG_MapItem_GetRealType):P(mapItem)/?(mitType:y);  
!!BA:P?(x:y)/?(y:y)/?(z:y); 

!!OB(x)/(y)/(z):U?(mipSubType:y);    
!!if&i^battle_hero_1^>-1;
  !!HE-20:P?(x)/?(y)/?(z);
  !!VR(mitType)=(OBJ_HERO);
!!en;

!!FU&(mitType)=(OBJ_TOWN):E;      

!!if&(mitType)=(OBJ_DRAGON_UTOPIA); 
  !!BA:B^BkBnUt.pcx^;  !!FU:E;   
!!en;

!!if&(mitType)=(OBJ_CREATURE_BANK);
 !!BA:B^BkBnNg.pcx^;   !!FU:E;           
!!en;

!!if&(mitType)=(OBJ_MINE)/(mipSubType)=7;
  !!BA:B^BkGr60.pcx^;  !!FU:E;  
!!en;

!!TR(x)/(y)/(z):T?(terrType:y)/?(terrSubType:y); 
!!if&(terrType)>=0/(terrType)<=7;                [если обычный тип почвы]
  !!BU:G?(bonuses:y);                             [есть ли бонусы от спец.земли в битве]
  !!if&(bonuses)=-1;                             [если не модифицируемая битва от типа земли]
    !!VR(backImgIndex:y):S(terrSubType) %4;       [вычисляем 1 из 4 фонов по остатку от деления]
    !!VR(backImgIndex)&(terrType)=7/v1000=1:+4;   [инферно под землей (есть доп.фоны)]
    !!BA:B^BkGr%(terrType)%(backImgIndex).pcx^;   [установить новый фон битвы]
  !!en;                                          [конец условия_2]
!!en;                                            [конец условия_1]

!?FU(WOG_CreateERMHook);                        [функция создания хука (см.1000 wog - base)]
!!UN:C4603972/1/235;                              [нет проверки на в подземелье (Z=1)]
!!UN:P(WOG_OPT_NEW_BATTLE_FIELDS)/?(wogOption:y); 
!!FU&(wogOption)<>(TRUE):E;                       
!!SN:Ex1/1/4601891/(WOG_OPT_SetNewBattleFields); 

** End of Script **
