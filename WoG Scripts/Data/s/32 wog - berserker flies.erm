ZVSE2

** BERSERKER FLIES
** «Ã»»-¡≈–—≈– ≈–€

** Option 046 by Timothy Pulver
** Rewritten by Archer30 (Oct 2022)

* Dragon Flies gain the ability to attack without retaliation,
* cast Berserk on their target, and return after attacking.

** Attention! The script works if the troops experience is enabled


!#DC(WOG_OPT_BERSERKER_FLIES) = 46;     

// Initialization (set No Retaliation flag)
!?FU(OnAfterErmInited);
!!UN:P(WOG_OPT_BERSERKER_FLIES)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:H^monname^/(MON_DRAGON_FLY)/0/^%T(wog.46.singular)^;  
!!SN:H^monname^/(MON_DRAGON_FLY)/1/^%T(wog.46.plural)^; 
!!SN:H^monname^/(MON_DRAGON_FLY)/2/?(description:z);
!!SN:H^monname^/(MON_DRAGON_FLY)/2/^%(description)%T(wog.46.description)^;

!!MA:X(MON_DRAGON_FLY)/?(flags:y);   
!!VR(flags):|(MON_FLAG_NO_RETALIATION); 
!!MA:X(MON_DRAGON_FLY)/(flags);

; Check if Strike and Return value has been modified by the other sources. Skip this feature if positive
!!UN:C7725156/(UNC_INT)/?(wogStrikeAndReturnMon:y);
; The initial value of i^wog_46_strikeAndReturnMon^ is 0. If the address does not return a proper value, i^wog_46_strikeAndReturnMon^ will still be 0
!!VRi^wog_46_strikeAndReturnMon^&(wogStrikeAndReturnMon)>0/(wogStrikeAndReturnMon)<1000:S(wogStrikeAndReturnMon);

// Give Strike and Return with the address of Darkness Dragon's ability
!?FU(OnBattleStackObtainsTurn)&i^wog_46_strikeAndReturnMon^>0;
!!UN:P(WOG_OPT_BERSERKER_FLIES)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BMi^battle_current_stack^:T?(mon:y);
!!FU(WOG_46_CheckIfMonIsEligible):P(mon)/?(result:y);
!!UN&(result):C7725156/(UNC_INT)/?i^wog_46_strikeAndReturnMon^ C7725156/(UNC_INT)/(mon);

// Restore Strike and Return modification
!?FU(OnBattleActionEnd)&i^wog_46_strikeAndReturnMon^>0;
!!UN:C7725156/(UNC_INT)/i^wog_46_strikeAndReturnMon^;

// Execute Berserk cast after attack (and retaliation)
!?FU(WOG_OnAfterMelee);
!#VA(atkStack:x) (defStack:x);

!!UN:P(WOG_OPT_BERSERKER_FLIES)/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BM(atkStack):T?(atkMon:y);
; Check if the attacker has Berserk casting
!!FU(WOG_46_CheckIfMonIsEligible):P(atkMon)/?(canCastBerserk:y);

; Check the defender's immunity to Berserk
!!if&(canCastBerserk);
  !!BM(atkStack):I?(atkSide:y);
  !!FU(WOG_Battle_CanStackReceiveSpell):P(defStack)/(SPELL_BERSERK)/(atkSide)/?(acceptSpell:y);

  ; Check if it is lucky enough to cast
  !!if&(acceptSpell);
    !!VR(random:y):R0/0/99;
    !!BM(defStack)&(random)<20:P?(pos:y) C(SPELL_BERSERK)/(pos)/(SKILL_BASIC)/1/(FALSE); [20% to cast Berserk with Dragon Flies' melee attack]
  !!en;
!!en;

// Funciton to check if the targeted monster is eligible for Strike and Return and Berserk cast (does not work for Pikemen)
!?FU(WOG_46_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_DRAGON_FLY):S(TRUE);

** End of Script **
