ZVSE2

** Commander Witch Huts ERM script v1.0 - Donald X. Vaccarino
** Updated: Oct. 25. 2003 by Hermann the Weird
** Updated: July 03. 2004 by Tobyn [only added correct Magic Resistance bonus when increasing Magic Power skill]
** Rewritten by Archer30 on Jul 2023
** Requires WOG version 3.58 or later
** Makes Witch Huts give out commander skills

** bonuses (once per type of hut):
**   air magic       fly                    mag-spe  16384
**   archery         shooting               att-spe  16
**   armorer         endless retaliation    def-hit  32
**   artillery       no retaliation         att-mag  8
**   ballistics      +1 attack              0
**   diplomacy       +1 damage              3
**   eagle eye       +1 damage              3
**   earth magic     paralyzing attack      hit-mag  1024
**   estates         +1 speed               5
**   fire magic      fire shield            def-mag  128
**   first aid       regeneration           hit-spe  2048
**   intelligence    +1 hit points          2
**   leadership      attack twice           hit-dmg  512
**   learning        +1 defense             1
**   logistics       champion bonus         dmg-spe  8192
**   luck            block                  def-spe  256
**   mysticism       +1 hit points          2
**   navigation      +1 defense             1
**   necromancy      death stare            dmg-mag  4096
**   offense         max damage             att-dmg  4
**   pathfinding     +1 attack              0
**   resistance      reduce defense         att-def  1
**   scholar         +1 magic power         4
**   scouting        +1 speed               5
**   sorcery         fear                   att-hit  2
**   tactics         attack all directions  def-dmg  64
**   water magic     +1 speed               5
**   wisdom          +1 magic power         4

** To use this code, put it into a Global Event, set to never happen.

** Variables Used: v600-v627, w40
** w40 may not be modified elsewhere!
** The other variables may be used elsewhere, but get trashed by this code.


** initialization

!?FU(OnAfterErmInstructions);
!!UN:P66/?(wogOption:y);                [Check for WoG option]
!!FU&(wogOption)<>(TRUE):E;             [Exit if its disabled]

!!re i/(HERO_FIRST)/(HERO_LAST_WOG);    [loop through all heroes to clear w variable]
  !!IF:Wi;                              [use hero i's w variables]
  !!VRw40:S0;                           [clear our variable (tracking types of witch huts visited)]
!!en;

** end of initialization


** Witch Hut object trigger

!$OB(OBJ_WITCH_HUT);
!!UN:P66/?(wogOption:y);                [Check for WoG option]
!!FU&(wogOption)<>(TRUE):E;             [Exit if its disabled]

!!CO(CURRENT_HERO):D?(isDead:y) E?(isAllowed:y);

!!if&(isDead)<>(TRUE)/(isAllowed);[handle witch hut if alive enabled commander]
  !!FU(WOG_66_CheckCommanderAbilityType):Pv998/v999/v1000/?(secSkill:y)/?(code:y)/?(mode:y);

  !!if&(mode)=0;
    !!FU(WOG_66_HandleSpecialAbility):P(secSkill)/(code); [handle special ability bonus]
  !!el;
    !!FU(WOG_66_HandleStatBonus):P(secSkill)/(code); [handle +stat bonus]
  !!en;
!!en;

** end of object trigger


** Adv Map Tile Hint trigger
; Be aware not to change the hint when the hero is visting (above) the witch hut!
!?FU(OnAdvMapTileHint);
!#VA(x:x) (y:x) (z:x);             Object entrance coordinates
!#VA(objType:x) (objSubtype:x);    Type and subtype of object. For active hero it's object under hero.
!#VA(tileX:x) (tileY:x) (tileZ:x); Real tile coordinates.

!!UN:P66/?(wogOption:y);                [Check for WoG option]
!!FU&(wogOption)<>(TRUE):E;             [Exit if its disabled]

; Exit if the location has been occupied by a hero
!!UN:P904/(TRUE) P905/(FALSE);
!!HE(tileX)/(tileY)/(tileZ):E?(exp:y);
!!UN:P905/?(hasError:y);
!!UN:P904/(FALSE);

!!if&(hasError);
  !!UN:P905/(FALSE);
!!el;
  !!FU:E;
!!en;

; Exit if the object type in the game is not Witch Hut (if hero is visiting, the object type would be hero)
!!OB(tileX)/(tileY)/(tileZ):T?(nativeObjType:y);
!!FU&(nativeObjType)<>(OBJ_WITCH_HUT):E;

; Check if the Witch Hut has been visited by the interacting player
!!OW:C?(currPlayer:y)/?(interactPlayer:y);
!!OB(tileX)/(tileY)/(tileZ):C?(controlWord:y);
!!VR(controlWordOrig:y):S(controlWord); [copy for checking later]
!!VR(playerBit:y):S1 Sd<<(interactPlayer) *32; [Set to bit value for checking Control Word]
!!VR(controlWord):|(playerBit);         [Try to "or" with current player's bit]
!!FU&(controlWord)<>(controlWordOrig):E;[Check if not same, then abort]

; Set up additional hint if the Witch Hut has been visiting by the interacting player
!!FU(WOG_66_CheckCommanderAbilityType):P(tileX)/(tileY)/(tileZ)/?(secSkill:y)/?(code:y)/?(mode:y);

!!if&(mode)=0;
  !!SN:T^wog.66.spec%(secSkill)^/?(bonus:z);
!!el;
  !!SN:T^wog.66.stat%(code)^/?(bonus);
!!en;

; Add in additional text
; Note that here we do not change the original hint as it might have compatibility impact to some languages
!!MM:M?(origHint:z);
!!MM:M^%(origHint) / (%T(wog.66.commander)%(bonus))^;

** end of Adv Map Tile Hint trigger


** function to check the type of commander ability given

!?FU(WOG_66_CheckCommanderAbilityType);
!#VA(x:x) (y:x) (z:x) (secSkill:x);
!#VA(code:x);
!#VA(mode:x);

!!VRv600:C100/16/8192/105/103/101/512/104/102/256; [table of effects]
!!VRv610:C100/103/4096/105/128/16384/105/1024/104/64; [100-105 are +stat, 0 is special estates]
!!VRv620:C8/101/4/32/102/2/1/2048;      [others are bit values of special abilities]

!!WH(x)/(y)/(z):S?(secSkill);           [get skill for this hut]
!!VR(index:y):S600 +(secSkill);         [get variable address]
!!VR(code):Sv(index);                   [now (code) is code for what to do]
!!VR(mode):S(FALSE);

!!if&(code)>=100/(code)<=105;
  !!VR(mode):S(TRUE);                   [flag for if it's a +stat bonus]
  !!VR(code):-100;
!!en;

** end of function


** function to handle giving special ability
** x1 = skill, x2 = ability bit value

!?FU(WOG_66_HandleSpecialAbility);
!#VA(secSkill:x) (abilityBit:x);
!#VA(usedY[10]:y);

!!CO(CURRENT_HERO):B0/?(abilityFlag:y);  [get bonuses]
!!VR(learnedAbility:y):S(abilityFlag) &(abilityBit); [check for bit already set]
!!FU&(learnedAbility):E;

!!CO(CURRENT_HERO):B0/d|(abilityBit);   [set bonuses]

!!if&(ERM_FLAG_IS_HUMAN);
  !!if&(secSkill)=(SKILL_ARCHERY);      [marksmanship]
    !!VR(skillIcon:y):S80;
  !!el&(secSkill)=(SKILL_LOGISTICS);    [jousting]
    !!VR(skillIcon):S98;
  !!el&(secSkill)=(SKILL_LEADERSHIP);   [swordsmanship]
    !!VR(skillIcon):S90;
  !!el&(secSkill)=(SKILL_LUCK);         [blocking]
    !!VR(skillIcon):S88;
  !!el&(secSkill)=(SKILL_NECROMANCY);   [staring]
    !!VR(skillIcon):S96;
  !!el&(secSkill)=(SKILL_FIRE_MAGIC);   [fire eating]
    !!VR(skillIcon):S86;
  !!el&(secSkill)=(SKILL_AIR_MAGIC);    [flying]
    !!VR(skillIcon):S100;
  !!el&(secSkill)=(SKILL_EARTH_MAGIC);  [stunning]
    !!VR(skillIcon):S92;
  !!el&(secSkill)=(SKILL_TACTICS);      [whirling attacks]
    !!VR(skillIcon):S84;
  !!el&(secSkill)=(SKILL_ARTILLERY);    [hitting and running]
    !!VR(skillIcon):S78;
  !!el&(secSkill)=(SKILL_OFFENCE);      [bone crushing]
    !!VR(skillIcon):S76;
  !!el&(secSkill)=(SKILL_ARMORER);      [revenge]
    !!VR(skillIcon):S82;
  !!el&(secSkill)=(SKILL_SORCERY);      [making scary faces]
    !!VR(skillIcon):S74;
  !!el&(secSkill)=(SKILL_RESISTANCE);   [pressure points]
    !!VR(skillIcon):S72;
  !!el&(secSkill)=(SKILL_FIRST_AID);    [healing]
    !!VR(skillIcon):S94;
  !!en;

  !!SN:T^wog.66.spec%(secSkill)^/?(bonus:z);

  !!if|(secSkill)=(SKILL_NECROMANCY)/(secSkill)=(SKILL_FIRE_MAGIC);
    !!UN:P51/?(enhancedCommander:y);
    !!SN&(enhancedCommander):T^wog.66.spec%(secSkill)a^/?(bonus);
  !!en;

  !!SN:T^wog.66.msg^/?(msg:z)/^bonus^/(bonus);

  ; Note that using "block" parametre here does not produce better results
  !!IF:M^%(msg)



{~>dlg_npc2.def:0:%(skillIcon)}

^;
!!en;

** end of function


** function to handle giving +stat
** x1 = skill, x2 = stat

!?FU(WOG_66_HandleStatBonus);
!#VA(secSkill:x) (stat:x);
!#VA(usedY[40]:y);

!!CO(CURRENT_HERO):S(stat)/?(skillLevel:y); [get current basic skill level]

; Exit if the current basic skill has already reached Grand Master level
!!if&(skillLevel)>=5;
  !!FU:E;
; Check if learning the new basic skill could result in exceeding skills limit
!!el&(skillLevel)=0;
  ; Get the skills limit
  !!UN:C7770979/(UNC_INT8)/?(skillLimit:y);

  ; Get the quanity of skills learned
  !!VR(counter:y):S0;

  !!re i/0/5;
    !!CO(CURRENT_HERO):Si/?(learnedSkillLevel:y);

    !!VR(counter)&(learnedSkillLevel)>0:+1;
  !!en;

  ; Exit if skills limit has been reached
  !!FU&(counter)>=(skillLimit):E;
!!en;

!!IF:W(CURRENT_HERO); [refer to current hero's w variables]

!!VRv600:C1/2/4/8/16/32/64/128/256/512; [bit table for skills]
!!VRv610:C1024/2048/4096/16384/32768/65536/131072/262144/524288/1048576;
!!VRv620:C2097152/4194304/8388608/16777216/33554432/67108864/134217728/268435456;

!!VR(index:y):S600 +(secSkill);         [get address of bit value]
!!VR(bit:y):Sv(index);                  [get bit value]
!!VR(hasVisited:y):Sw40 &(bit);         [look at just our bit of w40]
!!FU&(hasVisited):E;

; give one extra skill level
!!VR(skillLevel):+1;
!!CO(CURRENT_HERO):S(stat)/(skillLevel);

; Increase Magic Resistance along with Magic Power, doesn't work automatically
!!CO(CURRENT_HERO)&(stat)=4:S6/(skillLevel);

!!VRw40:|(bit);                         [remember so we don't give it again later]

!!if&(ERM_FLAG_IS_HUMAN);
  !!VR(skillIcon:y):S(stat) *6 +(skillLevel);
  !!SN:T^wog.66.stat%(stat)^/?(bonus:z) T^wog.66.msg^/?(msg:z)/^bonus^/(bonus);

  ; Note that using "block" parametre here does not produce better results
  !!IF:M^%(msg)



{~>dlg_npc2.def:0:%(skillIcon)}

^;
!!en;

** end of function
