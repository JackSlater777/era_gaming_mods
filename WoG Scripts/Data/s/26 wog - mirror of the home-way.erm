ZVSE2

** MIRROR OF THE HOME-WAY
** ЗЕРКАЛО ПУТИ ДОМОЙ

** Option 052 by Sir Four
** Rewritten by igrik (02.10.2020)

** This building works as a 'Town Portal' spell:
**   - for player 1000 coins.
**   - for AI 0 coins.


!#DC(WOG_OPT_MIRROR_HOME_WAY) = 52;


!?FU(OnAfterErmInstructions);
!!UN:P(WOG_OPT_MIRROR_HOME_WAY)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;

!!SN:T^wog.52.objName^/?(objName:z);
!!SN:H^object^/(OBJ_NEW_WOG_OBJECTS)/66/(objName);

!?OB(OBJ_NEW_WOG_OBJECTS)/66;
!!UN:P(WOG_OPT_MIRROR_HOME_WAY)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;

!!if&(ERM_FLAG_IS_HUMAN):; gamer
  !!HE(ANY_HERO):O?(owner:y);
  !!OW:R(owner)/(RES_GOLD)/?(gold:y);

  !!if&(gold)<1000:;
    !!SN:T^wog.52.noMoney^/?(pauper:z);
    !!IF:M1/(pauper);
    !!FU:E;    
  !!en;  

  !!OW:R(owner)/(RES_GOLD)/d-1000;
!!en:;

!!UN:C4314462/1/?(patch1:y);    [патч: сохраняем значения]
!!UN:C4315628/2/?(patch2:y);    [патч: сохраняем значения]

!!UN:C4314462/1/235;            [патч: временно убираем проверку на мувпоинты]
!!UN:C4315628/2/15851;          [патч: временно убираем стоимость мувов и маны]
!!UN:C6918840/4/?(advMgr:y);    [менеджер КП]

!!if&(ERM_FLAG_IS_HUMAN):; gamer
  !!UN:R7/0/0 R5/0/0;           [включаем курсор мыши]  
  !!SN:E4314336/2/(advMgr)/3;   [открыть диалог городского портала]
  
!!el:; AI
  !!SN:E4314336/2/(advMgr)/1;   [AI прыгает в ближайший город (базовый уровень)]
!!en:;

!!UN:C4314462/1/(patch1);       [патч: возвращаем проверку на мувпоинты]
!!UN:C4315628/2/(patch2);       [патч: возвращаем параметры стоимости мувов и маны]

** End of Script **
