ZVSE2
; Author:   Archer30
; Original Author: Bonizag
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Summon elementals with 10 times the usual spell cost on adventure map, once every turn for every hero
; AI will summon with the same costs if they learned an expert summon spell
; w84 - cast Summon Elementals this turn


// Initialization. Now all the summoning spells are also considered as adv spells
!#UN:P74/?i^wog_74_enabled^;

!?FU(OnAfterErmInstructions)&i^wog_74_enabled^;
!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:Fd|2;
!!en;

; Set up monsters for summon spells
; Customise here if you wish
!!VRi^wog_74_mon_%(SPELL_AIR_ELEMENTAL)^:S(MON_AIR_ELEMENTAL);
!!VRi^wog_74_mon_%(SPELL_EARTH_ELEMENTAL)^:S(MON_EARTH_ELEMENTAL);
!!VRi^wog_74_mon_%(SPELL_WATER_ELEMENTAL)^:S(MON_WATER_ELEMENTAL);
!!VRi^wog_74_mon_%(SPELL_FIRE_ELEMENTAL)^:S(MON_FIRE_ELEMENTAL);

// Reset all the cast flag for all heroes
!?FU(OnEveryDay)&i^timerDay^>1/i^timerOnce^/i^wog_74_enabled^;
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!IF:Wi;
  !!VRw84:S(FALSE);
!!en;

// Multiple spell points costs for summoning spells
!?FU(OnBeforeAdventureMagic)&999/i^wog_74_enabled^;
!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:C0/d*10;
  !!SSi:C1/d*10;
  !!SSi:C2/d*10;
  !!SSi:C3/d*10;
!!en;

// Add in elementals to the hero's army
!?FU(OnAfterAdventureMagic)&999/i^wog_74_enabled^;
; Divide costs for summoning spells
!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:C0/d:10;
  !!SSi:C1/d:10;
  !!SSi:C2/d:10;
  !!SSi:C3/d:10;
!!en;

!!OW:A(CURRENT_PLAYER)/?(hero:y);

!!HE(hero):P?(x:y)/?(y:y)/?(z:y);
!!TR(x)/(y)/(z):G?(terrain:y);
!!FU&(terrain)=21:E;                    [Cursed Ground]

!!VR(spell:y):Sv997;

!!if&(spell)>=(SPELL_FIRE_ELEMENTAL)/(spell)<=(SPELL_AIR_ELEMENTAL);
  !!IF:W(hero);
  ; Pop up msg if the hero has cast Summon Elementals this turn
  !!if&w84;
    !!FU(WOG_GetGeneralText):P126/?(string:z);
    !!IF:M^%(string)^;
  ; Summon Elementals for the hero if hasn't cast
  !!el;
    !!FU(WOG_74_SummonElementals):P(hero)/(spell)/(terrain)/(FALSE);
    !!SN:P^SUMNELM^;
    !!SN:D;
  !!en;
!!en;

!?FU(WOG_74_SummonElementals);
!#VA(hero:x) (spell:x) (terrain:x) (isAi:x);

; Get the creature type/quantity of summoning
!!VR(mon:y):Si^wog_74_mon_%(spell)^;
!!FU(WOG_74_GetRealSpellLevel):P(hero)/(spell)/(terrain)/?(level:y);
!!FU(WOG_74_GetSummonQty):P(hero)/(spell)/(level)/?(qty:y);

; Add creature to the army
; Auto select the weakest troop to replaced for AI
!!if&(isAi);
  !!HE(hero):C2/(mon)/(qty)/(FALSE);
!!el;
  !!HE(hero):C2/(mon)/(qty)/(TRUE);
!!en;

; Reduce spell points
!!SS(spell):C(level)/?(cost:y);
!!VR(cost)&(isAi):*10;                  [10 times the cost for AI as the cost for them hasn't been changed upon opening spell book]
!!HE(hero):Id-(cost);
; Mark the summoning spell as cast
!!IF:W(hero);
!!VRw84:S(TRUE);

!?FU(WOG_74_GetRealSpellLevel);
!#VA(hero:x) (spell:x) (terrain:x) (level:x);

!!if&(spell)=(SPELL_FIRE_ELEMENTAL);
  !!VR(ss:y):S(SKILL_FIRE_MAGIC);
  !!VR(favourTerrain:y):S226;           [Fiery Fields]
!!el&(spell)=(SPELL_AIR_ELEMENTAL);
  !!VR(ss):S(SKILL_AIR_MAGIC);
  !!VR(favourTerrain):S229;             [Magic Clouds]
!!el&(spell)=(SPELL_WATER_ELEMENTAL);
  !!VR(ss):S(SKILL_WATER_MAGIC);
  !!VR(favourTerrain):S228;             [Lucid Pools]
!!el&(spell)=(SPELL_EARTH_ELEMENTAL);
  !!VR(ss):S(SKILL_EARTH_MAGIC);
  !!VR(favourTerrain):S231;             [Rockland]
!!en;

; Set the level of magic ss depending on the terrain overlay the hero is standing on
!!if|(terrain)=(favourTerrain)/(terrain)=229; [Magic Plains]
  !!VR(level):S(SKILL_EXPERT);
!!el;
  !!HE(hero):S(ss)/?(level);
!!en;

// Function for getting the quantity of creature summon from a summon spell
// Customise here if you wish
!?FU(WOG_74_GetSummonQty);
!#VA(hero:x) (spell:x) (level:x) (qty:x);

; Calculate the quantity of summoning according to magic level and power
!!HE(hero):Fd/d/?(pwr:y)/d;

!!VR(multiplier:y):S(level) +1;
!!VR(qty):S(pwr) *(multiplier);

// Summon elementals for AI heroes
!?FU(OnEveryDay)&i^timerDay^>1/i^timerOnce^/i^wog_74_enabled^;
!!re (hero:y)/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HE(hero):P?(x:y)/?(y:y)/?(z:y);
  !!TR(x)/(y)/(z):G?(terrain:y);
  !!co&(terrain)=21;                    [Cursed Ground]

  !!HE(hero):O?(owner:y);
  !!OW:I(owner)/?(isAi:y);

  !!if&(isAi);
    !!VR(castFire:y):S(FALSE);
    !!VR(castEarth:y):S(FALSE);
    !!VR(castWater:y):S(FALSE);
    !!VR(castAir:y):S(FALSE);

    ; Check if the hero learned and has enough mana to cast a summon spell
    !!HE(hero):I?(mana:y);

    !!re (spell:y)/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
      !!FU(WOG_74_CheckIfAbleToSummon):P(hero)/(terrain)/(mana)/(spell)/?(canCast:y);

      !!if&(canCast);
        !!VR(castFire)&(spell)=(SPELL_FIRE_ELEMENTAL):S(TRUE);
        !!VR(castEarth)&(spell)=(SPELL_EARTH_ELEMENTAL):S(TRUE);
        !!VR(castWater)&(spell)=(SPELL_WATER_ELEMENTAL):S(TRUE);
        !!VR(castAir)&(spell)=(SPELL_AIR_ELEMENTAL):S(TRUE);
      !!en;
    !!en;

    ; Create the array for available summon spells with all considerations
    !!FU(NewIntArray):P?(availSpells:y);

    ; Check if the hero has an available slot
    !!re (slot:y)/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
      !!HE(hero):C0/(slot)/?(mon:y)/?(qty:y);

      !!if|(mon)<=(NO_MON)/(qty)<=0;
        !!FU(Array_Push)&(castFire):P(availSpells)/(SPELL_FIRE_ELEMENTAL);
        !!FU(Array_Push)&(castEarth):P(availSpells)/(SPELL_EARTH_ELEMENTAL);
        !!FU(Array_Push)&(castWater):P(availSpells)/(SPELL_WATER_ELEMENTAL);
        !!FU(Array_Push)&(castAir):P(availSpells)/(SPELL_AIR_ELEMENTAL);
      !!el&(castFire)/(mon)=i^wog_74_mon_%(SPELL_FIRE_ELEMENTAL)^;
        !!FU(Array_Push):P(availSpells)/(SPELL_FIRE_ELEMENTAL);
      !!el&(castEarth)/(mon)=i^wog_74_mon_%(SPELL_EARTH_ELEMENTAL)^;
        !!FU(Array_Push):P(availSpells)/(SPELL_EARTH_ELEMENTAL);
      !!el&(castWater)/(mon)=i^wog_74_mon_%(SPELL_WATER_ELEMENTAL)^;
        !!FU(Array_Push):P(availSpells)/(SPELL_WATER_ELEMENTAL);
      !!el&(castAir)/(mon)=i^wog_74_mon_%(SPELL_AIR_ELEMENTAL)^;
        !!FU(Array_Push):P(availSpells)/(SPELL_EARTH_ELEMENTAL);
      !!en;

      !!SN:M(availSpells)/?(size:y);
    !!en;

    !!if&(size)>0;
      !!VR(maxIndex:y):S(size) -1;
      !!VR(random:y):R0/0/(maxIndex);
      !!SN:M(availSpells)/(random)/?(spell);

      !!FU(WOG_74_SummonElementals):P(hero)/(spell)/(terrain)/(TRUE);
    !!en;
  !!en;
!!en;

!?FU(WOG_74_CheckIfAbleToSummon);
!#VA(hero:x) (terrain:x) (mana:x) (spell:x) (result:x);

!!VR(result):S(FALSE);
!!HE(hero):M(spell)/?(spellLearned:y);

!!if&(spellLearned);
  !!FU(WOG_74_GetRealSpellLevel):P(hero)/(spell)/(terrain)/?(level:y);

  ; Summon elementals only with expert magic for AI
  !!if&(level)>=(SKILL_EXPERT);
    !!SS(spell):C(level)/?(cost:y);
    !!VR(cost):*10;

    !!VR(result)&(cost)<=(mana):S(TRUE);
  !!en;
!!en;
