ZVSE2
; Author:   Archer30
; Original Author: Bonizag
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Summon elementals with 10 times the usual spell cost on adventure map


// Initialization. Now all the summoning spells are also considered as adv spells
!?FU(OnAfterErmInstructions);
!!UN:P74/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:Fd|2;
!!en;

// Reset all the cast flag for all heroes
!?FU(OnEveryDay)&i^timerDay^>1/i^timerOnce^;
!!UN:P74/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!VRi^summelem_%i_cast^:S(FALSE);
!!en;

// Multiple spell points costs for summoning spells
!?FU(OnBeforeAdventureMagic)&999;
!!UN:P74/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:C0/d*10;
  !!SSi:C1/d*10;
  !!SSi:C2/d*10;
  !!SSi:C3/d*10;
!!en;

// Add in elementals to the hero's army
!?FU(OnAfterAdventureMagic)&999;
!!UN:P74/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!OW:A(CURRENT_PLAYER)/?(hero:y);
!!VR(spell:y):Sv997;

!!if&(spell)>=(SPELL_FIRE_ELEMENTAL)/(spell)<=(SPELL_AIR_ELEMENTAL);
  !!if&i^summelem_%(hero)_cast^;
    !!FU(WOG_GetGeneralText):P126/?(string:z);
    !!IF:M^%(string)^;
  !!el;
    ; Get the creature type/quantity of summoning and level of the spell
    !!VR(value:y):S(spell) -(SPELL_FIRE_ELEMENTAL);
    !!VR(address:y):S21 *(value) +5906498;
    !!UN:C(address)/(UNC_INT8)/?(mon:y);

    !!VR(ss:y)&(spell)=(SPELL_FIRE_ELEMENTAL):S(SKILL_FIRE_MAGIC);
    !!VR(ss)&(spell)=(SPELL_AIR_ELEMENTAL):S(SKILL_AIR_MAGIC);
    !!VR(ss)&(spell)=(SPELL_WATER_ELEMENTAL):S(SKILL_WATER_MAGIC);
    !!VR(ss)&(spell)=(SPELL_EARTH_ELEMENTAL):S(SKILL_EARTH_MAGIC);
    !!HE(hero):S(ss)/?(level:y);

    !!HE(hero):Fd/d/?(pwr:y)/d;
    !!VR(multiplier:y):S(level) +1;
    !!VR(qty:y):S(pwr) *(multiplier);
    ; Add creature to the army
    !!HE(hero):C2/(mon)/(qty)/(TRUE);
    ; Reduce spell points
    !!SS(spell):C(level)/?(cost:y);
    !!HE(hero):Id-(cost);
    ; Mark the summoning spell as cast
    !!VRi^summelem_%(hero)_cast^:S(TRUE);
    ; Play sound and refresh
    !!SN:P^SUMNELM^;
    !!SN:D;
  !!en;
!!en;

; Divide costs for summoning spells
!!re i/(SPELL_FIRE_ELEMENTAL)/(SPELL_AIR_ELEMENTAL);
  !!SSi:C0/d:10;
  !!SSi:C1/d:10;
  !!SSi:C2/d:10;
  !!SSi:C3/d:10;
!!en;
