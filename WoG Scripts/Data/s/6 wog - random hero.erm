ZVSE2
; Author:   Archer30
; Original Author: Bonizag
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; There is a 1/4 chance that a random hero will appear on the monster's side and help them fight.

!?FU(WOG_CreateERMHook);
!!UN:P72/?(wogOption:y);
!!if&(wogOption:y);
  !!SN:Ex1/1/4908283/(WOG_72_AfterDefeatedHero);
!!en;

!?FU(WOG_72_AfterDefeatedHero)&i^wog_72_randHeroActive^;
!!SN:X?t/0;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4908292;

// Manage variables before the battle, generates the neutral hero
!?FU(OnBeforeBattleUniversal)&i^battle_hero_1^=(NO_HERO);
!!UN:P72/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Initilization
!!VRi^wog_72_randHeroActive^:S(FALSE);
; Exit if not battling neutral stacks
!!FU&i^wog_isNeutralBattle^<>(TRUE):E;
; Exit if AI visits
!!FU&-(ERM_FLAG_IS_HUMAN):E;

; Check if there is any player not in the game
!!re i/(PLAYER_FIRST)/(PLAYER_LAST);
  !!OW:Ii/?(isAi:y)/?(hasLost:y);

  !!br&(hasLost);
!!en;

!!if&i<=(PLAYER_LAST);
  !!VRi^wog_72_randHeroActive^:S(TRUE);

  ; Check if it is lucky enough to encounter the neutral hero or else
  !!VR(random:y):R0/0/3;                  [25% chance]
  !!VR(random)&i^Random_Neutral_Hero^:S0; [Always generate a neutral hero if Always Random Hero is enabled]

  ; Get an available random hero and generate on the battlefield if lucky enough
  !!if&(random)=0;
    !!FU(WOG_GetRandomUnoccupiedHero):P?(randHero:y);

    !!if&(randHero)>(NO_HERO);
      !!BA:H1/(randHero);
      !!HE(randHero):Oi;
      !!VRi^wog_72_randHero^:S(randHero);
    !!el;
      !!VRi^wog_72_randHeroActive^:S(FALSE);
    !!en;
  !!el;
    !!VRi^wog_72_randHeroActive^:S(FALSE);
  !!en;
!!en;

// Store neutral stacks in global vars (in order to return them on battle replay)
// The timing is late for the compatibility with battle replay
!?FU(OnBeforeBattleUniversal_Quit)&i^wog_72_randHeroActive^;
!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
  !!BA:M1/i/?i^wog_72_replayType_%i^/?i^wog_72_replayNum_%i^;
!!en;

!?FU(OnBeforeBattleReplay)&i^wog_72_randHeroActive^;
!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
  !!BA:M1/i/i^wog_72_replayType_%i^/i^wog_72_replayNum_%i^;
!!en;

// Reset variables
!?FU(OnAfterBattleUniversal)&i^wog_72_randHeroActive^;
!!VRi^wog_72_randHeroActive^:S(FALSE);

!?FU(OnAfterLoadGame)&i^wog_72_randHeroActive^;
!!VRi^wog_72_randHeroActive^:S(FALSE);
