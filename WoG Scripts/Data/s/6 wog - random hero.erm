ZVSE2
; Author:   Archer30
; Updated by daemon_n
; Original Author: Bonizag
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; There is a 1/4 chance that a random hero will appear on the monster's side and help them fight.
; The chance would become 100% if i^Random_Neutral_Hero^ is TRUE


// Disable double object removal sound
// by daemon_n
; Here we do not check option as the option can possibly be enabled after starting the game
!?FU(WOG_CreateERMHook);
!!SN:Ex1/1/4908283/(WOG_72_AfterDefeatedHero);  

!?FU(WOG_72_AfterDefeatedHero)&i^wog_72_randHeroActive^;  
!!SN:X?t/0;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4908292;

// Manage variables before the battle, generates the neutral hero
!?FU(OnBeforeBattle)&i^wog_isNeutralBattle^/(ERM_FLAG_IS_HUMAN);
!!UN:P72/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Initilization
!!VRi^wog_72_randHeroActive^:S(FALSE);

; Check if there is any player not in the game
!!re i/(PLAYER_FIRST)/(PLAYER_LAST);
  !!OW:Ii/?(isAi:y)/?(hasLost:y);

  !!br&(hasLost);
!!en;

!!if&i<=(PLAYER_LAST);
  !!VRi^wog_72_randHeroActive^:S(TRUE);

  ; Check if it is lucky enough to encounter the neutral hero or else
  !!VR(random:y):R0/0/3;                  [25% chance]
  !!VR(random)&i^Random_Neutral_Hero^:S0; [Always generate a neutral hero if Always Random Hero is enabled]

  ; Get an available random hero and generate on the battlefield if lucky enough
  !!if&(random)=0;
    !!FU(WOG_GetRandomUnoccupiedHero):P?(randHero:y);

    !!if&(randHero)>(NO_HERO);
      !!HE(randHero):Z?(heroStruct:y);  [DON'T use HE:O as it might fail setting a hero to a lost player]
      !!UN:C(heroStruct)/34/(UNC_INT8)/i;
      !!BA:H1/(randHero);
      !!VRi^wog_72_randHero^:S(randHero);
    !!el;
      !!VRi^wog_72_randHeroActive^:S(FALSE);
    !!en;
  !!el;
    !!VRi^wog_72_randHeroActive^:S(FALSE);
  !!en;
!!en;

// Store neutral stacks in global vars (in order to return them on battle replay)
// The timing is late for the compatibility with battle replay
!?FU(OnBeforeBattleUniversal_Quit)&i^wog_72_randHeroActive^;
!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
  !!BA:M1/i/?i^wog_72_replayType_%i^/?i^wog_72_replayNum_%i^;
!!en;

!?FU(OnBeforeBattleReplay)&i^wog_72_randHeroActive^;
!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
  !!BA:M1/i/i^wog_72_replayType_%i^/i^wog_72_replayNum_%i^;
!!en;

// Reset variables
!?FU(OnAfterBattleUniversal)&i^wog_72_randHeroActive^;
!!VRi^wog_72_randHeroActive^:S(FALSE);

!?FU(OnAfterLoadGame)&i^wog_72_randHeroActive^;
!!VRi^wog_72_randHeroActive^:S(FALSE);

// Disable Surrender in battle with neutral creatures
; Note: It's not possible to surrender in quick combat in heroes 3
; The timing cannot be earlier at OnSetupBattlefield. It doesn't work.
; Both FU(OnBattlefieldVisible) and FU(OnAfterTacticsPhase) are required
!?FU(OnBattlefieldVisible)&i^wog_72_randHeroActive^/i^battle_isVisible^;
!!FU(WOG_72_DisableSurrenderButton):P;

!?FU(OnAfterTacticsPhase)&i^wog_72_randHeroActive^/i^battle_isVisible^;
!!FU(WOG_72_DisableSurrenderButton):P;

!?FU(WOG_72_DisableSurrenderButton);
!!FU(H3Dlg_GetCurrentDlg):P?(dlgObj:y);
!!SN:E6288816/2/(dlgObj)/2001;

!!if&v1;
  !!VR(itemStructure:y):Sv1;

  ; Set disabled frame
  !!UN:C(itemStructure)/60/(UNC_INT)/2; [Frame #2 as disabled]
  ; Set disabled
  !!SN:E6287104/2/(itemStructure)/(FALSE);
  ; Refresh
  !!SN:E6288864/2/(dlgObj)/1/2001/2001; [update onlyItem]
!!en;
