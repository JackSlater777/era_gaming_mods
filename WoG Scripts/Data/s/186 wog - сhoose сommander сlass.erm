ZVSE2

** CHOOSE COMMANDER CLASS
** ВЫБОР ТИПА КОМАНДИРА

** Option 186 by Timothy Pulver
** Rewritten by igrik (30.09.2020)

** When you hire a Commander, you can choose any Commander type regardless of the hero's class.


** В оригинальном скрипте AI командир устанавливался случайного класса (VR:S0 R8)
** Я не вижу в этом никакого смысла, поэтому для AI функционала не будет (note: igrik)


!#DC(WOG_OPT_CHOOSE_COMMANDER_CLASS) = 186;


!?FU(OnAfterCommanderBuy)&1000;
!!UN:P(WOG_OPT_CHOOSE_COMMANDER_CLASS)/?(wogOption:y);  
!!FU&(wogOption)<>(TRUE):E;

!!OW:A(CURRENT_PLAYER)/?(heroId:y);
!!FU&(heroId)=(NO_HERO):E;

!!UN:N3/(paladin:z)/(MON_PALADIN_A)/0;
!!UN:N3/(hierophant:z)/(MON_HIEROPHANT_A)/0;
!!UN:N3/(temple:z)/(MON_TEMPLE_GUARDIAN_A)/0;
!!UN:N3/(succubus:z)/(MON_SUCCUBUS_A)/0;
!!UN:N3/(soul:z)/(MON_SOUL_EATER_A)/0;
!!UN:N3/(brute:z)/(MON_BRUTE_A)/0;
!!UN:N3/(ogre:z)/(MON_OGRE_LEADER_A)/0;
!!UN:N3/(shaman:z)/(MON_SHAMAN_A)/0;
!!UN:N3/(astral:z)/(MON_ASTRAL_SPIRIT_A)/0;

!!SN:T^wog.186.title^/?z2;
!!SN:T^wog.186.standart^/?z3;
!!SN:T^wog.186.cancel^/?z4;
!!SN:T^wog.186.noHired^/?z5;

!!IF:G(ERM_RADIO)/2/1024/z2/(paladin)/(hierophant)/(temple)/(succubus)/(soul)/(brute)/(ogre)/(shaman)/(astral)/z3/z4/0;
!!VR(bits:y):Sv2;

!!FU&(bits)=512:E; don't change the class

!!if&(bits)=1024:;  if cancel
  !!CO(heroId):E0;  remove Commander
  !!OW:R(CURRENT_PLAYER)/(RES_GOLD)/d1000;
  !!IF:M^%z5^;  show text: no hired
  !!FU:E;
!!en;

; initializate y-var
!!VR(type:y):S0; 
!!VR(type)&(bits)=1:S0;
!!VR(type)&(bits)=2:S1;
!!VR(type)&(bits)=4:S2;
!!VR(type)&(bits)=8:S3;
!!VR(type)&(bits)=16:S4;
!!VR(type)&(bits)=32:S5;
!!VR(type)&(bits)=64:S6;
!!VR(type)&(bits)=128:S7;
!!VR(type)&(bits)=256:S8;

; Set new Commander type
!!CO(heroId):T(type);

** End of Script **
