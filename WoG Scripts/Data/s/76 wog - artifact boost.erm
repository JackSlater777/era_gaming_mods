ZVSE2

** Artifact Boost ERM script v1.4 - Donald X. Vaccarino
** Updated Jul. 18. 2004 by Hermann the Weird
** Updated Dec 2022 by Archer30
** Requires WOG version 3.58 or later
** Make formerly weak artifacts do something special, usually once/week
** WoGify Name: script02.erm

** Bird Of Perception gives 6 Royal Griffins
** Pendant Of Life gives 24 Sprites
** Pendant Of Death gives 12 Zombies
** Targ Of The Rampaging Ogre provokes a fight with 30 Ogres
** Dead Man's Boots gives Dead Man's Boots
** Pendant Of Total Recall gives 1000 experience
** Emblem Of Cognizance gives 15 gold per troop
** Stoic Watchman reveals a large map area
** Pendant Of Second Sight upgrades lowly shooters
** Pendant Of Holiness gives a random holy spell
** Boots Of Polarity may be traded for a one-time movement bonus
** Garniture Of Interference may be traded for a magic skill
** Pendant Of Dispassion gives +1 knowledge
** Pendant Of Free Will upgrades golems and peasants

** following artifacts new as of script version 1.3

** Diplomat's Ring may be traded to double number of first level troops hero has
** Surcoat Of Counterpoise doubles the effects of these artifacts, where applicable
** Orb Of Vulnerability gives 1 Archmage plus another per level of Wisdom
** Sphere Of Permanence gives 1 Mighty Gorgon plus another per 10 Defense
** Statesman's Medal may be traded at a town for up to three buildings
** Orb Of Inhibition may be traded to modify a creature's abilities

** All artifacts function for the AI (if worn) but AI won't agree to trades.

** To use this code, put it into a Global Event, set to never happen.

** Variables Used: v600-v628, v631, v640-v648, z326-z331, i-l
** Flags Used: 1, 2, 5, 710-714

** w33 must not be modified by other programs, used for enabling/disabling weekly creatures!

** The other variables may be used elsewhere but will be trashed by this code.

** Functions Used: 599-609, 612, 613
!#DC(WOG_ART_BOOST_W_VAR_ID) = 33;


; Set up global vars for Diplomat's Ring and Emblem of Conizance
!?FU(OnAfterErmInstructions);
!!UN:P102/?(wogOption:y);                 [Check for Enhanced Artifacts I]
!!FU&(wogOption)<>(TRUE):E;

!!VR(unitLimit:y):S(INT_MAX);
!!VR(goldLimit:y):S(INT_MAX);
!!UN:J2/?(diff:y); // get difficulty (unlimited for easy, text won't be set)

!!if&(diff)>0;
  ; Diplomat's Ring
  !!VR(unitLimit)&(diff)=1:S1000; // max 1000 for normal and easy
  !!VR(unitLimit)&(diff)=2:S500; // max 500 for hard
  !!VR(unitLimit)&(diff)=3:S250; // max 250 for expert
  !!VR(unitLimit)&(diff)>=4:S100; // max 100 for impossible

  ; Emblem of Cognizance
  !!VR(goldLimit)&(diff)=1:S100000; // max 100000 gold for normal and easy
  !!VR(goldLimit)&(diff)=2:S50000; // max 50000 for hard
  !!VR(goldLimit)&(diff)=3:S25000; // max 25000 for expert
  !!VR(goldLimit)&(diff)>=4:S10000; // max 10000 for impossible
!!en;

; Set global var to be used each week
!!VRi^wog_102_diplomatsRingLimit^:S(unitLimit);
!!VRi^wog_102_emblemOfCognizanceLimit^:S(goldLimit);

** Timer routine

!?FU(OnEveryDay)&i^timerWeekday^=1;
*?FU(OnKeyPressed_AdvMap)&x1=(KEY_HOME);
  !!UN:P102/?(wogOption:y);               [Check if Artifact Boost Script is enabled in WoGify Options]
  !!FU&(wogOption)<>(TRUE):E;             [Exit if it isn't]

  !!OW:Hi^timerOwner^/1;

  !!re (vHeroId:y)/2/v1/1/1; loop this player hero!
    !!HEv(vHeroId):A2/(ART_SURCOAT_OF_COUNTERPOISE)/?y99/?(artBonus:y);
    !!FU602:Pv(vHeroId:y)/(artBonus);                          [Iterate through all player heroes]
  !!en;


!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_RMB_PRESSED)/999;[R-click trigger for enabling/disabling weekly creatures from artifacts]
  !!UN:P102/?y-1; [Check if Artifact Boost Script is enabled in WoGify Options]
  !!FU&y-1<>1:E;     [Exit if it isn't]

  !!VR(clickedSlot:y):Si^mouse_item^-2;

  !!if&(clickedSlot:y)>=(ART_SLOT_MISC_1)/(clickedSlot:y)<=(ART_SLOT_MISC_4)|(clickedSlot:y)=(ART_SLOT_MISC_5)/(clickedSlot:y)=(ART_SLOT_NECK);

    !!HE(CURRENT_HERO):A1/?y-2/(clickedSlot:y);     [Check for weekly creature artifacts]

    !!FU&y-2<>103/y-2<>104/y-2<>63/y-2<>92/y-2<>93:E;     [Exit if no weekly creature artifact]
    !!CM:R0;     [Disable standard r-click info]

    !!FU(NewIntArray):P?(arr:y);
    !!FU(Array_Push):P(arr)/63/92/93/103/104;
    !!SN:M(arr)/?(size:y);
    !!IF:W(CURRENT_HERO);     [w vars refer to current hero]

    !!re i/0/(size)/1/-1;
      !!SN:M(arr)/i/?(artId:y);
      !!if&(artId)=y-2;
        !!VR(bit:y):S1 Sd<<i;
        !!VR(bitCheck:y):Sw33 &(bit);

        !!if&(bitCheck);
          !!VR(stat:z):S^%T(wog.102.off)^;
          !!VR(rstat:z):S^%T(wog.102.on)^;
        !!el;
          !!VR(stat):S^%T(wog.102.on)^;
          !!VR(rstat):S^%T(wog.102.off)^;
        !!en;
        !!br;
      !!en;
    !!en;

    !!SN:H^art^/(artId)/1/?(artDesc:z) T^wog.102.switch^/?(msg:z)/^art_name^/(artDesc)/^stat^/(stat)/^rstat^/(rstat);

    ** Display question for enabling/disabling **
    !!IF:Q2/(PIC_TYPE_ART)/y-2/(MSG_TYPE_QUESTION)/(msg);
    !!VRw33&2:X(bit:y);
  !!en;
** end of r-click trigger for enabling/disabling weekly creature artifacts **


** function to check one hero for special artifacts
** note the code is broken up into multiple functions to speed it up
** checking 156 heroes for 12 artifacts would take forever

*?FU(WOG_102_Get)
  !?FU(WOG_102_GetBirdPercepBon);
  !#VA(heroId:x) (armyArrayId:x) (artsNum:x);
  !!VR(monBon:y):S6 *(artsNum);
  !!FU(Array_Push):P(armyArrayId)/(MON_ROYAL_GRIFFIN)/(monBon);
  !!if&i^timerIsAi^=(FALSE);
    !!FU(WOG_102_GetArmyAddMsg):P(heroId)/(MON_ROYAL_GRIFFIN)/1;
  !!en;

  !?FU(WOG_102_GetPermaSphereBon);
  !#VA(heroId:x) (armyArrayId:x) (artsNum:x);
  !!HE(heroId):S(SKILL_WISDOM)/?(skillLvl:y);
  !!VR(monBon:y):S(skillLvl:y) +1 *(artsNum);
  !!FU(Array_Push):P(armyArrayId)/(MON_ARCH_MAGE)/(monBon);
  !!if&i^timerIsAi^=(FALSE);
    !!VR(index:y):S(monBon) F1/2 Sd-1 |(Wisdom) F1/2;
    !!FU(WOG_102_GetArmyAddMsg):P(heroId)/(MON_ARCH_MAGE)/(index:y);
  !!en;
  !?FU(WOG_102_GetVelnerSphereBon);
  !#VA(heroId:x) (armyArrayId:x) (artsNum:x);
  !!HE(heroId):F?y99/?(defense:y)/?y99/?y99;
  !!VR(monBon:y):S(defense:y) :10 +1 *(artsNum);
  !!FU(Array_Push):P(armyArrayId)/(MON_MIGHTY_GORGON)/(monBon);
  !!if&i^timerIsAi^=(FALSE);
    !!VR(index:y):S(monBon) F1/2 Sd-1 |(defense) F1/2;
    !!FU(WOG_102_GetArmyAddMsg):P(heroId)/(MON_MIGHTY_GORGON)/1;
  !!en;
  !?FU(WOG_102_GetLifePendantBon);
  !#VA(heroId:x) (armyArrayId:x) (artsNum:x);
  !!VR(monBon:y):S24 *(artsNum);
  !!FU(Array_Push):P(armyArrayId)/(MON_SPRITE)/(monBon);
  !!if&i^timerIsAi^=(FALSE);
    !!FU(WOG_102_GetArmyAddMsg):P(heroId)/(MON_SPRITE)/1;
  !!en;
  !?FU(WOG_102_GetDeathPendantBon);
  !#VA(heroId:x) (armyArrayId:x) (artsNum:x);
  !!VR(monBon:y):S12 *(artsNum);
  !!FU(Array_Push):P(armyArrayId)/(MON_ZOMBIE)/(monBon);
  !!if&i^timerIsAi^=(FALSE);
    !!FU(WOG_102_GetArmyAddMsg):P(heroId)/(MON_ZOMBIE)/1;
  !!en;

  !?FU(WOG_102_GetArmyAddMsg);
  !#VA(heroId:x) (monId:x) (ind:x);
  !!HE(heroId):B0/?(heroName:z) R2/?(gender:y);
  !!SN:T^wog.whose%(gender:y)^/?(whose:z)
  T^wog.102.giveCreature.%(monId).%(ind)^/?(msg:z)/^heroName^/(heroName:z)/^whose^/(whose:z)/^whose^/(whose:z);
  !!VRs^wog_102_AddArmyMsg^:+^%(msg:z)%T(wog.endl)^;

!?FU(WOG_102_GivingSingleCreatureArts);
  !#VA(heroId:x) (artBonus:x);
  !#VA(artsToGiveCreatures[5]:y) (fooPtrs[5]:y);
  !!VR(artsToGiveCreatures[0]):C(ART_BIRD_OF_PERCEPTION)/(ART_SPHERE_OF_PERMANENCE)/
  (ART_ORB_OF_VULNERABILITY)/(ART_PENDANT_OF_LIFE)/(ART_PENDANT_OF_DEATH);

  !!VR(fooPtrs[0]):C(WOG_102_GetBirdPercepBon)/(WOG_102_GetPermaSphereBon)/
  (WOG_102_GetVelnerSphereBon)/(WOG_102_GetLifePendantBon)/(WOG_102_GetDeathPendantBon);

  !!FU(NewIntArray):P?(monArmy:y);
  !!IF:W(heroId);
  !!re i/0/(artsToGiveCreatures[SIZE])/1/-1;
    !!HE(heroId):A2/(artsToGiveCreatures[i])/?y99/?(equippedArts:y);
    !!if&(equippedArts);
      !!VR(bit:y):S1 Sd<<i &w(WOG_ART_BOOST_W_VAR_ID);
      !!if|(bit)=0/i^timerIsAi^;

        !!FU(fooPtrs[i]):P(heroId)/(monArmy)/(equippedArts:y);
      !!en;
    !!en;
  !!en;

  !!SN:M(monArmy)/?(size:y);

  !!if&(size);
    !!if&i^timerIsAi^=0;
      !!FU(NewIntArray):P(size)/?(monDlg:y);
      !!re i/0/(size)/2/-1;
        !!VRj:Si +1;
        !!SN:M(monArmy)/i/?(monType:y) M(monArmy)/j/?(monNum:y);
        !!VR(monNum):Sd<<(artBonus:x) Sd<<16 |(monType:y);
        !!SN:M(monDlg)/i/(PIC_TYPE_MONSTER) M(monDlg)/j/(monNum:y);
      !!en;
      !!FU(Array_Push)&(artBonus):P(monDlg)/(PIC_TYPE_ART)/(ART_SURCOAT_OF_COUNTERPOISE);
      !!FU(PrepareMultiPicDialog):P(monDlg);

      !!IF:N(MSG_TYPE_MES)/s^wog_102_AddArmyMsg^;^lol^;
      !!VRs^wog_102_AddArmyMsg^:S^^;
    !!en;

    !!re i/0/(size)/2/-1;
      !!VRj:Si +1;
      !!SN:M(monArmy)/i/?(monType:y) M(monArmy)/j/?(monNum:y);
      !!VR(monNum):Sd<<(artBonus);
      !!HE(heroId):C2/(monType:y)/(monNum)/1;
    !!en;
  !!en;



!?FU602;
!#VA(heroId:x) (artBonus:x);

!!FU(WOG_102_GivingSingleCreatureArts):P(heroId:x)/(artBonus:x);


!!OW:Ii^timerOwner^/?v603; [get AI (1) or human (0)]
!!VRv600:S(heroId);

!!HEv600:A2/16/0/?v613; [check for Targ Of The Rampaging Ogre]
!!HEv600:A2/56/0/?v614; [check for Dead Man's Boots]
!!HEv600:A2/107/0/?v615; [check for Pendant Of Total Recall]
!!HEv600:A2/65/0/?v616; [check for Emblem Of Cognizance]
!!HEv600:A2/64/0/?v617; [check for Stoic Watchman]
!!HEv600:A2/101/0/?v618; [check for Pendant Of Second Sight]
!!HEv600:A2/102/0/?v619; [check for Pendant Of Holiness]
!!HEv600:A2/59/0/?v620; [check for Boots Of Polarity]
!!HEv600:A2/57/0/?v621; [check for Garniture Of Interference]
!!HEv600:A2/100/0/?v622; [check for Pendant Of Dispassion]
!!HEv600:A2/105/0/?v623; [check for Pendant Of Free Will]
!!HEv600:A2/67/0/?v624; [check for Diplomat's Ring]
!!HEv600:A2/58/0/?v625; [check for Surcoat Of Counterpoise]



!!VRv608:Sv613 +v614 +v615 +v616 +v617 +v618 +v619 +v620 +v621 +v622 +v623;
!!VRv608:+v624 +v625;

!!FU601&v608>0:P; [handle any artifacts if found]

** end of function


** function to handle special artifacts for a hero who has one

!?FU601;

!!IF:Wv600;     [w vars refer to hero v600]
!!HEv600:B0/?z326; [get hero name]
!!HEv600:P?v604/?v605/?v606; [get hero x/y/level]
!!HEv600:R2/?v607; [get hero sex]
!!VRz327&v607=0:Sz102019;
!!VRz327&v607=1:Sz102020;
!!VRz328&v607=0:Sz102021;
!!VRz328&v607=1:Sz102022;
!!IF:V5/0;
!!IF&v603=0:V5/1; [use flag 5 to mean we've got a human player's hero]

!!IF&5/v625>0:M1/z102023;
!!VRv631:S1;
!!VRv631&v625>0:S2; [some effects will be multiplied by v631 - doubled for Surcoat]

!!if&v614>0;
  !!IF&5:Q1/8/56/1/z102033;
  !!HEv600:A56; [give Dead Man's Boots]
  !!HEv600&v625>0:A56; [another pair for Surcoat]
!!en;

!!VRy1:S1000;
!!VRy1:*v631; [Surcoat factor]
!!IF&5/v615>0:Q1/17/y1/1/z102034;
!!HEv600&v615>0:Edy1; [give 1000 experience]

!!VRv24:S0; [0 counted]
!!DO606/0/6/1&v616>0:P; [count number of creatures Hero has]

; only if hero has creatures, which may not be always true
!!if&v24>0;
  !!VRv27:S15;
  !!VRv27:*v631; [Surcoat factor]

  !!if&v616>0;
    !!VRv24:*v27; [15 gold per creature]
    !!VRv24:F0/i^wog_102_emblemOfCognizanceLimit^; // limit the gold (depending on the difficulty)

    !!VRv24:*v616; [gold per emblem - Archer30: a bit too imbalance? should count only 1 emblem?]
    
    !!IF&5:Q1/(PIC_TYPE_RES_GOLD)/v24/(MSG_TYPE_MES)/z102035;
    !!OW:Ri^timerOwner^/(RES_GOLD)/dv24; [give the gold]
  !!en;
!!en;

!!IF&5/v617>0:M1/z102036;
!!VRy1:S30; [area]
!!VRy1&v625>0:S42; [double area for Surcoat - 30 times square root of 2]

!!if&v617>0;
  !!UN:Sv604/v605/v606/i^timerOwner^/y1; [reveal area]
  !!UN:R1; [redraw screen]
!!en;

!!if&v618>0;
  !!IF&5:M1/z102037;
  !!HEv600:C1/2/3/d; [turn archers to marksmen]
  !!HEv600:C1/18/19/d; [turn wood elves to grand elves]
  !!HEv600:C1/28/29/d; [turn gremlins to master gremlins]
  !!HEv600:C1/44/45/d; [turn gogs to magogs]
  !!HEv600:C1/74/75/d; [turn beholders to evil eyes]
  !!HEv600:C1/88/89/d; [turn orcs to orc chieftans]
  !!HEv600:C1/100/101/d; [turn lizard men to lizard warriors]
  !!HEv600:C1/112/127/d; [turn air elementals to storm elementals]
!!en;

!!FU612&v619>0:P;     [Call function if Pendant of Holiness equipped]

!!if&5/v620>0;
  !!IF:Q2/z102040;
  !!IF&-2:M1/z102041;
  !!IF&2:M1/z102042;
!!en;

!!VRy1:S1700; [about a day's movement]
!!VRy1:*v631; [Surcoat factor]

!!if&5;
  !!if&v620>0/2;
    !!HEv600:Wdy1; [add movement]
    !!HEv600:A3/59/1/1; [remove Boots Of Polarity]
  !!en;

  !!if&v621>0;
    !!VRv26:S14 R3; [pick a random magic skill]
    !!UN:N4/z329/v26; [get name of skill]
    !!IF:Q2/z102043;

    !!if&-2;
      !!IF:M1/z102044;
    !!el;
      !!HEv600:Sv26/?v27; [get level in that skill]

      !!if&v27=3;
        !!IF:M1/z102045;
      !!el&v27<3;
        !!VRv24:Sv26 *3 +3 +v27; [get picture number of skill at level given]
        !!IF:Q1/20/v24/1/z102046;
        !!HEv600:A3/57/1/1; [remove Garniture Of Interference]
        !!HEv600:Sv26/d1; [give skill]
      !!en;
    !!en;
  !!en;

  !!IF&v622>0:Q1/34/v631/1/z102047;
!!en;

!!HEv600&v622>0:Fd/d/d/dv631; [+1 knowledge]

!!if&v623>0;
  !!IF&5:M1/z102048;

  !!HEv600:C1/139/143/d; [turn peasants to rogues]
  !!HEv600:C1/116/117/d; [turn gold golems to diamond golems]
  !!HEv600:C1/33/116/d; [turn iron golems to gold golems]
  !!HEv600:C1/32/33/d; [turn stone golems to iron golems]

  !!if&v625>0;
    !!HEv600:C1/116/117/d; [turn gold golems to diamond golems, again for Surcoat]
    !!HEv600:C1/33/116/d; [turn iron golems to gold golems, again for Surcoat]
  !!en;
!!en;

; Diplomat's Ring (works only for human players)
!!if&v624>0/5;
  ; Check if the hero has any level 1 monster
  !!VR(hasLvOneMon:y):S(FALSE);

  !!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
    !!HEv600:C0/i/?(mon:y)/?(qty:y);

    !!if&(mon)>(NO_MON)/(qty)>0;
      !!MA:L(mon)/?(lv:y);

      !!br&(lv)=(MON_MIN_LEVEL);
    !!en;

    !!VR(hasLvOneMon)&i<=(ARMY_SLOT_LAST):S(TRUE);
  !!en;

  ; Ask if the hero would like to double level 1 monsters if he/she has any
  !!if&(hasLvOneMon);
    !!IF:Q2/z102049;

    ; Double level 1 if agreed
    !!if&2;
      !!IF:M1/z102050;

      ; Loop through all the stacks and double level 1
      !!re (slot:y)/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
        !!HEv600:C0/(slot)/?(mon:y)/?(qty:y);

        !!if&(mon)>(NO_MON)/(qty)>0;
          !!MA:L(mon)/?(lv:y);

          !!if&(mon)>(NO_MON)/(qty)>0;
            !!VR(additionalUnits:y):S(qty) F0/i^wog_102_diplomatsRingLimit^;
            !!VR(additionalUnits)&v625>0:*2; // double bonus for Surcoat
            !!HEv600:C0/(slot)/?(mon)/d(additionalUnits); // add bonus creatures to the original creatures
          !!en;
        !!en;
      !!en;

      !!HEv600:A3/(ART_DIPLOMATS_RING)/1/1; [remove a Diplomat's Ring]

      !!SN:D; [redraw screen]
    ; Show msg if the hero disagreed
    !!el;
      !!IF:M1/z102051;
    !!en;
  !!en;
!!en;

; Orb Of Inhibition (works only for human players)
!!if&v628>0/5;
  ; Find a monater from the hero's army which did not learn either Attack Twice or No Retailiation
  !!FU(WOG_102_GetRandMonFromHeroArmy):P?(mon:y);

  ; Get the ability to learn if target is found
  !!if&(mon)>(NO_MON);                  [if the hero does have a creature]
    !!SN:H^monname^/(mon)/1/?z330;      [get plural name of creature into z330]
    !!MA:X(mon)/?(monFlags:y);
    !!VR(hasAtkTwice:y):S(monFlags) &(MON_FLAG_ATTACKS_TWICE);
    !!VR(hasNoRetail:y):S(monFlags) &(MON_FLAG_NO_RETALIATION);

    !!if&(hasAtkTwice)/(hasNoRetail)=(FALSE);
      !!VR(ability:y):S1;
    !!el&(hasAtkTwice)=(FALSE)/(hasNoRetail);
      !!VR(ability):S0;
    !!el&(hasAtkTwice)=(FALSE)/(hasNoRetail)=(FALSE);
      !!VR(ability):R0/0/1;             [pick no-retaliation or attack-twice to offer]
    !!en;

    !!if&(ability)=0;
      !!VRz331:Sz102052;
    !!el;
      !!VRz331:Sz102053;
    !!en;

    ; Ask if the player wants to procced with the trade
    !!IF:Q2/z102054;

    ; Remove the orb and add the ability to the found monster
    !!if&2;
      !!IF:M1/z102055;
      !!HEv600:A3/(ART_ORB_OF_INHIBITION)/1/1; [remove an Orb Of Inhibition]

      !!if&(ability)=0;
        !!MA:X(mon)/d|(MON_FLAG_ATTACKS_TWICE); [attack twice]
      !!el;
        !!MA:X(mon)/d|(MON_FLAG_NO_RETALIATION); [no retaliation]
      !!en;
    ; Refuse the offer
    !!el;
      !!IF:M1/z102056;
    !!en;
  !!en;
!!en;

; Targ Of The Rampaging Ogre
!!if&v613>0;
  !!IF&5:M1/z102057;
  !!VR(qty:y):S30;
  !!VR(qty):*v631; [Surcoat factor]
  !!HEv600:Tv604/v605/v606/90/(qty); [ogre fight]
!!en;

** end of function


** function to add one slot's number of troops
** slot number is in x16; result accumulates in k

!?FU606;

!!HEv600:C0/x16/d/?v26; [get # of troops in slot x16]
!!VRv24:+v26; [add them up]

** end of function


** function to get a creature form the hero's army which didn't learn either Attack Twice or No Retaliation

!?FU(WOG_102_GetRandMonFromHeroArmy);
!#VA(randMon:x);

!!VR(randMon):S(NO_MON);
!!FU(NewIntArray):P?(monArmy:y);

!!re i/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
  !!HEv600:C0/i/?(mon:y)/?(qty:y);

  !!if&(mon)>(NO_MON)/(qty)>0;
    !!MA:X(mon)/?(monFlags:y);

    !!VR(hasAtkTwice:y):S(monFlags) &(MON_FLAG_ATTACKS_TWICE);
    !!VR(hasNoRetail:y):S(monFlags) &(MON_FLAG_NO_RETALIATION);

    !!FU(Array_Push)|(hasAtkTwice)=(FALSE)/(hasNoRetail)=(FALSE):P(monArmy)/(mon);
  !!en;
!!en;

!!SN:M(monArmy)/?(size:y);

!!if&(size)>0;
  !!VR(maxIndex:y):S(size) -1;
  !!VR(random:y):R0/0/(maxIndex);

  !!SN:M(monArmy)/(random)/?(randMon);
!!en;

** end of function


** trigger for clicking on Village Hall

!?FU(OnTownMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED); // trigger for click inside a town (TH0 will not work, because Wog crashes, if you build something inside a TH0-trigger)
!!UN:P102/?y-1; [Check if Artifact Boost script is enabled in WoGify Options]
!!FU&y-1<>1:E;     [Exit if it isn't]

!!CM:I?y-1; // get the clicked field
!!FU|y-1<10/y-1>13:E; // exit, if not clicked at Village-, Town- or City-Hall or Capitol

!!CA-1:H0/?y1 H1/?y2 U?y3; // get garrison and visiting hero / town number

!!VRv628:S0; // init
!!HEy1&y1>-1:A2/66/0/?v628; // check for Statesman's Medal - garrision hero
!!VRy1&v628=0:Sy2; // use visiting instead, if garrison has no Medal (or there is no)
!!HEy1&y1>-1:A2/66/0/?v628; // check for Statesman's Medal - garrision hero
!!FU&v628<1:E; // exit, if nobody has the medal

!!IF:V2/0; [AI won't give medal]
!!IF&1000:Q2/8/66/2/z102058;
!!FU605&2:Py1; [handle giving the medal]
!!SN:D; // redraw screen

** end of trigger

** function to handle statesman's medal when trade chosen

!?FU605;
!!CA(CURRENT_TOWN):T?y7 U?y31; [get town type]

; Build 1st upgraded dwelling if it is allowed
!!VRy1:S(FALSE);
!!CA(CURRENT_TOWN):B3/37; [check for upgraded dwelling 1]
!!FU(WOG_Town_IsPossibleToBuildBuilding):Py31/30/?y32;
!!FU(WOG_Town_IsPossibleToBuildBuilding):Py31/37/?y33;

; If level 1 dwelling has not been built, check if the town is eligible to build one
!!if&-1/y32/y32;
  !!VRy1:S(TRUE); [y1=1 means we would build upgraded dwelling 1]
  !!CA(CURRENT_TOWN):B3/30; [check for basic dwelling 1 - to see if we should add creatures]
  !!CA(CURRENT_TOWN)&-1:B6/30; [build basic dwelling if needed]

  !!FU(WOG_Town_ShowBuildingConstructionAnimation):Py31/37;

  !!VRy8:Sy7;
  !!VRy8:*14; [get number of 1st level creature]
  !!VRy8:+1;  [upgraded]
  !!VRy8&y8=113:S119; [sprites]
  !!MA:Gy8/?y9; [get growth rate of that creature]
  !!CA(CURRENT_TOWN)&-1:M1/0/d/y9; [add creatures (0 = 1st level) if dwelling 1 wasn't there before]
  !!UN:N2/326/y7/37; [name of upgrade building]
!!en;

; Build special buildings if requirements are met
; look-up table of special building numbers by town
!!VRy4:S21;
!!VRy4|y7=(TOWN_RAMPART)/y7=(TOWN_NECROPOLIS):S17;
!!VRy4&y7=(TOWN_DUNGEON):S22;

!!VRy2:S(FALSE);
!!CA(CURRENT_TOWN):B3/y4; [check for special building]
!!FU(WOG_Town_IsPossibleToBuildBuilding):Py31/y4/?y32;

!!if&-1/y32;
  !!VRy2:S(TRUE); [y2=1 means we built special building]
  !!FU(WOG_Town_ShowBuildingConstructionAnimation):Py31/y4;
!!en;

!!UN:N2/327/y7/y4; [name of special building]

; Build Resources Silo if market is built
!!VRy3:S(FALSE); [assume nothing built]

!!CA(CURRENT_TOWN):B3/15; [check for resource silo]
!!FU(WOG_Town_IsPossibleToBuildBuilding):Py31/15/?y32;

!!if&-1/y32;
  !!VRy3:S(TRUE);
  !!FU(WOG_Town_ShowBuildingConstructionAnimation):Py31/15;
!!en;

!!UN:N2/328/y7/15; [name of resource silo]

; Show info and remove medal
!!IF&y1=1/y2=1/y3>0:M1/z102060;
!!IF&y1=1/y2=1/y3=0:M1/z102061;
!!IF&y1=1/y2=0/y3>0:M1/z102062;
!!IF&y1=0/y2=1/y3>0:M1/z102063;
!!IF&y1=1/y2=0/y3=0:M1/z102064;
!!IF&y1=0/y2=1/y3=0:M1/z102065;
!!IF&y1=0/y2=0/y3>0:M1/z102066;
!!VRy4:Sy1 +y2 +y3; // if y4 is now 0, nothing is built

!!if&y4=0;
  !!IF:M1/z102067; // nothing built
!!el;
  !!HEx1:A3/66/1/1; // remove Statesman's Medal, if something is built
  !!CM:R0;
!!en;

** end of function

!?FU612;     [Function for Pendant of Holiness]

!!HEv600:A2/0/?v24/?v25; [check for spell book]

!!if&v25=0;
  !!IF&5:Q1/8/102/1/z102038;     [Message if no spell book]
  !!FU:E;     [Exit if no spell book]
!!en;

!!IF:V1/0;     [Init flag for counting spells]
!!VRv1:S0;     [Init v var that carries spell number]
!!VRv920:C25/37/38/41/48; [table of spells]

!!DO613/920/924/1:P1/0/0;     [Looking in heroes spell book, counting spells not known to v1]

!!VRy2:Sv1 -1;     [Random number between 1 and v1]
!!VRy1:S1 Ry2;

!!VRv1&v1=0:S999;     [Abort if all spells known]

!!HEv600:B0/?z-1;     [Get hero name and sex]
!!HEv600:R2/?y-1;
!!VRz-2&y-1=0:Sz102021;
!!VRz-2&y-1=1:Sz102022;

!!IF&5/v1=999:Q1/8/102/1/z102071;

!!FU&v1=999:E;     [Exit if all spells known]

!!DO613/920/924/1:P1/y1/1;     [Find a random not-known spell]

!!HEv600:S7/?y-1;     [Get wisdom level]

!!IF&5/y-1=0/vv1=25:Q1/9/25/1/z102072;     [Display if spell is DU and no wisdom]
!!FU&y-1=0/vv1=25:E;

!!IF&5/y-1<2/vv1=38:Q1/9/38/1/z102073;     [Display if spell is Res. and no adv. wisdom]
!!FU&y-1<2/vv1=38:E;

!!IF&5/y-1<2/vv1=48:Q1/9/48/1/z102074;     [Display if spell is Pr. and no adv. wisdom]
!!FU&y-1<2/vv1=48:E;

!!IF&5:Q1/9/vv1/1^%Z102039^;     [Display the random not-known spell]
!!HEv600:Mvv1/1;     [Learn the random not-known spell]

!?FU613;     [Function to look up heroes spell book and find a random not-known spell]
!!HEv600:M=vx16/1;
!!VRvx1&x3=0/-1:+1;
!!VRvx1&x2=1/-1/x3=1:Sx16;
!!VRx16&x2=1/-1/x3=1:S999;
!!VRx2&x2>1/-1:-1;
 