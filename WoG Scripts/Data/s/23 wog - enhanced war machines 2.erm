ZVSE2

** ENHANCED WAR MACHINES  -  ERM script 55

** By Arstahd
** Version: 1.11   updated October 25, 2004
** Special thanks to Fnord for help with bug fixing and script maintenance
** Version: 2.0   updated December , 2021 by @daemon_n

** rebalances and improves war machines

** ballista base damage doubled
** first aid tent now resurrects [(1-25) x (first aid skill +1) + defense skill] worth of troops after normal healing
** no more than 1 extra Ballista/First Aid tent can appear on battlefield (from commanders, etc...)
** ammo cart gives +10 movement points per step
** all war machines cost 1000 gold
** first aid tent has 150 health
** ammo cart has 150 health
** heroes with Ballistics skill can use them during normal combat, siege combat use is unchanged
**   number of catapults equals Ballistics skill level
**   catapults can now target creatures
**   catapults deal 5 - (10 + Att + Def) damage
**   catapults can inflict a "critical hit" which slays one unit (chance = hero level x catapults - capped at 75%)
**   catapults have 250 health
**   catapults have no range penalty
**   catapults ignore 50% of enemy defense



** Variables used:
** V variables:    v7180 i^WOG_55_FirstAid_Hero_%(side)^-i^WOG_55_BattleIsSiege^
** Z variables:    z790-z791
** Y Variables:    y1-y10
** Functions used: 7120-7126
** Flags Used:     79  74(from S-50)


!#UN:P(WOG_OPT_ENHANCED_WAR_MACHINES_II)/?v7180;                                 [check if Enhanced Warmachines is enabled in WoGify Options]

*#VRv7180:S1;             [enable for stand-alone use]
!?FU(WOG_55_Initialization);

  !!MA:M(MON_BALLISTA)/4 E(MON_BALLISTA)/6 C(MON_BALLISTA)/(RES_GOLD)/1000;                                               [Ballista Dam Low 4/ Dam High 6/ Cost 1000]
  !!MA:P(MON_FIRST_AID_TENT)/150 C(MON_FIRST_AID_TENT)/(RES_GOLD)/1000;                                                   [First Aid Tent HP 150/ Cost 1000]
  !!MA:P(MON_AMMO_CART)/150 C(MON_AMMO_CART)/(RES_GOLD)/1000;                                                             [Ammo Cart HP 150/Cost 1000]

  !!SN:H^monname^/(MON_CATAPULT)/0/?(single:z) H^monname^/(MON_CATAPULT)/1/?(plural:z);
  !!SN:H^monname^/(MON_NOT_USED_2)/0/(single) H^monname^/(MON_NOT_USED_2)/1/(plural);
  !!MA:X(MON_CATAPULT)/?(balF:y);

  !!MA:X(MON_NOT_USED_2)/132201 M(MON_NOT_USED_2)/650 C(MON_NOT_USED_2)/(RES_GOLD)/1;                                                                    [set NU2 flags {catapult flags (132197) + shooter (4)}    NU2 Fight Value 650  ]
  !!MA:A(MON_NOT_USED_2)/10 D(MON_NOT_USED_2)/10 P(MON_NOT_USED_2)/250 N(MON_NOT_USED_2)/24;                              [NU2 Attack 10 NU2 Defense 10 NU2 HP 250  NU2 Shots 24]


  creature descriptions
  !!SN:H^monname^/(MON_CATAPULT)/2/z155002 H^monname^/(MON_FIRST_AID_TENT)/2/z155003 H^monname^/(MON_AMMO_CART)/2/z155004;


!#FU(WOG_55_Initialization)&v7180:P;


!?FU(OnSetupBattlefield)&v7180=1;

  !!VRv7230:C-1/-1;                                    [reset variables]

  !!BA:S?i^WOG_55_BattleIsSiege^;                                [check for siege combat]

  !!FU(WOG_55_BattleFieldSetup):P(BATTLE_LEFT)/i^WOG_55_BattleIsSiege^;
  !!FU(WOG_55_BattleFieldSetup)&i^battle_hero_1^>(NO_HERO):P(BATTLE_RIGHT)/i^WOG_55_BattleIsSiege^;

  !!UN:P900/?y-1;                         [check if creature experience is enabled]

  !!if&y-1=0;
    !!IF:V74/1;                           [set flag 74 to true if not enabled]
    !!UN:P900/1;                          [enable creature experience if not enabled]
  !!en;
!?FU(WOG_55_BattleFieldSetup);
!#VA(side:x) (siege:x);
  !!VR(balPos:y):S16 *(side) +51;
  !!VR(catPos:y):S(balPos) +68;
  !!VR(tentPos:y):S(balPos) +102;


  !!HEi^battle_hero_%(side)^:E?y9/?i^WOG_55_Lvl_Hero_%(side)^/1 S(SKILL_FIRST_AID)/?i^WOG_55_FirstAid_Hero_%(side)^ S10/?y10 A2/6/?y9/?y6 A2/4/?y9/?y7 F?y11/?i^WOG_55_Defense_Hero_%(side)^/?y13/?y14;   [get levels: hero, first aid, ballistics, check for Tent, Ballista, get stats]

  !!BU:E(tentPos)/?(tentStack:y);                       [check for tent stack - attacker]
  !#VA(heroWm:y);
  !!if&(tentStack)>(NO_STACK);
    !!HEi^battle_hero_%(side)^:A2/(ART_FIRST_AID_TENT)/d/?(heroWm);
    !!BM(tentStack):N?(tents:y);              [get quantity of tents - attacker]

    !!if&(tents)>1;
      !!BM(tentStack):N1 B1;   [set quantity of tents to 1 if more than 1 but hero has no tent - attacker]
      !!BM(tentStack)&(heroWm):Nd1 Bd1;   [set quantity of tents to 1 if more than 1 but hero has no tent - attacker]
    !!en;
  !!en;

  !!BU:E(balPos)/?(balStack:y);                       [check for tent stack - attacker]

  !!if&(balStack)>(NO_STACK);
    !!HEi^battle_hero_%(side)^:A2/(ART_BALLISTA)/d/?(heroWm);
    !!BM(balStack):N?(bals:y);              [get quantity of tents - attacker]
    
    !!if&(bals)>1;
      !!BM(balStack):N1 B1;   [set quantity of tents to 1 if more than 1 but hero has no tent - attacker]
      !!BM(balStack)&(heroWm):Nd1 Bd1;   [set quantity of tents to 1 if more than 1 but hero has no tent - attacker]
    !!en;
  !!en;

  !!HEi^battle_hero_%(side)^:S(SKILL_BALLISTICS)/?(skillBall:y);

  !!if&(skillBall)/(siege)=0;
    !!VR(checkStak:y):S(side) *(BATTLE_STACKS_PER_SIDE) -1;

    !!BU:S(MON_CATAPULT)/(skillBall)/(catPos)/(side)/-1/(FALSE);              [add catapults  - attacker]
    !!BU:E(catPos)/?(catStack:y);

    !!HEi^battle_hero_%(side)^:F?(att:y)/?(def:y)/d/d;
    !!VR(maxDamage:y):S(att) +(def) +10;
    !!BM(catStack):T(MON_NOT_USED_2) U1/5 U2/(maxDamage) H250 B(skillBall);
    !!VR(expStack:y):S(catStack) *-1 -1;
    132201
    *!BM(catStack):F132201;
    !!re i/0/19;
      !!EA(MON_CATAPULT):Bi/?y2/?y3/?y4/?y5/?y6/?y7/?y8/?y9/?y10/?y11/?y12/?y13/?y14/?y15;                                                                                       [NU2 (catapult) - get bonus line index]
      !!EA(MON_NOT_USED_2):Bi/y2/y3/y4/y5/y6/y7/y8/y9/y10/y11/y12/y13/y14/y15;                                                                                       [NU2 (catapult) - get bonus line index]
    !!en;

    !!UN:P(WOG_OPT_ENHANCED_MONSTERS)/?(isActive:y);                                                                        [Check if Enhanced Monsters is enabled in WoGify Options]

    !!if&(isActive);
      !!EA(MON_NOT_USED_2):F105/?y-1;                                                                                       [NU2 (catapult) - get bonus line index]
      !!EA(MON_NOT_USED_2):By-1/1/105/61/1/1/1/1/1/1/1/1/1/1/1;                                                             [set no range penalty]
      !!EA(MON_NOT_USED_2):F98/?y-1;                                                                                        [NU2 (catapult) - get bonus line index]
      !!EA(MON_NOT_USED_2):By-1/1/98/61/50/50/50/50/50/50/50/50/50/50/50;                                                   [set ignore defense to 50%]
    !!en; 

      *!EA(MON_NOT_USED_2):B10/1/102/115/1/1/1/1/1/1/1/1/1/1/1;
      *!EA(MON_NOT_USED_2):B11/1/98/61/50/50/50/50/50/50/50/50/50/50/50;
  !!en;


-------------------------- B E F O R E   S T A C K    T U R N --------------------------
!?FU(OnBattleStackObtainsTurn)&v7180=1;
!#VA(stackSide:x) (stackInd:x);

  !!BMi^battle_current_stack^:T?(monType:y);

  !!if&(monType)=(MON_FIRST_AID_TENT);

    !!if&i^battle_hero_%(stackSide)^>=0;
      !!HEi^battle_hero_%(stackSide)^:S(SKILL_FIRST_AID)/?(skillLvL:y) Fd/?(defense:y)/d/d; [store hero's first aid skill in v874]
    !!el;
      !!VR(skillLvL:y):S0;
    !!en;

    !!FU(WOG_55_BattleFunc_CheckForDamagedStacks):P(stackSide)/?(damagedStacks:y);/?(damagedMachines:y);
    !!VRi^WOG_55_Battle_HealPower^:S0;
    !!FU&(damagedStacks)=(FALSE):E;
    !!VR(skillLvL):+1;

    !!BMi^battle_current_stack^:N?(tentsAmount:y); 

    !!re i/1/(tentsAmount);
      !!VR(healPower:y):S1 R24 *(skillLvL) +(defense);  [set healing: 1-25 x (first aid skill +1) +defense x # of tents]
      !!VRi^WOG_55_Battle_HealPower^:+(healPower);
    !!en;

    *!if&(damagedStacks)=(damagedMachines);
      *!FU(WOG_55_BattleFunc_SetMonsterAttributeFromMachine):P(stackSide)/(TRUE);
    *!en;

    !!FU(WOG_55_BattleFunc_RemoveOneHP):P(stackSide)/(TRUE);
  !!en;



!?FU(OnBattleActionEnd)&v7180=1;
!#VA(usedY[1]:y);
  !!BMi^battle_current_stack^:T?(monType:y);
  *!if&(monType)=(MON_NOT_USED_2);
    *!BMi^battle_current_stack^:T(MON_CATAPULT);
  *!en;

  !!BG:A?(actionType:y);                [D?(healDest:y);]
  *!IF:L^%(actionType)^;
  !!if|(actionType)=(BATTLE_ACTION_TENT_HEAL)/(actionType)=(BATTLE_ACTION_DEFEND)/(actionType)=(BATTLE_ACTION_SURRENDER)/(actionType)=(BATTLE_ACTION_RETREAT)/(actionType)=(BATTLE_ACTION_WAIT);

    !!if&(actionType)=(BATTLE_ACTION_TENT_HEAL)/i^WOG_55_Battle_HealPower^;
      !!BG:E?(healTarget:y);           
      !!BM(healTarget):N?(numBefore:y);
      !!FU(WOG_55_BattleFunc_CalculateStackHealing):P(healTarget)/i^WOG_55_Battle_HealPower^/?(hpLost:y)/?(num:y);

      !!BM(healTarget):L(hpLost) N(num);[set stack to new - before anim, lost hp is 0, action will be: 0 hp removed]
      !!VRy1:Si^WOG_55_Battle_HealPower^;
      !!if&(num)>(numBefore);
        !!VR(logMsg:z):Sz155006;
      !!el;
        !!VR(logMsg:z):Sz155005;
      !!en;
      !!MM:S(logMsg);
    !!en;

    !!FU(WOG_55_BattleFunc_RemoveOneHP):Pi^battle_acting_side^/(FALSE);
    !!VRi^WOG_55_Battle_HealPower^:S0;    
  !!en;

  !!if&(actionType)=(BATTLE_ACTION_WALK_AND_ATTACK)/(monType)=(MON_FIRST_AID_TENT);
  *!IF:M^%^;
    !!BG:A(BATTLE_ACTION_CANCEL);
  !!en;


!?FU(OnAfterBattleAction)&v7180=1;/i^battle_humanOnly^;
  !!BG:A?(actionType:y) N?y4;                [D?(healDest:y);]

  !!BMy4:T?(monType:y) I?y5;
  !!if&(monType)=(MON_NOT_USED_2);
  *!IF:M^%^;
    !!FU(WOG_55_SkipCatapultaTurn):Py5/?(isOccupied:y);
    !!if&(isOccupied);

      !!BG:A(BATTLE_ACTION_SKIP);
    !!en;
  !!en;


!?FU(OnBeforeBattleAction)&v7180=1;

  !!BG:A?(actionType:y) N?y4;                [D?(healDest:y);]

  !!BMy4:T?(monType:y);               [D?(healDest:y);]
  !!if&(actionType)=(BATTLE_ACTION_WALK_AND_ATTACK)/(monType)=(MON_FIRST_AID_TENT);
    !!BG:A(BATTLE_ACTION_CANCEL);
  !!en;
!?FU(WOG_55_SkipCatapultaTurn)&v7180=1;

!#VA(side:x) (occupied:x);
!!VR(occupied):S0;
!!VR(mult:y):S13 *(side); +8;


!!re i/103/137/17;
  !!re j/0/1;
    !!VR(hex:y):Si +j +(mult);
    !!BU:E(hex)/?(stack:y);
    !!if&(stack)>(NO_STACK);
      !!BM(stack):I?(stackSide:y);
      
      !!if&(stackSide)<>(side);
        *!IF:L^%(hex) %(stack)^;
        !!VR(occupied):S(TRUE);
        !!FU:E;
      !!en;

    !!en;
  !!en;
!!en;

!?FU(WOG_55_BattleFunc_CheckForDamagedStacks);
!#VA(side:x) (damagedStack:x) (isMachine:x);

  !!VR(damagedStack):S0;                             [assume we have no damage stacks]
  !!VR(isMachine):S0;                             [assume we have no damage stacks]
  !!VR(firstStack:y):S(side) *(BATTLE_STACKS_PER_SIDE);
  !!VR(lastStack:y):S(firstStack) +(BATTLE_STACKS_PER_SIDE) -1;


  !!re i/(firstStack)/(lastStack);

    !!BMi:L?(hpLost:y) B?(startNum:y) N?(num:y); F?f; H130; //y1 hp y2 lost y3 original number y4 actual number y5 monster side
    !!if&(num);
      !!if&(startNum)>(num)|(hpLost);
        !!VR(damagedStack):+1;
        !!br;
        *!VRf:&(MON_FLAG_SIEGE_WEAPON);
        *!VR(isMachine)&f:+1;
      !!en;
    !!en;
  !!en;

!?FU(WOG_55_BattleFunc_RemoveOneHP);
!#VA(side:x) (remove:x);

  !!VR(firstStack:y):S(side) *(BATTLE_STACKS_PER_SIDE);
  !!VR(lastStack:y):S(firstStack) +(BATTLE_STACKS_PER_SIDE) -1;

  !!if&(remove);
    !!re i/(firstStack)/(lastStack);
      !!BMi:B?s N?n L?l P?p; F?f;
      !!BMi&s>n/l=0:Ld1;?l B?s N?n F?f;
    !!en;

  !!el;
    !!re i/(firstStack)/(lastStack);
      !!BMi:B?s N?n L?l P?p; F?f;
      !!BMi&s>n:Ld-1;?l B?s N?n F?f;
    !!en;
  !!en;

!?FU(WOG_55_BattleFunc_CalculateStackHealing);
!#VA(stack:x) (healPower:x) (hpLost:x) (num:x);
  !!BM(stack):H?(maxHp:y) L?(hpLost) B?(startNum:y) N?(num);

  !!if&(startNum)>(num);

    !!if&(healPower)>(hpLost);

      !!VR(qtyToResurrect:y):S(healPower) +(maxHp) -(hpLost) :(maxHp); -1;
      !!VR(hpLost):S(healPower) +(maxHp) %(maxHp);
      !!if&(qtyToResurrect);
        !!VR(num):+(qtyToResurrect);
        !!VR(num)&(num)>(startNum):S(startNum);
      !!en;
    !!el;
      !!VR(hpLost):-(healPower);
    !!en;

  !!el;
  
    !!VR(hpLost):-(healPower);
    !!VR(hpLost)&(hpLost)<0:S0;
  !!en;

!?BG1&v7180=1;                                [continue if enabled]

  !!BU:C?(isEnd:y);                                         [check for end of battle]
  !!UN&(isEnd)/74:P900/0;                               [re-disable stack experience if needed]



!?FU(OnKeyPressed_Battle)&x1=(KEY_TILDE);
*!re i/(BIT_0)/(BIT_16);
  *!IF:L^%i^;
  !!MA:X(MON_BALLISTA)/?(balF:y);
  !!MA:X(MON_NOT_USED_2)/?(balwF:y);
  !!IF|(balF)/(balwF):L^ || BALLISTA %(balF)/ NOT_USED_2 %(balwF)^;

** start of pre-damage trigger
!?FU(OnStackToStackDamage)&v7180=1/i^WOG_55_BattleIsSiege^=0/x7/x9=0; [x7 = shot, x9 = 0.. only for real damage]
!#VA(attacker:x) (defender:x);

  !!BM(attacker):T?(monType:y);

  !!if&(monType)=(MON_NOT_USED_2);
    !!BM(attacker):N?(catNum:y);
    !!BM(defender):N?(defNum:y);

    !!VR(rnd:y):S0 R99;                 [random roll - 1% chance per level]
    !!VR(oldFlag:y):S1;

    !!HEi^battle_hero_%i(battle_acting_side)^:Ed/?(heroLvl:y)/1;
    !!VR(chance:y):S(heroLvl) *(catNum) :2; [multiply level by catapults and reduced by 2]
    !!VR(chance)&(chance)>74:S74;       [cap at 75%]
    !!if&(chance)>(rnd);
      
      !!BM(defender):V82 Nd-1;          [show animation, set target quantity and reduce target quantity by 1]
      !!VR(logMsg:z):Sz155007;
      !!MM:S(logMsg);
    !!en;
  !!en;


** end of pre-damage trigger **


** start of movement trigger
!?HM-1&v7180=1;                               [continue if  enabled]
  !!HE-1:P?y-2/?y-3/?y-4;                        [get hero's position]
  !!TRy-2/y-3/y-4:T?(terrain:y)/d/d/d/d/d/d/d;   [get terrain type]
  !!if&(terrain)<>8;
    !!HE-1:A2/(ART_AMMO_CART)/d/?(isEquipped:y); [if on land check if hero has ammo cart]
    !!HE-1&(isEquipped):Wd10/1;                  [if on land add 10 movement if hero has ammo cart/add movement point without redraw screen]
  !!en;
  ** end of movement trigger
