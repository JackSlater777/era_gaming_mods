ZVSE2
** Author orig.  : Algor
** Name          : Mana arrow
** Name rus.     : Волшебная стрела маны
** Options       : 790
** Dialogs       : -
** Variables     : v943
** Tmp variables : z1
** Timers        : -
** Functions     : -
** PO-values     : -


!?FU(OnBeforeBattleAction);   [при действии в бою]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!BG&y1=1:S?y2;               [y2 - номер заклинания (если герой колдует)]
!!FU|y2<>15/y1<>1:E;          [Выход если герой не колдует волшебную стрелу]

!!BG:H?y1;                    [y1 - номер героя]
!!HEy1:I?y2 Fd/d/?y3/d;       [y2 - количество маны, y3 - сила магии героя]
!!HEy1:S14/?y4 S15/?y5 S16/?y6 S17/?y7; [y4-y7 - Уровень навыка Огня/Воздуха/Воды/Земли героя (0..3)]
!!VRy4&y4<y5:Sy5;             [---------------------------------------------]
!!VRy4&y4<y6:Sy6;             [     y4 - макс. уровень Школ магии (0..3)    ]
!!VRy4&y4<y7:Sy7;             [---------------------------------------------]
!!VRy7:Sy3 +y4 *10;           [y7 - расчетный урон обычной Волшебной стрелы SP*10 (+10) (+20) (+30)]
!!VRy4:*y2;                   [y4 - бонус урона от Школ магии (уровень*Мана)]
!!VRy6:Sy3 *y2 :2 +y4;        [y6 - расчетный урон Волшебной стрелы маны SP*Mana:2 (+Mana) (+Mana*2) (+Mana*3)]
!!FU&y6<=y7:E;                [Выход, если урон Волшебной стрелы маны меньше обычного]

!!HEy1:X?y10/?y11/d/d/d/d/d;  [y10=3,y11=15 для героя-спеца по Волшебной стреле]
!!HEy1&y10=3/y11=15:Ed/?y5;   [y5 - Уровень героя-спеца]
!!VRy7&y10=3/y11=15:Sy6 *y5 :20; [Расчет бонуса урона для героя-спеца (+5% за уровень)]
!!VRy6&y10=3/y11=15:+y7;      [y6 - скорректированный урон для героя-спеца]
!!HEy1:O?y8;                  [y8 - номер игрока]
!!OW:Iy8/?y9;                 [y9=1 для ИИ, 0 для человека]
!!VRv943:S0;                  [обнуление v943]
!!BA:Q?f;
!!FU&y9=0/f=1:E;    [герой-человек не использует усиление в быстрой битве]

!!VRz1&y9=0:Sz179011;         [для игрока-человека получаем текст вопроса в z1]
!!IF&y9=0:Q1/9/15/2/z1;       [для игрока-человека вопрос: вложить ли всю ману в заклинание]
!!VRv943|1/y9=1:Sy6;          [v943 - новый урон Волшебной стрелы]

!?FU(OnMagicBasicResistance); [перед расчетом сопротивления]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!BG&y1=1:S?y2;               [y2 - номер заклинания (если герой колдует)]
!!FU|y2<>15/v943=0:E;         [Выход если герой не колдует волшебную стрелу или если новый урон Волшебной стрелы = 0]

!!MR:D?y1;                    [y1 - базовый урон заклинания]
!!BG:H?y2 E?y3;               [y2 - номер героя, y3 - номер целевого стека]
!!MR&y1<v943:Dv943;           [Установка нового базового урона заклинания, если он не меньше стандартного]
!!HEy2&y1<v943:I0;            [Обнуление маны героя]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!BMy3&y1<v943/f=0:V64 V64;   [Дополнительная анимация на целевом стеке]
!!VRv943:S0;                  [обнуление v943]

** end
