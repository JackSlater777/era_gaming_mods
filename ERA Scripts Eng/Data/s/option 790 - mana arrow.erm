ZVSE2
** Author orig.  : Algor
** Name          : Mana arrow
** Name rus.     : Волшебная стрела маны
** Options       : 790


!?FU(OnBeforeBattleAction)&-998;   [при действии в бою]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?(action:y);                    [y1 - тип действия (1 - герой колдует заклинание)]

!!if&(action)=(BATTLE_ACTION_HERO_CAST);
  !!BG:S?(spell:y) Q?(side:y) H?(heroId:y);               [y2 - номер заклинания (если герой колдует)]
  !!HE(heroId):O?(owner:y);
  !!if&(spell)=(SPELL_MAGIC_ARROW);
    *!BG:H?(heroId:y);                    [y1 - номер героя]
    !!HE(heroId):I?(heroMana:y)/1 Fd/d/?(sp:y)/d;
    !!VR(skillLvl:y):S0;
    !!re i/(SKILL_FIRE_MAGIC)/(SKILL_EARTH_MAGIC);
      !!HE(heroId):Si/?l;
      !!VR(skillLvl):Fl/3;
    !!en;
    !!SS(SPELL_MAGIC_ARROW):C(skillLvl)/?(cost:y);
    !!if&(heroMana)>(cost);
      !!VR(bonus:y):S1 F1/(skillLvl);
      !!VR(oldDamage:y):S(sp) +(bonus) *10;
      !!VR(skillLvl):*(heroMana);

      !!VR(newDamage:y):S(sp) *(heroMana) *2 +(skillLvl);
          *!IF:L^%(newDamage) %(oldDamage)^;

      !!if&(newDamage)>(oldDamage);
        !!HE(heroId):X?(spec:y)/?(spell)/d/d/d/d/d;  [y10=3,y11=15 для героя-спеца по Волшебной стреле]
        !!if&(spec)=3/(spell)=(SPELL_MAGIC_ARROW);
          !!HE(heroId):Ed/?(heroLvL:y);
          !!VR(bonus):S(newDamage) *(heroLvL):20;
          !!VR(newDamage):+(bonus);
        !!en;

        !!OW:I(owner:y)/?(isAi:y) Cd/?(clicked:y);                 [y9=1 для ИИ, 0 для человека]
        !!IF:V1/(FALSE);

        !!VRi^es_790_manaArrowDmg^:S0; [обнуление i^es_790_manaArrowDmg^]
        !!if&i^battle_isVisible^/(isAi)=(FALSE)/(clicked)=(owner);
          !!SN:T^es.790.dlg^/?z1/^dmg^/(newDamage);  [для игрока-человека получаем текст вопроса в z1]
          !!IF:Q1/(PIC_TYPE_SPELL)/(SPELL_MAGIC_ARROW)/2/z1;       [для игрока-человека вопрос: вложить ли всю ману в заклинание]
        !!en;
        !!VRi^es_790_manaArrowDmg^:Sv7900;

        !!VRi^es_790_manaArrowDmg^&1|(isAi):S(newDamage);          [i^es_790_manaArrowDmg^ - новый урон Волшебной стрелы]

        
      !!en;
      
    !!en;
  !!en;

!!en;



!?FU(OnMagicBasicResistance); [перед расчетом сопротивления]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!BG&y1=1:S?y2;               [y2 - номер заклинания (если герой колдует)]
!!FU|y2<>15/i^es_790_manaArrowDmg^=0:E; [Выход если герой не колдует волшебную стрелу или если новый урон Волшебной стрелы = 0]

!!MR:D?y1;                    [y1 - базовый урон заклинания]
!!BG:H?y2 E?y3;               [y2 - номер героя, y3 - номер целевого стека]
!!MR&y1<i^es_790_manaArrowDmg^:Di^es_790_manaArrowDmg^;           [Установка нового базового урона заклинания, если он не меньше стандартного]
!!HEy2&y1<i^es_790_manaArrowDmg^:I0;            [Обнуление маны героя]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!BMy3&y1<i^es_790_manaArrowDmg^/f=0:V64 V64;   [Дополнительная анимация на целевом стеке]
!!VRi^es_790_manaArrowDmg^:S0;                  [обнуление i^es_790_manaArrowDmg^]

** end
