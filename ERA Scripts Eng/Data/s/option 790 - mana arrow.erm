ZVSE2
** Author orig.  : Algor
** Name          : Mana arrow
** Name rus.     : Волшебная стрела маны
** Options       : 790


!?FU(OnBeforeBattleAction);   [при действии в бою]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!BG&y1=1:S?y2;               [y2 - номер заклинания (если герой колдует)]
!!FU|y2<>15/y1<>1:E;          [Выход если герой не колдует волшебную стрелу]

!!BG:H?y1;                    [y1 - номер героя]
!!HEy1:I?y2 Fd/d/?y3/d;       [y2 - количество маны, y3 - сила магии героя]
!!HEy1:S14/?y4 S15/?y5 S16/?y6 S17/?y7; [y4-y7 - Уровень навыка Огня/Воздуха/Воды/Земли героя (0..3)]
!!VRy4&y4<y5:Sy5;             [---------------------------------------------]
!!VRy4&y4<y6:Sy6;             [     y4 - макс. уровень Школ магии (0..3)    ]
!!VRy4&y4<y7:Sy7;             [---------------------------------------------]
!!VRy7:Sy3 +y4 *10;           [y7 - расчетный урон обычной Волшебной стрелы SP*10 (+10) (+20) (+30)]
!!VRy4:*y2;                   [y4 - бонус урона от Школ магии (уровень*Мана)]
!!VRy6:Sy3 *y2 :2 +y4;        [y6 - расчетный урон Волшебной стрелы маны SP*Mana:2 (+Mana) (+Mana*2) (+Mana*3)]
!!FU&y6<=y7:E;                [Выход, если урон Волшебной стрелы маны меньше обычного]

!!HEy1:X?y10/?y11/d/d/d/d/d;  [y10=3,y11=15 для героя-спеца по Волшебной стреле]
!!HEy1&y10=3/y11=15:Ed/?y5;   [y5 - Уровень героя-спеца]
!!VRy7&y10=3/y11=15:Sy6 *y5 :20; [Расчет бонуса урона для героя-спеца (+5% за уровень)]
!!VRy6&y10=3/y11=15:+y7;      [y6 - скорректированный урон для героя-спеца]
!!HEy1:O?y8;                  [y8 - номер игрока]
!!OW:Iy8/?y9;                 [y9=1 для ИИ, 0 для человека]
!!VRi^es_790_manaArrowDmg^:S0; [обнуление i^es_790_manaArrowDmg^]
!!BA:Q?f;

!!if&y9=0;
  !!FU&f=1:E;    [герой-человек не использует усиление в быстрой битве]

  !!SN:T^es.790.dlg^/?z1/^dmg^/y6;  [для игрока-человека получаем текст вопроса в z1]
  !!IF:Q1/9/15/2/z1;       [для игрока-человека вопрос: вложить ли всю ману в заклинание]
!!en;

!!VRi^es_790_manaArrowDmg^|1/y9=1:Sy6;          [i^es_790_manaArrowDmg^ - новый урон Волшебной стрелы]

!?FU(OnMagicBasicResistance); [перед расчетом сопротивления]
!!UN:P790/?y1;                [проверяем включена ли опция 790 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!BG&y1=1:S?y2;               [y2 - номер заклинания (если герой колдует)]
!!FU|y2<>15/i^es_790_manaArrowDmg^=0:E; [Выход если герой не колдует волшебную стрелу или если новый урон Волшебной стрелы = 0]

!!MR:D?y1;                    [y1 - базовый урон заклинания]
!!BG:H?y2 E?y3;               [y2 - номер героя, y3 - номер целевого стека]
!!MR&y1<i^es_790_manaArrowDmg^:Di^es_790_manaArrowDmg^;           [Установка нового базового урона заклинания, если он не меньше стандартного]
!!HEy2&y1<i^es_790_manaArrowDmg^:I0;            [Обнуление маны героя]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!BMy3&y1<i^es_790_manaArrowDmg^/f=0:V64 V64;   [Дополнительная анимация на целевом стеке]
!!VRi^es_790_manaArrowDmg^:S0;                  [обнуление i^es_790_manaArrowDmg^]

** end
