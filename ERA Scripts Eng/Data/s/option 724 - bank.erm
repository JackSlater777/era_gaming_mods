ZVSE

** Author orig.  : Algor
** Name          : Bank
** Name rus.     : Банк
** Options       : 724

** Замена/расширение wog-опции "Банк"
**
** Займы
** Ресурсные займы теперь полностью независимы друг от друга.
** Добавлены целевые займы на отстройку фортификационных и финансовых структур, а также жилищ 7го уровня
** с автоматической отстройкой при получении займа.
** Добавлен займ для экстренной мобилизации с удвоенным максимальным объемом и с удвоенной ставкой.
**
** Условия предоставления
** По каждому займу устанавливаются индивидуальные параметры: ставка, макс. объем, ежедневный платеж.
** В пределах макс. объема игрок может брать несколько займов, но ежедневный платеж в этом случае может быть выше.
** На параметры займов влияет выбранный игроком уровень сложности, внутриигровое время и имеющиеся у игрока источники дохода.
** При невозможности уплатить ежедневный взнос по займу, Банк удерживает штраф за каждую неуплаченную единицу ресурса.
** При невозможности удержать штраф, Банк повышает ставку для всех новых займов в том виде ресурса, по которому произошла неуплата.
** Любой займ можно погасить в полном размере досрочно, частичные погашения не допускаются.
** Если объем досрочного погашения не меньше половины макс.доступного объема, Банк снижает ставку по данному виду займа.
**
** Интерфейс
** Информация по всем займам и текущим задолженностям отображается с одном "главном" окне Банка.
** Добавлены оповещения о повышении/снижении ставок по займам.
** Ежедневные оповещения о списании задолженнотей и закрытии займов отсутствуют.
**
** Скрипт отключает функционал wog-опций "Банк"

!#UN:P724/?v1;
!#UN&v1>0:P105/0;                       // Отключение wog-опции "Банк"

** Ежедневные списания задолженностей и пени
!?FU(OnEveryDay);                       // каждый день для каждого игрока
!!UN:P724/?y1; !!FU&y1=0:E;             // Выход, если опция "Банк" отключена
!!OW:C?y1; !!OW:Iy1/?y2; !!FU&y2=1:E;   // y1 - текущий игрок, выход, если ИИ
!!SN:W^opt724.b%Y1^/?y2;                // y2 - индекс массива с банковскими параметрами игрока
!!if&y2=0:;                             // инициализация массива, если массив еще не существует
  !!UN:J2/?y3; !!VRy3:*-1 +6;           // y3 ставка по-умолчанию завитит от выбранного уровня сложности (1/6,1/5,1/4,1/3,1/2)
  !!SN:M-1/28/0/1 W^opt724.b%Y1^/v1 W^opt724.b%Y1^/?y2; // Mi^opt724.b%Y1^ - новый сохраняемый массив на 28 целых элементов
  !!re i/14/20:;                        // установка ставки по-умолчанию для всех продуктов
    !!SN:My2/i/y3;                      // 
  !!en:;                                // 
!!en:;                                  // 
** списание задолженности
!!re i/0/6:;                            // для каждого вида реурса
  !!SN:My2/i/?y9;                       // y9 - задолженность
  !!if&y9>0:;                           // если есть задолженность
    !!OW:Ry1/i/?y3;                     // y3 - кол-во ресурса у игрока
    !!VRy4:Si +7; !!SN:My2/y4/?y4;      // y4 - дневной платеж
    !!VRy4&y4>y9:Sy9;                   // платеж не может превышать текущую задолженность
    !!if&y3>=y4:;                       // если текущих ресурсов хватает на платеж
      !!VRy4:*-1; !!OW:Ry1/i/dy4;       // списание ресурса у игрока
      !!SN:My2/i/dy4;                   // уменьшение задолженности
    !!el:;                              // если ресурсов недостаточно для платежа
      !!VRy5:Sy4 -y3;                   // y5 - недостача ресурса
      !!VRy3:*-1; !!OW:Ry1/i/dy3;       // списание ресурса у игрока
      !!SN:My2/i/dy3;                   // уменьшение задолженности
      !!VRy6|i=0/i=2:Sy5 *50;           // y6 - пеня (50/100 золотых за единицу простых/ценных ресурсов)
      !!VRy6&i<>0/i<>2:Sy5 *100;        // ...
      !!OW:Ry1/6/?y3;                   // y3 - кол-во золота у игрока
      !!if&y3>=y6:;                     // если золота хватает на уплату пени
        !!VRy6:*-1; !!OW:Ry1/6/dy6;     // списание пени у игрока
      !!el:;                            // если золота недостаточно
        !!VRy3:*-1; !!OW:Ry1/6/dy3;     // списание всего золота у игрока
        !!VRy7:Si +14; !!SN:My2/y7/?y8; // y8 - текущая ставка-делитель
        !!if&y8>1:;                     // если текущая ставка менее 100%
          !!VRy9:Sy8 -1; !!SN:My2/y7/y9;// повышение процентной ставки
          !!FU(WS.opt724.Notification):Pi/y8/y9;   // показ уведомления
        !!en:;                          // 
      !!en:;                            // 
    !!en:;                              // 
    !!UN:R2;                            // обновление строки ресурсов
  !!en:;                                // 
!!en:;                                  // 
** обновление макс. размера займа для игрока
; Базовый размер 8/7/6/5/4 +1 за каждые прошедшие 2 недели.
; Артефакты и специализации героев: + доход от источника за 4 дня
; Рудники и городские постройки: + доход от источника за 2 дня
!!UN:J2/?y3; !!VRy4:Sc :14 +8 -y3;      // установка базового размера от сложности и игрового времени
!!VRy21:Cy4/y4/y4/y4/y4/y4/y4;          // y21..y27 - переменные для подсчета макс. объема займа
!!OW:Wy1/?y3; !!VRy3:-1;                // y3 - кол-во городов у героя -1
!!re i/0/y3:;                           // перебор всех городов игрока
  !!OW:Wy1/i/?y4;                       // y4 - номер города на карте
  !!CA0/y4:T?y5 B3/15;                  // y5/f1 - тип города/отстроено хранилище ресурсов
  !!if&1:;                              // если хранилище ресурсов отстроено, учитываем его доход за 2 дня
    !!VRy21|y5=0/y5=4/y5=6/y5=7:+1;     // Замок, Некрополь, Крепость и Цитадель +1 Дерево
    !!VRy22|y5=3/y5=8:+2;               // Инферно, Сопряжение +2 Ртуть
    !!VRy23|y5=0/y5=4/y5=6/y5=7:+1;     // Замок, Некрополь, Крепость и Цитадель +1 Руда
    !!VRy24|y5=5:+2;                    // Темница +2 Сера
    !!VRy25|y5=1:+2;                    // Оплот +2 Кристаллы
    !!VRy26|y5=2:+2;                    // Башня +2 Самоцветы
  !!en:;                                // 
  !!CA0/y4:B3/10; !!VRy27&1:+2;         // +1000 (2x500) за Сельскую управу
  !!CA0/y4:B3/11; !!VRy27&1:+2;         // +1000 за Префектуру
  !!CA0/y4:B3/12; !!VRy27&1:+4;         // +2000 за Муниципалитет
  !!CA0/y4:B3/13; !!VRy27&1:+8;         // +4000 за Капитолий
  !!CA0/y4:B3/26; !!VRy27&1:+20;        // +10000 за Грааль
!!en:;
!!UN:U53/-1/?y3; !!VRv1:S-1;            // y3 - кол-во шахт на карте
!!re i/1/y3:;                           // перебор всех шахт
  !!UN:U53/-1/-1/1;                     // v1/v2/v3 - координаты шахты
  !!MNv1/v2/v3:O?y4 R?y5;               // y4/y5 - владелец/ресурс шахты
  !!co&y4<>y1:;                         // пропуск шахт, не принадлежащих игроку
  !!VRy6:Sy5 +21; !!co|y6<21\y6>27:;    // y6 - индекс ресурсной переменной, соотв. ресурсу шахты
  !!VRyy6:+2;                           // учитываем доход шахт за 2 дня
!!en:;
!!OW:Hy1/2/0;                           // v2 - количество героев у игрока
!!re i/1/v2:;                           // перебор всех героев игрока
  !!OW:Hy1/1/i; !!VRy3:Sv1;             // y3 - номер героя
  !!HEy3:X?y4/?y5/?j/?j/?j/?j/?j;       // y4=2 - для специалистов по ресурсу y5
  !!HEy3:S13/?y6;                       // y6 - уровень навыка Имущество у героя
  !!VRy21&y4=2/y5=0:+2;                 // учитываем доход от ресурсных специализаций героев
  !!VRy22&y4=2/y5=1:+4;                 // 
  !!VRy23&y4=2/y5=2:+2;                 // 
  !!VRy24&y4=2/y5=3:+4;                 // 
  !!VRy25&y4=2/y5=4:+4;                 // 
  !!VRy26&y4=2/y5=5:+4;                 // 
  !!VRy27&y4=2/y5=6:+2;                 // 
  !!VRy27&y6>0:+y6;                     // и навыков Поместья
  !!HEy3:A2/109/?y4/?j; !!VRy4:*4; !!VRy25:+y4; // доход от плащей клисталлов
  !!HEy3:A2/110/?y4/?j; !!VRy4:*4; !!VRy26:+y4; // доход от колец самоцветов
  !!HEy3:A2/111/?y4/?j; !!VRy4:*4; !!VRy22:+y4; // доход от склянок ртути
  !!HEy3:A2/112/?y4/?j; !!VRy4:*2; !!VRy23:+y4; // доход от подвод с рудой
  !!HEy3:A2/113/?y4/?j; !!VRy4:*4; !!VRy24:+y4; // доход от колец серы
  !!HEy3:A2/114/?y4/?j; !!VRy4:*2; !!VRy21:+y4; // доход от подвод с лесом
  !!HEy3:A2/115/?y4/?j; !!VRy4:*8; !!VRy27:+y4; // доход от мешка золота
  !!HEy3:A2/116/?y4/?j; !!VRy4:*6; !!VRy27:+y4; // доход от сумы золота
  !!HEy3:A2/117/?y4/?j; !!VRy4:*4; !!VRy27:+y4; // доход от мошны золота
  !!HEy3:A2/140/?y4/?j; !!VRy4:*5; !!VRy22:+y4; !!VRy24:+y4; !!VRy25:+y4; !!VRy26:+y4; // доход от рогов изобилия
!!en:;                                  // 
!!VRy21:*2; !!VRy23:*2; !!VRy27:*500;   // Множители объема для дерева/руды - x2, для золота - x500
!!re i/21/27:;                          // сохранение макс. объемов займов в массив
  !!VRy3:Syi; !!SN:Mi^opt724.b%Y1^/i/y3;// 
!!en:;                                  // 

** Нажатие кнопки Банка в экране города
!?FU(OnTownMouseClick);                 // клик в экране города

** проверка клика
!!UN:P724/?y1; !!FU&y1=0:E;             // Выход, если опция "Банк" отключена
!!CM:I?y1 F?y2 S?y3;                    // y1-y3 - параметры клика
!!SN:F^GetButtonID^/^bank^; !!VRy4:Sv1; // y4 - ID кнопки "bank"
!!FU&y1<>y4:E;                          // выход, если клик не на кнопке банка
!!CM:R0;                                // отмена действия по умолчанию
!!FU|y2<>0/y3<>13:E;                    // выход, если не отпущена ЛКМ на кнопке банка
!!FU(esr_bank):P;
!?FU(esr_bank);
!!OW:C?y1; !!CA-1:O?y2; !!FU&y2<>y1:E;  // y1 - текущий игрок, выход, если город не принадлежит игроку (город союзника)
** проверка отстройки рынка
!!CA-1:B3/14 U?y4;                      // проверка отстройки рынка в городе, y4 - номер города на карте
!!if&-1:;                               // если рынок не построен
  !!CA-1:T?y2; !!VRy2:+22;              // y2 - картинка рынка соотв. типу города для диалога
  !!IF:Q2/y2/14/1^%T(opt724.tnob)^;     // выдача сообщения о недоступности Банка
  !!FU:E;                               // и выход
!!en:;                                  // 
** массив банковских параметров игрока
; opt724.b: 00..06 - Задолженность, 07..13 - Дневной платеж, 14..20 - Ставка-делитель, 21..27 - Макс.размер займа
; opt724.b: 00..06 - Объем, 07..13 - Платеж, 14..20 - Долг
!!SN:W^opt724.b%Y1^/?y2;                // y2 - индекс массива с банковскими параметрами игрока
!!SN:W^opt724.c%Y1^/?y9; !!SN&y9<0:My9; // удаление временного массива текущего займа, если такой есть
!!SN:M-1/21/0/0 W^opt724.c%X1^/v1;      // Mi^opt724.c%Y1^ - временный массив текущего займа
** привязка элементов диалога ко временным z-переменным
!!DL70:N^opt724dl.txt^; !!FU&-1:E;      // подгрузка 70 диалога, выход, такого нет
!!VRi^es_p_var^:S-1;                              // p - страница диалога
!!VRj:S0 R10; !!SN:T^opt724.cnm%Vj^/?z1; !!CA0/y4:N?z2; // текст приветствия
!!SN:T^opt724.h008^/?z35/^ClerkName^/z1/^CityName^/z2;   //
!!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y4;    // подготовка начальной страницы диалога Банка
!!DL70:H1/z35 H2/z35 H3/z35 H4/z35 H5/z35 H6/z35 H7/z35 H8/z35 H9/z36; // подсказки приветствия и прощания
!!DL70:H10/z38 H20/z39 H30/z40;         // подсказки заголовков
!!DL70:H200/z54 H201/z55 H202/z56 H203/z57 H204/z58 H205/z59 H206/z60; // подсказки кнопок погашения
!!DL70:H220/z41 H221/z41 H222/z41 H223/z41 H224/z41 H225/z41 H226/z41 H21/z41; // подсказка заголовка и поля "задолженность"
!!DL70:H230/z42 H231/z42 H232/z42 H233/z42 H234/z42 H235/z42 H236/z42 H22/z42; // подсказка заголовка и поля "платеж"
!!DL70:H240/z34 H241/z34 H242/z34 H243/z34 H244/z34 H245/z34 H246/z37 H23/z34; // штрафы за неуплату (подсказки)
!!DL70:H100/z43 H101/z43 H102/z43 H103/z43 H104/z43 H105/z43 H106/z43; // стандартные займы (подсказки)
!!DL70:H120/z44 H121/z44 H122/z44 H123/z44 H124/z44 H125/z44 H126/z44 H11/z44; //
!!DL70:H130/z89 H131/z89 H132/z89 H133/z89 H134/z89 H135/z89 H136/z89 H12/z89; //
!!DL70:H140/z90 H141/z90 H142/z90 H143/z90 H144/z90 H145/z90 H146/z90 H13/z90; //
!!DL70:H107/z85 H108/z86 H109/z87 H110/z88 H111/z23; // целевые займы (подсказки)
!!DL70:H115/z22;                        // Подсказка кнопки Займ
!!SN:W^opt724.d70^/1;                   // установка флага вызова диалога
!!MP:C?z1; !!SN:W^opt724mp3^/z1;        // сохранение текущей музыкальной темы
!!MP:P^option 724 - bank^/0/1;          // смена музыкальной темы
!!DL70:S;                               // отображение диалога 70
!!SN:W^opt724mp3^/?z1; !!MP:Pz1/0/1;    // восстановление предыдущей музыкальной темы
!!SN:W^opt724.d70^/0;                   // сброс флага вызова диалога
!!if&i^es_p_var^>=100:;                           // если необходима анимация отстройки
  !!VRy9:Sp -100;                       // y9 - здание
  !!UN:C6919500/4/?y7;                  // отстройка здания с анимацией
  !!VRy8:Sy7 +440;                      // 
  !!UN:Cy8/4/y9;                        // 
  !!SN:E6123136/2/y7/y9;                // 
!!en:;                                  // 

!?FU(WS.opt724.PrepareDlg);             // подготовка и состояния x2 диалога Банка города x3 для игрока x1.
!!SN:W^opt724.b%X1^/?y2;                // y2 - номер массива банковских параметров игрока
** установка текстов для всех состояний
!!CA0/x3:N?z2; !!SN:T^opt724.t007^/?z61/^CityName^/z2; // Подпись окна
!!SN:T^opt724.h115^/?z22;               // подсказка кнопки "Займ"
!!SN:T^opt724.h010^/?z38 T^opt724.h020^/?z39 T^opt724.h030^/?z40; // подсказки заголовков
!!SN:T^opt724.t240^/?z31 T^opt724.t241^/?z32 T^opt724.t246^/?z33 T^opt724.h24x^/?z34 T^opt724.h246^/?z37; // тексты и подсказки штрафов
!!VRz1:S^opt724ex.def^; !!VRz1&x2>=0:S^opt724re.def^; !!DL70:A9/9/z1/1; // Кнопка Выход/Возврат
!!SN:T^opt724.h09x^/?z36; !!SN&x2>=0:T^opt724.h09a^/?z36; // подсказка Кнопка Выход/Возврат
!!VRz1:S^opt724tr.def^; !!DL70:A115/9/z1/1 E115/0; // деактивация и сокрытие кнопок да/нет
!!DL70:A10/4/0/1;                       // Заголовок страницы 1
** страница погашения
!!re i/0/6:;                            // параметры текущей задолженности
  !!VRy4:S47 +i; !!SN:T^opt724.tfil^/?z1; !!VRzy4:S^%Z1^; // zy4 - значение строки долга по-умолчанию
  !!SN:My2/i/?y3;                       // y3 - строка долга
  !!SN:T^opt724.h22x^/?z41;             // подсказка заголовка и поля "задолженность" 
  !!VRy1:S200 +i; !!VRy6:S54 +i;        // y1/y6 - кнопка погашения/подсказка кнопки погашения
  !!if&y3>0:;                           // если есть задолженность
    !!VRzy4:S^%Y3^;                     // вывод объема задолженности
    !!OW:Rx1/i/?y5;                     // y5 - текущий запас ресурса у игрока
    !!if&y5>=y3:;                       // если игрок может погасит задолженность
      !!DL70:Ey1/1;                     // актиация кнопки и активная подсказка
      !!SN:T^opt724.h20x^/?z1; !!VRzy6:S^%Z1^;
    !!el:;                              //  если игрок НЕ может погасит задолженность
      !!DL70:Ey1/0; // деактиация кнопки и неактивная подсказка
      !!SN:T^opt724.h20a^/?z1; !!VRzy6:S^%Z1^;
    !!en:;                              // 
  !!el:;                                // если у игрока НЕТ задолженности
    !!DL70:Ey1/0;                       // деактиация кнопки и неактивная подсказка
    !!SN:T^opt724.h20n^/?z1; !!VRzy6:S^%Z1^;
  !!en:;                                // 
  !!VRy4:S78 +i; !!SN:T^opt724.tfil^/?z1; !!VRzy4:S^%Z1^; // zy4 - значение строки дневного платежа по-умолчанию
  !!SN:T^opt724.h23x^/?z42;             // подсказка строки дневного платежа
  !!VRy5:Si +7; !!SN:My2/y5/?y5;        // y5 - текущий дневной платеж
  !!VRzy4&y3>0/y5>0:S^%Y5^;             // если есть долг и дневной платеж, вывод значения в строку платежа
!!en:;
** страница стандартных займов
!!SN:T^opt724.h10x^/?z43 T^opt724.h12x^/?z44 T^opt724.h13x^/?z89; // получение подсказок кнопок и полей объем/платеж/ставка стандартных займов
!!SN&x2<0:T^opt724.h14x^/?z90; !!SN&x2>=0:T^opt724.h14a^/?z90;    //
!!re i/0/6:;                            // для каждой из 7 возможных строк
  !!VRy1:Si; !!VRy1&x2>=0:Sx2; !!VRz1:S^opt724c%Y1.def^; // Установка иконки стандартного займа
  !!VRy10:Si +100;                      // y10 - кнопка
  !!VRy3:Si +242; !!VRy4:Si +270; !!VRy5:Si +310;        // y3/y4/y5 - номера значений полей объем/платеж/ставка
  !!SN:T^opt724.tfil^/?z1;!!VRzy3:S^%Z1^; !!VRzy4:S^%Z1^; !!VRzy5:S^%Z1^; // заполнитель полей по-умолчанию
  ** страница Займов
  !!if&x2<0:;                           // для главной страницы
    !!VRy6:Si; !!VRy7:Si +7; !!VRy8:Si +14; !!VRy9:Si +21; // y6/y7/y8/y9 - долг/платеж/ставка/макс.объем
    !!SN:My2/y6/?y6 My2/y7/?y7 My2/y8/?y8 My2/y9/?y9;//
    !!DL70:A13/4/2/1;                   // Заголовок "Ставка"
    !!VRy12:Sy9 -y6; !!VRy12&i=6:Sy9 -y6 :500 *500; // y12 - доступный лимит, округленный до 500 для золота
    !!if&y12>0:;                        // если долг меньше макс. объема
      !!VRz1:S^opt724c%Vi.def^;         // деф кнопки
      !!DL70:Ay10/9/z1/1 Ey10/1;        // кнопка активна
      !!VRy11:Sy8;                      // заполняем доступный объем y11..y12
      !!VRzy3&y12>y11:S^%Y11 .. %Y12^;  //
      !!VRzy3&i=6:S^.. %Y12^;           //
      !!VRzy3&y12<=y11/i<6:S^%Y12^;     // 
      !!VRy13:Sy11 :y8; !!VRy13&y13=0:S1; !!VRy13:+y11 :10;  !!VRy13&i<6:+1; // заполняем дневной платеж (10% от суммы выплаты, мин 1)
      !!VRy14:Sy12 :y8; !!VRy14&y14=0:S1; !!VRy14:+y12 :10;  !!VRy14&i<6:+1; //
      !!VRzy4&y14>y13:S^%Y13 .. %Y14^;  //
      !!VRzy4&i=6:S^.. %Y14^;           //
      !!VRzy4&y14<=y13/i<6:S^%Y14^;     // 
      !!FU(WS.opt724.PercentRate):Py8/1;// заполняем процентнцю ставку
      !!VRzy5:S^%Z1^;                   // 
    !!el:;                              // если займ не может быть выдан
      !!VRz1:S^opt724c%Vi.def^;         // деф кнопки
      !!DL70:Ay10/9/z1/1 Ey10/0;        // кнопка неактивна
    !!en:;                              // 
  ** страница выдачи стандартного займа
  !!el&x2<7:;                           // для страницы получения Стандартного займа
    !!SN:T^opt724.h10a^/?z43;           // подскзка кнопок выдачи займа
    !!VRy6:Sx2; !!VRy7:Sx2 +7; !!VRy8:Sx2 +14; !!VRy9:Sx2 +21; // y6/y7/y8/y9 - долг/платеж/ставка/макс.объем
    !!SN:My2/y6/?y6 My2/y7/?y7 My2/y8/?y8 My2/y9/?y9;          //
    !!DL70:A13/4/3/1;                   // Заголовок "Выплаты"
    !!FU(WS.opt724.LoanAmount):Pi/x2/x1/x3/?y11/?y12/?y13; // получение параметров стандартного займа
    !!if&y11>0:;                        // если займ доступен
      !!VRz1:S^opt724c%X2.def^;         // деф кнопки
      !!DL70:Ay10/9/z1/1 Ey10/1;        // кнопка активна
      !!VRzy3:S^%Y11^;                  // Объем займа
      !!VRzy4:S^%Y12^;                  // Платеж
      !!VRzy5:S^%Y13^;                  // Объем выплат
    !!el:;                              // если займ НЕ доступен
      !!VRz1:S^opt724c%X2.def^;         // деф кнопки
      !!DL70:Ay10/9/z1/1 Ey10/0;        // кнопка неактивна
    !!en:;                              // 
  ** страница выдачи целевого займа
  !!el:;                                // для страницы получения ЦЕЛЕВОГО займа
    !!SN:T^opt724.h030^/?z43;           // подскзка кнопок выдачи займа
    !!VRy20:Sx2 -4; !!SN:T^opt724.h030^/?z38; !!DL70:A10/4/y20/1; // заголовок и подсказка страницы 1
    !!DL70:A13/4/3/1;                   // Заголовок "Выплаты"  
    !!VRz1:S^opt724tr.def^;             // деактивация и сокрытие кнопок
    !!re j/0/6:;                        // 
      !!VRy20:Sj +100;                  // y20 - номер кнопки
      !!DL70:Ay20/9/z1/1 Ey20/0;        // деактивация и сокрытие кнопки
    !!en:;                              // 
    !!VRy20:S100;                       // y20 - номер кнопки
    !!re j/0/6:;                        // займ для каждого типа ресурса
      !!FU(WS.opt724.LoanAmount):Px2/j/x1/x3/?y11/?y12/?y13; // y11/y12/y13 - объем,платеж,выплата
      !!if&y11>0:;                      // если требуется ресурс
        !!VRz1:S^opt724c%Vj.def^;       // z1 - иконка кнопки
        !!DL70:Ay20/9/z1/1 Ey20/1;      // активация и отображение кнопки
        !!VRy21:Sy20 +142; !!VRy22:Sy20 +170; !!VRy23:Sy20 +210; // y21/y22/y23 - номера значений полей объем/платеж/ставка
        !!VRzy21:S^%Y11^;  !!VRzy22:S^%Y12^;  !!VRzy23:S^%Y13^;  //        
        !!VRy20 +1;                     // следующая кнопка
      !!en:;                            // 
    !!en:;                              // 
    !!VRz1:S^opt724ok.def^; !!DL70:A115/9/z1/1 E115/1; // перемещение, активация и отображение кнопки "да"
  !!en:;                                // 
!!en:;                                  // 
** страница целевых займов
** x3 - город, y10 - тип города, y11 - золотое дание для отстройки, y12 - фортификационное здание для отстройки, y13 - жилище 7го уровня
!!if&x2=-1:;                            // если отображается стартовая страница
  !!VRz1:S^opt724c7.def^; !!DL70:A107/9/z1/1 E107/1;  // активация кнопок целевых займов
  !!VRz1:S^opt724c8.def^; !!DL70:A108/9/z1/1 E108/1;  //
  !!VRz1:S^opt724c9.def^; !!DL70:A110/9/z1/1 E110/1;  //
  !!VRz1:S^opt724cm.def^; !!DL70:A111/9/z1/1 E111/1;  //
  !!CA0/x3:T?y10; !!VRz1:S^opt7247%Y10.def^; !!DL70:A109/9/z1/1 E109/1; //
  !!SN:T^opt724.h107^/?z85 T^opt724.h108^/?z86 T^opt724.h109^/?z87 T^opt724.h110^/?z88 T^opt724.h111^/?z23; // установка подсказок целевых займов
  !!VRz1:S^opt724tm.def^; !!DL70:A30/9/z1/1 E30/1;    // активация заголовка
  ** определение возможности выдачи целевого займа
  !!CA0/x3:R?y1;                        // y1 = 1, если сегодня в городе запрещена постройка зданий
  !!if&y1=1:;                           // если отстройка сегодня запрещена
    !!VRy11:C-1/-1/-1/-1;               // запрет строительных займов
  !!el:;                                // иначе
    !!VRy11:S10;   !!CA0/x3:B3/10;      // y11 - Управа, проверка наличия Управы
    !!VRy11&1:S11; !!CA0/x3:B3/11;      // y11 - Префектура, проверка наличия Префектуры
    !!VRy11&1:S12; !!CA0/x3:B3/12;      // y11 - Муниципалитет, проверка наличия Муниципалитета
    !!VRy11&1:S13;                      // y11 - Капитолий, если отстроен Муниципалитет
    !!VRy12:S7;    !!CA0/x3:B3/7;       // y12 - Форт, проверка наличия Форта
    !!VRy12&1:S8;  !!CA0/x3:B3/8;       // y12 - Цитадель, проверка наличия Цитадель
    !!VRy12&1:S9;                       // y12 - Замок, если отстроена Цитадель
    !!VRy13:S36;   !!CA0/x3:B3/36;      // y13 - Жилище 7го уровня, проверка наличия
    !!VRy13&1:S43;                      // y13 - Ул. жилище 7го уровня, если отстроено обычное
    !!VRy14:S0;    !!CA0/x3:B3/0;       // y14 - ГМ1, проверка наличия ГМ1
    !!VRy14&1:S1;  !!CA0/x3:B3/1;       // y14 - ГМ2, проверка наличия ГМ2
    !!VRy14&1:S2;  !!CA0/x3:B3/2;       // y14 - ГМ3, проверка наличия ГМ3
    !!VRy14&1:S3;  !!CA0/x3:B3/3;       // y14 - ГМ4, проверка наличия ГМ4
    !!VRy14&1:S4;                       // y14 - ГМ5
    ** расчет и определение достаточности займа
    !!re y21/11/14:;
      !!FU(WS.opt724.GetTownBuildingInfo):Px3/yy21/?y23/?y24/?y25/?y26/?y27/?y28/?y29/?y30/?y31/?y32/?y33; // y23-y33 - параметры здания
      !!VRyy21|y23=1/y24=0/y25=0/y26=1:S-1; // отключение займа если здание уже построено, запрещено, нет необходимых построек или средств достаточно без займа
      !!re y9/0/6:;                     // 
        !!VRy16:Sy9 +27; !!VRy16:Syy16; // y16 - цена здания
        !!OW:Rx1/y9/?y17;               // y17 - ресурс игрока
        !!VRy18:Sy9 +21;                // 
        !!SN:My2/y9/?y19 My2/y18/?y20;  // 
        !!VRy18:Sy20 -y19;              // y18 - доступный займ
        !!VRy19:Sy16 -y17;              // y19 - недостаток ресурсов
        !!VRyy21&y19>y18:S-1;           // отключение займа, если его не хватит на отстройку
      !!en:;                            // 
    !!en:;                              // 
  !!en:;                                 // 
  !!SN:My2/6/?y15 My2/27/?y16; !!VRy16:*2 -y15 :500 *500; // y16 - доступный лимит для займа "Враг у ворот"
  !!VRy15&y16<500:S-1;                // y15<0 если займ "Враг у ворот" не может быть выдан
  ** деактивация кнопок недоступных займов
  !!DL70&y11<0:E107/0;                  //если здание для отстройки НЕ ДОСТУПНО, деактивация кнопки займа "Капитолий"
  !!DL70&y12<0:E108/0;                  //если здание для отстройки НЕ ДОСТУПНО, деактивация кнопки займа "Замок"
  !!DL70&y13<0:E109/0;                  //если здание для отстройки НЕ ДОСТУПНО, деактивация кнопки займа "7й уровень"
  !!DL70&y14<0:E111/0;                  //если здание для отстройки НЕ ДОСТУПНО, деактивация кнопки займа "Чародейство"
  !!DL70&y15<0:E110/0;                  //если здание для отстройки НЕ ДОСТУПНО, деактивация кнопки займа "Враг у ворот"
  !!VRq:Sy11; !!VRr:Sy12; !!VRs:Sy13; !!VRt:Sy14; // q/r/s/t - номера зданий целевых займов
!!el:;                                  // если отображается страница займа
  !!VRz1:S^opt724tr.def^; 
  !!DL70:A107/9/z1/1 E107/0 A108/9/z1/1 E108/0 A109/9/z1/1 E109/0 A110/9/z1/1 E110/0 A111/9/z1/1 E111/0; // деактивация и сокрытие кнопок целевых займов
  !!DL70:A30/9/z1/1 E30/0;              // сокрытие заголовка целевых займов
!!en:;                                  // 
** Обновление тектов диалога
!!DL70:A7/3/z61/1 A8/3/z35/1; A12/3/z24/1 A13/3/z25/1 A20/3/z26/1 A21/3/z27/1 A22/3/z28/1 A23/3/z29/1 A30/3/z30/1; // заголовки (тексты)
!!DL70:A240/3/z31/1 A242/3/z31/1 A241/3/z32/1 A243/3/z32/1 A244/3/z32/1 A245/3/z32/1 A246/3/z33/1; // штрафы за неуплату (тексты)
!!DL70:A220/3/z47/1 A221/3/z48/1 A222/3/z49/1 A223/3/z50/1 A224/3/z51/1 A225/3/z52/1 A226/3/z53/1; // значения задолженнотей (тексты)
!!DL70:A230/3/z78/1 A231/3/z79/1 A232/3/z80/1 A233/3/z81/1 A234/3/z82/1 A235/3/z83/1 A236/3/z84/1; // значения платежей по долгам (тексты)
!!DL70:A120/3/z242/1 A121/3/z243/1 A122/3/z244/1 A123/3/z245/1 A124/3/z246/1 A125/3/z247/1 A126/3/z248/1; // значения объемов (тексты)
!!DL70:A130/3/z270/1 A131/3/z271/1 A132/3/z272/1 A133/3/z273/1 A134/3/z274/1 A135/3/z275/1 A136/3/z276/1; // значения платежей(тексты)
!!DL70:A140/3/z310/1 A141/3/z311/1 A142/3/z312/1 A143/3/z313/1 A144/3/z314/1 A145/3/z315/1 A146/3/z316/1; // значения ставок (тексты)

** обработчик диалога
!?DL&v998=70/v1000=13;                  // обработка диалога (отпущена ЛКМ)
!!SN:W^opt724.d70^/?y1; !!FU&y1<1:E;    // выход, если вызывается другой диалог с аналогичным номером
!!OW:C?y1; !!CA-1:U?y2;                 // y1/y2 - игрок/город
!!DL70&v999=9/i^es_p_var^=-1:C1;                  // Кнопка Выход на главной странице - Закрыть диалог
!!if&v999=9/i^es_p_var^>=0:;                      // Кнопка Выход на странице займа - Возврат на главную
  !!VRi^es_p_var^:S-1;                            // p - главная страница
  !!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y2;  // подготовка диалога Банка, страница стандартного займа
  !!FU:E;                               // остановка обработчика
!!en:;                                  // 
!!if&v999>=100/v999<=111/i^es_p_var^=-1:;         // Кнопки Займов
  !!VRi^es_p_var^:Sv999 -100;                     // p - страница стандартного займа
  !!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y2;  // подготовка диалога Банка, страница стандартного займа
  !!FU:E;                               // остановка обработчика
!!en:;                                  // 
!!if&v999>=100/v999<=106/i^es_p_var^>=0/i^es_p_var^<=6:;    // Получение стандартного займа
  !!VRy3:Sv999 -100;                    // y3 - номер займа в валюте p
  !!FU(WS.opt724.LoanAmount):Py3/i^es_p_var^/y1/y2/?y4/?y5/?y6; // y4/y5/y6 - объем/платеж/выплата
  !!OW:Ry1/i^es_p_var^/dy4; !!UN:R2;              // добавление ресурса и обновление строки ресурсов
  !!SN:Mi^opt724.b%Y1^/i^es_p_var^/dy6;           // увеличение долга
  !!VRy7:Si^es_p_var^ +7; !!SN:Mi^opt724.b%Y1^/y7/dy5; // увеличение платежа
  !!VRz1:S^PICKUP07^; !!SN:Pz1;         // звук совершения операции
  !!VRi^es_p_var^:S-1;                            // p - главная страница
  !!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y2;  // подготовка диалога Банка, страница стандартного займа
  !!FU:E;                               // остановка обработчика
!!en:;
!!if&v999=115/i^es_p_var^=10:;                    // Кнопка "Займ" на займе "Враг у ворот"
  !!FU(WS.opt724.LoanAmount):P10/6/y1/y2/?y4/?y5/?y6; // y4/y5/y6 - объем/платеж/выплата
  !!OW:Ry1/6/dy4; !!UN:R2;              // добавление ресурса и обновление строки ресурсов
  !!SN:Mi^opt724.b%Y1^/6/dy6;           // увеличение долга
  !!SN:Mi^opt724.b%Y1^/13/dy5;          // увеличение платежа
  !!VRz1:S^PICKUP07^; !!SN:Pz1;         // звук совершения операции
  !!VRi^es_p_var^:S-1;                            // p - главная страница
  !!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y2;  // подготовка диалога Банка, страница стандартного займа
  !!FU:E;                               // остановка обработчика
!!en:;
!!if&v999=115:;                         // Кнопка "Займ" на строительных займах
  !!if|i^es_p_var^=7/i^es_p_var^=8/i^es_p_var^=9/i^es_p_var^=11:;               // 
    !!VRy9&i^es_p_var^=7:Sq; !!VRy9&i^es_p_var^=8:Sr; !!VRy9&i^es_p_var^=9:Ss; !!VRy9&i^es_p_var^=11:St; // y9 - целевое здание
    !!FU(WS.opt724.GetTownBuildingInfo):Py2/y9/?y8/?y8/?y8/?y8/?y10/?y11/?y12/?y13/?y14/?y15/?y16; // y10-y16 - цена здания
    !!re j/0/6:;                        // займ для каждого типа ресурса
      !!FU(WS.opt724.LoanAmount):Pi^es_p_var^/j/y1/y2/?y21/?y22/?y23; // y21/y22/y23 - объем,платеж,выплата
      !!if&y21>0:;                      // если требуется ресурс
        !!OW:Ry1/j/dy21;                // добавление ресурса
        !!SN:Mi^opt724.b%Y1^/j/dy23;    // увеличение долга
        !!VRy7:Sj +7; !!SN:Mi^opt724.b%Y1^/y7/dy22; // увеличение платежа
      !!en:;                            // 
    !!en:;                              // 
    !!re y7/10/16:;                     // отрицательные значения цены для списания
      !!VRyy7&yy7<0:S0;                 // 
      !!VRyy7:*-1;                      // 
    !!en:;                              // 
    !!OW:Ry1/0/dy10 Ry1/1/dy11 Ry1/2/dy12 Ry1/3/dy13 Ry1/4/dy14 Ry1/5/dy15 Ry1/6/dy16; // списание ресурсов
    !!VRi^es_p_var^:S100 +y9;                     // p - флаг отстройки
    !!CA0/y2:R1;                        // запрет строительства и обновление строки ресурсов
    !!VRz1:S^PICKUP07^; !!SN:Pz1;       // звук совершения операции
    !!DL70:C1; !!UN:R2 R4;              // закрытие диалога и обновление экрана города
    !!FU:E;                             // остановка обработчика
  !!en:;
  !!IF:M^%i(es_p_var)^;                                //
!!en:;                                  // 
!!if&v999>=200/v999<=206:;              // Погашение задолженности
  !!VRy3:Sv999 -200;                    // y3 - ресурс
  !!SN:Mi^opt724.b%Y1^/y3/?y4;          // y4 - долг
  !!VRy5:Sy4 *-1; !!OW:Ry1/y3/dy5; !!UN:R2; // списание долга и обновление строки ресурсов
  !!SN:Mi^opt724.b%Y1^/y3/0;            // обнуление задолженности
  !!VRy5:Sy3 +7; !!SN:Mi^opt724.b%Y1^/y5/0; // обнуление платежа
  !!VRz1:S^PICKUP07^; !!SN:Pz1;         // звук совершения операции
  !!VRy5:Sy3 +21; !!SN:Mi^opt724.b%Y1^/y5/?y6; // y6 - максимально доступный объем займа
  !!VRy6:-y4;                           //
  !!if&y6<=y4:;                         // если гасится не менее половины максимально доступного объема
    !!VRy5:Sy3 +14; !!SN:Mi^opt724.b%Y1^/y5/?y7; // y7 - ставка до снижения
    !!VRy8:Sy7 +1; !!SN:Mi^opt724.b%Y1^/y5/y8; // снижение ставки
    !!FU(WS.opt724.Notification):Py3/y7/y8; // показ уведомления    
  !!en:;                                // 
  !!VRi^es_p_var^:S-1;                            // p - главная страница
  !!FU(WS.opt724.PrepareDlg):Py1/i^es_p_var^/y2;  // подготовка диалога Банка, страница стандартного займа
  !!FU:E;                               // остановка обработчика
!!en:;                                  // 
*!IF:M^%v999^;
** диалог-оповещение
!?FU(WS.opt724.Notification);           // Показ уведомления об изменении ставки c x2 на x3 по ресурсу x1
!!DL71:N^opt724dl.txt^;   !!FU&-1:E;    // подгрузка 71 диалога, выход, такого нет
!!VRz1:S^opt724c%X1.def^; !!DL71:A20/9/z1/1; // замена кнопки на соотв. ресурсу
!!FU(WS.opt724.PercentRate):Px2/2 Px3/3;// z2/z3 - тексты ставок
!!SN&x2<x3:T^opt724.mr01^/?z1;          // z1 - текст сообщения о снижении ставки
!!SN&x2>x3:T^opt724.mr02^/?z1;          // z1 - текст сообщения о повышении ставки
!!DL71:A11/3/z1/1;                      // установка заголовка и текстов
!!SN:T^opt724.mr00^/?z1; !!DL71:A10/3/z1/1; //
!!SN:T^opt724.mr12^/?z1; !!DL71:A12/3/z1/1; //
!!DL71:A13/3/z2/1;                      // 
!!SN:T^opt724.mr14^/?z1; !!DL71:A14/3/z1/1; //
!!DL71:A15/3/z3/1;                      //
!!SN:T^opt724.mr16^/?z1; !!DL71:A16/3/z1/1; //
!!SN:W^opt724.d71^/1;                   // установка флага вызова диалога
!!DL71&x2<x3:A13/8/15/1 A15/8/15/1;     // цвета текстов ставок
!!DL71&x2>x3:A13/8/27/1 A15/8/27/1;     // ...
!!DL71:S;                               // вывод диалога
!!SN:W^opt724.d71^/0;                   // сброс флага вызова диалога

** обработчик диалога-оповещения
!?DL&v998=71/v1000=13;                  // обработка диалога (отпущена ЛКМ)
!!SN:W^opt724.d71^/?y1; !!FU&y1<1:E;    // выход, если вызывается другой диалог с аналогичным номером
!!DL71|v999=20/v999=21:C1;              // закрытие диалога при нажатии кнопки

!?FU(WS.opt724.PercentRate);            // возвращает в zx2 значение 1/х1 в виде "хх,х %"
!!VRy1:S100 :x1;                        // 
!!VRy2:S100 %x1 *10 :x1;                // 
!!VRzx2:S^%Y1,%Y2%%^;                  // 

!?FU(WS.opt724.LoanAmount);             // возвращает в x5,x6,x7 объем,платеж,выплату по займу x1 ресурса x2 для игрока x3, города x4
; x1 (0..11) - номер займа
; x2 (0..6) - ресурс
; x3 (0..7) - игрок
; x4 (0..47) - город
!!VRy1:Sx2; !!SN:Mi^opt724.b%X3^/y1/?y11; // y11 - долг
!!VRy1:+7;  !!SN:Mi^opt724.b%X3^/y1/?y12; // y12 - платеж
!!VRy1:+7;  !!SN:Mi^opt724.b%X3^/y1/?y13; // y13 - ставка
!!VRy1:+7;  !!SN:Mi^opt724.b%X3^/y1/?y14; // y14 - макс.объем
!!if&x1<7:;                             // СТАНДАРТНЫЕ ЗАЙМЫ
  !!VRy15:Sy13;                         // y15 - эффективная ставка
  !!VRy1:Sy14 -y11;                     // y1 - лимит.
  !!if&x2=6:;                           // для золота 
    !!VRy1::500;                        // лимит и макс. объем - это кол-во объемов по 500
    !!VRy15:S1;                         // ставка - 1
  !!en:;                                // 
  !!if&y1<1:;                           // 
    !!VRx5:S-1;                         // не предоставляются займы менее 1 ресурса
  !!el:;                                // 
    !!VRy2:Sy15 *7;                     // y2 - объем 7 ставок
    !!if&y1<=y2:;                       // если лимит не превышает 7 ставок
      !!VRx5:Sx1 +1 *y15;               // распределаем по 1 ставке на займ
      !!VRy3:Sx5 -y1;                   // 
      !!VRx5&x5>y1/y3<y15:Sy1;          // разрешаем максимальный займ
      !!VRx5&x5>y1/y3>=y15:S-1;         // лишние займы запрещаем
    !!el:;                              // если лимит превышает 7 ставок
      !!VRx5:Sy1 -y15 *x1 :6 +y15;      // не крайние займы распределены равномерно
      !!VRx5&x1=0:Sy15;                 // первый займ равен ставке
      !!VRx5&x1=6:Sy1;                  // последний займ равен лимиту
    !!en:;                              // 
  !!en:;                                // 
  !!VRx5&x2=6/x5>0:*500;                // х500 для золота
  !!VRx7:Sx5 :y13 +x5; !!VRy3:Sx5 %y13; !!VRx7&y3>0:+1; // x7 - выплата
  !!VRx6:Sx7 :10; !!VRy3:Sx7 %10; !!VRx6&y3>0:+1;       // x6 - платеж
!!el:;                                  // ЦЕЛЕВЫЕ ЗАЙМЫ
  ** определение здания (y5)
  !!if&x1=7:; !!VRy5:S10; !!CA0/x4:B3/10; !!VRy5&1:S11; !!CA0/x4:B3/11; !!VRy5&1:S12; !!CA0/x4:B3/12; !!VRy5&1:S13; !!en:;
  !!if&x1=8:; !!VRy5:S7;  !!CA0/x4:B3/7;  !!VRy5&1:S8;  !!CA0/x4:B3/8;  !!VRy5&1:S9;  !!en:;
  !!if&x1=9:; !!VRy5:S36; !!CA0/x4:B3/36; !!VRy5&1:S43; !!en:; 
  !!if&x1=11:; !!VRy5:S0; !!CA0/x4:B3/0; !!VRy5&1:S1; !!CA0/x4:B3/1; !!VRy5&1:S2; !!CA0/x4:B3/2; !!VRy5&1:S3; !!CA0/x4:B3/3; !!VRy5&1:S4; !!en:; 
  !!VRy5&x1=10:S-1;                     // 
  ** определение параметров займа (x5-x7)
  !!VRy20:C0/0/0/0/0/0/0;               // 
  !!if&y5>=0:;                          // 
    !!FU(WS.opt724.GetTownBuildingInfo):Px4/y5/?y3/?y3/?y3/?y3/?y20/?y21/?y22/?y23/?y24/?y25/?y26; // y20-y26 - стоимость здания
    !!OW:Rx3/x2/?y27; !!VRy28:Sx2 +20; !!VRx5:Syy28 -y27; !!VRx5&y30<0:S0; // x5 - объем займа
    !!VRx7:Sx5 :y13 +x5; !!VRy3:Sx5 %y13; !!VRx7&y3>0:+1; // x7 - выплата
  !!el:;                                // 
    !!VRx5:Sy14 *2 -y11 :500 *500;      // для займа "Враг у ворот" объем равен двойному максимуму - долг, с округлением до 500 золотых
    !!VRx7:Sx5 *2 :y13 +x5; !!VRy3:Sx5 *2 %y13; !!VRx7&y3>0:+1; // x7 - выплата
  !!en:;                                // 
  !!VRx6:Sx7 :10; !!VRy3:Sx7 %10; !!VRx6&y3>0:+1; // x6 - платеж
!!en:;                                  // 

!?FU(WS.opt724.GetTownBuildingInfo);    // получение параметров жилища в конкретном городе
; x1 - номер города на карте (0...47)
; x2 - номер здания (0..43) (см. CA:B)
; Возвращаемые значения:
; x3 (0/1) - построено ли здание в городе
; x4 (0/1) - разрешено ли здание в городе
; x5 (0/1) - хватает ли необходимых зданий для постройки
; x6 (0/1) - хватает ли ресурсов для постройки
; x7..x12 - стоимость здания в соотв. ресурсе (дерево..золото)
; Пример:
; !FU(Fun_GetTownBuildingInfo):P4/15//?y1/////////?y2; - разрешено ли (y1) Хранилище ресурсов (15) в городе №(4) и сколько золота (y2) требуется для постройки
!!if|x1<0/x1>47/x2<0/x2>43:;            // проверка корректности входящих параметров
  !!IF:M^Wrong town (%X1) or buiding (%X2) in Fun_GetTownBuildingInfo.^;
  !!FU:E;                               // выход, если параметры заполнены неверно
!!en:;                                  // 
!!UN:C6919480/4/?y1; !!VRy1:+136724;    // 
!!UN:Cy1/4/?y2; !!VRy9:Sx1 *360 +y2;    // 
!!IF&y9<43200000:M^{Attention!} Error in getting town structure address. The game may fall at any time.^; // 
!!SN:E4392352/2/y9/x2; !!VRx3:Sv1;      // x3 (0/1) - построено ли здание в городе
!!CA0/x1:R?y1 R0;                       //
!!SN:E6033696/2/y9/x2;                  // 
!!VRx4:S0; !!VRx4&v1<>0:S1;             // x4 (0/1) - разрешено ли здание в городе
!!CA0/x1:Ry1;                           // 
!!VRx5:S1; !!CA0/x1:T?y8;               // y8 - тип города
!!CD:B1/y8/x2/?y3/?y4;                  // y3, y4 - 4хбайты построек, от которых зависит данная
!!re y1/0/43;                           // для каждого здания y1
  !!VRy5:Sy1; !!VRy5&y1>31:Sy1 -32;     // y5 - номер проверяемого бита
  !!VRy6:Sy3; !!VRy6&y1>21:Sy4;         // y6 - проверяемый 4хбайт
  !!re y2/1/y5:; !!VRy6::2; !!en:; !!VRy6:%2; // y6 = 0/1 требуется ли здание y1 для постройки
  !!if&y6=1:;                           // если здание требуется
    !!CA0/x1:B3/y1; !!VRx5&-1:S0;       // если не отстроено требуемое здание, x5=0
  !!en:;                                // 
!!en:;                                  // x5 (0/1) - хватает ли необходимых зданий для постройки
!!SN:E4590864/3/y9/x2; !!VRx6:Sv1;      // x6 (0/1) - хватает ли ресурсов для постройки
!!VRy10:Cv10/v11/v12/v13/v14/v15/v16;   // 
!!SN:E6034672/2/y9/x2/8943244;          // 
!!VRx7:Cv10/v11/v12/v13/v14/v15/v16;    // x7..x12 - стоимость здания в соотв. ресурсе (дерево..золото)
!!VRv10:Cy10/y11/y12/y13/y14/y15/y16;   // 

** end