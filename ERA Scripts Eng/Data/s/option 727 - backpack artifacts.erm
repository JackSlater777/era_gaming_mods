ZVSE
ERMS_ScriptDate=12.11(November).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Backpack artifacts
** Name rus.     : Артефакты рюкзака
** Options       : 727
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7885
** PO-values     : -

** Артефакты, производящие ресурсы или повышающие запас хода теперь дают бонусы даже находясь в рюкзаке героя.

!?FU(OnEveryDay)&x1>1;
!!OW:H-1/1/0; !!VRy1:Sv1;               // y1 - кол-во героев текущего игрока
!!re k/1/y1:;
  !!OW:H-1/1/k; !!VRy2:Sv1; !!VRy3:C0/0;// y2/y3/y4 - номер героя/бонус артефактов движения на суше/воде
  !!HEy2:A2/70/?i/?j; !!VRy3&j=0/i>0:+300;  // Перчатки Всадника +300 MP (суща)
  !!HEy2:A2/98/?i/?j; !!VRy3&j=0/i>0:+600;  // Сапоги-Скороходы +600 MP (суша)
  !!HEy2:A2/71/?i/?j; !!VRy4&j=0/i>0:+1000; // Ожерелье Морского Проведения +1000 MP (вода)
  !!HEy2:P?y5/?y6/?y7; !!TRy5/y6/y7:T?j/?i/?i/?i/?i/?i/?i/?i; // j - тип почвы под героем (8 - вода)
  !!HEy2:W?i/1; !!VRi&j<>8:+y3; !!VRi&j=8:+y4; // i - MP героя с учетом бонусов артефактов движения
  !!HEy2:Wi/1 Gi;                       // обновление MP героя
!!en:;

!?FU(OnAfterErmInstructions);           // при старте игры
!!UN:P727/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!FU(Fun_SetDescriptionFromERT):P3/109/1/179351;                // корректировка описаний артефактов и запрет надевания 
!!FU(Fun_SetDescriptionFromERT):P3/110/1/179352;                // ... кроме Рога изобилия и его компонентов (для возможности сборки)
!!FU(Fun_SetDescriptionFromERT):P3/111/1/179353;                // ... кроме Перчаток Всадника для возможности улучшения
!!FU(Fun_SetDescriptionFromERT):P3/112/1/179354; !!UN:A112/2/0; // ... в Сапоги-Скороходы
!!FU(Fun_SetDescriptionFromERT):P3/113/1/179355;                // ... кроме Ожерелье Морского Проведения для возможности сборки
!!FU(Fun_SetDescriptionFromERT):P3/114/1/179356; !!UN:A114/2/0; // ... Шляпы Адмирала
!!FU(Fun_SetDescriptionFromERT):P3/115/1/179357; !!UN:A115/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/116/1/179358; !!UN:A116/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/117/1/179359; !!UN:A117/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/140/1/179360;                // ...
!!FU(Fun_SetDescriptionFromERT):P3/70/1/179189;                 // Перчатки Всадника
!!FU(Fun_SetDescriptionFromERT):P3/98/1/179190; !!UN:A98/2/0;   // Сапоги-Скороходы
!!FU(Fun_SetDescriptionFromERT):P3/71/1/179191;                 // Ожерелье Морского Проведения

!?FU(OnAfterLoadGame);                  // при загрузке игры
!!UN:P727/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!FU(Fun_SetDescriptionFromERT):P3/109/1/179351;                // корректировка описаний артефактов и запрет надевания 
!!FU(Fun_SetDescriptionFromERT):P3/110/1/179352;                // ... кроме Рога изобилия и его компонентов (для возможности сборки)
!!FU(Fun_SetDescriptionFromERT):P3/111/1/179353;                // ... кроме Перчаток Всадника для возможности улучшения
!!FU(Fun_SetDescriptionFromERT):P3/112/1/179354; !!UN:A112/2/0; // ... в Сапоги-Скороходы
!!FU(Fun_SetDescriptionFromERT):P3/113/1/179355;                // ... кроме Ожерелье Морского Проведения для возможности сборки
!!FU(Fun_SetDescriptionFromERT):P3/114/1/179356; !!UN:A114/2/0; // ... Шляпы Адмирала
!!FU(Fun_SetDescriptionFromERT):P3/115/1/179357; !!UN:A115/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/116/1/179358; !!UN:A116/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/117/1/179359; !!UN:A117/2/0; // ...
!!FU(Fun_SetDescriptionFromERT):P3/140/1/179360;                // ...
!!FU(Fun_SetDescriptionFromERT):P3/70/1/179189;                 // Перчатки Всадника
!!FU(Fun_SetDescriptionFromERT):P3/98/1/179190; !!UN:A98/2/0;   // Сапоги-Скороходы
!!FU(Fun_SetDescriptionFromERT):P3/71/1/179191;                 // Ожерелье Морского Проведения

!?FU7885;                        [подсчет в v4..v10 бонуса от артефактов-ресурсников в рюкзаках героев (x16) игрока x1]
!!OW:Hx1/1/x16; !!VRy21:Sv1;     [y21 - номер героя]
!!HEy21:A2/109/?y30/?y40 A2/110/?y31/?y41 A2/111/?y32/?y42 A2/112/?y33/?y43 A2/113/?y34/?y44; [y30..39 - общее кол-во артов]
!!HEy21:A2/114/?y35/?y45 A2/115/?y36/?y46 A2/116/?y37/?y47 A2/117/?y38/?y48 A2/140/?y39/?y49; [y40..49 - кол-во надетых артов]
!!VRy30:-y40; !!VRy30&y30<0:S0;  !!VRy31:-y41; !!VRy31&y31<0:S0; [y30..39 - кол-во артов в рюкзаке]
!!VRy32:-y42; !!VRy32&y32<0:S0;  !!VRy33:-y43; !!VRy33&y33<0:S0; [...]
!!VRy34:-y44; !!VRy34&y34<0:S0;  !!VRy35:-y45; !!VRy35&y35<0:S0; [...]
!!VRy36:-y46; !!VRy36&y36<0:S0;  !!VRy37:-y47; !!VRy37&y37<0:S0; [...]
!!VRy38:-y48; !!VRy38&y38<0:S0;  !!VRy39:-y49; !!VRy39&y39<0:S0; [...]
!!VRy36:*1000; !!VRy37:*750; !!VRy38:*500; !!VRy39:*5;           [y30..39 - бонус ресурса от артефактов в рюкзаке]
!!VRv4:+y35; !!VRv6:+y33; !!VRv10:+y36 +y37 +y38;                [дерево, руда, золото]
!!VRv5:+y32 +y39; !!VRv7:+y34 +y39; !!VRv8:+y30 +y39; !!VRv9:+y31 +y39;        [ртуть, сера, кристаллы, драг.камни]

!?FU(Event_ArtGoldIncomeCalculation); // расчет дохода за артефакты героев игрока (золото)
!!UN:P727/?y1; !!FU&y1=0:E;      [выход, если опция не включена]
!!SN:X?y1/0;
!!VRy2:Sy1 +0; !!UN:Cy2/4/?y3;   [EDI]
!!VRy4:Sy1 +24;!!UN:Cy4/4/?y5;   [ECX]
!!VRy6:Sy1 +8; !!UN:Cy6/4/?y7;   [EBP]
!!VRy8:Sy7 -4; *!UN:Cy8/4/?y9;   [EBP+Gold]
!!VRy10:Sy7 +8;!!UN:Cy10/4/?y11; [y11 - цвет игрока]
!!OW:Hy11/1/0; !!VRy12:Sv1;      [y12 - кол-во героев текущего игрока]
!!VRv4:C0/0/0/0/0/0/0;           [инициализация v4-v10 для подсчета бонусов за ресурсогенерирующие артефакты в рюкзаках героев]
!!DO7885/1/y12/1&y12>0:Py11;     [перебираем героев игрока и подсчитываем бонусы в v4-v10]
!!VRy5:+v10;                     [добавляем бонус золота]
!!UN:Cy2/4/0;                    [xor edi, edi]
!!UN:Cy8/4/y5;                   [mov [ebp+gold], ecx]
!!VRy9:Sy1 +32; !!UN:Cy9/4/5011269;

!?FU(Event_ArtResIncomeCalculation); // расчет дохода за артефакты героев игрока (ресурсы)
!!UN:P727/?y1; !!FU&y1=0:E;      [выход, если опция не включена]
!!SN:X?y1;
!!VRy2:Sy1 +4;   !!UN:Cy2/4/?y3; [ESI] - структура игрока - ресурсы (ртуть)
!!VRy2:Sy3 -268; *!UN:Cy2/4/?y4; [EBX] - что-то в структуре игрока
!!VRy6:Sy1 +8;   !!UN:Cy6/4/?y7; [EBP]
!!VRy7:-4;       !!UN:Cy7/4/?y8; [y8 - цвет игрока в цикле в коде игры в проверке]
!!OW:Hy8/1/0; !!VRy9:Sv1;        [y9 - кол-во героев текущего игрока]
!!VRv4:C0/0/0/0/0/0/0;           [инициализация v4-v10 для подсчета бонусов за ресурсогенерирующие артефакты в рюкзаках героев]
!!DO7885/1/y9/1&y9>0:Py8;        [перебираем героев игрока и подсчитываем бонусы в v4-v10]
!!VRy5:Sy3 -4;   !!UN:Cy5/4/dv4; [дерево]
!!VRy5:Sy3;      !!UN:Cy5/4/dv5; [ртуть]
!!VRy5:Sy3 +4;   !!UN:Cy5/4/dv6; [руда]
!!VRy5:Sy3 +8;   !!UN:Cy5/4/dv7; [сера]
!!VRy5:Sy3 +12;  !!UN:Cy5/4/dv8; [кристаллы]
!!VRy5:Sy3 +16;  !!UN:Cy5/4/dv9; [драг.камни]

** end
