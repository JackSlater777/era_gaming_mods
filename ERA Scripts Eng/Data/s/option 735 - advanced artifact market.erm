ZVSE2
** Author orig.  : igrik, Algor
** Name          : Advanced artifact market
** Name rus.     : Улучшенный рынок артефактов
** Options       : 735
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7893
** PO-values     : -

** Торговцы артефактами в городах и на карте ежедневно вместо выкупленных товаров выкладывают на продажу новые, иногда более редкие артефакты.
; 80% treasue level, 20% minor level


!?FU(OnEveryDay)&i^timerOnce^/i^timerWeekday^=1;
!!UN:P735/?y1;                          [проверяем включена ли опция 735 в y1]
!!FU&y1=0:E;                            [выход если опция не включена]

!!UN:C(GAME_MANAGER)/(UNC_INT)/?y1;

!!re i/128612/128636/(UNC_INT);
  !!VRy2:Sy1+i;
  !!UN:Cy2/(UNC_INT)/?y3;

  !!if&y3<0;                            [если артефакт выкуплен]
    !!VRy4:S0 R4 :4 *2 +2;              [y4 - качество нового артефакта (2, в 20% случаев - 4)]
    !!SN:E5018000/2/y2/y4;              [генерация нового артефакта требуемого качества]
    !!UN:Cy2/(UNC_INT)/v1;              [установка артефакта в слот рынка]
  !!en;
!!en;

** обновление артефактов у торговцев на карте
!!UN:C(GAME_MANAGER)/(UNC_INT)/?y1;
!!UN:Cy1/128644/(UNC_INT)/?y3;          [y3 - _ptr_ первого черного рынка на карте]
!!UN:Cy1/128648/(UNC_INT)/?y5;          [y5 - _ptr_ последнего черного рынка на карте]
!!VRy5:-1;                              [y5, "последнего черного рынка" нет]

!!DO7893/y3/y5/28&y5>y3:Py1;            [циклическая функция прохода по торговцам]

!?FU7893;
; x16 - адрес текущего в цикле черного рынка на карте
!!re i/0/24/4;
  !!UN:Cx16/i/4/?y3;

  !!if&y3<0;                            [если артефакт выкуплен]
    !!VRy4:S0 R4 :4 *2 +2;              [y4 - качество нового артефакта (2, в 20% случаев - 4)]
    !!SN:E5018000/2/x1/y4;              [генерация нового артефакта требуемого качества]
    !!UN:Cx16/i/(UNC_INT)/v1;           [установка артефакта в слот рынка]
  !!en;
!!en;

** end
