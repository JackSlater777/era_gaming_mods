ZVSE2
** Author orig.  : DracoLich
** Updated by    : Archer30
** Name          : Capture the mills and leprechaun's gardens
** Name rus.     : «ахват мельниц и садов лепрекона
** Changes rus.  : [Algor] вынос опции в отдельный файл дл€ мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] технологи€ быстрого поиска дл€ UN:U
** Options       : 879
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3, z1
** Timers        : -
** Functions     : -
** PO-values     : V0, V1 (mills and leprechaun's gardens squares)


28.07.2022 Fully rewritten by daemon_n
15.10.2023 Updated by daemon_n: Added graphics with flags;
15.10.2023 Updated by daemon_n: Now supported capture status in MP game during enemy turn but not added in replay turn;

!?FU(ES_CreateERMHook);
!#VA(address:x);
!!UN:P879/?(isCaptureObjOpt:y);
!!if&(isCaptureObjOpt);
  !!SN:E(address)/1/4864048/(ES_OnPlayerVisit_MillOrFarden); [Trigger on player visiting Mysticism Garden]
  !!SN:E(address)/1/4880976/(ES_OnPlayerVisit_MillOrFarden); [Trigger on player viisiting Water Wheel]
  !!SN:E(address)/1/4881456/(ES_OnPlayerVisit_MillOrFarden); [Trigger on player visiting Wind Mill]
  !!SN:E(address)/1/4263804/(ES_OnMapItem_GetOwner);
  !!SN:E(address)/1/4263370/(ES_OnMapItem_Draw);
  !!SN:E(address)/1/4283008/(ES_Hook_GetObjectRMCDescription);
  !!SN:E(address)/1/5183597/(ES_Hook_OnPlayerDefeated);
!!en;

!?FU(OnGameEnter);
!!UN:P879/?(isCaptureObjOpt:y);
!!if&(isCaptureObjOpt);
  //replace wind mill grass
  !!re i/0/7;(end_value);
    !!SN:R^AVMwndd0.def:0_%i.png^/^Data\Defs\ESwind00.def\0_%i.png^;;
  !!en;
  //replace wind mill snow
  !!re i/0/7;(end_value);
    !!SN:R^AVMwmsn0.def:0_%i.png^/^Data\Defs\ESwind10.def\0_%i.png^;;
  !!en;

  //replace OBJ_WATER_WHEEL
  !!re i/0/7;(end_value);
    !!SN:R^AVMwwhl0.def:0_%i.png^/^Data\Defs\ESwtr00.def\0_%i.png^;;
  !!en;

    //replace OBJ_WATER_WHEEL snow
  !!SN:R^AVMwwsn0.def:0_0.png^/^Data\Defs\ESwtr10.def\0_0.png^;;

  //replace OBJ_MYSTICAL_GARDEN
  !!re i/0/14;(end_value);
    !!SN:R^avtmyst0.def:0_%i.png^/^Data\Defs\ESmyst0.def\0_%i.png^;;
  !!en;
!!en;

!?FU(ES_OnMapItem_GetOwner)&i^ES_879_DrawObjectType^;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-68/4/?(coords:y);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(coords);
  !!PO(x)/(y)/(z):O?(owner:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/(owner);
  !!VRi^ES_879_DrawObjectType^:S(NULL);


!?FU(ES_OnMapItem_Draw);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(mapitem:y) C(mapitem:y)/56/4/?(objType:y);

!!if|(objType)=(OBJ_WATER_WHEEL)/(objType)=(OBJ_MYSTICAL_GARDEN)/(objType)=(OBJ_WINDMILL);
  !!UN:C6916676/4/?(noShowObjectcts:y);
  !!if&(noShowObjectcts)=0;
     !!VRi^ES_879_DrawObjectType^:S(objType);
     !!SN:X?t/0;
     !!UN:Cx1/(STRUCT_HOOK_CONTEXT_RET)/4/4263617;
  !!en; 
!!en;

!?FU(ES_OnPlayerVisit_MillOrFarden)&i^ES_879_isAuto^=(FALSE);
  !!FU(ES_879_OnVisitResourceObject):Px1/?(packedCoords:y)/?(oldOwner:y)/?(newOwner:y);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(packedCoords);
  !!FU(ES_879_MP_SendObjectOwner):D(x)/(y)/(z)/i^timerOwner^;

!?FU(ES_879_MP_SendObjectOwner);
!#VA (packedCoords[3]:x) (newOwner:x) (newRes:x) (newVal:x);
!!POx1/x2/x3:O(newOwner);

!?FU(ES_879_OnVisitResourceObject);
!#VA(addr:x) (packedCoords:x) (oldOwner:x); (newOwner:x) (newRes:x) (newVal:x);
  *!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESP)/4/?(esp:y) C(esp)/16/4/0; emulate AI visit to block message
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/(UNC_INT)/?(ebp:y) C(ebp)/16/(UNC_INT)/?(packedCoords);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(packedCoords);
  !!PO(x)/(y)/(z):O?(oldOwner);

  !!if&(oldOwner)=(NO_OWNER);
    !!PO(x)/(y)/(z):Oi^timerOwner^;
    !!PO(x)/(y)/(z):V0/0 V1/0;          [res type and num]
  !!el;
    !!OW:T(oldOwner)/?(team:y) Ti^timerOwner^/?(newTeam:y);

    !!if&(team)<>(newTeam);
      !!PO(x)/(y)/(z):Oi^timerOwner^;
      !!PO(x)/(y)/(z):V0/0 V1/0;        [res type and num]
    !!en;
  !!en;


// after local visit send new owner to all players - MP support
!$OB(OBJ_MYSTICAL_GARDEN);
  !!FU(ES_879_MP_SendObjectOwner):Dv998/v999/v1000/i^timerOwner^;(newOwner);
!$OB(OBJ_WINDMILL);
  !!FU(ES_879_MP_SendObjectOwner):Dv998/v999/v1000/i^timerOwner^;(newOwner);

!$OB(OBJ_WATER_WHEEL);
  !!FU(ES_879_MP_SendObjectOwner):Dv998/v999/v1000/i^timerOwner^;(newOwner);


!?FU(OnAdvMapTileHint);
!#VA(x:x) (y:x) (z:x);             Object entrance coordinates
!#VA(objType:x) (objSubtype:x);    Type and subtype of object. For active hero it's object under hero.

  !!UN:P879/?(wogOption:y);
  !!FU&(wogOption)<>(TRUE):E;
  !!VRt:S(objType:x);

  !!if&(objType:x)=(OBJ_HERO)/(objSubtype)=(NO_HERO);
    !!FU(ES_AdvMgr_HideHero):P(x)/(y)/(z);
    !!OB(x)/(y)/(z):T?t;
  !!en;

  !!if|t=(OBJ_MYSTICAL_GARDEN)/t=(OBJ_WATER_WHEEL)/t=(OBJ_WINDMILL);
    *!IF:L^%^;
    !!PO(x)/(y)/(z):O?(owner:y);
    ; Correct owner if the owner has been removed from the game
    !!OW:I(owner)/?(isAi:y)/?(hasLost:y);
    !!if&(hasLost);
      !!VR(owner):S(NO_OWNER);
      !!PO(x)/(y)/(z):O(NO_OWNER);
    !!en;
    !!VR(newHint:z):S^^;
    !!if&(owner)>(NO_OWNER);
      !!VR(ownerStrIndex:y):S(owner) +23;
      !!FU(GetTextFileString):P^arraytxt^/(ownerStrIndex)/?(ownerStr:z);
      !!VR(newHint:z):+^ - %(ownerStr)^;
      !!MM:M?(hint:z) M^%(hint:z)%(newHint:z)^;
    !!en;
  !!en;

  !!if&(objType:x)=(OBJ_HERO)/(objSubtype)=(NO_HERO);
    !!FU(ES_AdvMgr_ShowHero):P(x)/(y)/(z);
  !!en;

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1/i^timerOnce^; [поставка ресурсов владельцам мельниц/садов каждый первый день недели, начина€ с 8го дн€]
  !!UN:P879/?(wogOption:y);             [провер€ем включена ли опци€ в y1]
  !!FU&(wogOption)<>(TRUE):E;           [выход, если опци€ не включена]
  !!FU(ES_879_CollectResources):P;

!?FU(ES_Hook_OnPlayerDefeated);
  !!FU(ES_879_CollectResources):P;

!?FU(ES_879_CollectResources);

  !!FU(NewIntArray):P8/?(liveArr:y); 
  !!re (playerID:y)/0/(PLAYER_LAST);
    !!OW:I(playerID)/d/?(isDead:y);
    !!SN&(isDead)=(FALSE):V(liveArr)/(playerID)/(TRUE);
  !!en;

  !!re i/0/(HERO_LAST_WOG);
    !!HEi:O?(owner:y);

    !!if&(owner)=(NO_OWNER);
      !!HEi:Z?(hero:y);
      !!br;
    !!en;
  !!en;

  !!VRi^ES_879_isAuto^:S(TRUE);
  !!FU(ES_879_IterateObjects):P4864048/(OBJ_MYSTICAL_GARDEN)/(hero)/(liveArr);
  !!FU(ES_879_IterateObjects):P4880976/(OBJ_WATER_WHEEL)/(hero)/(liveArr);
  !!FU(ES_879_IterateObjects):P4881456/(OBJ_WINDMILL)/(hero)/(liveArr);
  !!UN:C(hero)/34/1/(NO_OWNER);
  !!VRi^ES_879_isAuto^:S(FALSE);


!?FU(ES_879_IterateObjects); 
!#VA(funcAddr:x) (objType:x) (hero:x) (liveArr:x);
!#VA(x:y) (y:y) (z:y); 

!!VR(x):S-1;                                                  [init as start]

!!re i;                                                       [endless loop]
  !!UN:U(objType)/(ANY_OBJ)/-1/(x)/(y)/(z); 
  !!br&(x)<0;                                                 [exit loop if nothing found]

  !!PO(x)/(y)/(z):O?(owner:y);                                [get obj owner]

  !!if&(owner)<>(NO_OWNER);                                    [if there is one]
    !!SN:V(liveArr)/(owner)/?(isAlive:y);                     [check if lives]

    !!if&(isAlive);
      !!UN:C(hero)/34/1/(owner);                              [set hero as obj owner color]
      !!OB(x)/(y)/(z):T?(heroCheck:y);

      !!if&(heroCheck)=(OBJ_HERO);
        !!HE(x)/(y)/(z):Z?(badHero:y);
        !!SN:E5077328/(CALLCONV_THISCALL)/(badHero);         [hide hero if it is there]
      !!en;

      !!FU(ES_AdvMgr_GetMapItem):P(x)/(y)/(z)/?(mapItem:y);  [get mapItem]
      !!FU(ES_879_AdjustStoredRes):P(x)/(y)/(z)/(objType);
      !!SN:E(funcAddr)/(CALLCONV_STDCALL)/(hero)/(mapItem)/0; [visit mapItem as AI]
    !!el;
      !!PO(x)/(y)/(z):O(NO_OWNER) V0/0 V1/0;                            [set no owner if is dead]
      !!FU(ES_879_MP_SendObjectOwner):P(x)/(y)/(z)/(NO_OWNER);
    !!en;
  !!en;
!!en;

!?FU(ES_879_AdjustStoredRes);
!#VA(x:x) (y:x) (z:x) (objType:x); 

!!if&(objType)=(OBJ_MYSTICAL_GARDEN);
  !!GD(x)/(y)/(z):B?(resType:y);
  !!VR(value:y):S5;
  !!VR(value)&(resType)=(RES_GOLD):S500;
!!el&(objType)=(OBJ_WATER_WHEEL);
  !!WM(x)/(y)/(z):B?(resCoeff:y);
  !!VR(value:y):S(resCoeff)*500;
  !!VR(resType):S(RES_GOLD);
!!el;
  !!ML(x)/(y)/(z):B?(resType:y)/?(value:y);
!!en;

!!PO(x)/(y)/(z):V0/(resType) V1/(value);




!?FU(ES_Hook_GetObjectRMCDescription);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/?(mapItem:y);
!!if&(mapItem)>(BIT_16);
  !!UN:C(mapItem:y)/30/2/?(objType:y) C(mapItem:y)/34/2/?(objSubtype:y) C(ebp)/-32/4/?(coords:y);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(coords);

  !!SN:O?(x:y)/?(y:y)/?(z:y);
  !!VRt:S(objType);

  !!if&(objType)=(OBJ_HERO)/(objSubtype)=(NO_HERO);
    !!FU(ES_AdvMgr_HideHero):P(x)/(y)/(z);
    !!OB(x)/(y)/(z):T?t;
  !!en;

  !!if|t=(OBJ_MYSTICAL_GARDEN)/t=(OBJ_WATER_WHEEL)/t=(OBJ_WINDMILL);
    !!PO(x)/(y)/(z):O?(owner:y);
    !!if&(owner)<>(NO_OWNER);
      !!UN:Cx1/(STRUCT_HOOK_CONTEXT_ECX)/4/?(textBuffer:y);
      !!FU(ES_GameMgr_GetObjOwnerStr):P(owner)/?(ownerStr:z);
      !!SN:B(textBuffer)/d/?(currHint:z) B(textBuffer)/d/^%(currHint:z)%T(es.endl)%(ownerStr:z)^;
    !!en;
  !!en;

  !!if&(objType)=(OBJ_HERO)/(objSubtype)=(NO_HERO);
    !!FU(ES_AdvMgr_ShowHero):P(x)/(y)/(z);
  !!en;
!!en;



*?FU(ES_879_GetObjectName);
*#VA(x:x) (y:x) (z:x) (objType:x) (objNamePtr:x);

  ; Set up object names
  *!FU(GetTextFileString):P^objnames^/(objType)/?(objName:z); [windmill]
  ; Set up "Improved" object text if Mithril Enhancedment is enabled
  *!VR(isUpg:y):S(FALSE);
  *!UN:P36/?(enhMithril:y);
  *!if&(enhMithril:y);
    *!PO(x)/(y)/(z):N?(isUpg);
    *!if&(isUpg);
      *!SN:T^wog.36.hint.%(objType)^/?(upg:z);
      *!VR(objName):S^%(upg:z)%(objName)^;
    *!en;
  *!en;
  *!VR(objNamePtr):Z(objName);

** end
