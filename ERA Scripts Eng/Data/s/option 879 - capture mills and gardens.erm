ZVSE2
** Author orig.  : DracoLich
** Updated by    : Archer30
** Name          : Capture the mills and leprechaun's gardens
** Name rus.     : «ахват мельниц и садов лепрекона
** Changes rus.  : [Algor] вынос опции в отдельный файл дл€ мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] технологи€ быстрого поиска дл€ UN:U
** Options       : 879
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3, z1
** Timers        : -
** Functions     : -
** PO-values     : V0,V1 (mills and leprechaun's gardens squares)

** дублирование стандартных триггеров


!?OB(OBJ_WATER_WHEEL);        [посещение ¬од€ной мельницы игроком-человеком]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!WM998&y1=-1:B?y3 B0;        [y3 - количество золота на мельнице, если мельница ничейна€]
!!VRy3&y1=-1:*500;            [...]
!!OW&y1=-1:R-1/6/dy3;         [увеличиваем игроку золото, если мельница была ничейной]
!!PO998&y1=-1:V0/6 V1/y3;     [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.wheelSent)^;     [сообщение, если игрок уже получал золото]
!!IF&y1=-1:Q2/6/y3/1^%T(es.879.wheelGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.wheelOwned)^; [сообщение о захвате мельницы]

!?OB(OBJ_WINDMILL);           [посещение ¬етр€ной мельницы]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!ML998&y1=-1:B?y3/?y4 Bd/0;  [y3/y4 - тип/количество ресурса на мельнице, если мельница ничейна€]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если мельница была ничейной]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.millSent)^;      [сообщение, если игрок уже получал ресурсы]
!!IF&y1=-1:Q2/y3/y4/1^%T(es.879.millGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.millOwned)^; [сообщение о захвате мельницы]

!?OB(OBJ_MYSTICAL_GARDEN);    [посещение —ада лепрекона]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!GD998:B?y3; B0;              [y3/y4 - тип/количество ресурса в саду, если сад ничейный]
!!VRy4&y3<>6:S5;              [...]
!!VRy4&y3=6:S500;             [...]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если сад был ничейным]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.gardenSent)^;    [сообщение, если игрок уже получал ресурсы]
!!IF&y1=-1:Q2/y3/y4/1^%T(es.879.gardenGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.gardenOwned)^; [сообщение о захвате сада]

!?FU(OnAdventureMapRightMouseClick);
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;

!!CM:P?v2/?v3/?v4;            [v2-v4 - координаты клика]
!!SN:O?v2/?v3/?v4;            [корректировка координат]         
!!OB2:T?y1;                   [y1 - тип объекта]
!!FU&y1<>112/y1<>109/y1<>55:E;[выход если клик не на мельнице/садке]

!!CM:R0;
!!PO2:O?y2;                   [y2 - владелец объекта]
!!OW:C?y3;                    [y3 - текущий игрок]
!!VRy10:S(FALSE);
!!UN:P36/?y9;                 [y9 - проверка скрипта Mithril Enhancements]
!!PO2&y9>0:N?y10;             [y10 - пользовательское число, если Mithril Enhancements активен (см. скрипт 42wog)]

; Set up object names
!!FU(ES_GetStringByFilename):P^objnames^/(OBJ_WINDMILL)/?z2; [windmill]
!!FU(ES_GetStringByFilename):P^objnames^/(OBJ_WATER_WHEEL)/?z3; [water wheel]
!!FU(ES_GetStringByFilename):P^objnames^/(OBJ_MYSTICAL_GARDEN)/?z4; [myystical garden]

; Set up "Improved" object text if Mithril Enhancedment is enabled
!!if&y10;
  !!VRz5:S^%T(es.879.improved)^ +z2;
  !!VRz6:S^%T(es.879.improved)^ +z3;
!!en;

!!SN:T^es.879.owner^/?z6;

!!if&y1=(OBJ_WINDMILL);
  !!ify2<>y3;
    !!SN&y2=-1:T^es.879.rmb^/?z1/^obj^/z2/^owner^/^^; [сообщение "ничейна€ возд. мельница"]
    !!SN&y2>-1:T^es.879.rmb^/?z1/^obj^/z2/^owner^/z6; [сообщение "чужа€ возд. мельница"]
  !!el;
    !!SN:T^es.879.wheelBrought^/?z1/^obj^/z2; [сообщение "сво€ ветр€на€ мельница"]
  !!en;

  !!if&y10<4/y10>1;
    !!ify2<>y3;
      !!SN&y2=-1:T^es.879.rmb^/?z1/^obj^/z5/^owner^/^^; [сообщение "ничейна€ улучшенна€ возд. мельница"]
      !!SN&y2>-1:T^es.879.rmb^/?z1/^obj^/z5/^owner^/z6; [сообщение "чужа€ улучшенна€ возд. мельница"]
    !!en;

    !!SN&y2=y3:T^es.879.wheelBrought^/?z1/^obj^/z5; [сообщение "сво€ улучшенна€ ветр€на€ мельница"]
  !!en;
!!el&y1=(OBJ_WATER_WHEEL);
  !!ify2<>y3;
    !!SN&y2=-1:T^es.879.rmb^/?z1/^obj^/z3/^owner^/^^; [сообщение "ничейна€ водн. мельница"]
    !!SN&y2>-1:T^es.879.rmb^/?z1/^obj^/z3/^owner^/z6; [сообщение "чужа€ водн. мельница"]
  !!el;
    !!SN:T^es.879.millBrought^/?z1/^obj^/z3; [сообщение "сво€ водн. мельница"]
  !!en;

  !!if&y10<4/y10>1;
    !!if&y2<>y3;
      !!SN&y2=-1:T^es.879.rmb^/?z1/^obj^/z6/^owner^/^^; [сообщение "ничейна€ улучшенна€ водн. мельница"]
      !!SN&y2>-1:T^es.879.rmb^/?z1/^obj^/z6/^owner^/z6; [сообщение "чужа€ улучшенна€ водн. мельница"]
    !!el;
      !!SN:T^es.879.millBrought^/?z1/^obj^/z6; [сообщение "сво€ улучшенна€ водн. мельница"]
    !!en;
  !!en;
!!el&y1=(OBJ_MYSTICAL_GARDEN);
  !!if&y2<>y3;
    !!SN&y2=-1:T^es.879.rmb^/?z1/^obj^/z4/^owner^/^^; [сообщение "ничейный сад"]
    !!SN&y2>-1:T^es.879.rmb^/?z1/^obj^/z4/^owner^/z6; [сообщение "чужой сад"]
  !!el;
    !!SN:T^es.879.gardenBrought^/?z1/^obj^/z4;  [сообщение "свой сад"]
  !!en;
!!en;

!!PO2&y2=y3:V0/?y4 V1/?y5;    [y4/y5 - тип и количество принесенных ресурсов, если объект свой]
!!IF&y2=-1:Q2/-1/-1/4/z1;     [вывод сообщени€ "ничейный"]
!!IF&y3<>y2/y2>-1:Q2/10/y2/4/z1;[вывод сообщени€ "чужой"]
!!IF&y3=y2:Q2/y4/y5/4/z1;     [вывод сообщени€ "свой"]

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1; [поставка ресурсов владельцам мельниц/садов каждый первый день недели, начина€ с 8го дн€]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!OW:C?y1;                    [y1 - текущий игрок]
!!UN:U(OBJ_WINDMILL)/-1/?y2;             [y2 - количество ¬етр€ных мельниц]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/112; [дл€ каждого объекта передаем ресурсы владельцу]
!!UN:U(OBJ_WATER_WHEEL)/-1/?y2;             [y2 - количество ¬одных мельниц]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/109; [дл€ каждого объекта передаем ресурсы владельцу]
!!UN:U(OBJ_MYSTICAL_GARDEN)/-1/?y2;              [y2 - количество —адов лепрекона]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/55; [дл€ каждого объекта передаем ресурсы владельцу]

!?FU(ES_879_CollectResources); [x1 - текущий игрок, x2 - тип объекта]
!!UN:Ux2/-1/-1/1;             [получаем координаты объекта в v1-v3]
!!PO1:O?y1;                   [y1 - владелец объекта]
!!FU&y1<>x1:E;                [выход, если текущий игрок не владелец]

!!UN:P36/?y9;                 [y9 - проверка скрипта Mithril Enhancements]
!!PO1&y9>0:N?y10;             [y10 - пользовательское число, если Mithril Enhancements активен (см. скрипт 42wog)]
!!ML1&x2=112:B?y2/?y3 By2/0;  [y2/y3 - тип/количество текущих ресурсов объекта. обнул€ем количество]
!!VRy3&x2=112/y9>0/y10<4/y10>1:*2; [*2 количества ресурсов за апгрейд]
!!WM1&x2=109:B?y3 B0;         [...]
!!VRy3&x2=109:*500;           [...]
!!VRy2&x2=109:S6;             [...]
!!VRy3&x2=109/y9>0/y10<4/y10>1:*2; [х2 количества денег за апгрейд]
!!GD1&x2=55:B?y2;             [...]
!!VRy3&x2=55/y2<>6:S5;        [...]
!!VRy3&x2=55/y2=6:S500;       [...]
!!PO1:V0/y2 V1/y3;            [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!OW:Rx1/y2/dy3;              [увеличиваем ресурсы игрока]

** end
