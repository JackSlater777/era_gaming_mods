ZVSE2
** Author orig.  : DracoLich
** Updated by    : Archer30
** Name          : Capture the mills and leprechaun's gardens
** Name rus.     : «ахват мельниц и садов лепрекона
** Changes rus.  : [Algor] вынос опции в отдельный файл дл€ мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] технологи€ быстрого поиска дл€ UN:U
** Options       : 879
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3, z1
** Timers        : -
** Functions     : -
** PO-values     : V0,V1 (mills and leprechaun's gardens squares)

** дублирование стандартных триггеров


!?OB(OBJ_WATER_WHEEL);        [посещение ¬од€ной мельницы игроком-человеком]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!WM998&y1=-1:B?y3 B0;        [y3 - количество золота на мельнице, если мельница ничейна€]
!!VRy3&y1=-1:*500;            [...]
!!OW&y1=-1:R-1/6/dy3;         [увеличиваем игроку золото, если мельница была ничейной]
!!PO998&y1=-1:V0/6 V1/y3;     [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.wheelSent)^;     [сообщение, если игрок уже получал золото]
!!IF&y1=-1:Q2/6/y3/1^%T(es.879.wheelGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.wheelOwned)^; [сообщение о захвате мельницы]

!?OB(OBJ_WINDMILL);           [посещение ¬етр€ной мельницы]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!ML998&y1=-1:B?y3/?y4 Bd/0;  [y3/y4 - тип/количество ресурса на мельнице, если мельница ничейна€]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если мельница была ничейной]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.millSent)^;      [сообщение, если игрок уже получал ресурсы]
!!IF&y1=-1:Q2/y3/y4/1^%T(es.879.millGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.millOwned)^; [сообщение о захвате мельницы]

!?OB(OBJ_MYSTICAL_GARDEN);    [посещение —ада лепрекона]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!PO998:O?y1;                 [y1 - хоз€ин объекта]
!!OW:C?y2;                    [y2 - текущий игрок]
!!OB998:R;                    [разрешаем посещение объекта всем игрокам]
!!PO998:Oy2;                  [мен€ем владельца мельницы]
!!GD998:B?y3; B0;              [y3/y4 - тип/количество ресурса в саду, если сад ничейный]
!!VRy4&y3<>6:S5;              [...]
!!VRy4&y3=6:S500;             [...]
!!OW&y1=-1:R-1/y3/dy4;        [увеличиваем игроку ресурсы, если сад был ничейным]
!!PO998&y1=-1:V0/y3 V1/y4;    [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!FU&-1000:E;                 [выход, если игрок »»]

!!OB998:S;                    [запрещаем посещение объекта всем игрокам]
!!IF&y1=y2:M^%T(es.879.gardenSent)^;    [сообщение, если игрок уже получал ресурсы]
!!IF&y1=-1:Q2/y3/y4/1^%T(es.879.gardenGives)^; [сообщение о захвате и получении ресурсов]
!!IF&y1<>y2/y1>-1:M^%T(es.879.gardenOwned)^; [сообщение о захвате сада]

!?FU(OnAdventureMapRightMouseClick);
!!UN:P879/?(wogOption:y);               [провер€ем включена ли опци€]
!!FU&(wogOption)<>(TRUE):E;

!!CM:P?(x:y)/?(y:y)/?(z:y);             [координаты клика]
!!SN:O?(x)/?(y)/?(z);                   [корректировка координат]         
!!OB(x)/(y)/(z):T?(objType:y);          [тип объекта]
!!FU&(objType)<>(OBJ_WINDMILL)/(objType)<>(OBJ_WATER_WHEEL)/(objType)<>(OBJ_MYSTICAL_GARDEN):E;[выход если клик не на мельнице/садке]

!!CM:R0;

!!PO(x)/(y)/(z):O?(owner:y);            [владелец объекта]
!!OW:C?(player:y)/?(interactPlayer:y);  [текущий игрок]

!!FU(ES_879_GetObjectName):P(x)/(y)/(z)/(objType)/?(rmbStr:z);

!!if&(owner)>(NO_OWNER);
  !!VR(ownerStrIndex:y):S(owner) +23;
  !!FU(ES_GetStringByFilename):P^arraytxt^/(ownerStrIndex)/?(ownerStr:z);
  !!VR(rmbStr):+^
%(ownerStr)^;
!!en;

; Set up text for what have been brought this week if the clicked objects belongs to the interacting player
!!if&(owner)=(interactPlayer);
  !!if&(objType)=(OBJ_WINDMILL);
    !!VR(rmbStr):+^%T(es.879.millBrought)^;   [сообщение "сво€ ветр€на€ мельница"]
  !!el&(objType)=(OBJ_WATER_WHEEL);
    !!VR(rmbStr):+^%T(es.879.wheelBrought)^;  [сообщение "сво€ водн. мельница"]
  !!el&(objType)=(OBJ_MYSTICAL_GARDEN);
    !!VR(rmbStr):+^%T(es.879.gardenBrought)^; [сообщение "свой сад"]
  !!en;
!!en;

; Show up the text
!!if&(owner)=(NO_OWNER);
  !!IF:M0/(MSG_TYPE_POPUP)/(rmbStr);    [вывод сообщени€ "ничейный"]
!!el;
  !!if&(owner)=(interactPlayer);
    !!PO(x)/(y)/(z):V0/?(res:y) V1/?(num:y); [тип и количество принесенных ресурсов, если объект свой]
    !!IF:Q2/(res)/(num)/(MSG_TYPE_POPUP)/(rmbStr); [вывод сообщени€ "свой"]
  !!el;
    !!IF:Q2/(PIC_TYPE_FLAG)/(owner)/(MSG_TYPE_POPUP)/(rmbStr); [вывод сообщени€ "чужой"]
  !!en;
!!en;

!?FU(ES_879_GetObjectName);
!#VA(x:x) (y:x) (z:x) (objType:x) (objNamePtr:x);

; Set up object names
!!if&(objType)=(OBJ_WINDMILL);
  !!FU(ES_GetStringByFilename):P^objnames^/(OBJ_WINDMILL)/?(objName:z); [windmill]
!!el&(objType)=(OBJ_WATER_WHEEL);
  !!FU(ES_GetStringByFilename):P^objnames^/(OBJ_WATER_WHEEL)/?(objName); [water wheel]
!!el&(objType)=(OBJ_MYSTICAL_GARDEN);
  !!FU(ES_GetStringByFilename):P^objnames^/(OBJ_MYSTICAL_GARDEN)/?(objName); [mystical garden]
!!en;

; Set up "Improved" object text if Mithril Enhancedment is enabled
!!VR(isUpg:y):S(FALSE);
!!UN:P36/?(enhMithril:y);
!!PO(x)/(y)/(z)&(enhMithril):N?(isUpg);
!!VR(objName)&(isUpg):S^%T(es.879.improved)%(objName)^;
!!VR(objNamePtr):Z(objName);

!?FU(OnAdvMapTileHint);
!#VA(x:x) (y:x) (z:x);             Object entrance coordinates
!#VA(objType:x) (objSubtype:x);    Type and subtype of object. For active hero it's object under hero.

!!UN:P879/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!FU&(objType)<>(OBJ_WINDMILL)/(objType)<>(OBJ_WATER_WHEEL)/(objType)<>(OBJ_MYSTICAL_GARDEN):E;

!!PO(x)/(y)/(z):O?(owner:y);
!!FU(ES_879_GetObjectName):P(x)/(y)/(z)/(objType)/?(newHint:z);

!!if&(owner)>(NO_OWNER);
  !!VR(ownerStrIndex:y):S(owner) +23;
  !!FU(ES_GetStringByFilename):P^arraytxt^/(ownerStrIndex)/?(ownerStr:z);
  !!VR(newHint):+^ - %(ownerStr)^;
!!en;

!!MM:M^%(newHint)^;

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1; [поставка ресурсов владельцам мельниц/садов каждый первый день недели, начина€ с 8го дн€]
!!UN:P879/?y1;                [провер€ем включена ли опци€ в y1]
!!FU&y1=0:E;                  [выход, если опци€ не включена]

!!OW:C?y1;                    [y1 - текущий игрок]
!!UN:U(OBJ_WINDMILL)/-1/?y2;             [y2 - количество ¬етр€ных мельниц]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/112; [дл€ каждого объекта передаем ресурсы владельцу]
!!UN:U(OBJ_WATER_WHEEL)/-1/?y2;             [y2 - количество ¬одных мельниц]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/109; [дл€ каждого объекта передаем ресурсы владельцу]
!!UN:U(OBJ_MYSTICAL_GARDEN)/-1/?y2;              [y2 - количество —адов лепрекона]
!!VRv1:S-1;                   [инициализаци€ v1 дл€ быстрого поиска]
!!DO(ES_879_CollectResources)/1/y2/1:Py1/55; [дл€ каждого объекта передаем ресурсы владельцу]

!?FU(ES_879_CollectResources); [x1 - текущий игрок, x2 - тип объекта]
!!UN:Ux2/-1/-1/1;             [получаем координаты объекта в v1-v3]
!!PO1:O?y1;                   [y1 - владелец объекта]
!!FU&y1<>x1:E;                [выход, если текущий игрок не владелец]

!!UN:P36/?y9;                 [y9 - проверка скрипта Mithril Enhancements]
!!PO1&y9>0:N?y10;             [y10 - пользовательское число, если Mithril Enhancements активен (см. скрипт 42wog)]

!!if&x2=(OBJ_WINDMILL);
  !!ML1:B?y2/?y3 By2/0;  [y2/y3 - тип/количество текущих ресурсов объекта. обнул€ем количество]
!!el&x2=(OBJ_WATER_WHEEL);
  !!WM1:B?y3 B0;         [...]
  !!VRy3:*500;           [...]
  !!VRy2:S6;             [...]
!!el&x2=(OBJ_MYSTICAL_GARDEN);
  !!GD1:B?y2;             [...]
  !!VRy3&y2<>6:S5;        [...]
  !!VRy3&y2=6:S500;       [...]
!!en;

!!PO1:V0/y2 V1/y3;            [устанавливаем тип/количество текущих ресурсов в V0/V1 значени€ клетки]
!!OW:Rx1/y2/dy3;              [увеличиваем ресурсы игрока]

** end
