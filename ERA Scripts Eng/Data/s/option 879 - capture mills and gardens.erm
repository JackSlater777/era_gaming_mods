ZVSE2
** Author orig.  : DracoLich
** Updated by    : Archer30
** Name          : Capture the mills and leprechaun's gardens
** Name rus.     : «ахват мельниц и садов лепрекона
** Changes rus.  : [Algor] вынос опции в отдельный файл дл€ мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] технологи€ быстрого поиска дл€ UN:U
** Options       : 879
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3, z1
** Timers        : -
** Functions     : -
** PO-values     : V0, V1 (mills and leprechaun's gardens squares)

** дублирование стандартных триггеров
*?FU(ES_OnMapItem_HasFlag);
*!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(objType:y);
*!IF:L^%(objType:y)^;
*!if&(objType)=(OBJ_WATER_WHEEL);
  *!IF:L^1^;
*!en;
28.07.2022 Fully rewritten by daemon_n
!?FU(ES_CreateERMHook);
!#VA(address:x);
!!UN:P879/?(isCaptureObjOpt:y);
!!if&(isCaptureObjOpt);
  !!SN:E(address)/1/4864048/(ES_OnPlayerVisit_MystGarden); [Trigger on player visiting Mysticism Garden]
  !!SN:E(address)/1/4880976/(ES_OnPlayerVisit_WaterWheel); [Trigger on player viisiting Water Wheel]
  !!SN:E(address)/1/4881456/(ES_OnPlayerVisit_WindMill);   [Trigger on player visiting Wind Mill]
  !!SN:E(address)/1/4263804/(ES_OnMapItem_GetOwner);
  !!SN:E(address)/1/4263370/(ES_OnMapItem_Draw);

!!en;
!?FU(OnGameEnter);
!!UN:P879/?(isCaptureObjOpt:y);
!!if&(isCaptureObjOpt);
  //replace wind mill grass
  !!re i/0/7;(end_value);
    !!SN:R^AVMwndd0.def:0_%i.png^/^Data\Defs\ESwind00.def\0_%i.png^;;
  !!en;
  //replace wind mill snow
  !!re i/0/7;(end_value);
    !!SN:R^AVMwmsn0.def:0_%i.png^/^Data\Defs\ESwind10.def\0_%i.png^;;
  !!en;


  //replace OBJ_WATER_WHEEL
  !!re i/0/7;(end_value);
    !!SN:R^AVMwwhl0.def:0_%i.png^/^Data\Defs\ESwtr00.def\0_%i.png^;;
  !!en;

    //replace OBJ_WATER_WHEEL snow
  !!SN:R^AVMwwsn0.def:0_0.png^/^Data\Defs\ESwtr10.def\0_0.png^;;

  //replace OBJ_MYSTICAL_GARDEN
  !!re i/0/14;(end_value);
    !!SN:R^avtmyst0.def:0_%i.png^/^Data\Defs\ESmyst0.def\0_%i.png^;;
  !!en;
!!en;

!?FU(ES_OnMapItem_GetOwner)&i^ES_879_DrawObjectType^;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-68/4/?(coords:y);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(coords);
  !!PO(x)/(y)/(z):O?(owner:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/(owner);?(objType:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(mapitem:y) C(mapitem:y)/56/4/i^ES_879_DrawObjectType^; [restore obj type]

  !!VRi^ES_879_DrawObjectType^:S(NULL);


!?FU(ES_OnMapItem_Draw);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EDI)/4/?(mapitem:y) C(mapitem:y)/56/4/?(objType:y);
*!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/?(objType:y);

!!if|(objType)=(OBJ_WATER_WHEEL)/(objType)=(OBJ_MYSTICAL_GARDEN)/(objType)=(OBJ_WINDMILL);
  !!VRi^ES_879_DrawObjectType^:S(objType);
  !!UN:C(mapitem:y)/56/4/(OBJ_CREATURE_GENERATOR_1);                                      [emulate obj gen 1 to get color]
!!en;
!?FU(ES_OnPlayerVisit_MystGarden)&i^ES_879_isAuto^=(FALSE);
  !!FU(ES_879_OnVisitResourceObject):Px1/?(owner:y);

  *!if&999;
    *!GD2:B?(resType:y);
    *!VR(value:y):S5;
    *!VR(value)&(resType)=(RES_GOLD):S500;
    *!FU(ES_879_ShowMessage):P(owner)/(OBJ_MYSTICAL_GARDEN)/(resType)/(value);
  *!en;

!?FU(ES_OnPlayerVisit_WaterWheel)&i^ES_879_isAuto^=(FALSE);
  !!FU(ES_879_OnVisitResourceObject):Px1/?(owner:y);

  *!if&999;
    *!WM2:B?(value:y);
    *!VR(value):*500;
    *!FU(ES_879_ShowMessage):P(owner)/(OBJ_WATER_WHEEL)/(RES_GOLD)/(value);
  *!en;  

!?FU(ES_OnPlayerVisit_WindMill)&i^ES_879_isAuto^=(FALSE);
  !!FU(ES_879_OnVisitResourceObject):Px1/?(owner:y);

  *!if&999;
    *!ML2:B?(resType:y)/?(value:y);
    *!FU(ES_879_ShowMessage):P(owner)/(OBJ_WINDMILL)/(resType)/(value);
  *!en;


!?FU(ES_879_ShowMessage);
!#VA(owner:x) (objType:x) (res:x) (value:x);

  !!if&(owner)<>i^timerOwner^;
    !!if&(owner)=(NO_OWNER);
      !!SN:T^es.879.%(objType)_Gives^/?(msg:z);
      !!IF:Q2/(res)/(value)/(MSG_TYPE_MES)/(msg);
    !!el;
      !!OW:T(owner)/?(team:y) Ti^timerOwner^/?(newTeam:y);

      ; Show message if the owner and the visiting player are on different teams
      !!if&(team)<>(newTeam);
        !!SN:T^es.879.%(objType)_Owned^/?(msg:z);
        !!IF:M(msg);
      !!en;
    !!en;
  !!el;
    !!SN:T^es.879.%(objType)_Sent^/?(msg:z);
    !!IF:M(msg);
  !!en;

!?FU(ES_879_OnVisitResourceObject);
!#VA(addr:x) (owner:x);
  *!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESP)/4/?(esp:y) C(esp)/16/4/0;
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/(UNC_INT)/?(ebp:y) C(ebp)/16/(UNC_INT)/?(coords:y);
  !!FU(ES_UnPackedCoords):P?(x:y)/?(y:y)/?(z:y)/(coords);/8/?(sth:y)/8;
  !!VRv2:C(x)/(y)/(z);
  !!PO(x)/(y)/(z):O?(owner);

  !!if&(owner)=(NO_OWNER);
    !!PO(x)/(y)/(z):Oi^timerOwner^;
    !!PO(x)/(y)/(z):V0/0 V1/0;          [res type and num]
  !!el;
    !!OW:T(owner)/?(team:y) Ti^timerOwner^/?(newTeam:y);

    !!if&(team)<>(newTeam);
      !!PO(x)/(y)/(z):Oi^timerOwner^;
      !!PO(x)/(y)/(z):V0/0 V1/0;        [res type and num]
    !!en;
  !!en;

  !!if&(ERM_FLAG_IS_HUMAN);
    !!HE(CURRENT_HERO):P?(xH:y)/?(yH:y)/?(zH:y);

    !!if|(xH)<>(x)/(yH)<>(y)/(zH)<>(z);
      !!VRi^ES_879_isRMC^:S(TRUE);
    !!en;
  !!en;


!?FU(OnAdventureMapRightMouseClick_Quit)&i^ES_879_isRMC^;
  !!VRi^ES_879_isRMC^:S(FALSE);

!?FU(OnAdventureMapRightMouseClick)&i^ES_879_isRMC^<>(TRUE);
  !!UN:P879/?(wogOption:y);               [провер€ем включена ли опци€]
  !!FU&(wogOption)<>(TRUE):E;

  !!OW:C?(player:y)/?(interactPlayer:y) A(interactPlayer)/?(activeHero:y);

  !!if&(activeHero)<>(NO_HERO);
    !!HE(activeHero):P?(x:y)/?(y:y)/?(z:y);
    !!FU&i^mouse_mapX^=(x)/i^mouse_mapY^=(y)/i^mouse_mapZ^=(z):E; // exit if click on hero
  !!en;

  !!CM:P?(x:y)/?(y:y)/?(z:y);             [координаты клика]

  !!OB(x)/(y)/(z):T?(objType:y);          [тип объекта]
  !!FU&(objType)<>(OBJ_WINDMILL)/(objType)<>(OBJ_WATER_WHEEL)/(objType)<>(OBJ_MYSTICAL_GARDEN):E;[выход если клик не на мельнице/садке]

  !!SN:O?(x)/?(y)/?(z);                   [корректировка координат]         
  !!PO(x)/(y)/(z):O?(owner:y);            [владелец объекта]

  !!if&(owner)>(NO_OWNER);
    ; Disable standard action
    !!CM:R0;
    ; Set up strings
    !!FU(ES_879_GetObjectName):P(x)/(y)/(z)/(objType)/?(rmbStr:z);
    !!FU(ES_GameMgr_GetObjOwnerStr):P(owner)/?(ownerStr:z);
    !!VR(rmbStr):+^%T(es.endl)%(ownerStr)^;

    !!VR(res:y):S(NO_PIC_TYPE);
    !!VR(num:y):S(NO_PIC_TYPE);

    ; Set up text for what have been brought this week if the clicked objects belongs to the interacting player
    !!if&(owner)=(interactPlayer);
      !!PO(x)/(y)/(z):V0/?(res:y) V1/?(num:y); [тип и количество принесенных ресурсов, если объект свой]

      !!if&(num)=0;
        !!VR(res:y):S(NO_PIC_TYPE);
        !!VR(num:y):S(NO_PIC_TYPE);
      !!el;
        !!SN:T^es.879.%(objType)_Brought^/?(txt:z);
        !!VR(rmbStr):+(txt);
      !!en;
    !!en;

    !!IF:Q2/(PIC_TYPE_FLAG)/(owner)/(res)/(num)/(MSG_TYPE_POPUP)/(rmbStr); [вывод сообщени€ "чужой"]
  !!en;


!?FU(OnAdvMapTileHint);
!#VA(x:x) (y:x) (z:x);             Object entrance coordinates
!#VA(objType:x) (objSubtype:x);    Type and subtype of object. For active hero it's object under hero.
!#VA(tileX:x) (tileY:x) (tileZ:x); Real tile coordinates.

  !!UN:P879/?(wogOption:y);
  !!FU&(wogOption)<>(TRUE):E;


  !!FU&(objType)<>(OBJ_WINDMILL)/(objType)<>(OBJ_WATER_WHEEL)/(objType)<>(OBJ_MYSTICAL_GARDEN):E;

  !!OW:Cd/?(clickedPlayer:y) A(clickedPlayer:y)/?(activeHero:y);
  !!if&(activeHero)<>(NO_HERO);/(objType)=(OBJ_FIRST);
    !!HE(activeHero):P?(xH:y)/?(yH:y)/?(zH:y);
    !!FU&(xH)=(tileX)/(yH)=(tileY)/(zH)=(tileZ):E;
  !!en;

  !!PO(x)/(y)/(z):O?(owner:y);

  ; Correct owner if the owner has been removed from the game
  !!OW:I(owner)/?(isAi:y)/?(hasLost:y);
  !!if&(hasLost);
  	!!VR(owner):S(NO_OWNER);
	!!PO(x)/(y)/(z):O(NO_OWNER);
  !!en;
  !!FU(ES_879_GetObjectName):P(x)/(y)/(z)/(objType)/?(newHint:z);

  !!if&(owner)>(NO_OWNER);
    !!VR(ownerStrIndex:y):S(owner) +23;
    !!FU(GetTextFileString):P^arraytxt^/(ownerStrIndex)/?(ownerStr:z);
    !!VR(newHint):+^ - %(ownerStr)^;
  !!en;

  !!MM:M^%(newHint)^;

!?FU(OnEveryDay)&i^timerWeekday^=1/i^timerDay^>1/i^timerOnce^; [поставка ресурсов владельцам мельниц/садов каждый первый день недели, начина€ с 8го дн€]
  !!UN:P879/?(wogOption:y);             [провер€ем включена ли опци€ в y1]
  !!FU&(wogOption)<>(TRUE):E;           [выход, если опци€ не включена]

  !!FU(NewIntArray):P8/?(liveArr:y); 
  !!re (playerID:y)/0/(PLAYER_LAST);
    !!OW:I(playerID)/d/?(isDead:y);
    !!SN&(isDead)=(FALSE):V(liveArr)/(playerID)/(TRUE);
  !!en;

  !!re i/0/(HERO_LAST_WOG);
    !!HEi:O?(owner:y);

    !!if&(owner)=(NO_OWNER);
      !!HEi:Z?(hero:y);
      !!br;
    !!en;
  !!en;

  !!VRi^ES_879_isAuto^:S(TRUE);
  !!FU(ES_879_CollectResources):P4864048/(OBJ_MYSTICAL_GARDEN)/(hero)/(liveArr);
  !!FU(ES_879_CollectResources):P4880976/(OBJ_WATER_WHEEL)/(hero)/(liveArr);
  !!FU(ES_879_CollectResources):P4881456/(OBJ_WINDMILL)/(hero)/(liveArr);
  !!UN:C(hero)/34/1/(NO_OWNER);
  !!VRi^ES_879_isAuto^:S(FALSE);


!?FU(ES_879_CollectResources); 
!#VA(funcAddr:x) (objType:x) (hero:x) (liveArr:x);
!#VA(x:y) (y:y) (z:y); 

!!VR(x):S-1;                                                  [init as start]

!!re i;                                                       [endless loop]
  !!UN:U(objType)/(ANY_OBJ)/-1/(x)/(y)/(z); 
  !!br&(x)<0;                                                 [exit loop if nothing found]

  !!PO(x)/(y)/(z):O?(owner:y);                                [get obj owner]

  !!if&(owner)<>(NO_OWNER);                                    [if there is one]
    !!SN:V(liveArr)/(owner)/?(isAlive:y);                     [check if lives]

    !!if&(isAlive);
      !!UN:C(hero)/34/1/(owner);                              [set hero as obj owner color]
      !!OB(x)/(y)/(z):T?(heroCheck:y);

      !!if&(heroCheck)=(OBJ_HERO);
        !!HE(x)/(y)/(z):Z?(badHero:y);
        !!SN:E5077328/(CALLCONV_THISCALL)/(badHero);         [hide hero if it is there]
      !!en;

      !!FU(ES_AdvMgr_GetMapItem):P(x)/(y)/(z)/?(mapItem:y);  [get mapItem]
      !!FU(ES_879_AdjustStoredRes):P(x)/(y)/(z)/(objType);
      !!SN:E(funcAddr)/(CALLCONV_STDCALL)/(hero)/(mapItem)/0; [visit mapItem as AI]

    !!el;
      !!PO(x)/(y)/(z):O(NO_OWNER) V0/0 V1/0;                            [set no owner if is dead]
    !!en;
  !!en;
!!en;

!?FU(ES_879_AdjustStoredRes);
!#VA(x:x) (y:x) (z:x) (objType:x); 

!!if&(objType)=(OBJ_MYSTICAL_GARDEN);
  !!GD(x)/(y)/(z):B?(resType:y);
  !!VR(value:y):S5;
  !!VR(value)&(resType)=(RES_GOLD):S500;
!!el&(objType)=(OBJ_WATER_WHEEL);
  !!WM(x)/(y)/(z):B?(resCoeff:y);
  !!VR(value:y):S(resCoeff)*500;
  !!VR(resType):S(RES_GOLD);
!!el;
  !!ML(x)/(y)/(z):B?(resType:y)/?(value:y);
!!en;

!!PO(x)/(y)/(z):V0/(resType) V1/(value);

!?FU(ES_879_GetObjectName);
!#VA(x:x) (y:x) (z:x) (objType:x) (objNamePtr:x);

  ; Set up object names
  !!FU(GetTextFileString):P^objnames^/(objType)/?(objName:z); [windmill]
  ; Set up "Improved" object text if Mithril Enhancedment is enabled
  !!VR(isUpg:y):S(FALSE);
  !!UN:P36/?(enhMithril:y);
  !!PO(x)/(y)/(z)&(enhMithril):N?(isUpg);
  !!VR(objName)&(isUpg):S^%T(es.879.improved)%(objName)^;
  !!VR(objNamePtr):Z(objName);

** end
