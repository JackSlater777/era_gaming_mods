ZVSE2
** Author orig.  : Berserker
** Updated by    : Archer30
** Name          : Gnolls-marauders strike first
** Name rus.     : Гноллы-Марадеры атакуют первыми
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA
** Options       : 996

; Gnolls-marauders strike first: option 996


!?FU(OnAfterErmInstructions);
!!UN:P996/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!FU(NewIntArray):P5/?i^ES_996_ArrayId^/(M_TEMP);

!?FU(OnBeforeBattleUniversal);
!!UN:P996/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:Vi^ES_996_ArrayId^/0/(NO_STACK)/(NO_STACK)/0;

!?FU(OnBeforeBattleAction);
!!UN:P996/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!SN:Mi^ES_996_ArrayId^/2/?(value:y);
!!SN&(value)<>0:Mi^ES_996_ArrayId^/2/0;

!!BG:A?(action:y) E?(targetStack:y) N?(actingStack:y);
!!FU|(action)<>(BATTLE_ACTION_WALK_AND_ATTACK)/(targetStack)=(NO_STACK):E;

!!BM(targetStack):T?(targetType:y) R?(retaliation:y);
!!FU&(retaliation)=0:E;

!!FU(ES_996_CheckIfMonIsEligible):P(targetType)/?(targetResult:y);
!!FU&(targetResult)<>(TRUE):E;

!!BM(actingStack):T?(actingType:y) F?(monFlags:y);
!!VR(noRetaliation:y):S(monFlags) &(MON_FLAG_NO_RETALIATION);
!!FU&(noRetaliation)<>(FALSE):E;

!!FU(ES_996_CheckIfMonIsEligible):P(actingType)/?(actingResult:y);
!!FU&(actingResult)=(TRUE):E;

!!BM(actingStack):N?(num:y);
!!SN:Vi^ES_996_ArrayId^/0/(actingStack)/(targetStack)/1/(num);

!?FU(OnMonsterPhysicalDamage);
!!UN:P996/?y1;
!!FU&y1<>1:E;

!!SN:Mi^ES_996_ArrayId^/2/?y1;
!!FU&y1<>1:E;

!!MF:F?y2;
!!SN:Mi^ES_996_ArrayId^/2/2 Mi^ES_996_ArrayId^/4/y2;
!!MF:E0;

!?FU(OnBattleActionEnd);
!!UN:P996/?y1;
!!FU&y1<>1:E;

!!SN:Mi^ES_996_ArrayId^/2/?y1;
!!FU&y1<>2:E;

!!SN:Vi^ES_996_ArrayId^/0/?y1/?y5/0/?y2/?y4;

!!BMy1:N?y3;
!!VRy4:*y3:y2;
!!BMy5:H?y6 L?y7 N?y8;
!!VRy8:*y6-y7;
!!FU&y4=0:E;

!!BMy5:Ky4;
!!BMy5&y4>=y8:N0 L0;
!!BU:R;

// Funciton to check if the targeted monster is eligible for strike first
!?FU(ES_996_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_GNOLL_MARAUDER):S(TRUE);

** end
