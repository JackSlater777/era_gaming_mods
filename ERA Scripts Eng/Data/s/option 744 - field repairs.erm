ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor [Idea: Ivor]
** Name          : Field repairs
** Name rus.     : Ремонт в полевых условиях
** Options       : 744
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** Герой, имеющий навыки управления боевыми машинами после сразу после боя может починить разрушенную машину.
** Базовая стоимость починки (с одним уровнем навыка) равна удвоенной стоимости машины, базовое время починки - 400 очков движения.
** Каждый уровень каждого навыка управления боевыми машинами, а также специализация героя на таком навыке или боевой машине снижают стоимость и время ремонта на 10%.
** ИИ всегда чинит машины, если это возможно.

!?BA52;                       [перед боем]
!!UN:P744/?y1;                [проверяем включена ли опция 744 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BA:H0/?y1 H1/?y2;           [y1/y2 - герои участвующие в битве]
!!HEy1&y1>=0:A2/3/d/?y3 A2/4/d/?y4 A2/5/d/?y5 A2/6/d/?y6; [y3-y6 - боевые машины героя]
!!SN&y1>=0:W^opt744.H%Y1.3^/y3 W^opt744.H%Y1.4^/y4 W^opt744.H%Y1.5^/y5 W^opt744.H%Y1.6^/y6; [сохраняем кол-во машин перед боем для атакующего героя]
!!HEy2&y2>=0:A2/3/d/?y3 A2/4/d/?y4 A2/5/d/?y5 A2/6/d/?y6; [y3-y6 - боевые машины героя]
!!SN&y2>=0:W^opt744.H%Y2.3^/y3 W^opt744.H%Y2.4^/y4 W^opt744.H%Y2.5^/y5 W^opt744.H%Y2.6^/y6; [сохраняем кол-во машин перед боем для защищающегося героя]

!?BA53;                       [после боя]
!!UN:P744/?y1;                [проверяем включена ли опция 744 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BA:H0/?y1 H1/?y2;           [y1/y2 - герои участвующие в битве]
!!VRy3:S-1; !!VRy4:S-1;       [инициализация переменных]
!!HEy1&y1>=0:P?y3/?i/?j;      [y3=-1 если атакующий герой убит]
!!HEy2&y2>=0:P?y4/?i/?j;      [y4=-1 если защищающийся герой убит]
!!FU&y3<0/y4<0:E;             [выход, если нет победившего героя]
!!VRy1&y3<0/y4>=0:Sy2;        [y1 - номер победившего героя]
!!SN:W^opt744.H%Y1.3^/?y3 W^opt744.H%Y1.4^/?y4 W^opt744.H%Y1.5^/?y5 W^opt744.H%Y1.6^/?y6; [кол-во машин перед боем]
!!HEy1:A2/3/d/?y13 A2/4/d/?y14 A2/5/d/?y15 A2/6/d/?y16; [y13-y16 - боевые машины героя после боя]
!!HEy1:S10/?y7 S20/?y8 S27/?y9 X?y10/?y11/d/d/d/d/d; [y7-y9 "машинные навыки героя", y10/y11 - специальность героя]
!!VRy12:Sy7 +y8 +y9;          [y12 - сумма уровней "машинных" навыков и "машинной" специализации героя]
!!VRy12&y10=0/y11=10:+1;      [...]
!!VRy12&y10=0/y11=20:+1;      [...]
!!VRy12&y10=0/y11=27:+1;      [...]
!!VRy12&y10=1/y11=145:+1;     [...]
!!VRy12&y10=1/y11=146:+1;     [...]
!!VRy12&y10=1/y11=147:+1;     [...]
!!VRy12&y10=1/y11=148:+1;     [...]
!!VRy12&y10=4/y11=145:+1;     [...]
!!VRy12&y10=4/y11=146:+1;     [...]
!!VRy12&y10=4/y11=147:+1;     [...]
!!VRy12&y10=4/y11=148:+1;     [...]
!!VRy12&y12>10:S10;           [...]
!!FU&y12<1:E;                 [выход, если у героя нет "машинных" навыков/специализации]
!!VRy12:*-20 +200 +20;        [y12 - стоимость починки в % (200%..20%)]
!!HEy1:O?y17;                 [y17 - хозяин героя]
!!FU&y17<0:E;                 [выход, если у героя нет хозяина]
!!OW:Iy17/?y18/?y19;          [y18=1 для ИИ героя]
!!FU&y19=1:E;                 [выход, если игрок мертв]
!!VRy21:S200 *y12 :100;       [y21 - время ремонта машины 40..400 очков движения]
!!HEy1:B0/?z2;
!!IF:Wy1;                     [доступ к w-переменным героя]
** Катапульта
!!if&y13<y3/y13<1:;           [если есть разрушенные Катапульты и нет ни одной целой]
  !!MA:C145/6/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19; !!VRy19:*-1;[y19 - золото героя, отрицательное значение]
  !!if&y20>=y19:;             [если золота достаточно]
    !!VRz1:Sz179280;          [z1 - текст вопроса о починке]
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/21/145/36/y22/2/1; [вопрос о починке машины для игрока человека]
    !!if|y18=1/1:;            [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/3/16 W?y23/1; [даем герою Катапульту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]
    !!en:;
  !!en:;
!!en:;
** Баллиста
!!if&y14<y4/y14<1:;           [если есть разрушенные Баллисты и нет ни одной целой]
  !!MA:C146/6/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19; !!VRy19:*-1;[y19 - золото героя, отрицательное значение]
  !!if&y20>=y19:;             [если золота достаточно]
    !!VRz1:Sz179280;          [z1 - текст вопроса о починке]
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/21/146/36/y22/2/1; [вопрос о починке машины для игрока человека]
    !!if|y18=1/1:;            [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/4/13 W?y23/1; [даем герою Баллисту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]
      !!VRw82&v847=1:S1;      [Cовместимость с WoG-опцией 73 "Усиленные военные машины III", кол-во баллист = 1]
    !!en:;
  !!en:;
!!en:;
** Подвода
!!if&y15<y5/y15<1:;           [если есть разрушенные Подводы и нет ни одной целой]
  !!MA:C148/6/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19; !!VRy19:*-1;[y19 - золото героя, отрицательное значение]
  !!if&y20>=y19:;             [если золота достаточно]
    !!VRz1:Sz179280;          [z1 - текст вопроса о починке]
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/21/148/36/y22/2/1; [вопрос о починке машины для игрока человека]
    !!if|y18=1/1:;            [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/5/14 W?y23/1; [даем герою Подводу, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]
      !!VRw83&v847=1:S1;      [Cовместимость с WoG-опцией 73 "Усиленные военные машины III", кол-во подвод = 1]
    !!en:;
  !!en:;
!!en:;
** Палатка
!!if&y16<y6/y16<1:;           [если есть разрушенные Палатки и нет ни одной целой]
  !!MA:C147/6/?y20;           [y20 - цена машины]
  !!VRy20:*y12 :100 *-1;      [y20 - цена ремонта с учетом навыков героя (отрицательное значение)]
  !!OW:Ry17/6/?y19; !!VRy19:*-1;[y19 - золото героя, отрицательное значение]
  !!if&y20>=y19:;             [если золота достаточно]
    !!VRz1:Sz179280;          [z1 - текст вопроса о починке]
    !!VRy22:Sy20 -100000;     [y22 - цена для показа в диалоге]
    !!IF&y18=0:Q1/21/147/36/y22/2/1; [вопрос о починке машины для игрока человека]
    !!if|y18=1/1:;            [если ответ положительный или ИИ (ИИ всегда восстанавливает машины)]
      !!HEy1:A1/6/15 W?y23/1; [даем герою Катапульту, y23 - текущие ОД]
      !!VRy23:-y21;           [снижаем запас ОД на время ремонта]
      !!VRy23&y23<0:S0;       [...]
      !!HEy1:Wy23/1;          [...]
      !!OW:Ry17/6/dy20;       [Уменьшаем количество золота игрока]
      !!VRw81&v847=1:S1;      [Cовместимость с WoG-опцией 73 "Усиленные военные машины III", кол-во палаток = 1]
    !!en:;
  !!en:;
!!en:;

**end
