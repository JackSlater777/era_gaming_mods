ZVSE2
** Author        : Algor (Idea: Antib)
** Rewritten by  : Archer30
** Name          : Refusal of dwellings
** Name rus.     : Отказ от жилищ
** Options       : 748

** ПКМ на принадлежащем герою внешнем жилище вызывает диалог,в котором от данного жилища можно отказаться,
** сделав его нейтральным и переведя всех невыкупленных существ в охрану (существа 1-4 уровней только до конца недели).
** Отказ от жилищ существ "8го уровня" (3й грейд замковых существ 7го уровня) запрещен,
** если жилища 8го уровня функционируют не как обычные.


!?FU(OnAdventureMapRightMouseClick);
!!UN:P748/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit if not clicking on an entrance
!!CM:P?(x:y)/?(y:y)/?(z:y);
!!TR(x)/(y)/(z):E?(isNotYellow:y);
!!FU&(isNotYellow):E;

; Exit if not clicking on a dwelling
!!OB(x)/(y)/(z):T?(type:y) U?(subtype:y);
!!FU&(type)<>(OBJ_CREATURE_GENERATOR_1)/(type)<>(OBJ_CREATURE_GENERATOR_4):E;

; Exit if the interacting player is not the owner of the dwelling
!!DW(x)/(y)/(z):O?(owner:y);
!!OW:C?(player:y)/?(interactPlayer:y);
!!FU|(owner)<>(player)/(owner)<>(interactPlayer):E;

!!CM:R0;

; Set up the dialogue with monster and numbers
!!FU(NewIntArray):P?(availMons:y);

!!re (slot:y)/0/3;
  !!DW(x)/(y)/(z):M(slot)/?(mon:y)/?(num:y);

  !!if&(mon)>(NO_MON);
    !!VR(num):F0/32767;
    !!VR(monAndNum:y):S65536 *(num) +(mon);
    !!FU(Array_Push):P(availMons)/(PIC_TYPE_MONSTER)/(monAndNum);
  !!en;
!!en;

!!FU(ES_SetupMultiPicDlg):P(availMons);

; Get the string for the title
!!if&(type)=(OBJ_CREATURE_GENERATOR_1);
  !!FU(ES_GetStringByFilename):P^crgen1^/(subtype)/?(dwellName:z);
!!el&(type)=(OBJ_CREATURE_GENERATOR_4);
  !!FU(ES_GetStringByFilename):P^crgen4^/(subtype)/?(dwellName);
!!en;

!!FU(ES_GetStringByFilename):P^plcolors^/(owner)/?(colourStr:z);
!!FU(StrToLower):P(colourStr)/?(lowerColourStr:z);
!!SN:T^es.owner^/?(ownerStr:z)/^colour^/(lowerColourStr);

; Open the dialogue
!!IF:N(MSG_TYPE_QUESTION)/^%(dwellName)
%(ownerStr)

%T(es.748.dlg)^/?(choice:y);

; Set the dwelling to no owner and convert all the available monsters to guards
!!if&(choice);
  !!DW(x)/(y)/(z):O(NO_OWNER)/1;

  !!re (slot)/0/3;
    !!DW(x)/(y)/(z):M(slot)/?(availMon:y)/?(availNum:y);
    !!DW(x)/(y)/(z):G(slot)/?(guardMon:y)/?(guardlNum:y);

    !!if|(guardlNum)=(availMon)/(guardMon)=(NO_MON);
      !!DW(x)/(y)/(z):G(slot)/(availMon)/d(availNum);
      !!DW(x)/(y)/(z):M(slot)/(availMon)/0;
    !!en;
  !!en;
!!en;

**end
