ZVSE
** Author orig.  : Algor (по мотивам опции 880 от Alex)
** Update        : Archer30
** Name          : Leadership
** Name rus.     : Ћидерство
** Options       : 766
** Dialogs       : -
** Variables     : v7917-v7919, z799-z801
** Tmp variables : z1-z5
** Timers        : TM51(standard)
** Functions     : FU7940-FU7941
** PO-values     : -

!#TM51:S8/999/7/255;          [каждую неделю дл€ всех игроков начина€ с 8го дн€]

!?PI;                         [пост-инструкци€: установка описаний]
!!UN:P766/?y1;                [провер€ем включена ли опци€ 766 в y1]
!!FU&y1=0:E;                  [выход если опци€ не включена]
!!VRz1:S^Data\s\option 766 - leadership.ini^; [z1 - им€ ini-файла]
!!VRz2:S^option 766^;         [z2 - секци€ ini-файла]
!!UN:N6/3/0/2/1 N6/4/1/2/1 N6/5/2/2/1; [z3-z5 - текстровые значени€ опций 0..2]
!!VRv7917:Vz3;                [v7917 - числовое значение опции 0]
!!VRv7918:Vz4;                [v7918 - числовое значение опции 1]
!!VRv7919:Vz5;                [v7919 - числовое значение опции 2]
!!VRz799:Sz179165;            [получаем базовое описание навыка Ћидерство]
!!UN:G0/6/1/799;              [”станавливаем базовое описание навыка Ћидерство]
!!VRz800:Sz179166;            [получаем продвинутое описание навыка Ћидерство]
!!UN:G0/6/2/800;              [”станавливаем продвинутое описание навыка Ћидерство]
!!VRz801:Sz179167;            [получаем экспертное описание навыка Ћидерство]
!!UN:G0/6/3/801;              [”станавливаем экспертное описание навыка Ћидерство]

!?TM51;                       [каждую неделю]
!!UN:P766/?y1;                [провер€ем включена ли опци€ 766 в y1]
!!FU&y1=0:E;                  [выход если опци€ не включена]
!!OW:C?y1;                    [y1 - текущий игрок]
!!DO7940/0/155/1:Py1;         [увеличиваем живые армии героев с навыком Ћидерство]

!?FU7940;                     [x16 - номер геро€, x1 - текущий игрок]
!!HEx16:O?y1;                 [y1 - игрок-владелец геро€]
!!FU&y1<>x1:E;                [выход, если герой не текущего игрока]
!!HEx16:S6/?y1;               [y1 - уровень Ћидерства геро€]
!!DO7941/0/6/1&y1>0:Px16/y1;  [прирост численности отр€да]

!?FU7941;                     [x16 - номер отр€да, x1 - номер геро€, x2 - уровень Ћидерства]
!!HEx1:C0/x16/?y1/?y2;        [y1/y2 - тип/количество существ в отр€де]
!!FU|y2<1/y1<0:E;             [выход, если отр€да нет]
!!MA:Xy1/?y3;                 [y3 - флаги существа]
!!VRy3:&16;                   [y3=16, если существа живые]
!!FU&y3=0:E;                  [выход, если существа не живые]
!!VRy3:Sx2 +7916;             [y3 - индекс v-переменной с коэффициентом прироста дл€ нужного уровн€ Ћидерства]
!!VRy3:Svy3 *y2 :100;         [y3 - прирост отр€да]
!!HEx1:C0/x16/d/dy3/0/0;      [добавл€ем в стек новобранцев с нулевым опытом]

** end
