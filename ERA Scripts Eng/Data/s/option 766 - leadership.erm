ZVSE
ERMS_ScriptDate=19.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor (по мотивам опции 880 от Alex)
** Name          : Leadership
** Name rus.     : Лидерство
** Options       : 766
** Dialogs       : -
** Variables     : v7917-v7919, z799-z801
** Tmp variables : z1-z5
** Timers        : TM51(standard)
** Functions     : FU7940-FU7941
** PO-values     : -

!#TM51:S8/999/7/255;          [каждую неделю для всех игроков начиная с 8го дня]

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P766/?y1;                [проверяем включена ли опция 766 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz1:S^Data\s\option 766 - leadership.ini^; [z1 - имя ini-файла]
!!VRz2:S^option 766^;         [z2 - секция ini-файла]
!!UN:N6/3/0/2/1 N6/4/1/2/1 N6/5/2/2/1; [z3-z5 - текстровые значения опций 0..2]
!!VRv7917:Vz3;                [v7917 - числовое значение опции 0]
!!VRv7918:Vz4;                [v7918 - числовое значение опции 1]
!!VRv7919:Vz5;                [v7919 - числовое значение опции 2]
!!VRz799:Sz179165;            [получаем базовое описание навыка Лидерство]
!!UN:G0/6/1/799;              [Устанавливаем базовое описание навыка Лидерство]
!!VRz800:Sz179166;            [получаем продвинутое описание навыка Лидерство]
!!UN:G0/6/2/800;              [Устанавливаем продвинутое описание навыка Лидерство]
!!VRz801:Sz179167;            [получаем экспертное описание навыка Лидерство]
!!UN:G0/6/3/801;              [Устанавливаем экспертное описание навыка Лидерство]

!?TM51;                       [каждую неделю]
!!UN:P766/?y1;                [проверяем включена ли опция 766 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!OW:C?y1;                    [y1 - текущий игрок]
!!DO7940/0/155/1:Py1;         [увеличиваем живые армии героев с навыком Лидерство]

!?FU7940;                     [x16 - номер героя, x1 - текущий игрок]
!!HEx16:O?y1;                 [y1 - игрок-владелец героя]
!!FU&y1<>x1:E;                [выход, если герой не текущего игрока]
!!HEx16:S6/?y1;               [y1 - уровень Лидерства героя]
!!DO7941/0/6/1&y1>0:Px16/y1;  [прирост численности отряда]

!?FU7941;                     [x16 - номер отряда, x1 - номер героя, x2 - уровень Лидерства]
!!HEx1:C0/x16/?y1/?y2;        [y1/y2 - тип/количество существ в отряде]
!!FU|y2<1/y1<0:E;             [выход, если отряда нет]
!!MA:Xy1/?y3;                 [y3 - флаги существа]
!!VRy3:&16;                   [y3=16, если существа живые]
!!FU&y3=0:E;                  [выход, если существа не живые]
!!VRy3:Sx2 +7916;             [y3 - индекс v-переменной с коэффициентом прироста для нужного уровня Лидерства]
!!VRy3:Svy3 *y2 :100;         [y3 - прирост отряда]
!!HEx1:C0/x16/d/dy3/0/0;      [добавляем в стек новобранцев с нулевым опытом]

** end
