ZVSE2
** Author orig.  : Berserker
** Name          : Ranged Death Stare
** Name rus.     : Взгляд Смерти на расстоянии
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA
**                 [Algor] вынос текстов в ert-файл
** Options       : 995

; Mighty Gorgons attack with Death Stare on distance: option 995
; Kill depending on target level: 36/22/11/8/6/4/2 : 10 * Gorgons count
; GorgDeathMode: boolean (0 - normal mode, 1 - distant attack)


!?FU(OnBattleMouseHint);
!!UN:P995/?y1;
!!FU&y1<>1:E;

!!BG:N?y1 Q?y2;
!!SN:W^BA0.IsAI.%Y2^/?y60;
!!FU&y60=1:E;

!!SN:W^GorgDeathMode^/?y61;
!!FU&y61=0:E;

!!BMy1:T?y3 P?y4;
!!FU&y3<>103:E;

!!MM:D?y5;
!!FU|y5<0/y5>186:E;

!!BU:Ey5/?y6;
!!FU&y6=-1:E;

!!VRy8:Sy6:21;
!!FU&y2=y8:E;

!!BMy6:P?y5 F?y20;
!!VRy20:&1;
!!FU222000:Py4/y5/?v2;
!!VRy10:Sy2*-2+1+y4;
!!FU222000:Py10/y5/?v3;
!!VRv2&v3<v2:Sv3;

!!if&y20=1;
  !!VRy11:S1-y2*-2+1+y5;
  !!FU222000:Py4/y11/?v3;
  !!VRv2&v3<v2:Sv3;
  !!FU222000:Py10/y11/?v3;
  !!VRv2&v3<v2:Sv3;
!!en;

!!BMy1:S?y12;
!!VRy12:+1;
!!FU&y12<v2:E;

!!BMy6:F?y50;
!!VRy51:Sy50&262144;
!!FU&y51<>0:E;

!!BMy1:N?y30;
!!UN:P49/?y99;
!!VRy70:Sy2+7207 Svy70;

!!if&y1=y70/y99=1;
  !!BHy2:N?y71;
  !!IF:Wy71;

  !!if&w118>-1/w119=1;
    !!FU(hench_GetHenchExpPerLevel):Py3;
    !!HEy71:E?y72/?y73/1;
    !!VRy74:Sy73 -1 *10 +w117 +c;
    !!VRy75:Sy74 :i^hench_expPerLevel^ +1;
    !!VRy30:Sy75;
  !!en;
!!en;

!!BMy6:T?y31;
!!VRy33:S600+y32;
!!VRv600:C36/22/11/8/6/4/2;
!!VRy33:Svy33*y30;
!!BMy6:H?y34 L?y35 N?y36;
!!VRy37:Sy36*y34-y35;
!!VRy38:Sy34*y33:10;
!!UN:P49/?y99;
!!VRy98:S1-y2;
!!VRy70:Sy98+7207 Svy70;

!!if&y6=y70/y99=1;
  !!BHy98:N?y71;
  !!IF:Wy71;

  !!if&w118>-1/w119=1/y71>=0;
    !!FU(hench_GetHenchExpPerLevel):Py31;
    !!HEy71:E?y72/?y73/1;
    !!VRy74:Sy73 -1 *10 +w117 +c;
    !!VRy75:Sy74 *100 :i^hench_expPerLevel^ +100;
    !!VRy38:Sy34 *100 :y75 * y33 :10;
  !!en;
!!en;

!!if&y31>=174/y31<=191;
  !!VRy38:Sy30*44;
!!en;

!!VRy39:Sy37-y38;
!!VRy39&y39<0:S0;
!!VRy40:Sy39:y34;
!!VRy41:Sy39%y34;
!!VRy40&y41>0:+1;
!!VRy42:Sy36-y40;
!!VRy43:Sy38:y34;
!!VRy43&y43<y42:Sy42;
!!VRz1:S^%Y42^;

!!if&y42=y36;
  !!VRz2:S^{~0F2223E}%Z1{~}^;
  !!VRz1:Sz2;
!!en;

!!VRz3:S^%Y38^;

!!if&y42=y36;
  !!VRz2:S^{~0F2223E}%Z3{~}^;
  !!VRz3:Sz2;
!!en;

!!VRz4:S^%Z1  (%Z3)^;
!!VRz1&y42=y36:S^^;
!!UN:R5/4/84;
!!MM:Mz4;

!?FU(OnBattleScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED);
!!UN:P995/?y1;
!!FU&y1<>1:E;

!!BG:N?y1 Q?y2;
!!SN:W^BA0.IsAI.%Y2^/?y60;
!!FU&y60=1:E;

!!SN:W^GorgDeathMode^/?y61;
!!FU&y61=0:E;

!!UN:C6919200/4/?y60;
!!VRy60:+78532;
!!UN:Cy60/1/?y61;
!!FU&y61=1:E;

!!BG:A?y60;
!!FU&y60<>0:E;

!!BMy1:T?y3 P?y4;
!!FU&y3<>103:E;

!!CM:D?y5;
!!FU|y5<0/y5>186:E;

!!BU:Ey5/?y6;
!!FU&y6=-1:E;

!!VRy8:Sy6:21;
!!FU&y2=y8:E;

!!BMy6:P?y5 F?y20;
!!VRy20:&1;
!!FU222000:Py4/y5/?v2;
!!VRy10:Sy2*-2+1+y4;
!!FU222000:Py10/y5/?v3;
!!VRv2&v3<v2:Sv3;

!!if&y20=1;
  !!VRy11:S1-y2*-2+1+y5;
  !!FU222000:Py4/y11/?v3;
  !!VRv2&v3<v2:Sv3;
  !!FU222000:Py10/y11/?v3;
  !!VRv2&v3<v2:Sv3;
!!en;

!!BMy1:S?y12;
!!VRy12:+1;
!!FU&y12<v2:E;

!!BMy6:F?y50;
!!VRy51:Sy50&262208;
!!FU&y51<>0:E;

!!BMy1:N?y30;
!!UN:P49/?y99;
!!VRy70:Sy2+7207 Svy70;

!!if&y1=y70/y99=1;
  !!BHy2:N?y71;
  !!IF:Wy71;

  !!if&w118>-1/w119=1;
    !!FU(hench_GetHenchExpPerLevel):Py3;
    !!HEy71:E?y72/?y73/1;
    !!VRy74:Sy73 -1 *10 +w117 +c;
    !!VRy75:Sy74 :i^hench_expPerLevel^ +1;
    !!VRy30:Sy75;
  !!en;
!!en;

!!BMy6:T?y31;
!!MA:Ly31/?y32;
!!VRy33:S600+y32;
!!VRv600:C36/22/11/8/6/4/2;
!!VRy33:Svy33*y30;
!!BMy6:H?y34 L?y35 N?y36;
!!VRy37:Sy36*y34-y35;
!!VRy38:Sy34*y33:10;
!!UN:P49/?y99;
!!VRy98:S1-y2;
!!VRy70:Sy98+7207 Svy70;

!!if&y6=y70/y99=1;
  !!BHy98:N?y71;
  !!IF:Wy71;

  !!if&w118>-1/w119=1/y71>=0;
    !!FU(hench_GetHenchExpPerLevel):Py31;
    !!HEy71:E?y72/?y73/1;
    !!VRy74:Sy73 -1 *10 +w117 +c;
    !!VRy75:Sy74 *100 :i^hench_expPerLevel^ +100;
    !!VRy38:Sy34 *100 :y75 * y33 :10;
  !!en;
!!en;

!!if&y31>=174/y31<=191;
  !!VRy38:Sy30*44;
!!en;

!!VRy39:Sy37-y38;
!!VRy39&y39<0:S0;
!!VRy40:Sy39:y34;
!!VRy41:Sy39%y34;
!!VRy40&y41>0:+1;
!!VRy42:Sy36-y40;
!!VRy43:Sy38:y34;
!!VRy43&y43<y42:Sy42;
!!SN:P^deathstr.wav^;
!!BMy6:V80 Ky38;

!!if&y42=y36;
  !!BMy6:N0 L0;
!!en;

!!SN:T^es.995.kill^/?z1/^dmg^/y38/^killed^/y42;
!!MM:Sz1;
!!CM:R0;
!!BG:A12;
!!SN:D;
*!SN:Q;                                 [Do we really need this?]

!?FU(OnKeyPressed);
!!UN:P995/?y1;
!!FU&y1<>1:E;

!!SN:L^Era.dll^/?y1 Ay1/^GetGameState^/?y2 Ey2/1/?y3;
!!FU&y4<>4662240:E;

!!SN:X?y1/?y2;
!!FU|y1<>27/y2=1:E;

!!BG:N?y1 Q?y2;
!!SN:W^BA0.IsAI.%Y2^/?y60;
!!FU&y60=1:E;

!!UN:C6919200/4/?y60;
!!VRy60:+78532;
!!UN:Cy60/1/?y61;
!!FU&y61=1:E;

!!BG:A?y60;
!!FU&y60<>0:E;

!!SN:W^GorgDeathMode^/?y61;
!!VRy62:S1-y61;
!!SN:W^GorgDeathMode^/y62;

** end
