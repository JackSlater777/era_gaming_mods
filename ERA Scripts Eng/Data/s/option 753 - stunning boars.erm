ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor
** Name          : Stunning boars
** Name rus.     : Ошеломляющие кабаны
** Options       : 753
** Dialogs       : -
** Variables     : z187
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** Орки на кабанах при атаке получают способность ошеломить противника (действует только на живых существ).
** Ошеломленный отряд теряет способность ходить и контратаковать в текущем раунде.
** Шанс ошеломления тем больше, чем сильнее отряд орков на кабанах и слабее их противник.
** FV орков на кабанах повышено на 10%.

!?PI;
!!UN:P753/?y1; !!FU&y1=0:E;   [выход если опция 753 не включена]
!!VRz187:Sz179258;            [получаем из ert новую подсказку по существу]
!!UN:G1/140/2/187;            [установка новой подсказки]
!!MA:F140/?y1;                [y1 - FV кабанов]
!!VRy1:*110 :100;             [увеличиваем FV на 10%]
!!MA:F140/y1;                 [обновляем FV]

!?MF1;
!!UN:P753/?y1; !!FU&y1=0:E;   [выход если опция 753 не включена]
!!BG:N?y1 A?y2;               [y1/y2 - активный отряд/действие]
!!FU&y2<>6:E;                 [выход, если не атака]
!!BMy1:T?y2 N?y3 H?y4 L?y5;   [y2/y3/y4/y5 - тип/численность/макс./потерянное здоровье активного отряда]
!!FU&y2<>140:E;               [выход, если атакуют не кабаны]
!!MF:N?y6;                    [y6 - отряд получающий урон]
!!FU&y1=y6:E;                 [выход, если контрудар]
!!BMy6:F?y7 N?y8 H?y9 L?y10;  [y7/y8/y9/y10/y11 - флаги/численность/макс./потерянное здоровье целевого отряда]
!!VRy11:Sy7 &16;              [y11>0 если в отряде получающем урон "живые" существа]
!!FU&y11=0:E;                 [выход, если целевой отряд - не "живой"]
!!VRy4:*y3 -y5;               [y4 - суммарное здоровье отряда Кабанов]
!!VRy8:*y9 -y10;              [y8 - суммарное здоровье целевого отряда]
!!FU&y8<1:E;                  [выход, если целевой отряд уже мертв]
!!VRy4:*100 :y8;              [шанс срабатывания оглушения (в %)]
!!VRy11:S1 R99;               [y11 - случайное число (1..100)]
!!FU&y4<y11:E;                [выход, если оглушение не сработало]
!!VRy7:|67108864;             [y7 +флаг отряд уже ходил в этом раунде]
!!BMy6:R0 Fy7;                [цель теряет возможность контратаковать и ходить в текущем раунде]
!!BA:Q?f; !!FU&f=1:E;         [выход, если быстрая битва]
!!UN&y3=1:N3/2/140/1;         [z2 - название кабана]
!!UN&y3>1:N3/2/140/1;         [z2 - название кабанов]
!!VRz1&y3=1:Sz179259;         [z1 - текст сообщения для вывода в лог]
!!VRz1&y3>1:Sz179260;         [...]
!!MM:Sz1;                     [вывод текста в лог битвы]
!!BMy1:V49;                   [анимация на отряде кабанов]

**end
