ZVSE
ERMS_ScriptDate=19.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Fnord
** Name          : Monsters drop artefacts
** Name rus.     : Монстры с артефактами
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
** Options       : 769

** Approximately 25% of Monster Stacks will have artifacts unless the stack is
** set to always join or already has an artifact. The artifact class depends on
** the initial gold value of the stack as follows:
**
**           under 1000 = none
**            1000-2999 = Treasure
**           3000-11999 = Minor
**          12000-39999 = Major
**          40000+      = Relic
**
** Only enabled artifacts will be given (meaning no assembled combos unless
** specifically enabled) and WoG Artifacts may be given if they're not banned.
** Boots of Speed, Equestrian Gloves, Boosts of Levitation and Angel Wings
** will never be given, even if not banned.

** [Before show map]
!?FU(OnEveryDay);
!!UN:P769/?y3; !!FU&y3<>1:E;  [exit if option isn't enabled]
!!UN:P769/2; [only once]
!!UN:U54/-1/?y1; [Number of monsters on map: y1]
!!VRv1:S -1; [Initialize v1 to -1 for fast UN:U backwards looping]
!!DO(ES.opt769.GiveArt)/1/y1/1:P; [Loop through all monsters]

** [Loop through all monsters and possibly give an artifact]
!?FU(ES.opt769.GiveArt);
!!UN:U54/-1/-1/1; [Previous monster on map at v1/v2/v3]
!!VRy1:S1 R99; [Random number: 1-100: y1]
!!FU&y1>25:E; [Only a 25% chance of monster having an artifact]
!!MOv1/v2/v3:R?y2/1; [Check monster's aggression: y2]
!!FU&y2=0:E; [Skip if monster is set to always join]
!!UN:P904/1 P905/0; [Disable ERM error messages and clear error flag]
!!MOv1/v2/v3:A?y3; [Check monster's artifact: y3]
!!UN:P905/?y4; [Check ERM error flag: y4=1 if stack has no setup]
!!UN:P904/0; [Re-enable ERM error messages]
!!FU&y4=0/y3>=0:E; [Skip if monster already has an artifact]
!!MOv1/v2/v3:G?y5 O?y6 U?y7; [Number of monsters: y5, Grow Flag: y6, Escape Flag: y7]
!!OBv1/v2/v3:U?y8; [Type of monster: y8]
!!FU|y5<1/y8<0:E; [Skip if no monster is really there]
!!UN&y4=1:Ov1/v2/v3; [Delete monster if no setup]
!!UN&y4=1:Iv1/v2/v3/54/y8/54/y8/-1/0; [Place new stack of monsters (same type) if no setup]
!!MOv1/v2/v3&y4=1:Ry2/1 Gy5 Oy6 Uy7; [Copy attributes to replacement monster stack]
** [Determine Class from 1 to 5 based on stack's gold value: y11]
!!MA:Cy8/6/?y9;  [Gold cost of 1 monster: y9]
!!VRy10:Sy9 *y5; [Gold cost of monster stack: y10]
!!VRy11&y10<=999:S0;              [Stack is Class 0 (No Artifact): y11]
!!VRy11&y10>=1000/y10<=2999:S1;   [Stack is Class 1 (Treasure): y11]
!!VRy11&y10>=3000/y10<=11999:S2;  [Stack is Class 2 (Minor): y11]
!!VRy11&y10>=12000/y10<=39999:S3; [Stack is Class 3 (Major): y11]
!!VRy11&y10>=40000:S4;            [Stack is Class 4 (Relic): y11]
!!FU&y11=0:E; [Exit if Class 0]
!!VRy12:S1 R99; [Random number: 1-100: y12]
!!VRy22:S-1; [Initialize y22 to -1]
!!UN:A70/?y23 A72/?y24 A90/?y25 A98/?y26; [Store enabled/disabled status of Equestrian Gloves, Angel Wings, Boots of Levitation, and Boots of Speed: y23-y26]
!!UN:A70/1 A72/1 A90/1 A98/1; [Temporarily ban movement artifacts]
** [Check if various WoG Artifacts are banned]
!!UN:P238/?y13; [Check if Commander Artifacts are banned: y13=1 if banned]
!!UN:P177/?y14; [Check if Slava's Ring of Power is banned: y14=1 if banned]
!!UN:P234/?y15; [Check if Warlord's Banner is banned: y15=1 if banned]
!!UN:P236/?y16; [Check if Crimson Shield of Retribution is banned: y16=1 if banned]
!!UN:P176/?y17; [Check if Magic Wand is banned: y17=1 if banned]
!!UN:P226/?y18; [Check if Gold Tower Arrow is banned: y18=1 if banned]
!!UN:P227/?y19; [Check if Monster's Power is banned: y19=1 if banned]
!!UN:P241/?y20; [Check if Dragonheart is banned: y20=1 if banned]
!!UN:P243/?y21; [Check if Gate Key is banned: y21=1 if banned]
** [Class 1: Treasure, Dragon Eye Ring, Pendant of Sorcery]
!!VRy22&y11=1/y13=0/y12<=2:S153; [2% chance of Dragon Eye Ring if Commander Artifacts not banned: y22]
!!VRy22&y11=1/y13=0/y12>=3/y12<=4:S150; [2% chance of Pendant of Sorcery if Commander Artifacts not banned: y22]
!!UN&y11=1/y22<0:J6/2/?y22; [Random Treasure artifact: y22]
!!MOv1/v2/v3&y11=1:Ay22; [Give artifact to monster: y22]
!!UN:A70/y23 A72/y24 A90/y25 A98/y26; [Restore banned movement artifacts]
!!FU&y11=1:E; [Exit if class 1]
** [Class 2: Minor, Commander Artifacts (excluding Helm of Immortality, Slava's Ring of Power), Warlord's Banner]
!!VRy22&y11=2/y13=0/y12>=1/y12<=2:S146; [2% chance of Axe of Smashing if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=3/y12<=4:S147; [2% chance of Mithril Mail if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=5/y12<=6:S148; [2% chance of Sword of Sharpness if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=7/y12<=8:S150; [2% chance of Pendant or Sorcery if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=9/y12<=10:S151; [2% chance of Boots of Haste if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=11/y12<=12:S152; [2% chance of Bow of Seeking if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=13/y12<=14:S153; [2% chance of Dragon Eye Ring if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y13=0/y12>=15/y12<=16:S154; [2% chance of Hardened Shield if Commander Artifacts not banned: y22]
!!VRy22&y11=2/y15=0/y12>=17/y12<=18:S156; [2% chance of Warlord's Banner if not banned: y22]
!!UN&y11=2/y22<0:J6/4/?y22; [Random Minor artifact: y22]
!!MOv1/v2/v3&y11=2:Ay22; [Give artifact to monster: y22]
!!UN:A70/y23 A72/y24 A90/y25 A98/y26; [Restore banned movement artifacts]
!!FU&y11=2:E; [Exit if class 2]
** [Class 3: Major, Commander Artifacts (excluding Slava's Ring of Power, Pendant of Sorcery, Dragon Eye Ring), Warlord's Banner, Crimson Shield of Retribution]
!!VRy22&y11=3/y13=0/y12>=1/y12<=2:S146; [2% chance of Axe of Smashing if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=3/y12<=4:S147; [2% chance of Mithril Mail if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=5/y12<=6:S148; [2% chance of Sword of Sharpness if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=7/y12<=8:S149; [2% chance of Helm of Immortality if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=9/y12<=10:S151; [2% chance of Boots of Haste if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=11/y12<=12:S152; [2% chance of Bow of Seeking if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y13=0/y12>=13/y12<=14:S154; [2% chance of Hardened Shield if Commander Artifacts not banned: y22]
!!VRy22&y11=3/y15=0/y12>=15/y12<=16:S156; [2% chance of Warlord's Banner if not banned: y22]
!!VRy22&y11=3/y16=0/y12>=17/y12<=18:S157; [2% chance of Crimson Shield of Retribution if not banned: y22]
!!UN&y11=3/y22<0:J6/8/?y22; [Random Major artifact: y22]
!!MOv1/v2/v3&y11=3:Ay22; [Give artifact to monster: y22]
!!UN:A70/y23 A72/y24 A90/y25 A98/y26; [Restore banned movement artifacts]
!!FU&y11=3:E; [Exit if class 3]
** [Class 4: Relic, Helm of Immortality, Slava's Ring of Power, Crimson Shield of Retribution, Magic Wand, Gold Tower Arrow, Monster's Power, Dragonheart, Gate Key]
!!VRy22&y11=4/y13=0/y12>=1/y12<=2:S149; [2% chance of Helm of Immortality if Commander Artifacts not banned: y22]
!!VRy22&y11=4/y13=0/y14=0/y12>=3/y12<=4:S155; [2% chance of Slava's Ring of Power if Commander Artifacts and Slava's Ring not banned: y22]
!!VRy22&y11=4/y16=0/y12>=5/y12<=6:S157; [2% chance of Crimson Shield of Retribution if not banned: y22]
!!VRy22&y11=4/y17=0/y12>=7/y12<=8:S141; [2% chance of Magic Wand if not banned: y22]
!!VRy22&y11=4/y18=0/y12>=9/y12<=10:S142; [2% chance of Gold Tower Arrow if not banned: y22]
!!VRy22&y11=4/y19=0/y12>=11/y12<=12:S143; [2% chance of Monster's Power if not banned: y22]
!!VRy22&y11=4/y20=0/y12>=13/y12<=14:S159; [2% chance of Dragonheart if not banned: y22]
!!VRy22&y11=4/y21=0/y12>=15/y12<=16:S160; [2% chance of Gate Key if not banned: y22]
!!UN&y11=4/y22<0:J6/16/?y22; [Random Relic: y22]
!!MOv1/v2/v3&y11=4:Ay22; [Give artifact to monster: y22]
!!UN:A70/y23 A72/y24 A90/y25 A98/y26; [Restore banned movement artifacts]

** [Monster stack is trigger]
!?OB54;
!!UN:P769/?y3; !!FU&y3=0:E;  [exit if option isn't enabled]
!!UN:P904/1 P905/0; [Disable ERM error messages and clear error flag]
!!MO998:A?y-1; [Check monster's artifact: y-1]
!!UN:P905/?y-2; [Check ERM error flag: y-2=1 if stack has no setup]
!!UN:P904/0; [Re-enable ERM error messages]
!!FU|y-1<0/y-2=1:E; [Exit if monster has no setup or no artifact]
!!MO998:G?y-3; [Number of monsters in stack: y-3]
!!OB998:U?y-4; [Monster type: y-4]
!!UN&y-3=1:N3/-1/y-4/0; [Name of monsters (singular): z-1]
!!UN&y-3>1:N3/-1/y-4/1; [Name of monsters (plural): z-1]
!!UN:N0/-2/y-1; [Name of artifact: z-2]
!!VRz-3&y-3=1:Sz179116;
!!VRz-3&y-3>1:Sz179117;
!!VRy-5:S65536 *y-3 +y-4; [Display variable for monster subtype showing numbers: y-5]
!!IF&1000:Q1/21/y-5/8/y-1/1^%Z-3^; [Display artifact message when monster stack is attacked]

** [Post-Battle Trigger]
!?FU(OnAfterBattleUniversal);
!!UN:P769/?y3; !!FU&y3=0:E;  [exit if option isn't enabled]
!!BA:P?y-1/?y-2/?y-3 H0/?y-4; [Battle Location: y-1/y-2/y-3, Hero: y-4]
!!OBy-1/y-2/y-3:T?y-5 U?y-9; [Object at battle location: y-5, Subtype: y-9]
!!HEy-4&y-4>=0:O?y-6 B0/?z-1; [Hero's owner: y-6, Name: z-1]
!!FU|y-5<>54/y-4<0/y-6<0:E; [Exit if not a monster stack, no hero, or hero lost battle]
!!UN:P904/1 P905/0; [Disable ERM error messages and clear error flag]
!!MOy-1/y-2/y-3:A?y-7; [Check monster's artifact: y-7]
!!UN:P905/?y-8; [Check ERM error flag: y-8=1 if stack has no setup]
!!UN:P904/0; [Re-enable ERM error messages]
!!FU|y-7<0/y-8=1:E; [Skip if monster has no setup or no artifact]
!!MOy-1/y-2/y-3:A-1 G?y-10; [Remove artifact from monster stack, Number of monsters: y-10]
!!UN&y-10=1:N3/-2/y-9/0; [Name of monsters (singular): z-2]
!!UN&y-10>1:N3/-2/y-9/1; [Name of monsters (plural): z-2]
!!UN:N0/-3/y-7; [Name of artifact: z-3]
!!VRz1&1000:Sz179118;
!!IF&1000:Q1/8/y-7/1/z1;
!!HEy-4:A4/y-7; [Give artifact to hero]

** end
