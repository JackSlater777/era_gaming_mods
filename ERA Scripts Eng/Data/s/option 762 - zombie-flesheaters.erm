ZVSE2
** Author orig.  : Algor (Idea: Ivor)
** Rewritten by  : Archer30
** Name          : zombie-flesheaters
** Name rus.     : зомби-трупоеды
** Options       : 762


!?FU(OnAfterErmInstructions);
!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set up new names and description
!!SN:H^monname^/(MON_ZOMBIE)/0/^%T(es.762.singName)^;
!!SN:H^monname^/(MON_ZOMBIE)/1/^%T(es.762.plurName)^;
!!SN:H^monname^/(MON_ZOMBIE)/2/?(desc:z);
!!SN:H^monname^/(MON_ZOMBIE)/2/^%(desc)%T(es.762.desc)^;
; Increase the fight value
!!MA:F(MON_ZOMBIE)/?(fv:y);
!!VR(fv):*105 :100;
!!MA:F(MON_ZOMBIE)/(fv);

!?FU(OnStackToStackDamage);
!#VA(atkStack:x) (defStack:x) (finalDmgConst:x) (finalDmg:x) (basicDmg:x) (dmgBonus:x) (isDistant:x) (distancArg:x) (isTheoretical:x);

!!FU&(isTheoretical):E;

!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Check if the attacking monster is a flesh eater
!!BM(atkStack):T?(type:y);
!!FU(ES_762_CheckIfMonIsEligible):P(type)/?(result:y);

!!if&(result);
  ; Check if the defedning monster is a living one
  !!BM(defStack):F?(monFlags:y);
  !!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE);

  !!if&(isAlive);
    ; Check if the defending monster would be be killed by the flesh eater
    !!BM(defStack):N?(num:y) H?(hp:y) L?(lostHp:y);
    !!VR(defCurrTotalHp:y):S(num) *(hp) -(lostHp);

    ; Store the hp of the def stack if it would be killed, set up flags for the victim to disappear 
    !!if&(finalDmg)>=(defCurrTotalHp);
      !!VRi^es_762_regenHp^:S(defCurrTotalHp);
      !!BM(defStack):Fd|813694976 E0 K1;    [добавляем цели флаги жертвы/клона/неизменяемости цвета]
    !!en;
  !!en;
!!en;

!?FU(ES_OnAfterMelee)&i^es_762_regenHp^;
!#VA(zombieStack:x) (defStack:x);

!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Calculate the new HP and number of the flesheater
!!VR(noHpLost:y):S(FALSE);
!!BM(zombieStack):N?(num:y) H?(hp:y) L?(lostHp:y) B?(startNum:y);

!!VR(totalStartHp:y):S(startNum) *(hp);
!!VR(totalHp:y):S(num) *(hp) -(lostHp);
!!VR(noHpLost)&(totalStartHp)=(totalHp):S(TRUE);

!!VR(newTotalHp:y):S(totalHp) +i^es_762_regenHp^ F(totalHp)/(totalStartHp);
!!VR(newCurrHp:y):S(newTotalHp) %(hp);
!!VR(newNum:y):S(newTotalHp) :(hp) +1;

!!if&(newCurrHp)=0;
  !!VR(newCurrHp):S(hp);
  !!VR(newNum):-1;
!!en;

!!VR(newLostHp:y):S(hp) -(newCurrHp);

; Show battle log and play animation/sound
!!if&i^battle_isVisible^/(noHpLost)<>(TRUE);
  !!BM(zombieStack):T?(type:y);

  !!if&(num)=1;
    !!SN:H^monname^/(type)/0/?(flesheaterName:z);
  !!el;
    !!SN:H^monname^/(type)/1/?(flesheaterName);
  !!en;

  !!SN:T^es.762.battleLog^?(battleLog:z)/^flesheater^/(flesheaterName);
  !!MM:S(battleLog);

  ; Play sound and animation
  !!SN:P^ANIMDEAD^;
  !!BM(zombieStack):V74;
!!en;

; Set up new number and hp for the Zombises
!!BM(zombieStack):N(newNum) L(newLostHp);
; Restore the global variable
!!VRi^es_762_regenHp^:S0;

// Funciton to check if the targeted monster is eligible for flesh eating
!?FU(ES_762_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_ZOMBIE):S(TRUE);

** end
