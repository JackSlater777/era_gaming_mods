ZVSE2
** Author orig.  : Algor (Idea: Ivor)
** Update        : Archer30
** Name          : zombie-flesheaters
** Name rus.     : зомби-трупоеды
** Options       : 762
** Dialogs       : -
** Variables     : v7920-v7922


!?FU(OnAfterErmInstructions);
!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set up new names and description
!!SN:H^monname^/(MON_ZOMBIE)/0/^%T(es.762.singName)^;
!!SN:H^monname^/(MON_ZOMBIE)/1/^%T(es.762.plurName)^;
!!SN:H^monname^/(MON_ZOMBIE)/2/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.762.desc)^;
!!SN:H^monname^/(MON_ZOMBIE)/2/(newDesc);

!!MA:F(MON_ZOMBIE)/?(fv:y);             [FV зомби]
!!VR(fv):*105 :100;                     [увеличиваем FV на 5%]
!!MA:F(MON_ZOMBIE)/(fv);                [обновляем FV зомби]

!?FU(OnMonsterPhysicalDamage);
!#VA(atkStack:x);

!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit if the attacking stack ID is out-of-limit (Would be screwed if missing and damaged by Fire Shield)
!!FU|(atkStack)<(BATTLE_STACK_FIRST)/(atkStack)>(BATTLE_STACK_LAST):E;

; Check if the defedning monster (must be a living one) would be be killed by the Zombie attacker
!!MF:N?(defStack:x) F?(dmg:y);

!!BM(atkStack):T?(type:y);
!!BM(defStack):F?(monFlags:y) N?(num:y) H?(hp:y) L?(lostHp:y);
!!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE);'
!!VR(defCurrTotalHp:y):S(num) *(hp) -(lostHp);

; Store the hp of the def stack if it would be killed, set up flags for the victim to disappear 
!!if&(type)=(MON_ZOMBIE)/(isAlive)/(dmg)>=(defCurrTotalHp);
  !!VRi^es_762_regenHp^:S(defCurrTotalHp);
  !!BM(defStack):Fd|813694976 E0 K1;    [добавляем цели флаги жертвы/клона/неизменяемости цвета]
!!en;

!?FU(ES_OnAfterMelee)&i^es_762_regenHp^;
!#VA(zombieStack:x) (defStack:x);

!!UN:P762/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Calculate the new HP and number of the flesheater
!!VR(noHpLost:y):S(FALSE);
!!BM(zombieStack):N?(num:y) H?(hp:y) L?(lostHp:y) B?(startNum:y);

!!VR(totalStartHp:y):S(startNum) *(hp);
!!VR(totalHp:y):S(num) *(hp) -(lostHp);
!!VR(noHpLost)&(totalStartHp)=(totalHp):S(TRUE);

!!VR(newTotalHp:y):S(totalHp) +i^es_762_regenHp^ F(totalHp)/(totalStartHp);
!!VR(newCurrHp:y):S(newTotalHp) %(hp);
!!VR(newNum:y):S(newTotalHp) :(hp) +1;

!!if&(newCurrHp)=0;
  !!VR(newCurrHp):S(hp);
  !!VR(newNum):-1;
!!en;

!!VR(newLostHp:y):S(hp) -(newCurrHp);

; Show battle log and play animation/sound
!!if&i^battle_isVisible^/(noHpLost)<>(TRUE);
  !!if&(num)=1;
    !!SN:H^monname^/(MON_ZOMBIE)/0/?(flesheaterName:z);
  !!el;
    !!SN:H^monname^/(MON_ZOMBIE)/1/?(flesheaterName);
  !!en;

  !!SN:T^es.762.battleLog^?(battleLog:z)/^flesheater^/(flesheaterName);
  !!MM:S(battleLog);

  ; Play sound and animation
  !!SN:P^ANIMDEAD^;
  !!BM(zombieStack):V74;
!!en;

; Set up new number and hp for the Zombises
!!BM(zombieStack):N(newNum) L(newLostHp);
; Restore the global variable
!!VRi^es_762_regenHp^:S0;

** end
