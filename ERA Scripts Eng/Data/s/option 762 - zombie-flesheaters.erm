ZVSE2
** Author orig.  : Algor (Idea: Ivor)
** Update        : Archer30
** Name          : zombie-flesheaters
** Name rus.     : зомби-трупоеды
** Options       : 762
** Dialogs       : -
** Variables     : v7920-v7922


!?FU(OnAfterErmInstructions); [пост-инструкция: установка описаний]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

; Set up new names and description
!!SN:H^monname^/(MON_ZOMBIE)/0/^%T(es.762.singName)^;
!!SN:H^monname^/(MON_ZOMBIE)/1/^%T(es.762.plurName)^;
!!SN:H^monname^/(MON_ZOMBIE)/2/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.762.desc)^;
!!SN:H^monname^/(MON_ZOMBIE)/2/(newDesc);

!!MA:F(MON_ZOMBIE)/?(fv:y);   [FV зомби]
!!VR(fv):*105 :100;           [увеличиваем FV на 5%]
!!MA:F(MON_ZOMBIE)/(fv);      [обновляем FV зомби]

!?FU(OnBeforeBattleAction);   [перед действием]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!VRv7920:C-1/-1/-1;          [обнуляем v-переменные]
!!BG:N?y1 A?y2;               [y1 - активный отряд, y2 - действие]
!!BMy1:T?y3;                  [y3 - тип атакующих существ]
!!FU|y3<>(MON_ZOMBIE)/y2<>(BATTLE_ACTION_WALK_AND_ATTACK):E; [выход если не атака зомби]

!!BG:E?y2;                    [y2 - целевой отряд]
!!BMy2:F?y3;                  [y3 - флаги существ целевого отряда]
!!VRy4:Sy3 &(MON_FLAG_ALIVE); [если существа целевого отряда живые]
!!FU&y4=0:E;                  [выход если зомби атакуют не отряд живых существ]

!!BMy2:N?y5 H?y6 L?y7;        [y5/y6/y7 - количество существ/макс. здоровье/потер. здоровье целевого отряда]
!!VRy5:*y6 -y7;               [y5 - суммарное здоровье целевого отряда перед атакой]
!!VRv7920:Cy1/y2/y5;          [v7920/v7921/v7922 - номер атакующего отряда/номер целевого отряда/здоровье целевого отряда]

!?FU(OnMonsterPhysicalDamage)&v7920>(NO_STACK); [при нанесении урона]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!MF:F?y1;                    [y1 - урон, который получит целевой отряд]

!!if&y1<v7922;                [если атака не уничтожит целевой отряд полностью]
  !!VRv7920:C-1/-1/-1;        [обнуляем v-переменные]
  !!FU:E;                     [выход]
!!en;

!!BMv7920:N?y1 H?y2 L?y3 B?y4;[y1/y2/y3/y4 - количество существ/макс. здоровье/потер. здоровье/начальное количество отряда зомби]

!!if&y1=y4/y3=0;              [если атака не уничтожит целевой отряд полностью]
  !!VRv7920:C-1/-1/-1;        [обнуляем v-переменные]
  !!FU:E;                     [выход если у отряда зомби нет потерь и раненных]
!!en;

!!VRy5:Sy1 *y2 -y3 +v7922;    [y5 - новое суммарное здоровье отряда зомби]
!!VRy6:Sy4 *y2;               [y6 - максимально допустимое здоровье отряда зомби]
!!VRy5&y5>y6:Sy6;             [y5 - фактическое здоровье отряда зомби после трупоедства]
!!VRy6:Sy5 :y2;               [y6 - количество нераненых сомби после трупоедства]
!!VRy7:Sy5 %y2;               [y7 - здоровье раненого зомби]
!!VRy6&y7>0:+1;               [добавляем раненого зомби к общему количеству]
!!VRy8&y7>0:Sy2 -y7;          [y8 - потерянное здоровье раненого зомби]

; Show battle log and play animation/sound
!!if&i^battle_isVisible^;
  !!if&y1=1;
    !!SN:H^monname^/(MON_ZOMBIE)/0/?(flesheaterName:z);
  !!el;
    !!SN:H^monname^/(MON_ZOMBIE)/1/?(flesheaterName);
  !!en;

  !!SN:T^es.762.battleLog^?(battleLog:z)/^flesheater^/(flesheaterName);
  !!MM:S(battleLog);

  !!SN:P^ANIMDEAD^;           [вывод звука поднятия нежити]
  !!BMv7920:V74;              [анимация вампирской регенерации]
!!en;

!!BMv7920:Ny6 Ly8;            [и восстановление отряда зомби]
!!BMv7921:F?y1;               [y1 - флаги целевого отряда]
!!VRy1:|813694976;            [добавляем цели флаги жертвы/клона/неизменяемости цвета]
!!BMv7921:Fy1 E0 K1;          [обновляем флаги и убиваем цель без возможности воскрешения]
!!VRv7920:C-1/-1/-1;          [обнуляем v-переменные]

** end
