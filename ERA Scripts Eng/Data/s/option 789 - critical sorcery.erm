ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Critical Sorcery
** Name rus.     : Критическое Волшебство
** Options       : 789
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!#UN:P789/?v1;                [проверяем включена ли опция 789 в y1]
!#UN&v1>0:P213/0;             [если включена, отключаем опцию Волшебство II]
!#VRv7198&v1>0:S0;            [...]

!?FU(OnAfterErmInstructions); [перед началом новой игры или после загрузки старой]
!!UN:P789/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!FU(Fun_SetDescriptionFromERT):P0/25/1/179012;      [Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/25/2/179013;      [Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/25/3/179014;      [Устанавливаем экспертное описание навыка]
!!re y1/0/155:;               [Устанавливаем описание специализации]
  !!HEy1:X?y2/?y3/?y4/?y5/?y6/?y7/?y8; 
  !!FU(Fun_SetDescriptionFromERT)&y2=0/y3=25:P2/y1/2/179015;
!!en:;
!!FU(Fun_SetDescriptionFromERT):P3/76/1/179002;      [Описание Ожерелья]
!!FU(Fun_SetDescriptionFromERT):P3/77/1/179003;      [Описание Кольца]
!!FU(Fun_SetDescriptionFromERT):P3/78/1/179004;      [Описание накидки]
!!FU(Fun_SetDescriptionFromERT):P3/139/1/179304;     [Описание Сборника]

!?FU(OnAfterLoadGame);        [после загрузки]
!!UN:P789/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!FU(Fun_SetDescriptionFromERT):P0/25/1/179012;      [Устанавливаем базовое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/25/2/179013;      [Устанавливаем продвинутое описание навыка]
!!FU(Fun_SetDescriptionFromERT):P0/25/3/179014;      [Устанавливаем экспертное описание навыка]
!!re y1/0/155:;               [Устанавливаем описание специализации]
  !!HEy1:X?y2/?y3/?y4/?y5/?y6/?y7/?y8; 
  !!FU(Fun_SetDescriptionFromERT)&y2=0/y3=25:P2/y1/2/179015;
!!en:;
!!FU(Fun_SetDescriptionFromERT):P3/76/1/179002;      [Описание Ожерелья]
!!FU(Fun_SetDescriptionFromERT):P3/77/1/179003;      [Описание Кольца]
!!FU(Fun_SetDescriptionFromERT):P3/78/1/179004;      [Описание накидки]
!!FU(Fun_SetDescriptionFromERT):P3/139/1/179304;     [Описание Сборника]

!?MR1;                        [перед расчетом сопротивления]
!!UN:P789/?y1;                [проверяем включена ли опция 789 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!BG:A?y1;                    [y1 - тип действия (1 - герой колдует заклинание)]
!!FU&y1<>1:E;                 [выход, если не колдовство героя]
!!BG:S?y2 Q?y3;               [y2/y3 - номер заклинания/сторона]
!!MR:F?y4;                    [y4 - конечный урон заклинания]
!!FU&y4<=0:E;                 [выход, если маг.урона нет]
!!BA:Hy3/?y5;                 [y2 - номер колдующего героя]
!!FU&y5<0:E;                  [выход, если героя нет (а вдруг?:)]
!!UN:C42231940/4/?y6;         [y6 - номер отряда-цели заклинания]
!!UN:C6919200/4/?y7;          [...]
!!VRy6:-y7 -21708 :1352;      [...]
!!HEy5:S25/?y9;               [y9 - Уровень навыка Волшебства героя (0..3)]
!!HEy5:X?y7/?y8/d/d/d/d/d;    [y7=0,y8=25 для специалистов по Волшебству]
!!VRy9&y7=0/y8=25:+1;         [y9 - Уровень навыка Волшебства героя (0..3) + 1 для специалистов]
!!VRy9:*10;                   [y9 шанс критического удара заклианния 10% за каждый уровень навыка/специализацию]
!!HEy5:A2/76/?i/?y11 A2/77/?i/?y12 A2/78/?i/?y13 A2/139/?i/?y14; [y11-y14 - Ожерелье/Кольцо/Накидка/Сборник Мага надетые на героя]
!!VRy9&y11>0:+5;              [+5% крит за Ожерелье]
!!VRy9&y12>0:+10;             [+10% крит за Кольцо]
!!VRy9&y13>0:+15;             [+15% крит за Накидку]
!!VRy9&y14>0:+50;             [+50% крит за Накидку]
!!VRy10:S0 R99;               [y10 -случайное число (0..99)]
!!if&y10<y9:;
  !!VRy4:*3 :2;               [y4 - полуторный урон при критическом эффекте]
  !!MR:Fy4;                   [установка полуторного урона при критическом эффекте]
  !!BA:Q?f;                   [f-статус быстрой битвы]
  !!BMy6&f=0:V82;             [дополнительная анимация на целевом стеке при критическом эффекте]
!!en:;

** end
