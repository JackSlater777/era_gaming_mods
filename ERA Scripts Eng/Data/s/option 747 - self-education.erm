ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor
** Name          : Self-education
** Name rus.     : Самообучение
** Options       : 747
** Dialogs       : -
** Variables     : z279,z310,z311 (используются также как и в "3 wog - secondary skill text")
** Tmp variables : -
** Timers        : TM2 (переопределяется стандартный)
** Functions     : FU7979,FU7980
** PO-values     : -

** Герои с неизрасходованными очками передвижения тратят их на самообучение, получая опыт.
** Самообучение возможно при нахождении в гарнизоне города (не для гостевого героя) с отстроенными "образовательными учреждениями"
** К "образовательным учреждениям" оносятся:
** Гильдия магии (каждый уровень отдельно) - 5% неизрасходованных ОД переходит в опыт
** Библиотека (Башня) - 5%
** Академия боевых искусств (Темница) - 5%
** Университет магии (Сопряжение) - 5%
** Все проценты суммируются.
** Вторичный навык "Обучение" позволяет герою заниматься образованием даже вне города, переводит 10/20/30% ОД в опыт
** и увеличивает эффективность "образовательных зданий" до 6/7/8% (на 20/40/60%).

** Прим: самообучение отключается, если в игре всего один игрок

!#TM2:S1/999/1/255;           [каждый день для каждого игрока, но после всех операций триггрера TM1]

!?PI;                         [Установка описаний]
!!UN:P747/?y1;                [проверяем включена ли опция 747 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!SN:W^opt747.PrevPlayer^/-1; [Инициализация значения "предыдущий игрок"]
!!UN:P205/?y2;                [y2>0 при включенном расширении Обучаемость II]
!!UN:P217/?y3;                [y3>0 при включенном расширении Обучаемость I]
!!VRz279:Sz179269;            [получаем базовое описание навыка]
!!VRz279&y2>0:+^%Z179272^;    [...]
!!VRz279&y3>0:+^%Z179275^;    [...]
!!UN:G0/21/1/279;             [Устанавливаем базовое описание навыка]
!!VRz310:Sz179270;            [получаем продвинутое описание навыка]
!!VRz310&y2>0:+^%Z179273^;    [...]
!!VRz310&y3>0:+^%Z179276^;    [...]
!!UN:G0/21/2/310;             [Устанавливаем продвинутое описание навыка]
!!VRz311:Sz179271;            [получаем экспертное описание навыка]
!!VRz311&y2>0:+^%Z179274^;    [...]
!!VRz311&y3>0:+^%Z179277^;    [...]
!!UN:G0/21/3/311;             [Устанавливаем экспертное описание навыка]

!?TM2;                        [каждый день для каждого игрока]
!!UN:P747/?y1;                [проверяем включена ли опция 747 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!SN:W^opt747.PrevPlayer^/?y1;[y1 - предудущий игрок]
!!OW:C?y2;                    [y2 - текущий игрок]
!!SN:W^opt747.PrevPlayer^/y2; [сохраняем текущего игрока как предыдущего]
!!FU|y1=y2/y1=-1:E;           [выход, если игрок единственный или первый день первого игрока]
!!DO7979/0/155/1:Py1/y2;      [начисление опыта от навыка "Обучение" героям предыдущего игрока и получения опыта героями текущего игрока]
!!UN:U98/-1/?y3;              [y3 - количество городов на карте]
!!VRy3:-1;                    [y3 - максимальный номер города на карте]
!!DO7980/0/y3/1:Py1;          [начисление опыта от "образовательных построек" героям предыдущего игрока]

!?FU7979;                     [начисление опыта от навыка "Обучение" героям предыдущего игрока и получения опыта героями текущего игрока]
** x1 - предыдущий игрок, x2 -  текущий игрок
!!HEx16:O?y1 S21/?y2 W?y3/1;  [y1 - хозяин героя, y2 - навык "Обучение", y3 - оставшиеся ОД]
!!if&y1=x1:;                  [для героев предыдущего игрока]
  !!VRy2:*10 +100 *y3 :100 -y3; [y2 - опыт который будет получен от навыка "Обучение"]
  !!SN:W^opt747.Hero%X16Exp^/y2;[сохраняем значение опыта, который будет получен от "Обучения"]
!!en:;
!!if&y1=x2:;                  [для героев текущего игрока]
  !!SN:W^opt747.Hero%X16Exp^/?y2 W^opt747.Hero%X16Exp^/0; [получаем и обнуляем накопленный опыт]
  !!HEx16:Edy2;               [добавляем герою полученный опыт]
!!en:;

!?FU7980;                     [начисление опыта от "образовательных построек" героям предыдущего игрока]
** x1 - предыдущий игрок
!!CA0/x16:H0/?y1;             [y1 - герой в гарнизоне города]
!!FU&y1<0:E;                  [выход, если в гарнизоне героя нет]
!!HEy1:O?y2 W?y3 S21/?y4;     [y2 - хозяин героя, y3 - оставшиеся ОД героя, y4 - навык "Обучение"]
!!FU&y2<>x1:E;                [выход, если герой не предыдущего игрока]
!!VRy4:+5;                    [y4 - эффективность "образовательных" зданий]
!!VRy10:S0;                   [y10 - Общая Эффективность (% обучения)]
!!CA0/x16:T?y5;               [y5 - тип города]
!!CA0/x16:B3/0;               [проверка наличия ГМ 1го уровня]
!!VRy10&1:Sy4;                [если построено, увеличиваем эффективность]
!!CA0/x16:B3/1;               [проверка наличия ГМ 2го уровня]
!!VRy10&1:Sy4 *2;             [если построено, увеличиваем эффективность]
!!CA0/x16:B3/2;               [проверка наличия ГМ 3го уровня]
!!VRy10&1:Sy4 *3;             [если построено, увеличиваем эффективность]
!!CA0/x16:B3/3;               [проверка наличия ГМ 4го уровня]
!!VRy10&1:Sy4 *4;             [если построено, увеличиваем эффективность]
!!CA0/x16:B3/4;               [проверка наличия ГМ 5го уровня]
!!VRy10&1:Sy4 *5;             [если построено, увеличиваем эффективность]
!!CA0/x16&y5=2:B3/22;         [проверка наличия Библиотеки (Башня)]
!!VRy10&1/y5=2:+y4;           [если построено, увеличиваем эффективность]
!!CA0/x16&y5=5:B3/23;         [проверка наличия Академии боевых искусств (Темница)]
!!VRy10&1/y5=5:+y4;           [если построено, увеличиваем эффективность]
!!CA0/x16&y5=8:B3/21;         [проверка наличия Университета магии (Сопряжение)]
!!VRy10&1/y5=8:+y4;           [если построено, увеличиваем эффективность]
!!SN:W^opt747.Hero%Y1Exp^/?y6;[y6 - получаемый опыт героя от навыка "Обучение"]
!!VRy10:+100 *y3 :100 -y3;    [y10 - получаемый опыт героя от "образовательных" зданий]
!!VRy6:+y10;                  [y6 - суммарный получаемый опыт]
!!SN:W^opt747.Hero%Y1Exp^/y6; [устанавливаем суммарный получаемый опыт]

**end
