ZVSE2
** Author        : Algor
** Update        : Archer30
** Name          : Self-education
** Name rus.     : Самообучение
** Options       : 747

** Герои с неизрасходованными очками передвижения тратят их на самообучение, получая опыт.
** Самообучение возможно при нахождении в гарнизоне города (не для гостевого героя) с отстроенными "образовательными учреждениями"
** К "образовательным учреждениям" оносятся:
** Гильдия магии (каждый уровень отдельно) - 5% неизрасходованных ОД переходит в опыт
** Библиотека (Башня) - 5%
** Академия боевых искусств (Темница) - 5%
** Университет магии (Сопряжение) - 5%
** Все проценты суммируются.
** Вторичный навык "Обучение" позволяет герою заниматься образованием даже вне города, переводит 10/20/30% ОД в опыт
** и увеличивает эффективность "образовательных зданий" до 6/7/8% (на 20/40/60%).

** Прим: самообучение отключается, если в игре всего один игрок


// Initialization
!?FU(OnAfterErmInstructions);
!!UN:P747/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Initialise variable of the previous player
!!VRi^opt747.PrevPlayer^:S(NO_PLAYER);

; Change the description of Learning
!!UN:P205/?(learning2:y);
!!UN:P217/?(learning1:y);
!!VR(basicDesc:z):Sz179269;
!!VR(basicDesc)&(learning2)>(FALSE):+^%z179272^;
!!VR(basicDesc)&(learning1)>(FALSE):+^%z179275^;
!!SN:H^secskill^/(SKILL_LEARNING)/(SKILL_BASIC)/(basicDesc);
!!VR(advancedDesc:z):Sz179270;
!!VR(advancedDesc)&(learning2)>(FALSE):+^%z179273^;
!!VR(advancedDesc)&(learning1)>(FALSE):+^%z179276^;
!!SN:H^secskill^/(SKILL_LEARNING)/(SKILL_ADVANCED)/(advancedDesc);
!!VR(expertDesc:z):Sz179271;
!!VR(expertDesc)&(learning2)>(FALSE):+^%z179274^;
!!VR(expertDesc)&(learning1)>(FALSE):+^%z179277^;
!!SN:H^secskill^/(SKILL_LEARNING)/(SKILL_EXPERT)/(expertDesc);


// Calculate and give exp to heroes on every turn end of every player
!?FU(OnEveryDay);
; Exit if the option self-learning is not enabled
!!UN:P747/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(player:y):Si^opt747.PrevPlayer^;
!!VRi^opt747.PrevPlayer^:Si^timerOwner^;
!!FU|(player)=i^timerOwner^/(player)=(NO_PLAYER):E;

; Loop through all towns and calculate the bonuses from educational totalBuildEfficiencys
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/?(townNum:y);

!!re (townId:y)/0/(townNum)/1/-1;
  !!CA0/(townId):O?(townOwner:y) H0/?(hero:y) T?(townType:y);
  !!co&(townOwner)<>(player);

  !!if&(hero)<=(NO_HERO);
    !!CA0/(townId):H1/?(hero);

    !!co&(hero)<=(NO_HERO);
  !!en;

  !!HE(hero):S(SKILL_LEARNING)/?(skill:y);
  !!VR(buildEfficiency:y):S(skill) +5;  [5% to *%8 per building depending on learning level]
  !!VR(totalBuildEffect:y):S0;
  ; Check whether Mage Guild is built
  !!CA0/(townId):B3/0;

  !!if&1;
    !!VR(totalBuildEffect):+(buildEfficiency);
    !!CA0/(townId):B3/1;

    !!if&1;
      !!VR(totalBuildEffect):+(buildEfficiency);
      !!CA0/(townId):B3/2;

      !!if&1;
        !!VR(totalBuildEffect):+(buildEfficiency);
        !!CA0/(townId):B3/3;
        
        !!if&1;
          !!VR(totalBuildEffect):+(buildEfficiency);
          !!CA0/(townId):B3/4;
          !!VR(totalBuildEffect)&1:+(buildEfficiency);
        !!en;
      !!en;
    !!en;
  !!en;

  ; Check Library for Tower
  !!if&(townType)=(TOWN_TOWER);
    !!CA0/(townId):B3/22;
    !!VR(totalBuildEffect)&1:+(buildEfficiency);
  ; Check Battle Scholar Academy for Dungeon
  !!el&(townType)=(TOWN_DUNGEON);
    !!CA0/(townId):B3/23;
    !!VR(totalBuildEffect)&1:+(buildEfficiency);
  ; Check Magic University for Conflux
  !!el&(townType)=(TOWN_CONFLUX);
    !!CA0/(townId):B3/21;
    !!VR(totalBuildEffect)&1:+(buildEfficiency);
  !!en;

  !!VRi^opt747.Hero%(hero)Exp^:S(totalBuildEffect);
!!en;

; Loop through all the heroes to calculate bonus from skill level and give exp
!!re (hero)/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HE(hero):O?(owner:y) S(SKILL_LEARNING)/?(skill) W?(movement:y)/1;

  !!if&(owner)=(player);
    !!VR(bonusExp:y):S(skill) *10 + i^opt747.Hero%(hero)Exp^ +100 *(movement) :100 -(movement);
    !!HE(hero):Ed(bonusExp);
    !!VRi^opt747.Hero%(hero)Exp^:S0;
  !!en;
!!en;

**end
