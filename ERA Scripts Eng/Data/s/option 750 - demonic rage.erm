ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor
** Name          : Demonic rage
** Name rus.     : Демоническая ярость
** Options       : 750
** Dialogs       : -
** Variables     : z249
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** Рогатые демоны получают способность перед атакой впадать в ярость,
** теряя защиту (-1) и значительно увеличивая силу атаки (+3). Эффект кумулятивен.
** FV Рогатых демонов повышено на 10%.

!?PI;
!!UN:P750/?y1;                [проверяем включена ли опция 750 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz249:Sz179267;            [получаем из ert новую подсказку по существу]
!!UN:G1/49/2/249;             [установка новой подсказки]
!!MA:F49/?y1;                 [y1 - FV рогатых демонов]
!!VRy1:*110 :100;             [увеличиваем FV на 10%]
!!MA:F49/y1;                  [обновляем FV]

!?BG0;
!!UN:P750/?y1;                [проверяем включена ли опция 750 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BG:N?y1 A?y2;               [y1/y2 - активный отряд/тип действия]
!!BMy1:T?y3;                  [y3 - тип существ в активном отряде]
!!FU|y2<>6/y3<>49:E;          [выход, если не атака рогатых демонов]
!!BMy1:D?y4;                  [y4 - текущая защита рогатых демонов]
!!if&y4>0:;                   [если защита не нулевая
  !!BA:Q?f;                   [f-статус быстрой битвы]
  !!VRz1&f=0:Sz179350;        [получаем текст для вывода в лог битвы из ert]
  !!MM&f=0:Sz1;               [вывод текста в лог битвы]
  !!VRz1&f=0:S^FRENZY^;       [z1 - звуковой файл бешенства]
  !!SN&f=0:P1;                [вывод звука бешенства]
  !!BMy1:Dd-1 Ad3 V17;        [снижение защиты, повышение атаки и анимация бешенства]
!!en:;

**end
