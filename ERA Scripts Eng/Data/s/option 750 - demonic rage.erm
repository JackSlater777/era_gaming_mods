ZVSE2
** Author        : Algor
** Update        : Archer30
** Name          : Demonic rage
** Name rus.     : ƒемоническа€ €рость
** Options       : 750

** –огатые демоны получают способность перед атакой впадать в €рость,
** тер€€ защиту (-1) и значительно увеличива€ силу атаки (+3). Ёффект кумул€тивен.
** FV –огатых демонов повышено на 10%.


!?FU(OnAfterErmInstructions);
!!UN:P750/?(wogOption:y);               [провер€ем включена ли опци€ 750]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!SN:H^monname^/(MON_HORNED_DEMON)/2/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.750.desc)^;
!!SN:H^monname^/(MON_HORNED_DEMON)/2/(newDesc:z);

!!MA:F(MON_HORNED_DEMON)/?(fv:y);       [FV рогатых демонов]
!!VR(fv):*110 :100;                     [увеличиваем FV на 10%]
!!MA:F(MON_HORNED_DEMON)/(fv);          [обновл€ем FV]

!?FU(OnBeforeBattleAction);
!!UN:P750/?(wogOption:y);               [провер€ем включена ли опци€ 750]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!BG:N?(stack:y) A?(action:y);          [активный отр€д/тип действи€]
!!BM(stack):T?(type:y);                 [тип существ в активном отр€де]
!!FU|(action)<>(BATTLE_ACTION_WALK_AND_ATTACK)/(type)<>(MON_HORNED_DEMON):E; [выход, если не атака рогатых демонов]

!!BM(stack):D?(def:y);                  [текуща€ защита рогатых демонов]

!!if&(def)>0;                           [если защита не нулева€]
  
  ; Show battle log and animation
  !!if&i^battle_isVisible^;
    !!BM(stack):N?(num:y);

    !!if&(num)=1;
      !!SN:H^monname^/(MON_HORNED_DEMON)/0/?(hornedDemonName:z);
      !!SN:T^es.750.singBattleLog^?(battleLog:z)/^hornedDemon^/(hornedDemonName);
    !!el;
      !!SN:H^monname^/(MON_HORNED_DEMON)/1/?(hornedDemonName);
      !!SN:T^es.750.plurBattleLog^?(battleLog:z)/^hornedDemon^/(hornedDemonName);
    !!en;

    !!MM:S(battleLog);                  [вывод текста в лог битвы]

    !!SN:P^FRENZY^;                     [вывод звука бешенства]
    !!BM(stack):V17;
  !!en;

  !!BM(stack):Dd-1 Ad3;                 [снижение защиты, повышение атаки]
!!en;

**end
