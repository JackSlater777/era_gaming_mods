ZVSE2
** Author orig.  : Berserker
** Rewritten by  : Archer30
** Name          : Grand manouvre
** Name rus.     : Великий Манёвр
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA
**                 [Algor] вынос текстов в ert-файл
** Options       : 997


// Initialization
!?FU(OnBeforeBattleUniversal);
; Initialise variable - This must to be execute regardless of the option
!!VRi^es_997_elfPos^:S-1;

// Store the Elf's target position if he has attacks twice and shooting
!?FU(OnBattleScreenMouseClick)&i^battle_isVisible^/i^es_997_elfPos^=-1;
!!UN:P997/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!CM:D?(clickedHex:y);
!!FU|(clickedHex)<1/(clickedHex)>185:E;

; Check the type of current stack and monster flags. store the clicked position if ideal
!!BMi^battle_current_stack^:T?(type:y);

!!if&(type)=(MON_GRAND_ELF);
  !!BMi^battle_current_stack^:F?(monflags:y);
  !!VR(canShoot:y):S(monflags) &(MON_FLAG_SHOOTER);
  !!VR(attacksTwice:y):S(monflags) &(MON_FLAG_ATTACKS_TWICE);

  !!VRi^es_997_elfPos^&(canShoot)/(attacksTwice):S(clickedHex);
!!en;

// Reset the morale to zero to the Elf to prevent morale proc in his first move of one hex
!?FU(OnBeforeBattleAction)&i^es_997_elfPos^>-1;
!!BG:A?(action:y);

!!if&(action)=(BATTLE_ACTION_WALK);
  !!BMi^battle_acting_stack^:P?(currPos:y);
  !!FU(ES_997_CalcDistanceBetweenHexes):P(currPos)/i^es_997_elfPos^/?(distance:y);

  ; Store the current morale and reset it to 0
  !!if&(distance)=1;
    !!BMi^battle_acting_stack^:Fd~(MON_FLAG_ATTACKS_TWICE) G212/?i^es_997_elfMorale^/d G212/0/d;
  ; Set position to -1 if the Elf walked for more than one hex or did another action than walking
  !!el;
    !!VRi^es_997_elfPos^:S-1;
  !!en;
!!el;
  !!VRi^es_997_elfPos^:S-1;
!!en;

// Restore to the Elf's turn if he just walked by one hex
!?FU(OnBeforeBattleStackTurn)&i^es_997_elfPos^>-1;
!#VA(stack:x);

!!VR(stack):Si^battle_acting_stack^;
; Restore the morale so the second attack have a chance to proc morale
!!BM(stack):G212/i^es_997_elfMorale^/d;
; Set position to -2 in order to restore the attacks twice flag
!!VRi^es_997_elfPos^:S-2;

// Give back attack twice to the Elf
!?FU(OnBattleActionEnd)&i^es_997_elfPos^=-2;
!!BMi^battle_acting_stack^:Fd|(MON_FLAG_ATTACKS_TWICE);
!!VRi^es_997_elfPos^:S-1;

// Funciton for calculating distance between two hexes
!?FU(ES_997_CalcDistanceBetweenHexes);
!#VA(hex1:x) (hex2:x) (distance:x);

!!VR(x1:y):S(hex1)%17;
!!VR(y1:y):S(hex1):17;
!!VR(x2:y):S(hex2)%17;
!!VR(y2:y):S(hex2):17;
!!VR(distanceY:y):S(y1)-(y2);
!!VR(distanceY)&(distanceY)<0:*-1;
!!VR(x1):*2;
!!VR(y1):&1;
!!VR(x2):*2;
!!VR(y2):&1;
!!VR(distanceX:y):S(x1)-(y1)-(x2)+(y2);
!!VR(distanceX)&(distanceX)<0:*-1;
!!VR(distanceX):-(distanceY);
!!VR(distance):S(distanceX);
!!VR(distance)&(distance)<0:S0;
!!VR(distanceY):*2;
!!VR(distance):+(distanceY):2;

** end
