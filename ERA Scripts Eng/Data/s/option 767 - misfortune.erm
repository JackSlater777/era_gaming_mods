ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Misfortune and demoralization
** Name rus.     : Ќеудача и деморализаци€
** Options       : 767
** Dialogs       : -
** Variables     : v7907-v7908
** Tmp variables : z1
** Timers        : -
** Functions     : -
** PO-values     : -

** ƒобавл€ет в игру отрицательную удачу (неудачу).
** Ўанс срабатывани€ положительной удачи - 5% за уровень удачи отр€да.
** Ўанс срабатывани€ отрицательной удачи (неудачи) - 10% за уровень неудачи отр€да.
** устанавливает ограничени€ на максимльную/минимальную мораль отр€дов в -10..+20.
** Ўанс срабатывани€ положительной морали - 5% за уровень морали отр€да.
** Ўанс срабатывани€ отрицательной морали - 10% за уровень деморалиации отр€да.


!?FU(OnStartOrLoad);                    // при входе в игру - установка параметров морали (спс. Igor)
!!UN:P767/?(wogOption:y);               // выход если опци€ не включена

!!SN:L^badluck.era^/?(badLuck:y);
!!UN&(badLuck)<>0:P767/(FALSE);

!!if&(wogOption)<>0;                    // если опци€ включена
  !!UN:C4605318/1/20;                   // максимум морали
  !!UN:C4605344/1/20;                   // ...
  !!UN:C4605354/1/20;                   // делитель шанса выпадени€ положительной морали - 1/20
  !!UN:C4605324/1/-10;                  // минимум морали
  !!UN:C4605331/1/-10;                  // ...
  !!UN:C4605854/1/10;                   // делитель шанса выпадени€ отрицательной морали- 1/10
!!el;                                   // если выключена, выставл€ем значени€ по-умолчанию
  !!UN:C4605318/1/3;                    // ...
  !!UN:C4605344/1/3;                    // ...
  !!UN:C4605354/1/24;                   // ...
  !!UN:C4605324/1/-3;                   // ...
  !!UN:C4605331/1/-3;                   // ...
  !!UN:C4605854/1/12;                   // ...
!!en; 

!?FU(OnBeforeBattleAction);   [перед действием]
!!UN:P767/?y1;
!!FU&y1=0:E;                  [выход если опци€ не включена]

!!BG:N?y1 A?y2 E?y3;          [y1/y2/y3 - активный отр€д/действие/целевой отр€д]
!!FU&y3=-1:E;                 [выход, если целевой отр€д не существует (например, убит абилкой перед основной атакой)]

!!if|y2=6/y2=7;               [если отр€д атакует]
  !!BMy1:G213/?v7907/d G213/0/d;[сохранение в v7907 и обнуление текущей (не)удачи атакующего отр€да]
  !!BMy3:G213/?v7908/d G213/0/d;[сохранение в v7908 и обнуление текущей (не)удачи целевого отр€да]
!!en; 

!?FU(OnMonsterPhysicalDamage);[перед нанесением физического урона]
!!UN:P767/?y1;
!!FU&y1=0:E;                  [выход если опци€ не включена]

!!MF:W?y1 N?y3;               [y1/y3 - тип атаки, отр€д получающий урон]
!!FU&y1>0:E;                  [выход, если урон от рва или башни]

!!BG:N?y1 E?y4;               [y1/y4 - ход€щий отр€д/цель]
!!FU|y1<0/y4<0:E;             [выход, если одного из взаимодействующих отр€дов нет (например, огненный щит от трупа)]

!!VRy2:Sv7907;                [y2 - (не)удача атакующего отр€да]

!!if&y3=y1;                   [если ответный удар]
  !!VRy1:Sy4;                 [y1 - отр€д, нанос€щий урон - отр€д-цель]
  !!VRy2:Sv7908;              [y2 - текуща€ (не)удача отр€да-цели]
!!en; 

!!FU&y2=0:E;                  [выход если (не)удача нулева€]

!!BMy1:G213/0/d;              [обнул€ем текущую (не)удачу отр€да]
!!VRy5:Sy2 *5;                [y5 - шанс выпадени€ удачи - 5% за уровень]
!!VRy5&y2<0:*-2;              [y5 - шанс выпадени€ Ќ≈удачи - 10% за уровень]
!!VRy4:S0 R99;                [y4 - случайное число 0.99]
!!FU&y5<y4:E;                 [выход, если (не)удача не выпала]

!!MF:F?y6;                    [y6 - урон, который получит отр€д]
!!BMy1:T?y7;                  [y7 - тип существ в отр€де нанос€щем урон]
!!UN:N3/2/y7/1;               [Z2 - название существ нанос€щих урон (мн.число)]

!!if&y2>0;                    [срабатывание удачи]
  !!VRy6:*3 :2;               [урон 150%]

  !!if&i&^battle_isVisible^;
    !!SN:T^es.767.goodLuck^/?z1/^mon^/z2; [получаем текст дл€ вывода в лог битвы]     
    !!MM:Sz1;                 [вывод текста в лог битвы]
    !!SN:P^GOODLUCK^;         [вывод звука удачи]
    !!BMy1:V18;               [анимаци€ удачи на отр€де]
  !!en;
!!el;                         [срабатывание неудачи]
  !!VRy6::2;                  [урон 50%...]
  !!VRy6&y6=0:S1;             [...но не менее 1]

  !!if&^battle_isVisible^;
    !!SN:T^es.767.badLuck^/?z1/^mon^/z2; [получаем текст дл€ вывода в лог битвы]
    !!MM:Sz1;                 [вывод текста в лог битвы]
    !!SN:P^BADLUCK^;          [вывод звука удачи]
    !!BMy1:V48;               [анимаци€ неудачи на отр€де]
  !!en;
!!en; 

!!MF:Fy6;                     [корректировка урона]

** end
