ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor
** Name          : Machines capture
** Name rus.     : Захват боевых машин
** Options       : 737
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** Победивший герой после боя получает уцелевшие машины врага (Подводу/Палатку/Баллисту)
** если у него есть свободное место для них. Если места нет - машины разрушаются.
** При капитуляции машины остаются у побежденного.
** Ненужная машина может быть разрушена по ПКМ в окне героя.

!?BG0;
!!UN:P737/?y1;             [проверяем включена ли опция 737 в y1]
!!FU&y1=0:E;               [выход если опция не включена]
!!BG:A?y1;                 [y1 - тип действия в бою]
!!SN&y1=5:W^opt737^/1;     [установка флага капитуляции при капитуляции героя]
!!SN&y1<>5:W^opt737^/0;    [сброс флага капитуляции, если действие - не капитуляция]

!?BA53;                    [после боя]
!!UN:P737/?y1;             [проверяем включена ли опция 737 в y1]
!!FU&y1=0:E;               [выход если опция не включена]
!!BA:H0/?y1 H1/?y2;        [y1/y2 - сражавшиеся герои]
!!FU|y2<0/y1<0/y2>155/y1>155:E; [выход если одного из героев не было]
!!HEy1:O?y3; !!HEy2:O?y4;  [y3/y4 - владелец атакующего/защищающегося]
!!VRy9:S-1; !!VRy10:S-1;   [y9 - победивший, y10 - проигравший]
!!VRy9&y3>=0:Sy1;          [...]
!!VRy9&y4>=0:Sy2;          [...]
!!VRy10&y3<0:Sy1;          [...]
!!VRy10&y4<0:Sy2;          [...]
!!FU|y9<0/y10<0:E;         [выход, если нет победившего или проигравшего героя]
!!SN:W^opt737^/?y3;        [y3 - флаг капитуляции]
!!FU&y3=1:E;               [выход, если проигравций капитулировал]
!!HEy9:A2/4/d/?y11 A2/5/d/?y12 A2/6/d/?y13; [y11/y12/y13 - баллиста/подвода/палатка победителя]
!!HEy10:A2/4/d/?y21 A2/5/d/?y22 A2/6/d/?y23; [y21/y22/y23 - баллиста/подвода/палатка проигравшего]
!!HEy10:A3/4/1/1 A3/5/1/1 A3/6/1/1; [удаление боевых машин проигравшего]
!!HEy9&y11=0/y21>0:A4/4;   [захват баллисты]
!!HEy9&y12=0/y22>0:A4/5;   [захват подводы]
!!HEy9&y13=0/y23>0:A4/6;   [захват палатки]
!!HEy9:O?y3;               [y3 - игрок-победитель]
!!OW:Iy3/?y4/?y5;          [y4=1 для ИИ-победителя]
!!FU&y4=1:E;               [выход, если победил ИИ-герой]
!!VRy3:S0; !!VRy4:S0; !!VRy5:S0; [инициализация переменных]
!!VRy3&y11=0/y21>0:S4;           [y3/y4/y5 - первая/вторая/третья захваченные машины]
!!VRy4&y12=0/y22>0/y3>0:S5;      [...]
!!VRy3&y12=0/y22>0/y3=0:S5;      [...]
!!VRy5&y13=0/y23>0/y3>0/y4>0:S6; [...]
!!VRy4&y13=0/y23>0/y3>0/y4=0:S6; [...]
!!VRy3&y13=0/y23>0/y3=0/y4=0:S6; [...]
!!VRz1&y4>0:Sz179302;      [вывод сообщения о захваченных машинах]
!!VRz1&y4=0:Sz179303;      [вывод сообщения о захваченных машинах]
!!IF&y3>0/y4=0/y5=0:Q1/8/y3/1/1;           [...]
!!IF&y3>0/y4>0/y5=0:Q1/8/y3/8/y4/1/1;      [...]
!!IF&y3>0/y4>0/y5>0:Q1/8/y3/8/y4/8/y5/1/1; [...]

!?CM2;                     [клик в окне героя]
!!CM:F?y1 S?y2 I?y3;       [y1-y3 - параметры клика]
!!FU|y1<>4/y2<>12/y3<15/y3>17:E; [выход, если не Ctrl+Click на баллисте/подводе/палатке]
!!OW:A-1/?y4; !!HEy4:A2/4/?i/?y5 A2/5/?i/?y6 A2/6/?i/?y7; [y5-y7 - наличие у героя баллисты/подводы/палатки]
!!FU&y3=15/y5<1:E;         [выход, если клик на несуществующей баллисте]
!!FU&y3=16/y6<1:E;         [выход, если клик на несуществующей подводе]
!!FU&y3=17/y7<1:E;         [выход, если клик на несуществующей палатке]
!!VRy3:-11;                [номер БМ]
!!VRz1:Sz179335;           [z1 - текст вопроса]
!!IF:Q1/8/y3/2/1;          [вопрос об уничтожении БМ]
!!if&1:;                   [при положительном ответе:]
  !!HEy4:A3/y3/1/1;        [удаление БМ]
  !!CM:R0;                 [отключение стандартного действия по ПКМ]
  !!UN:R3/y4;              [обновление экрана героя]
!!en:;

** end
