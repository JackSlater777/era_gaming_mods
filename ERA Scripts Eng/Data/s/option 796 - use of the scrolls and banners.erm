ZVSE2
ERMS_ScriptDate=10.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Use of the scrolls and banners
** Name rus.     : Использование свитков и знамён
** Options       : 796
** Dialogs       : -
** Variables     : -
** Tmp variables : z1-z2
** Timers        : -
** Functions     : FU7896,FU7929-FU7931
** PO-values     : -


!?FU(OnEveryDay)&i^timerDay^>1; [в начале дня]
!!UN:P796/?y1;                [проверяем включена ли опция 796 в y1]
!!OW:I-1/?y2;                 [y2=1 если ИИ, y2=0 если человек]
!!FU|y1=0/y2=0:E;             [выход если опция не включена или если ходит человек]

!!OW:H-1/1/0;                 [v1 - количество героев текущего игрока]
!!VRy3:Sv1;                   [y3 - количество героев текущего игрока]
!!DO7929/1/y3/1&y3>0:P;       [вызов FU7929 для каждого героя текущего игрока]

!?FU7929;                     [Разрушение свитков и знамён - AI проверка необходимости разрушения]
!!OW:H-1/1/x16;               [v1=номер героя]
!!VRy3:Sv1;                   [y3=номер героя]
!!HEy3:I?y1;                  [y1 - мана героя]
!!VRy1:*5;                    [y1 - порог маны при котором разрушать свиток 1/5=20%]
!!FU7896:Py3/?y2;             [y2 - макс. мана героя]
!!DO7930/19/82/1&y2>y1:P1/y3; [если маны у героя меньше 20%, ищем в рюкзаке (слоты 19-82) свиток (x1=1) для уничтожения]
!!HEy3:Y65/?y1/d/d;           [проверяем наличие бонуса скорости в y1]
!!DO7930/19/82/1&y1<500:P2/y3;[если бонус скорости меньше 500, ищем в рюкзаке (слоты 19-82) знамя (x1=2) для уничтожения]
!!HEy3:E?i/?y1;               [y1 - уровень героя]
!!DO7930/19/82/1&y1<15:P3/y3; [если уровень героя меньше 15, в рюкзаке (слоты 19-82) артефакт (x1=3) для уничтожения]

!?FU7930;                     [AI поиск и разрушение свитка/знамени]
** x1=1 - разрушить свиток, x1=2 - разрушить знамя, x2 - номер героя, x16 - номер слота (рюкзака)
!!HEx2:A1/?y1/x16;            [y1 - номер артефакта в слоте]
!!UN:J2/?y11;                 [y11 - выбранный человеком уровень сложности]
!!VRy11&y11=0:S80;            [y11 - бонус к эффективности пожертвований, равен бонусу уровня сложности]
!!VRy11&y11=1:S100;           [...]
!!VRy11&y11=2:S130;           [...]
!!VRy11&y11=3:S160;           [...]
!!VRy11&y11=4:S200;           [...]

!!if&x1=1/y1>1000; 
  !!VRy2:Sy1-1001;            [y2 - номер заклинания на свитке]
  !!SSy2:L?y10;               [получаем уровень заклинания в y10]
  !!VRy2:Sy10 *15 *y11 :100;  [бонус маны в y2 как 15*уровень заклинания + бонус за уровень сложности]
  !!HEx2:I?y3/1;              [Получение текущей маны в y3]
  !!VRy3:+y2;                 [увеличение текущей маны на y2]
  !!FU7896:Px2/?y4;           [y4 - макс. мана героя]
  !!VRy3&y3>y4:Sy4;           [новое значение маны не может превышать максимум маны героя]
  !!HEx2:Iy3/1;               [Установка нового значения маны]
  !!HEx2:A3/y1/1/0;           [Удаление свитка из слота]
  !!VRx16:+64;                [переход на конец цикла]
!!en; 

!!if&x1=2/y1=156; 
  !!VRy12:S500 *y11 :100;     [бонус движения с учетом уровня сложности]
  !!VRy13:S3 *y11 :100;       [длительность бонуса с учетом уровня сложности]
  !!HEx2:Y65/y12/y13/1;       [Добавление очков передвижения за подаренное знамя]
  !!HEx2:A3/156/1/0;          [Удаление знамени из слота рюкзака]
  !!VRx16:+64;                [переход на конец цикла]
!!en; 

!!if&x1=3/y1<>156/y1>0/y1<1000; 
  !!UN:Ay1/3/?y4 Ay1/5/?y5;   [y4 - класс артефакта (2/4/8/16), y5 - часть сборника (-1 = нет)]
  !!FU|y4<2/y4>4/y5<>-1:E;    [пропуск "ценных" артефактов или частей борников]
  !!FU&y1>=146/y1<=156:E;     [пропуск командирских артефактов и знамен]
  !!FU&y1>=161/y1<=170:E;     [пропуск "пустых" артефактов]
  !!VRy2&y4=2:S500;           [y2 - базовое кол-во опыта, получаемое в зависимости от класса артефакта (50% от опыта за пожертвование на алтаре)]

  !!VRy2&y4=4:S750;           [...]
  !!VRy2&y4=8:S1500;          [...]
  !!VRy2&y4=16:S3000;         [...]
  !!HEx2:S21/?y3;             [уровень навыка "Обучение" героя]
  !!VRy3:*5 +100;             [y3 = 100/105/110/115% - коэффициент полученного опыта]
  !!VRy2:*y3 :100 *y11 :100;  [y2 - опыт получаемый с учетом Обучения и уровня сложности]
  !!HEx2:Edy2;                [Добавление y2 очков опыта герою]
  !!HEx2:A3/y1/1/0;           [Удаление артефакта из слота рюкзака]
  !!VRx16:+64;                [переход на конец цикла]
!!en; 

!?FU(OnHeroScreenMouseClick);                        [по правому клику в окне героя]
!!UN:P796/?y1;                [проверяем включена ли опция 796 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!CM:I?y1 S?y2 F?y3;          [y1=место клика, y2=тип клика, y3-подтип клика]

!!if&i^mouse_action^=(MOUSE_LMB_PRESSED)/i^mouse_flags^=(MOUSE_FLAG_CTRL);
  !!FU&y1<>11/y1<>12/y1<>13/y1<>14/y1<>20/y1<>40/y1<>41/y1<>42/y1<>43/y1<>44:E; [выход если клик не на рюкзаке или разном]

  !!VRy2|y1=11/y1=12/y1=13/y1=14/y1=20:Sy1 -2; [y2=позиция разное для HE:A]
  !!VRy2&y1>=40/y1<=44:Sy1 -21; [y2=позиция рюкзака для HE:A]
  !!HE-1:A1/?y3/y2;             [получаем в y3 номер артефакта в слоте]
  !!VRy4&y3>1000:Sy3 -1001;     [получаем в y4 номер заклинания в свитке]
  !!FU7931&y3>1000:P1/y4/y1/y2; [для свитка вызываем FU7931 1/#заклинания/место клика]
  !!FU7931&y3=156:P2/0/y1/y2;   [для знамени вызываем FU7931 2/0/место клика]
  !!FU7931&y3>0/y3<1000/y3<>156/y1>=40/y1<=44:P3/y3/y1/y2; [для артефакта вызываем FU7931 2/0/место клика]
!!en;

*!FU|y2<>12/y3<>4:E;          [выход если не нажата Ctrl+ЛКМ]


!?FU7931;                     [Разрушение свитков и знамён в слоте рюкзака по правому клику (диалог)]
** x1=1(свиток)/2(знамя)/3(артефакт), x2=#заклинания (свиток)/0 (знамя)/#артефакта (артефакт), x3=место клика, x4-слот "разное"
!#VA(itemType:x) (itemSubType:x) (clickPlace:x) (miscSlot:x);

!!if&x1=1; 
  !!UN:N1/1/x2;               [получаем название заклинания на свитке в z1]
  !!SSx2:L?y1;                [получаем уровень заклинания в y1]
  !!VRy1:*15;                 [расчитываем стоимость получаемой при разрушении маны в y1 как 15*уровень заклинания]
  !!HE-1:I?y2/1;              [y2 - текущее значение маны]
  !!VRy3:Sy2 +y1;             [y3 - новое значение маны]
  !!FU7896:P-1/?y4;           [y4 - макс. мана героя]
  !!VRy1&y3>y4:Sy4 -y2;       [снижаем значение получаемой маны, если мана после разрущения превысит естественный максимум]
  !!VRy1&y1<0:S0;             [мана не может снижаться, даже если у героя сейчас запас превышает максимально возможный]
  !!VRz2:Sz179000;            [получаем текст вопроса в z2]
  !!IF:Q1/9/x2/2/z2;          [Вопрос: разрушить ли свиток]
  !!FU&-1:E;                  [выход, если ответ отрицательный]

  !!HE-1:Idy1/1;              [Установка нового значения маны]
  !!VRy1:Sx2 +1001;           [в y1 номер свитка]
  !!VRy2&x3>=40:S0;           [y2=0 если слот рюкзака]
  !!VRy2&x3<40:S1;            [y2=1 если слот разного]
  !!HE-1:A3/y1/1/y2;          [Удаление свитка из слота]
  !!HE-1&y2=1:A4/1080 A3/1080/1/1; [добавляем/удаляем свиток с несуществующим заклинанием для обновления списка заклинаний у героя]
  !!CM:R0;                    [отмена стандартной реакции на ПКМ]
  !!UN:R3/-1;                 [Обновление экрана героя]
!!en; 

!!if&x1=2; 
  !!VRz2:Sz179001;            [получаем текст вопроса в z2]
  !!IF:Q1/8/156/2/z2;         [Вопрос: подарить ли знамя]
  !!FU&-1:E;                  [выход, если ответ отрицательный]

  !!HE-1:Y65/500/3/1;         [Добавление +500 очков передвижения на 3 дня за подаренное знамя]
  !!HE-1:A3/156/1/0;          [Удаление знамени из слота рюкзака]
  !!CM:R0;                    [отмена стандартной реакции на ПКМ]
  !!UN:R3/-1;                 [Обновление экрана героя]
!!en; 

!!if&x1=3; 
  !!UN:Ax2/3/?y1;             [y1 - класс артефакта (2/4/8/16)]

  !!FU&y1<2:E;                [разрушение книги/грааля/БМ запрещено]

  !!VRy2&y1=2:S500;           [y2 - базовое кол-во опыта, получаемое в зависимости от класса артефакта (50% от опыта за пожертвование на алтаре)]
  !!VRy2&y1=4:S750;           [...]
  !!VRy2&y1=8:S1500;          [...]
  !!VRy2&y1=16:S3000;         [...]
  !!HE-1:S21/?y3;             [уровень навыка "Обучение" героя]
  !!VRy3:*5 +100;             [y3 = 100/105/110/115% - коэффициент полученного опыта]
  !!VRy2:*y3 :100;            [y2 - опыт получаемый с учетом Обучения]
  !!VRz2:Sz179336;            [получаем текст вопроса в z2]
  !!IF:Q1/8/x2/2/z2;          [Вопрос: пожертвовать ли артефакт]
  !!FU&-1:E;                  [выход, если ответ отрицательный]

  !!HE-1:Edy2;                [Добавление y2 очков опыта герою]
  !!HE-1:A2/x2/?y1/?y2;
  !!HE-1:Z?y4;

  !!if&y1>0;
    !!HE-1:A3/x2/1/0;           [Удаление артефакта из слота рюкзака]
  !!el&y2=0;
    !!re i/468/500/8;

      !!UN:Cy4/i/4/?y5;
      !!if&y5=x2;
        !!UN:Cy4/i/4/-1;
        !!VRy5:Si+4;
        !!UN:Cy4/y5/4/-1;
      !!en;
    !!en;
  !!en;

  *!UN:Cy4/980/1/?y5;
    *!IF:L^%x2 %y1 %y2 %y5^;
  !!CM:R0;                    [отмена стандартной реакции на ПКМ]
  !!UN:R3/-1;                 [Обновление экрана героя]
!!en; 

!?FU7896;                     [возврат в x2 макс. маны героя x1]
!!HEx1:Fd/d/d/?y5;            [y5 - Знание героя]
!!VRy5:*10;                   [y5 базовый макс. запас маны героя]
!!HEx1:S24/?y2;               [y2 - уровень навыка Интеллект [0..3]]
!!HEx1:X?y3/?y4/d/d/d/d/d;    [для специалистов по Интеллекту y3=0,y4=24]
!!HEx1:Ed/?y6;                [y6 - уровень героя]
!!VRy7:S0;                    [------------------------------------------]
!!VRy7&y2=1:Sy5 :4;           [                                          ]
!!VRy7&y2=2:Sy5 :2;           [ y7 - бонус запаса маны от Интеллекта     ]
!!VRy7&y2=3:Sy5;              [     и специализации Интеллект            ]
!!VRe99&y3=0/y4=24:Sy6 :20 +1;[                                          ]
!!VRy7&y3=0/y4=24:*e99;       [------------------------------------------]
!!VRx2:Sy5 +y7;               [макс. запас маны = запас от Знания + бонус от Интеллекта]

!?BA52&1000;                  [перед не-теоретической битвой]
!!UN:P796/?y1;                [проверяем включена ли опция 796 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!BA:H0/?y1 H1/?y2 O?y3/?y4;  [y1,y2 - сражающиеся герои, y3,y4 - сражающиеся игроки]
!!OW:Iy3/?y5;                 [y7 - номер ИИ-героя (-1, если нет)]
!!OW&y2>=0:Iy4/?y6;           [y8 - номер героя-человека (-1, если нет)]
!!VRy7:S-1; !!VRy8:S-1;       [...]
!!VRy7&y5=1:Sy1;              [...]
!!VRy8&y5=0:Sy1;              [...]
!!VRy7&y6=1/y2>=0:Sy2;        [...]
!!VRy8&y6=0/y2>=0:Sy2;        [...]
!!FU|y7<0/y8<0:E;             [выход, если нет ИИ-героя или героя-человека]

!!HEy7:Ed/?y9; !!HEy8:Ed/?y10;[y9/y10 - уровни героев ИИ/человека]
!!VRy11:Sy9 -y10;             [y11 - разница уровней героев (в пользу ИИ)]
!!HEy7:I?y1/1;                [y1 - мана ИИ-героя]
!!FU7896:Py7/?y2;             [y2 - макс. мана ИИ-героя]
!!VRy3:Sy2 *80 :100;          [y3 - 80% макс. маны ИИ-героя]
!!VRy4:Sy2 *60 :100;          [y4 - 60% макс. маны ИИ-героя]
!!VRy5:Sy2 *40 :100;          [y5 - 40% макс. маны ИИ-героя]
!!DO7930/19/82/1&y5>y1/y11<=10:P1/y7; [если маны у ИИ-героя меньше 40%, и он сильнее героя-человека не более чем на 10 уровней, уничтожаем свиток из рюкзака]
!!HEy7:I?y1/1;                [y1 - мана ИИ-героя]
!!DO7930/19/82/1&y4>y1/y11<=5:P1/y7; [если маны у ИИ-героя меньше 60%, и он сильнее героя-человека не более чем на 5 уровней, уничтожаем свиток из рюкзака]
!!HEy7:I?y1/1;                [y1 - мана ИИ-героя]
!!DO7930/19/82/1&y3>y1/y11<=0:P1/y7; [если маны у ИИ-героя меньше 80%, и он НЕ сильнее героя-человека, уничтожаем свиток из рюкзака]

** end
