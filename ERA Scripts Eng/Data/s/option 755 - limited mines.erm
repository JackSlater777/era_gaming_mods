ZVSE2
** Author        : Algor (Idea from Phoenix)
** Updated by    : Archer30
** Name          : Limited mines
** Name rus.     : Истощающиеся шахты
** Options       : 755


!?FU(OnAfterErmInstructions); [перед началом игры: установка запаса ресурсов]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!UN:X?y1/?y2;                [y1/y2 - размер/этажность карты]
!!VRy1::36 +y2;               [y1 - коэффициент от размера карты: S=1,L=2...G=7, +1 за подземелье]
!!UN:U53/-1/?y2;              [y2 - количество шахт]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO(ES_755_SetMineStorage)/1/y2/1:Py1; [для каждой шахты устанавливаем запас]

!?FU(ES_755_SetMineStorage);  [x1 - коэффициент от размера карты]
!!UN:U53/-1/-1/1;             [получаем координаты шахты в v1-v3]
!!VRy1:Sx1 *10 Ry1;           [y1 - запас шахты S:10-20, L:20-40 ... G:70-140, +(10-20) за подземелье]
!!SN:W^opt755_%V1_%V2_%V3^/y1;[установка запаса]

!?FU(OnEveryDay)&i^timerDay^>1; [каждый день: истощение работающих шахт]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!UN:U53/-1/?y2;              [y2 - количество шахт]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO(ES_755_ReduceMineStorage)/1/y2/1:P; [для каждой работающей шахты уменьшаем запас]

!?FU(ES_755_ReduceMineStorage); [x1 - текущий игрок]
!!UN:U53/-1/-1/1;             [получаем координаты шахты в v1-v3]
!!MNv1/v2/v3:O?y1/1;          [y1 - владелец шахты]
!!FU&y1<>i^timerOwner^:E;                [выход, если шахта не принадлежит текущему игроку]

!!VRi^opt755_%V1_%V2_%V3^:-1;[обновление запаса]

!!if&i^opt755_%V1_%V2_%V3^=0;[выход, если шахта не истощена]
  !!MNv1/v2/v3:O-1;             [убираем владельца]
  !!TRv1/v2/v3:T?y2/?i/?i/?i/?i/?i/?i/?i;[y2 - тип земли]
  !!UN:Iv1/v2/v3/1/0/91/0/y2/1; [вешаем "знак" <Путо> на вход]
  !!TRv1/v2/v3:E1 P0;           [закрываем вход]
!!en;

!?FU(OnAdventureMapRightMouseClick)&i^mouse_flags^=(MOUSE_FLAG_RMB); [клик на карте приключений]
!!UN:P755/?y1;                [проверяем включена ли опция 755 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]


!!OBi^mouse_mapX^/i^mouse_mapY^/i^mouse_mapZ^:T?y5;          [y5 - тип объекта]
!!if&y5=(OBJ_MINE);
  !!CM:P?y1/?y2/?y3;                                         [координаты и параметры клика]
  !!SN:O?y1/?y2/?y3;
  !!MNi^mouse_mapX^/i^mouse_mapY^/i^mouse_mapZ^:R?y6/1 O?y4; [y6 - ресурс]
  !!SN:W^opt755_%Y1_%Y2_%Y3^/?y7;                            [y7 - запас]
  !!VRy7|y6=0/y6=2:*2;                                       [удвоенный запас для дерева/руды]
  !!VRy7&y6=6:*1000;                                         [*1000 для золота]

  !!FU(ES_GetStringByFilename):P^minename^/y6/?(mineName:z);
  !!FU(ES_GameMgr_GetObjOwnerStr):Py4/?(ownerName:z);
  !!IF:Q1/y6/y7/4/^%(mineName)
  %(ownerName)^;            [вывод сообщения]
  !!CM:R0;                                                   [отключение стандартного действия]
!!en;


**end
