ZVSE2
** Author orig.  : Jim Vogan
** Rewritten by  : Archer30 and daemon_n
** Name          : Buckler of Beelzebub
** Name rus.     : Щит Вельзевула
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
**               : [Algor] вынос текстов в ert-файл
**               : [Algor] перенумерация переменных
** Options       : 281


!?FU(OnAfterErmInstructions);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:A15/(ART_BLANK_SHIELD)/(ART_SWORD_OF_HELLFIRE)/(ART_SHIELD_OF_THE_DAMNED)/(ART_HELLSTORM_HELMET)/(ART_BREASTPLATE_OF_BRIMSTONE); new combo #15

!!SN:H^art^/(ART_BLANK_SHIELD)/0/^%T(es.281.name)^;
!!SN:H^art^/(ART_BLANK_SHIELD)/1/^%T(es.281.desc)^;

; Enable spell on the artifact
!!UN:A(ART_BLANK_SHIELD)/8/(TRUE);

// artifact #(ART_BLANK_SHIELD) put on
!?FU(OnEquipArt)&v998=(ART_BLANK_SHIELD);
; disable this script if option disabled
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; increase primary skills by 2 if v998=(ART_BLANK_SHIELD)
!!HE(CURRENT_HERO):Fd2/d2/d2/d2;

// artifact #(ART_BLANK_SHIELD) taken off
!?FU(OnUnequipArt)&v998=(ART_BLANK_SHIELD);
; disable this script if option disabled
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; decrease primary skills by 2 if v998=(ART_BLANK_SHIELD)
!!HE(CURRENT_HERO):Fd-2/d-2/d-2/d-2;

// Change the spell of AB depending on the actual artifact equipped
!?FU(ES_OnArtifactGiveSpell);
!#VA(address:x);

!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/?(art:y);

!!if&(art)=(ART_BLANK_SHIELD);
  !!UN:C5085052/1/(SPELL_DIMENSION_DOOR);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!el&(art)=(ART_ARMAGEDDONS_BLADE);
  !!UN:C5085052/1/(SPELL_ARMAGEDDON);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!en;

// Add no retaliation flag to the stacks with the owner having a Buckler of Beelzebub
!?FU(OnSetupBattlefield);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!HEi:A2/(ART_BLANK_SHIELD)/?(backpacked:y)/?(equipped:y);

  !!if&(equipped)>0;
    !!VR(firstStack:y):Si *(BATTLE_DEFENDER_STACK_FIRST);
    !!VR(lastStack:y):S(firstStack) +(BATTLE_STACKS_PER_SIDE) -1;

    !!re (stack:y)/(firstStack)/(lastStack);
      !!BM(stack):T?(type:y);
      !!BM(stack)&(type)>(NO_MON)/(type)<>(MON_ARROW_TOWERS):Fd|(MON_FLAG_ATTACKS_ALL_AROUND);
    !!en;
  !!en;
!!en;

; For summoned stacks
!?FU(OnBattleStackObtainsTurn);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BG:H?(hero:y);

!!if&(hero)>(NO_HERO);
  !!HE(hero):A2/(ART_BLANK_SHIELD)/?(backpacked:y)/?(equipped:y);

  !!if&(equipped)>0;
    !!BMi^battle_current_stack^:T?(type:y);
    !!BMi^battle_current_stack^&(type)<>(MON_ARROW_TOWERS):Fd|(MON_FLAG_NO_RETALIATION);
  !!en;
!!en;

// Fix the returned values of EEF function for getting the hero's primary skills without artifact
!?FU(GetHeroPrimarySkillsWithoutArts);
!#VA(hero:x);      Hero ID or (CURRENT_HERO).
!#VA(attack:x);    Out. Attack value.
!#VA(defense:x);   Out. Defense value.
!#VA(power:x);     Out. Power value.
!#VA(knowledge:x); Out. Knowledge value.

!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!HE(hero):A2/(ART_BLANK_SHIELD)/?(backped:y)/?(equipped:y);

!!if&(equipped);
  !!VR(attack):-2;
  !!VR(defense):-2;
  !!VR(power):-2;
  !!VR(knowledge):-2;
!!en;


** end
