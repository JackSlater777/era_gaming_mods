ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : Cutthroats
** Name rus.     : Головорезы
** Options       : 758

** Воры становятся хладнокровными головорезами не зависящими от боевого духа,
** но каждый раз после убийства отряда воодушевляются получая возможность совершить еще одно действие.


// Initialization
!?FU(OnAfterErmInstructions);
!!UN:P758/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set up new names and description
!!SN:H^monname^/(MON_ROGUE)/0/^%T(es.758.singName)^;
!!SN:H^monname^/(MON_ROGUE)/1/^%T(es.758.plurName)^;
!!SN:H^monname^/(MON_ROGUE)/2/?(desc:z);
!!VR(newDesc:z):S(desc) +^%T(es.758.desc)^;
!!SN:H^monname^/(MON_ROGUE)/2/(newDesc);

; Give No Morale monster flag and increse the fight value
!!MA:F(MON_ROGUE)/?(fv:y) X(MON_ROGUE)/d|(MON_FLAG_NO_MORALE);
!!VR(fv):*105 :100;
!!MA:F(MON_ROGUE)/(fv);

!?FU(OnBeforeBattleUniversal);
; Initialise variable - This must to be execute regardless of the option
!!VRi^es_758_defStack^:S(NO_STACK);

// Store defending stack ID if it is Rogue attacking another monster
!?FU(OnBeforeBattleAction);
!!UN:P758/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BG:A?(action:y);
!!BMi^battle_acting_stack^:T?(type:y);

!!if&(action)=(BATTLE_ACTION_WALK_AND_ATTACK)/(type)=(MON_ROGUE);
  !!BG:D?(target:y);
  !!BU:E(target)/?(defStack:y);
  !!VRi^es_758_defStack^:S(defStack);
!!en;

// Check if Rogue is eligible to attack again
!?FU(OnBattleActionEnd)&i^es_758_defStack^>(NO_STACK);
; Check if the defending stack is dead
!!BMi^es_758_defStack^:N?(defNum:y);

!!if&(defNum)<=0;
  ; Remove the acted flag so cutthroat can act again
  !!BMi^battle_acting_stack^:Fd~(MON_FLAG_ACTED);


  ; Show battle log and play animation/sound
  !!if&i^battle_isVisible^;
    !!BMi^battle_acting_stack^:N?(cutthroatNum:y);

    !!if&(cutthroatNum)=1;
      !!SN:H^monname^/(MON_ROGUE)/0/?(cutthroatName:z);
    !!el;
      !!SN:H^monname^/(MON_ROGUE)/1/?(cutthroatName);
    !!en;

    !!SN:T^es.758.battleLog^?(battleLog:z)/^cutthroat^/(cutthroatName);
    !!MM:S(battleLog);

    !!SN:P^MIRTH^;                      [вывод звука морали]
    !!BMi^battle_acting_stack^:V20;     [анимация морали на отряде]
  !!en;
!!el;
  !!VRi^es_758_defStack^:S(NO_STACK);
!!en;
// Restore the turn to Rogue's turn
!?FU(OnBeforeBattleStackTurn)&i^es_758_defStack^>(NO_STACK);
!#VA(stack:x);

!!VR(stack):Si^battle_acting_stack^;
!!VRi^es_758_defStack^:S(NO_STACK);

** end
