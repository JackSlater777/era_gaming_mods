ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Battle experience
** Name rus.     : Боевой опыт
** Options       : 736
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7898,FU7899, FU7999
** PO-values     : -

** Отряды в битвах с участием игрока-человека получают опыт и новые ранги непосредственно в процессе боя.
** Опыт начисляется только за убийства вражеских существ и только убившему отряду.
** Отряд, который был непосредственной целью атаки получает опыт за все потери врага, связанные
** с данной атакой, в т.ч. из-за многоклеточных атак/огненного щита/огненной стены/рва/мин и пр.
** ИИ-отряды получают бонус опыта в зависимости от выбранного уровня сложности.
** Знамена полководца с бонусом опыта дают соответствующую прибавку.
** Призываемые существа опыта не получают. Убийство Клона опыта не дает.

!?FU(OnAfterBattleUniversal);           // после окончания битвы
!!UN:P736/?y1; !!SN&y1<>0:W^opt736.restorexp^/0; // сброс флага переигрываемой битвы, если опция включена

!?FU(OnSetupBattlefield)&1000;          // перед нетеоретической битвой
!!UN:P736/?y1 P900/?y2; !!FU|y1=0/y2=0:E; // выход если хоть одна опций 736 и 900 не включена
!!SN:W^opt736.restorexp^/?r W^opt736.restorexp^/1; // r=1, если битва переигрывается
!_!IF:M^ReBattle=%Vr^;
!!SN:W^opt736.bm.1^/-1; !!SN:W^opt736.bm.2^/-1; // инициализация флагов
!!BA:H0/?y1 H1/?y2 S?y3 P?y7/?y8/?y9;   // y1/2 - герои, y3>0 для осадной битвы, y7-y9 - координаты битвы
!!re i/0/6:;
  !!SN:W^opt736.0.%Vi^/0; !!SN:W^opt736.1.%Vi^/0; // обнуление массива опыта существ
  !!HEy1:C0/i/d/?y20;                   // y20 - кол-во сущест в i-м отряде атакующего
  !!SN&r=1:W^opt736.r0.%Vi^/?y30; !!EXy1/i&y20>0/r=1:Ey30; // восстановление опыта отряда для переигрываемой битвы
  !!EXy1/i&y20>0:E?y10;                 // y10 - опыт отряда атакующего, если есть существа
  !!SN&y20>0:W^opt736.0.%Vi^/y10;       // сохранение опыта отряда атакующего, если есть существа
  !!SN&y20>0/r=0:W^opt736.r0.%Vi^/y10;  // сохранение опыта отряда атакующего для переигрываемой битвы
  !!if&y2>=0:;                          // если есть герой-защитник
    !!HEy2:C0/i/d/?y20;                 // y20 - кол-во сущест в i-м отряде защищающегося
    !!SN&r=1:W^opt736.r1.%Vi^/?y30; !!EXy2/i&y20>0/r=1:Ey30; // восстановление опыта отряда для переигрываемой битвы    
    !!EXy2/i&y20>0:E?y10;               // y10 - опыт отряда защищающегося, если есть существа
    !!SN&y20>0:W^opt736.1.%Vi^/y10;     // сохранение опыта отряда защищающегося, если есть существа
    !!SN&y20>0/r=0:W^opt736.r1.%Vi^/y10;// сохранение опыта отряда защищающегося для переигрываемой битвы
  !!en:;
  !!if&y2<0/y3>0:;                      // если осадная битва без героя-защитника 
    !!CAy7/y8/y9:M2/i/d/?y20;           // y20 - кол-во сущест в i-м отряде гарнизона
    !!SN&r=1:W^opt736.r1.%Vi^/?y30; !!EXy7/y8/y9/i&y20>0/r=1:Ey30; // восстановление опыта отряда для переигрываемой битвы    
    !!EXy7/y8/y9/i&y20>0:E?y10;         // y10 - опыт отряда гарнизона, если есть существа
    !!SN&y20>0:W^opt736.1.%Vi^/y10;     // сохранение опыта отряда гарнизона, если есть существа
    !!SN&y20>0/r=0:W^opt736.r1.%Vi^/y10;// сохранение опыта отряда гарнизона для переигрываемой битвы
  !!en:;
!!en:; 
!!if&r=1:;                              // если битва переигрывается
  !!re i/0/41:;                         // восстановление опыта существам на поле боя при переигрывани битвы
    !!VRs:Si :21; !!BMi:O?h N?n;        // s/h - сторона отряда (0/1) / номер слота в армии / численность отряда
    !!co&n<1:;                          // пропуск пустого отряда
    !!SN:W^opt736.r%Vs.%Vi^/?j;         // j - сохраненный опыт отряда
    !!VRo:Si *-1 -1;                    // o номер отряда для EA
    !!EAo:Ej/2/d/d;                     // восстановление опыта отряда
  !!en:;
!!en:;

!?BG0&1000;                      [для нетеоретической битвы]
!!UN:P736/?y1 P900/?y2;          [проверяем включены ли опция 736 и 900 в y1 и y2 соотв.]
!!FU|y1=0/y2=0:E;                [выход если хоть одна опция не включена]
!!BG:A?y1;                       [y1 - тип действия в бою]
!!SN&y1=5:W^opt736.surrend^/1;   [установка флага капитуляции при капитуляции героя]
!!SN&y1<>5:W^opt736.surrend^/0;  [сброс флага капитуляции, если действие - не капитуляция]
!!FU&y1<>6/y1<>7/y1<>10:E;       [выход, если не атака отряда]
!!BG:N?y1; !!BMy1:O?y2;          [y1/y2 - номер отряда/номер отряда в армии героя]
!!SN:W^opt736.bm.1^/-1; !!SN:W^opt736.bm.2^/-1;     [...]
!!if&y2>=0:;                     [если отряд состоит в армии (не призван)]
  !!VRv1:S0;                     [суммирование FV вражеских отрядов в v1]
  !!DO7999/0/20/1&y1>20:P;       [...]
  !!DO7999/21/41/1&y1<21:P;      [...]
  !!SN:W^opt736.fv.1^/v1;        [сохранение параметров перед действием]
  !!SN:W^opt736.bm.1^/y1;        [...]
  !!SN:W^opt736.sn.1^/y2;        [...]
!!en:;
!!BG:E?y1; !!BMy1&y1>=0:O?y2;    [y1/y2 - номер атакуемого отряда/номер отряда в армии героя]
!!if&y1>=0/y2>=0:;               [если атакуемый отряд существует и состоит в армии (не призван)]
  !!VRv1:S0;                     [суммирование FV вражеских отрядов в v1]
  !!DO7999/0/20/1&y1>20:P;       [...]
  !!DO7999/21/41/1&y1<21:P;      [...]
  !!SN:W^opt736.fv.2^/v1;        [сохранение параметров перед действием]
  !!SN:W^opt736.bm.2^/y1;        [...]
  !!SN:W^opt736.sn.2^/y2;        [...]
!!en:;

!?FU(OnBattleStackObtainsTurn)&1000; [фаза получения хода]
!!UN:P736/?y1 P900/?y2;          [проверяем включены ли опция 736 и 900 в y1 и y2 соотв.]
!!FU|y1=0/y2=0:E;                [выход если хоть одна опция не включена]
!!FU7899:P1/1;                   [вызов функции начисления опыта атакующему]
!!FU7899:P1/2;                   [вызов функции начисления опыта атакуемуму]

!?FU(OnBattleRegeneratePhase)&1000; [фаза регенерации]
!!UN:P736/?y1 P900/?y2;          [проверяем включены ли опция 736 и 900 в y1 и y2 соотв.]
!!FU|y1=0/y2=0:E;                [выход если хоть одна опция не включена]
!!FU7899:P1/1;                   [вызов функции начисления опыта атакующему]
!!FU7899:P1/2;                   [вызов функции начисления опыта атакуемуму]

!?FU(OnAfterBattleUniversal)&1000; [окончание нетеоретической битвы]
!!UN:P736/?y1 P900/?y2;          [проверяем включены ли опция 736 и 900 в y1 и y2 соотв.]
!!FU|y1=0/y2=0:E;                [выход если хоть одна опция не включена]
!!FU7899:P0/1;                   [вызов функции начисления опыта атакующему (без анимации, текста и звука)]
!!FU7899:P0/2;                   [вызов функции начисления опыта атакуемому (без анимации, текста и звука)]
!!BA:H0/?y1 H1/?y2 S?y3 P?y7/?y8/?y9; [y1/2 - герои, y3>0 для осадной битвы, y7-y9 - координаты битвы]
!!SN&y1=5:W^opt736.surrend^/?y4; [y4>0, если была капитуляция]
!!HEy1:O?y5; !!HEy2&y2>=0:O?y6;  [y5/y6 - владельцы героев]
!!if|y5>=0/y4=1:;                [если нападающий жив или капитулировал]
  !!SN:W^opt736.0.0^/?y10 W^opt736.0.1^/?y11 W^opt736.0.2^/?y12 W^opt736.0.3^/?y13 W^opt736.0.4^/?y14 W^opt736.0.5^/?y15 W^opt736.0.6^/?y16; [опыты отрядов атакующего]
  !!HEy1:C0/0/d/?y20 C0/1/d/?y21 C0/2/d/?y22 C0/3/d/?y23 C0/4/d/?y24 C0/5/d/?y25 C0/6/d/?y26; [количества существ в отрядах]
  !!EXy1/0&y20>0/y10>0:Ey10; !!EXy1/1&y21>0/y11>0:Ey11; !!EXy1/2&y22>0/y12>0:Ey12; [установка опытов отрядов атакующего]
  !!EXy1/3&y23>0/y13>0:Ey13; !!EXy1/4&y24>0/y14>0:Ey14; !!EXy1/5&y25>0/y15>0:Ey15; !!EXy1/6&y26>0/y16>0:Ey16; [...]
  !_!IF:M^DEB OUT: %Y10(%Y20), %Y11(%Y21), %Y12(%Y22), %Y13(%Y23), %Y14(%Y24), %Y15(%Y25), %Y16(%Y26)^;
!!en:;
!!if&y2>=0:;                     [если был защищающийся герой]
  !!if|y6>=0/y4=1:;              [и он жив или капитулировал]
    !!SN:W^opt736.1.0^/?y10 W^opt736.1.1^/?y11 W^opt736.1.2^/?y12 W^opt736.1.3^/?y13 W^opt736.1.4^/?y14 W^opt736.1.5^/?y15 W^opt736.1.6^/?y16; [опыты отрядов защищающегося]
    !!HEy2:C0/0/d/?y20 C0/1/d/?y21 C0/2/d/?y22 C0/3/d/?y23 C0/4/d/?y24 C0/5/d/?y25 C0/6/d/?y26; [количества существ в отрядах]
    !!EXy2/0&y20>0/y10>0:Ey10; !!EXy2/1&y21>0/y11>0:Ey11; !!EXy2/2&y22>0/y12>0:Ey12; [установка опытов отрядов защищающегося]
    !!EXy2/3&y23>0/y13>0:Ey13; !!EXy2/4&y24>0/y14>0:Ey14; !!EXy2/5&y25>0/y15>0:Ey15; !!EXy2/6&y26>0/y16>0:Ey16; [...]
  !!en:;
!!en:;
!!if&y2<0/y3>0/y5<0:;              [если была осадная битва без защищающегося героя и атакующий проиграл]
  !!SN:W^opt736.1.0^/?y10 W^opt736.1.1^/?y11 W^opt736.1.2^/?y12 W^opt736.1.3^/?y13 W^opt736.1.4^/?y14 W^opt736.1.5^/?y15 W^opt736.1.6^/?y16; [опыты отрядов защищающегося]
  !!CAy7/y8/y9:M2/0/d/?y20 M2/1/d/?y21 M2/2/d/?y22 M2/3/d/?y23 M2/4/d/?y24 M2/5/d/?y25 M2/6/d/?y26; [количества существ в отрядах]
  !!EXy7/y8/y9/0&y20>0/y10>0:Ey10; !!EXy7/y8/y9/0&y21>0/y11>0:Ey11; !!EXy7/y8/y9/0&y22>0/y12>0:Ey12; [установка опытов отрядов защищающегося]
  !!EXy7/y8/y9/0&y23>0/y13>0:Ey13; !!EXy7/y8/y9/0&y24>0/y14>0:Ey14; !!EXy7/y8/y9/0&y25>0/y13>0:Ey15; !!EXy7/y8/y9/0&y26>0/y16>0:Ey16; [...]
!!en:;

!?FU7999;                        [подсчет суммарного FV отрядов]
!!BMx16:T?y1 N?y2;               [y1/y2 - тип/кол-во существ в отряде]
!!FU&y2<1:E;                     [выход, если существ нет]
!!BMx16:F?y4;                    [флаги отряда]
!!VRy4:&8388608;                 [y4>0 для Клонов]
!!FU&y4>0:E;                     [Отряды-Клоны не дают опыта]
!!MA:Fy1/?y3;                    [y3 - FV существа]
!!VRy2:*y3;                      [y2 - FV отряда]
!!VRv1:+y2;                      [суммирование FV в v1]

!?FU7899;
!!SN:W^opt736.bm.%X2^/?y1;       [получение сохраненых параметров]
!!SN:W^opt736.sn.%X2^/?y2;       [...]
!!SN:W^opt736.fv.%X2^/?y3;       [...]
!!SN:W^opt736.bm.%X2^/-1;        [...]
!!FU&y1=-1:E;                    [выход, если опыт начислять не требуется]
!!BMy1:N?y4;                     [y4 - численность отряда]
!!FU&y4<1:E;                     [выход, если отряд уничтожен]
!!VRv1:S0;                       [суммирование FV вражеских отрядов в v1]
!!DO7999/0/20/1&y1>20:P;         [...]
!!DO7999/21/41/1&y1<21:P;        [...]
!!VRy3:-v1 *20;                  [y3 - изменение FV вражеской армии *20]
!!VRy5:Sy1 *-1 -1;               [y5 - номер отряда для EA]
!!EAy5:R?y13/?y14;               [y13/y14 - наличие у отряда и выбранная опция Знамени полководца]
!!VRy3&y13=156/y14=5:*3 :2;      [+50% опыта, если есть Знамя с бонусом к опыту]
!!BA:O?y13/?y14;                 [y13/y14 - номера сражающихся игроков]
!!OW&y1<21:Iy13/?y15;            [y15 - ИИ-признак отряда]
!!OW&y1>20/y14>=0:Iy14/?y15;     [...]
!!VRy15&y14<0/y1>20:S1;          [нейтралы получают бонус опыта как ИИ]
!!UN:J2/?y16;                    [y16 - уровень сложности (0..4)]
!!VRy16:+1;                      [y16 - бонус опыта ИИ за уровень сложности (х1..х4)]
!!VRy16&y15=0:S1;                [для игрока-человека бонус х1)
!!VRy3:*y16;                     [y3 - опыт с учетом бонусов за Знамя и ИИ]
!!BMy1:T?y17;                    [y17 - тип существ]
!!MA:Ly17/?y18;                  [y18 - уровень существ (0..6)]
!!VRy18:+1;                      [y18 - уровень существ (1..7)]
!!VRy18|y17=132/y17=133/y17=134/y17=135/y17=150/y17=151/y17=152/y17=153/y17=154/y17=155/y17=156/y17=157/y17=158/y17=196:S8;
!!VRy3::y18 :y4;                 [y3 - получаемый опыт деленный на уровень и кол-во существ в отряде]
!!FU&y3<=0:E;                    [выход, если армия врага не стала слабее или полученный опыт слишком мал]
!!EAy5:E?y6/2/d/d P?y10 L?y11;   [y6 / y10 / y11 - опыт отряда до получения опыта / опыт выше 10 ранга / опыт 10го ранга]
!!VRy10:+y11;                    [y10 - максимально возможный опыт отряда]
!!VRy11:Sy6 +y3;                 [если полученный опыт превышает максимум]
!!VRy3&y11>y10:Sy10 -y6;         [корректируем его]
!!FU&y3<=0:E;                    [выход, если опыт отряда уже максимален]
**
!!BA&y1<21:H0/?y30; !!BA&y1>20:H1/?y30; !!BA:S?y31 P?y32/?y33/?y34; [y30 - герой, y31>0 для осадной битвы, y32-y34 - координаты битвы]
!!FU&y30<0/y31=0:E;              [выход, если отряд без героя и не в гарнизоне]
!!EAy5:Ey3/3/d/d;                [добавление опыта отряду на поле боя]
!!EAy5:E?y7/2/d/d;               [y7 - новый опыт отряда на поле боя]
!!EXy30/y2&y30>=0/y2>=0:Ey7;     [установка опыта отряду в армии героя]
!_!IF&y1<21:M^DEB BATTLE: %Y7(stack %Y2)^;
!!EXy32/y33/y34/y2&y1>20/y30<0/y31>0/y2>=0:Ey7; [установка опыта отряду в гарнизоне города без защитника]
!!SN&y1<21/y2>=0:W^opt736.0.%Y2^/y7; !!SN&y1>20/y2>=0:W^opt736.1.%Y2^/y7; [сохранение опыта отряда]
**
!!BA:Q?y9;                       [y9 - признак быстрой битвы]
!!BMy1:T?y8;                     [y8 - тип существа]
!!VRy11:S0; !!VRy12:S0;          [инициализация]
!!FU7898:Py18/y6/?y11;           [y11 - ранг существа до получения опыта]
!!FU7898:Py18/y7/?y12;           [y12 - ранг существа после получения опыта]
!!if&y12>y11/y9=0/x1=1:;         [если отряд получил новый ранг, битва не быстрая и бой еще не окончен]
  !!VRz1:Sz179305;               [получаем текст для вывода в лог битвы из ert]
  !!MM:Sz1;                      [вывод текста в лог битвы]
  !!VRz1:S^GOODLUCK^;            [z1 - звуковой файл удачи]
  !!SN:P1;                       [вывод звука удачи]
  !_!SN:L^era.dll^/?y91 Ay91/^RedirectFile^/?y92 Ey92/1/^zmgc01.def^/^ExpUP.def^; [подмена def'а анимации]
  !!BMy1:V17;85;                    [воспроизведение анимации]
  !_!SN:Ey92/1/^zmgc01.def^/^^;   [сброс подмены def'а анимации]
!!en:;

!?FU7898;                        [возврат в x3 ранга опыта существа уровня x1 (1..8) с опытом x2]
!!MA:Lx1/?y11;                   [y11 - уровень существа]
!!VRy1:Sx1 *1000;                [y1-y10 - уровни опыта обычных существ]
!!VRy2:Sx1 *2000;                [...]
!!VRy3:Sx1 *3200;                [...]
!!VRy4:Sx1 *4600;                [...]
!!VRy5:Sx1 *6200;                [...]
!!VRy6:Sx1 *8000;                [...]
!!VRy7:Sx1 *10000;               [...]
!!VRy8:Sx1 *12200;               [...]
!!VRy9:Sx1 *14700;               [...]
!!VRy10:Sx1 *17500;              [...]
!!if&x1=8:;
  !!VRy1:S8400;                  [y1-y10 - уровни опыта существ "8 уровня" и реликтовых драконов]
  !!VRy2:S16800;                 [...]
  !!VRy3:S26880;                 [...]
  !!VRy4:S38640;                 [...]
  !!VRy5:S52080;                 [...]
  !!VRy6:S67200;                 [...]
  !!VRy7:S84000;                 [...]
  !!VRy8:S102480;                [...]
  !!VRy9:S123480;                [...]
  !!VRy10:S147000;               [...]
!!en:;
!!VRx3&x2<y1:S0;                 [определение и возврат ранга]
!!VRx3&x2>=y1/x2<y2:S1;          [...]
!!VRx3&x2>=y2/x2<y3:S2;          [...]
!!VRx3&x2>=y3/x2<y4:S3;          [...]
!!VRx3&x2>=y4/x2<y5:S4;          [...]
!!VRx3&x2>=y5/x2<y6:S10;         [...]
!!VRx3&x2>=y6/x2<y7:S10;         [...]
!!VRx3&x2>=y7/x2<y8:S10;         [...]
!!VRx3&x2>=y8/x2<y9:S10;         [...]
!!VRx3&x2>=y9/x2<y10:S10;        [...]
!!VRx3&x2>=y10:S10;              [...]

** end
