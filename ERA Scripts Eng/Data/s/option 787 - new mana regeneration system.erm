ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : New mana regeneration system
** Name rus.     : Новая система восполнения маны
** Options       : 787
** Dialogs       : -
** Variables     : w99


!?FU(ES_787_Initialization);
!!UN:P787/?(manaSys:y);
!!FU&(manaSys)<>(TRUE):E;               [выход если опция не включена]

!!UN:P35/(FALSE);                       [принудительно выключаем опцию 35 (Мистицизм 1)]

!!VRi^es_787_prevPlayer^:S(NO_PLAYER);  [при старте игры (нет предыдущего игрока)]

!!UN:A(ART_CHARM_OF_MANA)/1/1000;
!!UN:A(ART_TALISMAN_OF_MANA)/1/2000;    [цена Амулета маны = 1000, Талисмана маны = 2000]
!!UN:A(ART_MYSTIC_ORB_OF_MANA)/1/3000 A(ART_MYSTIC_ORB_OF_MANA)/3/4;      [цена Медали маны = 3000, класс = Minor]

!#FU(ES_787_Initialization):P;

!?FU(OnAfterErmInstructions);           [перед началом игры]
!!UN:P787/?(manaSys:y);
!!FU&(manaSys)<>(TRUE):E;               [выход если опция не включена]

; Set up new skill description
!!SN:H^secskill^/(SKILL_MYSTICISM)/0/?(name:z);
!!VR(basicDesc:z):S^{%T(es.basic)%(name)}%T(es.787.desc1)^;
!!VR(advancedDesc:z):S^{%T(es.advanced)%(name)}%T(es.787.desc2)^;
!!VR(expertDesc:z):S^{%T(es.expert)%(name)}%T(es.787.desc3)^;
!!VR(newLine:z):S^
^;

; Add in Mysticism II description if enabled
!!UN:P207/?(mysticismII:y);

!!if&(mysticismII);
  !!VR(basicDesc):+(newLine) +z175152;
  !!VR(advancedDesc):+(newLine) +z175153;
  !!VR(expertDesc):+(newLine) +z175154;
!!en;

; Change the descriptions
!!SN:H^secskill^/(SKILL_MYSTICISM)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_MYSTICISM)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_MYSTICISM)/(SKILL_EXPERT)/(expertDesc);

; Set up new spec description
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_MYSTICISM):H^spec^/i/2/^%T(es.787.spec)^;
!!en;

; Set Up new artifact descriptions
!!SN:H^art^/(ART_CHARM_OF_MANA)/0/?(charmName:z);
!!SN:H^art^/(ART_CHARM_OF_MANA)/1/^{%(charmName)}%T(es.787.desc1)^;

!!SN:H^art^/(ART_TALISMAN_OF_MANA)/0/?(tailsmanName:z);
!!SN:H^art^/(ART_TALISMAN_OF_MANA)/1/^{%(tailsmanName)}%T(es.787.desc2)^;

!!SN:H^art^/(ART_MYSTIC_ORB_OF_MANA)/0/?(orbName:z);
!!SN:H^art^/(ART_MYSTIC_ORB_OF_MANA)/1/^{%(orbName)}%T(es.787.desc3)^;

!?FU(OnEveryDay)&i^timerDay^>1; [каждый день для каждого игрока]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!VRy1:Si^es_787_prevPlayer^; [y1 - номер предыдущего игрока 0-7]
!!OW:C?i^es_787_prevPlayer^;  [номер текущего игрока сохраняем для использования как предыдущего при следующем срабатывании]
!!DO(ES_787_SaveCurrSpellPoints)/0/155/1&y1>-1:Py1; [Для каждого героя предыдущего игрока y1 сохраняем текущую ману в w99]
!!DO(ES_787_RegenSpellPoints)/0/155/1:P;[Для каждого героя текущего игрока i^es_787_prevPlayer^ восстанавливаем ману]

!?FU(ES_787_SaveCurrSpellPoints); [Сохранение текущей маны героя x16 в w99 для героев игрока x1]
!!HEx16:O?y1;                 [y1 - игрок-владелец героя x16 [0..7]]
!!FU&y1<>x1:E;                [выход, если владелец героя не предыдущий игрок]

!!IF:Wx16;                    [выбор героя для установки w99]
!!HEx16:I?w99;                [сохранение текущей маны героя в w99]

!?FU(OnAfterBattleUniversal); [в конце боя обновляем текущим количеством маны w99 сражавшихся героев]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!BA:H0/?y1 H1/?y2;           [y1/y2 - номера бьющихся героев (-2 - нет защитника)]

!!if&y1>-1;
  !!HEy1:I?y3;                [y3 - кол-во маны у героя]
  !!IF:Wy1;                   [выбор героя для использования w99]
  !!VRw99:Sy3;                [обновление w99]
!!en;

!!if&y2>-1;
  !!HEy2:I?y3;                [y3 - кол-во маны у героя]
  !!IF:Wy2;                   [выбор героя для использования w99]
  !!VRw99:Sy3;                [обновление w99]
!!en;

!?FU(ES_787_RegenSpellPoints); [ежедневное восстановление маны героя x16 для героев игрока i^es_787_prevPlayer^]
!!HEx16:O?y1;                 [y1 - игрок-владелец героя x16 [0..7]]
!!FU&y1<>i^es_787_prevPlayer^:E; [выход, если владелец героя не текущий игрок]

!!HEx16:A2/138/d/?y1;         [y1>0, если на герое надет Колодец Волшебника(138)]
!!FU&y1>0:E;                  [выход, если на герое надет Колодец Волшебника]

!!FU(ES_787_GetHeroData):Px16/?y1/?y2/?y3; [y1/y2/y3 - рейтинг/макс.мана/макс.восст.]
!!IF:Wx16;                    [выбор героя для использования w99]
!!VRy3:+w99;                  [y3 - мана после восстаногвления]
!!VRy3&y3>y2:Sy2;             [не более макс. возможного запаса]
!!VRy3&y3<w99:Sw99;           [и не менее того что было (экстра мана) ]
!!VRy3&y3<0:S0;               [не меньше нуля]
!!HEx16:Iy3/1;                [восстановление маны герою]

!?OB(OBJ_MAGIC_WELL);         [посещение колодца маны (перед посещением)]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!HE-1:I?y1;                  [y1 - текущая мана героя]
!!IF:W-1;                     [выбор текущего героя для использования w99]
!!VRw99&w99>-1:Sy1;           [сохраняем текущую ману героя в w99, если герой сегодня еще не посещал колодец (w99>-1)]

!$OB(OBJ_MAGIC_WELL);         [посещение колодца маны (после посещения)]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!IF:W-1;                     [выбор текущего героя для использования w99]
!!FU&w99<0:E;                 [выход если герой сегодня уже посещал колодец]

!!HE-1:Y61/d/?y1/2;           [y1 - длительность проклятия, запрещающего посещать Колодец]
!!FU&y1>1/y1<10000:E;         [выход, если герою запрещено богами посещать Колодец]

!!FU(ES_787_GetHeroData):P-1/?y1/?y2/?y3;[y1/y2/y3 - рейтинг/макс.мана/макс.восст.]
!!VRy1:*20;                   [y1 - % восстановления маны в колодце (20% за ед. рейтинга), минимум восстановленной маны]
!!VRy3:Sy2 *y1 :100;          [y3 - макс. возможное восстановление маны в колодце]
!!VRy3&y3<y1:Sy1;             [...]
!!VRy3:+w99;                  [мана после посещения колодца]
!!VRy3&y3>y2:Sy2;             [не больше максимума]
!!VRy3&y3<w99:Sw99;           [и не меньше того, что было до посещения]
!!HE-1:Iy3/1;                 [восстановление маны герою]
!!VRw99:Sy3 *-1;              [сохраняем текущую ману героя с минусом в w99 (признак посещения колодца сегодня)]

!?FU(ES_787_GetHeroData);     [для героя x1 возврат в x2 рейтинга восполнения маны, в x3 - макс. запаса маны героя x1, в x4 - макс. восст. маны]
** рейтинг восполенния маны
!!HEx1:S8/?y1;                [y1 - уровень навыка Мистицизм [0..3]]
!!HEx1:X?y2/?y3/d/d/d/d/d;    [для специалистов по Мистицизму y2=0,y3=8]
!!VRy1&y2=0/y3=8:+1;          [y1 - уровень навыка Мистицизм с учетом специализации [0..4]]
!!HEx1:A2/73/d/?y2;           [y2>0, если на герое надет Амулет Маны]
!!VRy2&y2>0:S1;               [y2=1, если на герое надет Амулет Маны]
!!HEx1:A2/74/d/?y3;           [y3>0, если на герое надет Талисман Маны]
!!VRy3&y3>0:S2;               [y3=2, если на герое надет Талисман Маны]
!!HEx1:A2/75/d/?y4;           [y4>0, если на герое надета Медаль Маны]
!!VRy4&y4>0:S3;               [y4=3, если на герое надета Медаль Маны]
!!VRy2:+y3 +y4;               [y2 - суммарный коэффициент для надетых артефактов из сборника Колодец Волшебника [0..6]]
!!VRy10:S1;                   [y10 - базовый рейтинг восполнения]
** бонус базового рейтинга для ИИ за уровень сложности
!!HEx1:O?y11;                 [y11 - игрок-владелец героя]
!!OW:Iy11/?y12;               [y12=1, для ИИ-игрока]
!!UN:J2/?y13;                 [y13 - выбранный уровень сложности 0..4]
!!VRy10&y12=1:+y13;           [ИИ получает +1 к базовому рейтингу восполнения за каждый уровень сложности выше легкого]
**
!!VRx2:Sy10 +y1 +y2;          [рейтинг восполнения = базовый рейтинг + бонус Мистизизма + бонус артефактов]
** макс. запас маны
!!HEx1:Fd/d/d/?y5;            [y5 - Знание героя]
!!VRy5:*10;                   [y5 базовый макс. запас маны героя]
!!HEx1:S24/?y2;               [y2 - уровень навыка Интеллект [0..3]]
!!HEx1:X?y3/?y4/d/d/d/d/d;    [для специалистов по Интеллекту y3=0,y4=24]
!!HEx1:Ed/?y6;                [y6 - уровень героя]
!!VRy7:S0;                    [------------------------------------------]
!!VRy7&y2=1:Sy5 :4;           [                                          ]
!!VRy7&y2=2:Sy5 :2;           [ y7 - бонус запаса маны от Интеллекта     ]
!!VRy7&y2=3:Sy5;              [     и специализации Интеллект            ]
!!VRe99&y3=0/y4=24:Sy6 :20 +1;[                                          ]
!!VRy7&y3=0/y4=24:*e99;       [------------------------------------------]
!!VRx3:Sy5 +y7;               [макс. запас маны = запас от Знания + бонус от Интеллекта]

** максимальное восстановление маны
; Check if the hero is in a town
!!VRy6:S(FALSE);

!!HEx1:P?y3/?y4/?y5;          [y3,y4,y5 - координаты героя]
!!VRy30:Sy3 -1;
!!VRy31:Sy4;
!!VRy32:Sy5;
; Single UN:U call starts from x-1 of the hero's coordinate, if the found town is at the same coordinate of the hero, then the hero is in a town
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/-1/y30/y31/y32;

!!VRy6&y30=y3/y31=y4/y32=y5:S(TRUE);[если герой в городе (слева и справа объект город)]

!!VRy7:S0;                    [------------------------------------------]

!!if&y6=1;
  !!CAy3/y4/y5:B3/0;          [                                          ]
  !!VRy7&1:S1;                [                                          ]
  !!CAy3/y4/y5:B3/1;          [                                          ]
  !!VRy7&1:S2;                [                                          ]
  !!CAy3/y4/y5:B3/2;          [y7 - уровень гильдии магов в городе [0..5]]
  !!VRy7&1:S3;                [                                          ]
  !!CAy3/y4/y5:B3/3;          [                                          ]
  !!VRy7&1:S4;                [                                          ]
  !!CAy3/y4/y5:B3/4;          [                                          ]
  !!VRy7&1:S5;                [------------------------------------------]
!!en;

** y9 - % восстановления маны, y8 - мин. кол-во восстановленной маны
!!VRy8:Sx2;                   [мин. 1 маны за единицу рейтинга восполнения на открытой местности/в городе без ГМ]
!!VRy8&y7>0:Sx2 + y7 -1 *20;  [мин. 20 маны за доп.единицу рейтинга и уровень ГМ в городе с ГМ]
!!VRy9:Sx2 * 3;               [3% за единицу рейтинга восполнения на открытой местности/в городе без ГМ]
!!VRy9&y7>0:Sx2 + y7 -1 * 20; [20% за доп.единицу рейтинга и уровень ГМ в городе с ГМ]
**
!!VRy2:Sx3 *y9 :100;          [y2 - макс. количество маны, которое может восстановить герой]
!!VRy2&y2<y8:Sy8;             [...]
!!HEx1:Y6/?y11/?y12/d;        [y11/y12 - сила/продолжительность проклятия потери маны]
!!HEx1:Y7/?y13/?y14/d;        [y13/y14 - сила/продолжительность благословения посполнения маны]
!!VRy10:S0;                   [y10 - суммарный эффект проклятий/благословений маны]
!!VRy10&y11>0/y12>0/y12<1000:-y11 -1; [...]
!!VRy10&y13>0/y14>0/y14<1000:+y13;    [...]
!!VRx4:Sy2 +y10;              [макс. восстановление = восстановление на базе рейтинга/ГМ + бонус/штраф благословений/проклятий]
!!VRx4&x4<0:S0;               [не ниже 0]

!?FU(OnHeroScreenMouseClick); [клик в окне героя]
!!UN:P787/?y1; !!FU&y1=0:E;   [выход если опция не включена]

!!CM:I?y1 S?y2 F?y8;          [y1/y2 - место/флаги/тип клика]
!!FU&y1<>109/y1<>113/y1<>120:E; [выход если нажатие не на мане]

!!CM:H?y3/?y4;                [y3/y4 - левый/правый герои]
!!FU(ES_787_GetHeroData):Py3/?y4/?y5/?y6;[y4/y5/y6 - рейтинг/макс.мана/макс.восст.]
!!HEy3:B0/?z1 I?y7/1;         [z1/y7 - имя/мана героя]
!!SN:T^es.787.heroScr^/?z2/^heroName^/z1/^regenSp^/y6;
!!IF&y2=13/y8=0:M^%Z2^;       [вывод сообщения ЛКМ (отпущена, без модификаторов)]
!!IF&y2=14:M0/4/^%Z2^;        [вывод сообщения ПКМ]
!!CM:R0;                      [отключаем стандартное действие по клику]

!?FU(OnHeroesMeetScreenMouseClick)|i^mouse_item^=113/i^mouse_item^=83/i^mouse_item^=114/i^mouse_item^=84;
!!UN:P787/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!CM:R0;                                [отключаем стандартное действие по клику]

!!CM:H?(leftHero:y)/?(rightHero:y);

!!if|i^mouse_item^=113/i^mouse_item^=83;
  !!VR(hero:y):S(leftHero);
!!el|i^mouse_item^=114/i^mouse_item^=84;
  !!VR(hero):S(rightHero);
!!en;

!!FU(ES_787_GetHeroData):P(hero)/?(rating:y)/?(maxSp:y)/?(regenSp:y); [рейтинг/макс.мана/макс.восст.]
!!HE(hero):B0/?(heroName:z) I?(sp:y)/1;
!!SN:T^es.787.heroScr^/?z2/^heroName^/(heroName)/^regenSp^/(regenSp);

!!if&i^mouse_action^=(MOUSE_LMB_PRESSED);
  !!IF:M^%Z2^;                          [вывод сообщения ЛКМ (отпущена, без модификаторов)]
!!el&i^mouse_action^=(MOUSE_RMB_PRESSED);
  !!IF:M0/(MSG_TYPE_POPUP)^%Z2^;        [вывод сообщения ПКМ]
!!en;

!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^=2010; [клик на карте приключений]
!!UN:P787/?y1;                [проверяем включена ли опция 787 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!CM:I?y1;                    [y1 - место нажатия]
!!FU&y1<>2010:E;              [выход если нажатие не на мане]

!!OW:A-1/?y3;
!!FU(ES_787_GetHeroData):Py3/?y4/?y5/?y6;[y4/y5/y6 - рейтинг/макс.мана/макс.восст.]
!!HEy3:B0/?z1 I?y7/1;         [z1/y7 - имя/мана героя]
!!SN:T^es.787.advMap^/?z2/^heroName^/z1/^currSp^/y7/^maxSp^/y5/^regenSp^/y6;
!!IF:M0/4/^%Z2^;              [вывод сообщения]
!!CM:R0;                      [отключаем стандартное действие по клику]

** end
