ZVSE
ERMS_ScriptDate=26.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Not a place for people
** Name rus.     : Не место для людей
** Options       : 792
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : TM2(standard), TM51(standard)
** Functions     : FU7923-FU7924
** PO-values     : -

!#TM2:S1/999/1/255;           [каждый день для каждого игрока, но после всех операций триггрера TM1]
!#TM51:S8/999/7/255;          [каждую неделю для всех игроков начиная с 8го дня]

!?GM0;                        [при загрузке игры]
!!UN:P792/?y1;                [проверяем включена ли опция 792 в y1]
!!FU&y1>0:E;                  [выход, если опция не включена]
!!UN:J4/64000;                [Восстанавливаем удвоенный SoD'овский "радиус раздумий" ИИ]

!?TM2;                        [В первый день]
!!UN:P792/?y1;                [проверяем включена ли опция 792 в y1]
!!FU&y1<>1:E;                 [выход, если опция не включена]
!!UN:J4/64000;                [Восстанавливаем удвоенный SoD'овский "радиус раздумий" ИИ]
!!UN:J2/?y1;                  [y1=0..4 уровень сложности выбранный игроком-человеком]
!!VRy1:*y1 *1000;             [y1=0,1000,4000,9000,16000 - бонус опыта ИИ-героям]
!!DO7923/1/155/1:Py1;         [каждому ИИ-герою добавляем бонус опыта]
!!UN:P792/2;                  [опция 792 = 2]

!?FU7923;                     [x1 - бонус опыта, x16 - номер героя]
!!HEx16:O?y1;                 [y1 - хозаин героя]
!!OW:Iy1/?y2;                 [y2=1 для ИИ]
!!FU|y1=-1/y2=0:E;            [Выход, если герой не нанят или принадлежит игроку человеку]
!!HEx16:E?y3;                 [y3 - текущий опыт героя]
!!VRy3:+x1;                   [y3 - увеличенный опыт]
!!HEx16:Ey3;                  [обновляем опыт героя]

!?TM51;                       [каждую неделю начиная со второй]
!!UN:P792/?y1;                [проверяем включена ли опция 792 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!AI:D-1/-1/-1;               [Удаление всех точек назначения]
!!UN:U98/-1/?y2;              [Количество городов в y2]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7924/1/y2/1:P;            [Для каждого города вызываем функцию FU7924]

!?FU7924;                     [Не место для людей - установка точек значимости и открытие областей вокруг них для ИИ]
!!UN:U98/-1/-1/1;             [получаем координаты города в v1-v3]
!!CA1:O?y1;                   [получаем владельца в y1]
!!OW&y1<>-1:Iy1/?y2;          [y2=1 для ИИ]
!!FU&y1<>-1/y2=1:E;           [выход, если город принадлежит ИИ]
!!OW&y1<>-1:Ty1/?y3;          [получаем союз владельца в y3]
!!VRy2&y1=-1:Sy1;             [y2=-1 для нейтрального города]
!!VRy3&y1=-1:Sy1;             [y3=-1 для нейтрального города]
!!VRy4&y2=0:-y4 +5000000;     [y4 значимость точки для ИИ (5 млн для города игрока-человека)]
!!VRy4&y2=-1:-y4 +500000;     [y4 значимость точки для ИИ (500 тыс для нейтрального города)]
!!OW:I0/?y5/?y6 T0/?y7;       [игрок 0: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/0/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/0/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I1/?y5/?y6 T1/?y7;       [игрок 1: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/1/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/1/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I2/?y5/?y6 T2/?y7;       [игрок 2: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/2/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/2/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I3/?y5/?y6 T3/?y7;       [игрок 3: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/3/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/3/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I4/?y5/?y6 T4/?y7;       [игрок 4: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/4/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/4/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I5/?y5/?y6 T5/?y7;       [игрок 5: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/5/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/5/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I6/?y5/?y6 T6/?y7;       [игрок 6: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/6/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/6/5; [открываем область вокруг значимой точки для несоюзного ИИ]
!!OW:I7/?y5/?y6 T7/?y7;       [игрок 7: y5=1 для ИИ, y6=0, если игрок жив, y7=номер союза]
!!AI&y5=1/y6=0/y7<>y3:S-1/7/x16/v1/v2/v3/y4/0; [Установка значимой точки для несоюзного ИИ]
!!UN&y5=1/y6=0/y7<>y3:Sv1/v2/v3/7/5; [открываем область вокруг значимой точки для несоюзного ИИ]

** end
