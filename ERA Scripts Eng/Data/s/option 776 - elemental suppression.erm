ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Elemental suppression
** Name rus.     : Подавление стихий
** Options       : 776


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P776/?(elemSupp:y);                [проверяем включена ли опция 776]
!!FU&(elemSupp)<>(TRUE):E;              [выход если опция не включена]

; Disable the feature to remove Eagle Eye skill when the hero has learned all the spells of Eagle Eye I
!!VRi^wog_103_removeEagleEye^:S(FALSE);

; Set up new skill name and description for Elemental Suppression 
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/0/^%T(es.776.name)^;

!!SN:T^es.776.desc^/?(desc1:z)/^lv^/1;
!!SN:T^es.776.desc^/?(desc2:z)/^lv^/2;
!!SN:T^es.776.desc^/?(desc3:z)/^lv^/3;

!!VR(basicDesc:z):S^{%T(es.basic)%T(es.776.name)}%(desc1)%T(es.776.neutralized)^;
!!VR(advancedDesc:z):S^{%T(es.advanced)%T(es.776.name)}%(desc2)%T(es.776.neutralized)^;
!!VR(expertDesc:z):S^{%T(es.expert)%T(es.776.name)}%(desc3)%T(es.776.neutralized)^;
!!VR(newLine:z):S^
^;

; Add in text of Eagle Eye I if the option is enabled
!!UN:P103/?(eagleEyeI:y);

!!if&(eagleEyeI);
  !!VR(basicDesc):+z175167;
  !!VR(advancedDesc):+z175168;
  !!VR(expertDesc):+z175169;
!!en;

; Add in text of Eagle Eye II if the option is enabled
!!UN:P202/?(eagleEyeII:y);

!!if&(eagleEyeII);
  !!VR(basicDesc):+z175170;
  !!VR(advancedDesc):+z175171;
  !!VR(expertDesc):+z175172;
!!en;

; Change the skill descriptions
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_EAGLE_EYE)/(SKILL_EXPERT)/(expertDesc);

; Set up spec text for Eagle Eye specialists
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_EAGLE_EYE):H^spec^/i/2/^%T(es.776.spec)^;
!!en;

; Set up text for Suppression Artifacts
!!SN:H^art^/(ART_BIRD_OF_PERCEPTION)/0/?(birdName:z);
!!SN:H^art^/(ART_BIRD_OF_PERCEPTION)/1/^{%(birdName)}%(desc1)%T(es.776.neutralized)%T(es.776.suppArt)^;

!!SN:H^art^/(ART_STOIC_WATCHMAN)/0/?(watchmanName:z);
!!SN:H^art^/(ART_STOIC_WATCHMAN)/1/^{%(watchmanName)}%(desc2)%T(es.776.neutralized)%T(es.776.suppArt)^;

!!SN:H^art^/(ART_EMBLEM_OF_COGNIZANCE)/0/?(cognizanceName:z);
!!SN:H^art^/(ART_EMBLEM_OF_COGNIZANCE)/1/^{%(cognizanceName)}%(desc3)%T(es.776.neutralized)%T(es.776.suppArt)^;

// Disable the standard effect of Eagle Eye and related artifacts
!?FU(OnStartOrLoad);
!!UN:P776/?(wogOption:y);     [проверяем включена ли опция 776]
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

!!UN:C5129872/(UNC_INT8)/?i^es_776_patch1^;
!!UN:C5129873/(UNC_INT)/?i^es_776_patch2^;
!!UN:C5129872/(UNC_INT8)/233; [отключаем стандартное действие навыка Зоркость (спс. feanor'у)]
!!UN:C5129873/(UNC_INT)/340;  [отключаем стандартное действие артефактов Зоркости (спс. feanor'у)]

!?FU(OnGameLeave);
!!UN:P776/?(wogOption:y);     [проверяем включена ли опция 776]
!!FU&(wogOption)<>(TRUE):E;   [выход если опция не включена]

!!UN:C5129872/(UNC_INT8)/i^es_776_patch1^;
!!UN:C5129873/(UNC_INT)/i^es_776_patch2^;

!?FU(OnBeforeBattleUniversal);[Начало боя: сохранение параметров школ маги героев и их подавление]
!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!BA:H0/?y1;                  [y1 - атакующий герой]
!!BA:H1/?y2;                  [y2 - защищающийся герой]
!!FU&y2<0:E;                  [выход если битва против монстров без героя]
!!IF:Wy1;                     [выбор атакующего героя для установки w95-w98]
!!HEy1:S14/?w95;              [w95 - уровень магии огня (0..3) атакующего героя]
!!HEy1:S15/?w96;              [w96 - уровень магии воздуха (0..3) атакующего героя]
!!HEy1:S16/?w97;              [w97 - уровень магии воды (0..3) атакующего героя]
!!HEy1:S17/?w98;              [w98 - уровень магии земли (0..3) атакующего героя]
!!IF:Wy2;                     [выбор защищающегося героя для установки w95-w98]
!!HEy2:S14/?w95;              [w95 - уровень магии огня (0..3) защищающегося героя]
!!HEy2:S15/?w96;              [w96 - уровень магии воздуха (0..3) защищающегося героя]
!!HEy2:S16/?w97;              [w97 - уровень магии воды (0..3) защищающегося героя]
!!HEy2:S17/?w98;              [w98 - уровень магии земли (0..3) защищающегося героя]
!!HEy1:S11/?y3;               [y3 - уровень СП (0..3) атакующего героя]
!!HEy1:X?y5/?y6/d/d/d/d/d;    [y5=0, y6=11 для специалиста по ПС]
!!VRy3&y5=0/y6=11:+1;         [y3 - уровень СП атакующего героя с учетом специализации(0..4)]
!!HEy1:A2/63/d/?y5;           [y5 - кол-во надетых на героя Птица познания]
!!VRy3&y5>0:+1;               [y3+=1, если надета Птица познания]
!!HEy1:A2/64/d/?y5;           [y5 - кол-во надетых на героя Бесстрашных хранителей]
!!VRy3&y5>0:+1;               [y3+=1, если надет Бесстрашный хранитель]
!!HEy1:A2/65/d/?y5;           [y5 - кол-во надетых на героя Символов Знаний]
!!VRy3&y5>0:+2;               [y3+=2, если надет Символ знаний]
!!HEy2:S11/?y4;               [y4 - уровень СП (0..3) защищающегося героя]
!!HEy2:X?y5/?y6/d/d/d/d/d;    [y5=0, y6=11 для специалиста по ПС]
!!VRy4&y5=0/y6=11:+1;         [y4 - уровень СП атакующего героя с учетом специализации(0..4)]
!!HEy2:A2/63/d/?y5;           [y5 - кол-во надетых на героя Птица познания]
!!VRy4&y5>0:+1;               [y4+=1, если надета Птица познания]
!!HEy2:A2/64/d/?y5;           [y5 - кол-во надетых на героя Бесстрашных хранителей]
!!VRy4&y5>0:+1;               [y4+=1, если надет Бесстрашный хранитель]
!!HEy2:A2/65/d/?y5;           [y5 - кол-во надетых на героя Символов Знаний]
!!VRy4&y5>0:+2;               [y4+=2, если надет Символ знаний]
!!FU&y3=y4:E;                 [выход если суммарные уровни ПС у противников равные (y3=y4)]
!!VRy1&y3>y4:Sy2;             [y1 - номер героя с более слабым ПС]
!!VRy2:Sy4 -y3;               [y2 - разница уровней ПС героев]
!!VRy2&y3>y4:*-1;             [y2 - уровень результирующего подавления]
!!IF:Wy1;                     [выбор героя для чтения w95-w98]
!!VRy3:Sw95 -y2;              [y3 - новый уровень магии огня]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S14/y3;                [подавляем магию огня]
!!VRy3:Sw96 -y2;              [y3 - новый уровень магии воздуха]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S15/y3;                [подавляем магию воздуха]
!!VRy3:Sw97 -y2;              [y3 - новый уровень магии воды]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S16/y3;                [подавляем магию воды]
!!VRy3:Sw98 -y2;              [y3 - новый уровень магии земли]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S17/y3;                [подавляем магию земли]

!?FU(OnAfterBattleAction);    [восстановление параметров школ маги героев по окончании боя]
!!BU:C?y1;                    [y1=1, если бой закончен]
!!FU&y1=0:E;                  [выход, если бой не закончен]

!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BA:H0/?y1;                  [y1 - атакующий герой]
!!BA:H1/?y2;                  [y2 - защищающийся герой]
!!FU&y2<0:E;                  [выход если битва против монстров без героя]
!!IF:Wy1;                     [выбор атакующего героя для чтения w95-w98]
!!HEy1:S14/w95;               [w95 - уровень магии огня (0..3) атакующего героя]
!!HEy1:S15/w96;               [w96 - уровень магии воздуха (0..3) атакующего героя]
!!HEy1:S16/w97;               [w97 - уровень магии воды (0..3) атакующего героя]
!!HEy1:S17/w98;               [w98 - уровень магии земли (0..3) атакующего героя]
!!IF:Wy2;                     [выбор защищающегося героя для чтения w95-w98]
!!HEy2:S14/w95;               [w95 - уровень магии огня (0..3) защищающегося героя]
!!HEy2:S15/w96;               [w96 - уровень магии воздуха (0..3) защищающегося героя]
!!HEy2:S16/w97;               [w97 - уровень магии воды (0..3) защищающегося героя]
!!HEy2:S17/w98;               [w98 - уровень магии земли (0..3) защищающегося героя]

** end
