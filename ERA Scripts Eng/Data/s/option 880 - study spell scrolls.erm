ZVSE2
** Author orig.  : DracoLich
** Name          : Study spell scrolls
** Name rus.     : Учеба свитков заклинаний
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] уровень заклинания определяется через SS:L, а не "hardcoded"
**                 [Algor] проверка то, что герой уже изучил заклинание
**                 [Algor] сообщение о том, что гильдия недостаточно высокого уровня не выводится
**                 [Algor] цена за обучение привязана к базовой стоимости заклинания S (S^2 * 5)
**                 [Algor] перенумерация функций/переменных и пр.
** Options       : 880
** Dialogs       : -
** Variables     : -
** Tmp variables : z1
** Timers        : -
** Functions     : FU7935


!?FU(OnTownMouseClick)&i^mouse_item^>=0/i^mouse_item^<=5/i^mouse_action^=(MOUSE_LMB_PRESSED)/i^key_shift^;
!!UN:P880/?y1;                [проверяем включена ли опция]
!!FU&y1=0:E;

!!CA-1:H1/?y1 B3/y2;          [y1 - номер героя-гостя, флаг 1 ложь, если здание не построено]
!!FU|y1=-1/-1:E;              [выход, если нет героя-гостя или гильдии]

!!DO7935/0/69/1:Py2/y1;       [для каждого заклинания проверяем наличие свитка и предлагаем изучение]
!!IF:Mz179095;                [сообщение "больше нет свитков"]

!?FU7935;                     [x1 - уровень ГМ, x2 - герой, x16 - номер заклинания]
!!VRy1:Sx16 +1001;            [y1 - номер артефакта-свитка]
!!HEx2:A2/y1/?y2/?y3;         [y2/y3 - общее/одетое количество свитков у героя]
!!FU&y2=0/y3=0:E;             [выход, если у героя нет свитков]

!!HEx2:M=x16/1;               [флаг 1 истина, если герой уже изучил заклинание]
!!FU&1:E;                     [выход, если заклинание уже изучено]

!!SSx16:L?y4;                 [y4 - уровень заклинания (1..5)]
!!VRy5:Sx1 +1;                [y5 - уровень гильдии (1..5)]
!!FU&y4>y5:E;                 [выход, если гильдия недостаточно высокого уровня]

!!SSx16:C0/?y6;               [y6 - базовая стоимость заклинания S]
!!VRy6:*y6 *5;                [y6 - стоимость изучения в гильди магов S^2*5]
!!VRz1:Sz179093;              [сообщение "хотите переписать заклинания?"]
!!IF:Q1/9/x16/2^%Z1^;         [...]
!!FU&-1:E;                    [выход, если ответ отрицателен]

!!OW:R-1/6/?y7;               [y7 - количество золота у игрока]

!!if&y7<y6;  
  !!IF:Mz179094;              [сообщение "недостаточно золота"]
  !!FU:E;                     [выход, если золота недостаточно]
!!en;

!!VRy6:*-1;                   [y6 - отрицательное количество золота для списания]
!!OW:R-1/6/dy6;               [списание оплаты]
!!UN:R2 R4;                   [обновление строки ресурсов и экрана города]
!!HEx2:Mx16/1 A3/y1/1/1;      [изучение заклинания и удаление свитка]

** end
