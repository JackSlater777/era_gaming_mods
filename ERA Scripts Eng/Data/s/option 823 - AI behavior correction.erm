ZVSE2
** Author orig.  :Sergroj
** Update        :Archer30
** Name          :AI behavior correction
** Name rus.     :Исправление поведения ИИ
** Changes rus.  :[Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
** Options       :823


!?FU(OnBeforeBattle)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE);
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

!!MA:F(MON_CATAPULT)/?i^es_823_catapultFv^;
!!MA:F(MON_BALLISTA)/?i^es_823_ballistaFv^;
!!MA:F(MON_FIRST_AID_TENT)/?i^es_823_firstAidTentFv^;
!!MA:F(MON_AMMO_CART)/?i^es_823_ammoCartFv^;

!!MA:I(MON_CATAPULT)/?i^es_823_catapultAv^;
!!MA:I(MON_BALLISTA)/?i^es_823_ballistaAv^;
!!MA:I(MON_FIRST_AID_TENT)/?i^es_823_firstAidTentAv^;
!!MA:I(MON_AMMO_CART)/?i^es_823_ammoCartAv^;

!!MA:F(MON_CATAPULT)/4;
!!MA:F(MON_BALLISTA)/3;
!!MA:F(MON_FIRST_AID_TENT)/2;
!!MA:F(MON_AMMO_CART)/1;

!!MA:I(MON_CATAPULT)/4;
!!MA:I(MON_BALLISTA)/3;
!!MA:I(MON_FIRST_AID_TENT)/2;
!!MA:I(MON_AMMO_CART)/1;

!?FU(OnAfterBattle)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE);
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

!!MA:F(MON_CATAPULT)/i^es_823_catapultFv^;
!!MA:F(MON_BALLISTA)/i^es_823_ballistaFv^;
!!MA:F(MON_FIRST_AID_TENT)/i^es_823_firstAidTentFv^;
!!MA:F(MON_AMMO_CART)/i^es_823_ammoCartFv^;

!!MA:I(MON_CATAPULT)/i^es_823_catapultAv^;
!!MA:I(MON_BALLISTA)/i^es_823_ballistaAv^;
!!MA:I(MON_FIRST_AID_TENT)/i^es_823_firstAidTentAv^;
!!MA:I(MON_AMMO_CART)/i^es_823_ammoCartAv^;

!?FU(OnBattleStackObtainsTurn)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE)/i^battle_ai_1^;;
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Exit if not in siege
!!BA:S=0;
!!FU&1:E;

; Change 
!!if&i^battle_ai_1^;
  !!MA:X(MON_CATAPULT)/d~(MON_FLAG_WIDE);[Catapult 1 sq]
!!el;
  !!MA:X(MON_CATAPULT)/d|(MON_FLAG_WIDE);[Catapult 2 sq]
!!en;

!?FU(OnBattleRound)&i^battle_round^=0/i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE)/i^battle_ai_1^; [Make catapult unreachable]
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Exit if not in siege
!!BA:S=0;
!!FU&1:E;

!!re i/(BATTLE_ATTACKER_STACK_FIRST)/(BATTLE_ATTACKER_BASE_STACK_LAST);
  !!BMi:T?(type:y);

  !!br&(type)=(MON_CATAPULT);
!!en;

!!if&i<=(BATTLE_ATTACKER_STACK_LAST);
  !!BMi:P?(pos:y);
  !!BF:O19/(pos);
!!en;

** end
