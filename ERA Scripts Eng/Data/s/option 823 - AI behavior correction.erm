ZVSE2
** Author orig.  : Sergroj
** Rewritten by  : Archer30
** Name          : AI behavior correction
** Name rus.     : Исправление поведения ИИ
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
** Options       : 823


!?FU(OnBeforeBattle)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE);
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Lower the fght values and AI values of war machines
!!MA:F(MON_BALLISTA)/?i^es_823_ballistaFv^;

!!MA:I(MON_CATAPULT)/?i^es_823_catapultAv^;
!!MA:I(MON_BALLISTA)/?i^es_823_ballistaAv^;
!!MA:I(MON_FIRST_AID_TENT)/?i^es_823_firstAidTentAv^;
!!MA:I(MON_AMMO_CART)/?i^es_823_ammoCartAv^;

!!MA:F(MON_CATAPULT)/?(fightValue:y);
!!VR(fightValue):*2;
!!MA:F(MON_BALLISTA)/(fightValue);

!!MA:I(MON_AZURE_DRAGON)/?(aiValue:y);
!!MA:I(MON_BALLISTA)/(aiValue);
!!MA:I(MON_CRYSTAL_DRAGON)/?(aiValue);
!!MA:I(MON_CATAPULT)/(aiValue);
!!MA:I(MON_FAERIE_DRAGON)/?(aiValue);
!!MA:I(MON_FIRST_AID_TENT)/(aiValue);
!!MA:I(MON_RUST_DRAGON)/?(aiValue);
!!MA:I(MON_AMMO_CART)/(aiValue);

!?FU(OnAfterBattle)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE);
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Restore the fight values and AI values 
!!MA:F(MON_BALLISTA)/i^es_823_ballistaFv^;

!!MA:I(MON_CATAPULT)/i^es_823_catapultAv^;
!!MA:I(MON_BALLISTA)/i^es_823_ballistaAv^;
!!MA:I(MON_FIRST_AID_TENT)/i^es_823_firstAidTentAv^;
!!MA:I(MON_AMMO_CART)/i^es_823_ammoCartAv^;

!?FU(OnBattleStackObtainsTurn)&i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE)/i^battle_ai_1^;;
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Exit if not in siege
!!BA:S=0;
!!FU&1:E;

; Change the catapult to 1 hex on AI's turn, change it back for human
!!if&i^battle_acting_side^=(BATTLE_RIGHT);
  !!MA:X(MON_CATAPULT)/d~(MON_FLAG_WIDE);[Catapult 1 sq]
!!el;
  !!MA:X(MON_CATAPULT)/d|(MON_FLAG_WIDE);[Catapult 2 sq]
!!en;

!?FU(OnBattleRound)&i^battle_round^=0/i^battle_hasHuman^/i^battle_humanOnly^<>(TRUE)/i^battle_ai_1^; [Make catapult unreachable]
!!UN:P823/?(wogOption:y);
!!FU&(wogOption)=0:E;

; Exit if not in siege
!!BA:S=0;
!!FU&1:E;

; Look for the catapult on the attacker's side
!!re i/(BATTLE_ATTACKER_STACK_FIRST)/(BATTLE_ATTACKER_STACK_LAST);
  !!BMi:T?(type:y);

  !!br&(type)=(MON_CATAPULT);
!!en;

; Place a rock on the hex of the back if the catapult is found
!!if&i<=(BATTLE_ATTACKER_STACK_LAST);
  !!BMi:P?(pos:y);
  !!BF:O19/(pos);
!!en;

** end
