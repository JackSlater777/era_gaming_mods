ZVSE2
** Author orig.  : Algor [Idea: Ivor]
** Rewritten by  : Archer30
** Name          : Hi-level monsters
** Name rus.     : Высокоуровневые монстры
** Options       : 745

** Отряды существ на карте могут заменяться аналогичными по силе и расе отрядами существ более высокого уровня.
** Отряды нейтральных существ не заменяются.
** Новая численность округляется вверх от 1/3.


!?FU(OnEveryDay)&i^timerDay^=1/i^timerOnce^;
!!UN:P745/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VR(x:y):S-1;                          [setting x-coordinate to -1 will force to start search from scratch]

!!re i;                                 [endless loop]
  !!UN:U(OBJ_MONSTER)/(ANY_OBJ)/-1/(x)/(y:y)/(z:y); [find next monster, (x) = -1 on failure]
  !!br&(x)<0;                           [exit loop if nothing found]

  !!OB(x)/(y)/(z):U?(objSubtype:y);
  !!FU(ES_745_UpgradeMonster):P(x)/(y)/(z)/(objSubtype);
!!en;

!!SN:D;

!?FU(ES_745_UpgradeMonster);
!#VA(x:x) (y:x) (z:x) (mon:x);

!!VR(random:y):R0/1/100;                [Random number: 1-100]
!!FU&(random)>25:E;                     [Only a 25% chance of monster would be replaced]

!!MO(x)/(y)/(z):R?(aggression:y)/1;     [Check monster's aggression]
!!FU&(aggression)=0:E;                  [Skip if monster is set to always join]

!!FU|(mon)>129/(mon)=116/(mon)=117:E;  [выход, если существо - нейтрал]

!!MO(x)/(y)/(z):G?(num:y);              [кол-во существ]
!!MA:L(mon)/?(lv:y);             [уровень существа (0..6)]
!!VR(lv):*-1 +6;                        [уровень существа (6..0)]
!!VR(lv)&(lv)>3:S3;                     [макс. кол-во уровней для повышения (0..3)]
!!VR(typeOffset:y):S0 R(lv) *2;         [кол-во "номеров" для повышения (0/2/4/6)]
!!FU&(typeOffset)=0:E;                  [выход, если уровень существ не повышеается]

!!VR(newType:y):S(mon) +(typeOffset); [(newType) - новый тип существа]

!!if&(mon)>97/(mon)<130;  [нестандартные номера существ]
  !!VR(newType)&(mon)=98/(newType)=102:S106;   [...]
  !!VR(newType)&(mon)=99/(newType)=103:S107;   [...]
  !!VR(newType)&(mon)=104/(newType)=110:S102;  [...]
  !!VR(newType)&(mon)=105/(newType)=111:S103;  [...]
  !!VR(newType)&(mon)=106/(newType)=112:S102;  [...]
  !!VR(newType)&(mon)=107/(newType)=113:S103;  [...]
  !!VR(newType)&(mon)=102/(newType)=104:S108;  [...]
  !!VR(newType)&(mon)=102/(newType)=106:S110;  [...]
  !!VR(newType)&(mon)=103/(newType)=105:S109;  [...]
  !!VR(newType)&(mon)=103/(newType)=107:S111;  [...]
  !!VR(newType)&(mon)=118/(newType)=120:S112;  [...]
  !!VR(newType)&(mon)=118/(newType)=122:S115;  [...]
  !!VR(newType)&(mon)=118/(newType)=124:S114;  [...]
  !!VR(newType)&(mon)=119/(newType)=121:S127;  [...]
  !!VR(newType)&(mon)=119/(newType)=123:S123;  [...]
  !!VR(newType)&(mon)=119/(newType)=125:S129;  [...]
  !!VR(newType)&(mon)=112/(newType)=114:S115;  [...]
  !!VR(newType)&(mon)=112/(newType)=116:S114;  [...]
  !!VR(newType)&(mon)=112/(newType)=118:S113;  [...]
  !!VR(newType)&(mon)=127/(newType)=129:S123;  [...]
  !!VR(newType)&(mon)=127/(newType)=131:S129;  [...]
  !!VR(newType)&(mon)=127/(newType)=133:S125;  [...]
  !!VR(newType)&(mon)=115/(newType)=117:S114;  [...]
  !!VR(newType)&(mon)=115/(newType)=119:S113;  [...]
  !!VR(newType)&(mon)=115/(newType)=121:S120;  [...]
  !!VR(newType)&(mon)=123/(newType)=125:S129;  [...]
  !!VR(newType)&(mon)=123/(newType)=127:S125;  [...]
  !!VR(newType)&(mon)=123/(newType)=129:S121;  [...]
  !!VR(newType)&(mon)=114/(newType)=116:S113;  [...]
  !!VR(newType)&(mon)=114/(newType)=118:S120;  [...]
  !!VR(newType)&(mon)=114/(newType)=120:S130;  [...]
  !!VR(newType)&(mon)=129/(newType)=131:S125;  [...]
  !!VR(newType)&(mon)=129/(newType)=133:S121;  [...]
  !!VR(newType)&(mon)=129/(newType)=135:S131;  [...]
  !!VR(newType)&(mon)=113/(newType)=115:S120;  [...]
  !!VR(newType)&(mon)=113/(newType)=117:S130;  [...]
  !!VR(newType)&(mon)=125/(newType)=127:S121;  [...]
  !!VR(newType)&(mon)=125/(newType)=129:S131;  [...]
  !!VR(newType)&(mon)=120/(newType)=122:S130;  [...]
  !!VR(newType)&(mon)=121/(newType)=123:S131;  [...]
!!en;                                   [...]

!!MA:F(mon)/?(fv:y) F(newType)/?(newFv:y); [FV старого и нового существа]
!!VR(newNum:y):S(num) *(fv) :(newFv);   [кол-во новых существ]
!!VR(totalFv:y):S(num) *(fv);           [FV старого отряда]
!!VR(newTotalFv:y):S(newNum) *(newFv);  [FV нового отряда]
!!VR(tripleFvGap:y):S(totalFv) -(newTotalFv) *3; [утроенное "потерянное" FV]
!!VR(newNum)&(tripleFvGap)>=(newFv):+1 F1/(INT_MAX); [кол-во новых существ, с округлением вверх от 1/3]
!!OB1:C?(controlWords:y);               [контрольное число монстров]
!!UN:O(x)/(y)/(z)/1;                    [удаление старого существа]
!!UN:I(x)/(y)/(z)/(OBJ_MONSTER)/(newType)/(OBJ_MONSTER)/(newType)/-1/0; [установка нового существа]
!!VR(newControlWords:y):S(controlWords) -(num) +(newNum); [новое контрольное число монстров]
!!OB1:C(newControlWords);               [обновление контрольного числа]

**end
