ZVSE2
** Author orig.  : DracoLich
** Name          : Custom primary skills
** Name rus.     : Измененная прокачка героев
** Changes rus.  : [Algor] вынос опции в отдельный файл для мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] исправлен бонус Грамотности в соответствии с описанием (было 0,5/0,75/1, стало 0,25/0,5/1)
**                 [Algor] убраны сообщения об ошибочном вводе значений. Теперь некорректный ввод молча игнорируется.
**                 [Algor] перенумерация функций/переменных и пр.
** Options       : 881
** Dialogs       : 51
** Variables     : w150


!?FU(OnAfterErmInstructions); [пост-инструкция]
!!UN:P881/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!rei/0/155;
  !!IF:Wi;
  !!VRw150:S0;                [обнуляем w150 для всех героев]
!!en;

!?FU(OnHeroGainLevel)&1000;   [Получение героем-человеком уровня]
!!UN:P881/?y1;
!!FU&y1=0:E;   [выход, если опция не включена]

!!HL:S?y1/-2/-2;              [y1 - получаемый первичный навык]
!!HE(CURRENT_HERO)&y1=0:Fd-1/d/d/d;       [отменяем увеличение навыка]
!!HE(CURRENT_HERO)&y1=1:Fd/d-1/d/d;       [...]
!!HE(CURRENT_HERO)&y1=2:Fd/d/d-1/d;       [...]
!!HE(CURRENT_HERO)&y1=3:Fd/d/d/d-1;       [...]
!!IF:W(CURRENT_HERO);                     [переменные героя]
!!VRw150:+100;                [увеличиваем запас нераспределенных навыков (w150)]
!!HE(CURRENT_HERO):S18/?y1;               [y1 - уровень Грамотности героя]
!!VRy1&y1=3:+1;               [Добавляем бонус Грамотности]

!!if&y1>0;
  !!VRy1:*25;                 [...]
  !!VRw150:+y1;               [...]
!!en;

!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_PRESSED)/i^mouse_item^>=50/i^mouse_item^<=53/i^key_shift^;                        [клик мышкой в окне героя]
!!UN:P881/?y1;
!!FU&y1=0:E;   [выход, если опция не включена]

!!CM:R0;                      [отключаем стандартное действие мыши]

!!IF:W(CURRENT_HERO);                     [переменные героя]
!!VRz1&w150<100:Sz179089;     [сообщение "нет свободных очков навыка"]

!!if&w150<100;
  !!IF:M^%Z1^;         [...]
  !!FU:E;              [выход, если нет свободных очков навыка]
!!en;

!!VRy1:Sy2 -19;               [y1 - картинка-навык для IF:Q]
!!VRy2:Sw150 :100;            [y2 - количество свободных навыков]
!!VRz2:Sz179090;              [сообщение "хотите добавить в этот навык?"]
!!IF:Q1/y1//2/2;              [...]
!!FU&-1:E;                    [выход, если ответ отрицателен]

!!VRz2:Sz179091;              [сообщение "Сколько очков навыка добавить?"]
!!VRz3:Sz179164;              [сообщение "Доступно оков:"]
!!IF:D51/2/3///////////// E1/51;[показ диалога]

!!if&v1>-1;
  !!VRy3:Vz1;                   [y3 - введенное число]
  !!FU|y3<1/y3>y2:E;            [выход, если введено некорректное значение]

  !!VRy2:Sy3 *100;              [y2 - распределяемые очки *100]
  !!VRw150:-y2;                 [уменьшаем свободные очки героя]
  !!HE(CURRENT_HERO)&y1=31:Fdy3/d/d/d;      [повышаем первичные параметры]
  !!HE(CURRENT_HERO)&y1=32:Fd/dy3/d/d;      [...]
  !!HE(CURRENT_HERO)&y1=33:Fd/d/dy3/d;      [...]
  !!HE(CURRENT_HERO)&y1=34:Fd/d/d/dy3;      [...]
  !!UN:R3/-1;                   [обновляем экран героя]
!!en;


** end