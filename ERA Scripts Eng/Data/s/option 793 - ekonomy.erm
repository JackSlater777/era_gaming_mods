ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Economy
** Name rus.     : Экономика
** Options       : 793
** Dialogs       : -
** Variables     : v940-v944


!?FU(OnAfterErmInstructions);           // при старте игры
!!UN:P793/?(economy:y);                 [проверяем включена ли опция 793]
!!FU&(economy)<>(TRUE):E;

; Set up new name
!!SN:H^secskill^/(SKILL_ESTATES)/0/^%T(es.793.name)^;

; Set up new skill description
!!VR(basicDesc:z):S^%T(es.793.desc1)^;
!!VR(advancedDesc:z):S^%T(es.793.desc2)^;
!!VR(expertDesc:z):S^%T(es.793.desc3)^;

; Add in Estates I description if enabled
!!UN:P203/?(estatesI:y);

!!if&(estatesI);
  !!VR(basicDesc):+z175179;
  !!VR(advancedDesc):+z175180;
  !!VR(expertDesc):+z175181;
!!en;

; Add in Estates II description if enabled
!!UN:P191/?(estatesII:y);

!!if&(estatesII);
  !!VR(basicDesc):+z175182;
  !!VR(advancedDesc):+z175183;
  !!VR(expertDesc):+z175184;
!!en;

; Change the descriptions
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_EXPERT)/(expertDesc);

; Set up new spec description
!!SN:H^spec^/(HERO_LORD_HAART)/2/^%T(es.793.spec)^;

// Disable standard income increase from Estates
!?FU(OnStartOrLoad);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

!!UN:C5129744/2/?i^es_793_patch1^ C5129746/4/?i^es_793_patch2^;
!!UN:C5129744/2/177 C5129746/4/2425393296;  [если опция включена - отключаем стандартный доход от Поместий]

!?FU(OnGameLeave);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

!!UN:C5129744/2/i^es_793_patch1^ C5129746/4/i^es_793_patch2^;

!?FU(ES_793_GetDailyIncomeOfTowns); [получаем размер дневного дохода городов игрока x1 в v943]
!!UN:U(OBJ_TOWN)/-1/-1/2;     [v1..v3 - координаты города]
!!CA2:O?y1;                   [y1 - владелец города]
!!FU&y1<>x1:E;                [выход, если город принадлежит не текущему герою]

!!CA2:B3/10;                  [проверка наличия префектуры]
!!VRy1&1:S500;                [y1=500, если есть префектура]
!!CA2:B3/11;                  [проверка наличия городского холла]
!!VRy1&1:S1000;               [y1=1000, если есть городской холл]
!!CA2:B3/12;                  [проверка наличия муниципалитета]
!!VRy1&1:S2000;               [y1=2000, если есть муниципалитет]
!!CA2:B3/13;                  [проверка наличия капитолия]
!!VRy1&1:S4000;               [y1=4000, если есть капитолий]
!!CA2:B3/26;                  [проверка наличия Грааля]
!!VRy1&1:+5000;               [y1+=5000, если есть Грааль]
!!VRv943:+y1;                 [увеличиваем суммарный доход городов от строений]
!!UN:P785/?y1;                [проверяем включена ли опция 785 Батраки в y1]
!!FU&y1=0:E;                  [выход, если опция не активна]

!!CA2:P?y2/?y3/?y4;           [y2/y3/y4 - координаты города]
!!SN:W^peons_%Y2_%Y3_%Y4^/?y5;[y5 - ежедневный доход от батраков в городе]
!!VRv943:+y5;                 [увеличиваем суммарный доход городов на доход батраков]

!?FU(OnHeroScreenMouseClick)&i^mouse_item^>=80/i^mouse_item^<=101; [клик в окне героя]
!!UN:P793/?(wogOption:y);               [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!VR(ssIndex:y):Si^mouse_item^ -79 %8 +1;[номер слота втор.навыка у героя]
!!CM:H?(hero:y)/?(rightHero:y);         [левый/правый герои]
!!HE(hero):S(ssIndex)/?(ss:y)/1;        [навык в слоте]

; Show up current bonus if clicking on a Economy skill slot or clicking on skill level/name if either Estates I and Estates II is disabled
!!if&(ss)=(SKILL_ESTATES);
  !!UN:P203/?(estatesI:y) P191/?(estatesII:y);

  !!if&i^mouse_item^<=86;
    !!FU|(estatesI)/(estatesII):E;
  !!en;

  !!FU(ES_793_ShowCurrentBonusDlg):P(hero);
!!en;

!?FU(OnHeroesMeetScreenMouseClick)&i^mouse_item^>=201/i^mouse_item^<=214; [клик в окне обмена между героями]
!!UN:P793/?(wogOption:y);               [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!CM:H?(hero:y)/?(rightHero:y);         [левый/правый герои]
!!VR(hero:y)&i^mouse_item^>207:S(rightHero); [герой, на навык которого кликнули]
!!VR(ssIndex:y):Si^mouse_item^ -200 %8 +1;[номер слота втор.навыка у героя]
!!HE(hero):S(ssIndex)/?(ss:y)/1;        [навык в слоте]

; Show up current bonus if both Estates I and Estates II are disabled
!!if&(ss)=(SKILL_ESTATES);
  !!UN:P203/?(estatesI:y) P191/?(estatesII:y);
  !!FU|(estatesI)/(estatesII):E;

  !!FU(ES_793_ShowCurrentBonusDlg):P(hero);
!!en;

!?FU(ES_793_CalcBonusIncome);           [установка расширенного описания Экономики для героя x1]
!#VA(hero:x) (bonusIncome:x);

!!HE(hero):S(SKILL_ESTATES)/?(lv:y) X?(spec:y)/?(spec2:y)/d/d/d/d/d O?(owner:y); [y1 - Уровень Экономики, y2=0,y3=13 для специалистов по Экономике, y5 - владелец героя]
!!VR(lv)&(spec)=0/(spec2)=(SKILL_ESTATES):+1; [+1 уровень экономики, если герой - спец. по Экономике]
!!VRv942:S(lv);                         [v942 - уровень Экономики героя]

!!VRv943:S0;                            [инициализация v943]
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/?v941;       [v941 Количество городов на карте]
!!VRv2:S-1;                             [v2, инициализация v1 для быстрого поиска]
!!DO(ES_793_GetDailyIncomeOfTowns)/1/v941/1&v941>=0:P(owner); [получаем размер дневного дохода городов текущего игрока в v943]
!!VR(bonusIncome):Sv943 *(lv) :20;      [реальный бонус экономики героя на текущий момент]

!?FU(ES_793_ShowCurrentBonusDlg);
!#VA(hero:x);

!!CM:R0;

!!FU(ES_793_CalcBonusIncome):P(hero)/?(bonusIncome:y); [подмена описания, если нажатие на экономике]
!!HE(hero):S(SKILL_ESTATES)/?(lv:y);
!!SN:T^es.793.desc%(lv)^/?(desc:z) T^es.793.currBonus^/?(currBonus:z)/^gold^/(bonusIncome);

!!if&i^mouse_action^=(MOUSE_LMB_PRESSED);
  !!IF:M^%(desc)%(currBonus)^;
!!el&i^mouse_action^=(MOUSE_RMB_PRESSED);
  !!IF:M0/(MSG_TYPE_POPUP)^%(desc)%(currBonus)^;
!!en;

** НАЧИСЛЕНИЯ

!?FU(Event_TownGoldIncomeCalculation); [расчет дохода города]
!!SN:W^tmp.y1^/y1 W^tmp.y2^/y2 W^tmp.y3^/y3 W^tmp.y4^/y4 W^tmp.y5^/y5 W^tmp.y6^/y6 W^tmp.y7^/y7; [сохранение значений y-переменных]
!!SN:W^tmp.y8^/y8 W^tmp.y9^/y9 W^tmp.y10^/y10 W^tmp.y11^/y11 W^tmp.y12^/y12 W^tmp.y13^/y13;      [сохранение значений y-переменных]
!!UN:P793/?y1;                [y1 - статус опции 793]

!!if&y1=0;                    [если опция не включена]
  !!SN:W^tmp.y1^/?y1;         [восстановление y1]
  !!FU:E;                     [выход]
!!en;

!!SN:X?y1; !!VRy2:Sy1+24;
!!UN:Cy2/4/?y3;               [структура текущего в функции города]
!!UN:Cy3/1/?y4;               [получить номер города на карте]
!!CA0/y4:P?y5/?y6/?y7 O?y8;   [y5/y6/y7 - координаты города, y8 - владелец]
!!SN:W^peons_%Y5_%Y6_%Y7^/?y10; [y10 - ежедневный доход от батраков в городе]
!!CA0/y4:B3/10; !!VRy9&1:S500;  [y9 - доход от строений Префектура..Капитолий + Грааль]
!!CA0/y4:B3/11; !!VRy9&1:S1000; [...]
!!CA0/y4:B3/12; !!VRy9&1:S2000; [...]
!!CA0/y4:B3/13; !!VRy9&1:S4000; [...]
!!CA0/y4:B3/26; !!VRy9&1:+5000; [...]
!!OW:Hy8/940/0;               [v940 - количество героев текущего игрока]
!!VRv942:S0;                  [v942 - суммарный уровень Экономики героев текущего игрока (обнуление)]
!!DO(ES_793_CalcEconomyLevels)/1/v940/1&v940>0:Py8; [для каждого героя текущего игрока получаем сумму уровней Экономики в v942]
!!VRv942:*5;                  [v942 - процент увеличения дохода от Экономик героев]
!!VRy10:+y9 *v942 :100;       [y10 - бонус дохода города от Экономик героев]
!!VRy3:Sy1+28; !!UN:Cy3/4/?y1;[y1 - текущий доход города]
!!VRy1:+y10;                  [добавление дохода от батраков]
!!UN:Cy3/4/y1;                [обновление дохода]
!!SN:W^tmp.y1^/?y1 W^tmp.y2^/?y2 W^tmp.y3^/?y3 W^tmp.y4^/?y4 W^tmp.y5^/?y5 W^tmp.y6^/?y6 W^tmp.y7^/?y7; [восстановление значений y-переменных]
!!SN:W^tmp.y8^/?y8 W^tmp.y9^/?y9 W^tmp.y10^/?y10 W^tmp.y11^/?y11 W^tmp.y12^/?y12 W^tmp.y13^/?y13;       [восстановление значений y-переменных]

!?FU(ES_793_CalcEconomyLevels); [для каждого героя игрока x1 получаем сумму уровней Экономики в v942 а стандартный прирост золота от Поместий в v944]
!!OW:Hx1/945/x16;             [v945=номер героя]
!!HEv945:S13/?y11;            [y11 - Уровень Экономики]
!!HEv945:X?y12/?y13/d/d/d/d/d;[y12=0,y13=13 для специалистов по Экономике]
!!VRy11&y12=0/y13=13:+1;      [+1 уровень экономики, если герой - спец. по Экономике]
!!VRv942:+y11;                [увеличиваем сумму уровней Экономики]

** end
