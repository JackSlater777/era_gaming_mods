ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Economy
** Name rus.     : Экономика
** Options       : 793
** Dialogs       : -
** Variables     : v940-v944
** Tmp variables :
** Timers        : -
** Functions     : FU7925-FU7926,FU7960
** PO-values     : -


!?FU(OnAfterErmInstructions);           // при старте игры
!!UN:P793/?(economy:y);                 [проверяем включена ли опция 793]
!!FU&(economy)<>(TRUE):E;

; Set up new name
!!SN:H^secskill^/(SKILL_ESTATES)/0/^%T(es.793.name)^;

; Set up new skill description
!!VR(basicDesc:z):S^%T(es.793.basicDesc)^;
!!VR(advancedDesc:z):S^%T(es.793.advancedDesc)^;
!!VR(expertDesc:z):S^%T(es.793.expertDesc)^;

; Add in Estates I description if enabled
!!UN:P203/?(estatesI:y);

!!if&(estatesI);
  !!VR(basicDesc):+z175179;
  !!VR(advancedDesc):+z175180;
  !!VR(expertDesc):+z175181;
!!en;

; Add in Estates II description if enabled
!!UN:P191/?(estatesII:y);

!!if&(estatesII);
  !!VR(basicDesc):+z175182;
  !!VR(advancedDesc):+z175183;
  !!VR(expertDesc):+z175184;
!!en;

; Change the descriptions
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_ESTATES)/(SKILL_EXPERT)/(expertDesc);

; Set up new spec description
!!SN:H^spec^/(HERO_LORD_HAART)/2/^%T(es.793.spec)^;

// Disable standard income increase from Estates
!?FU(OnStartOrLoad);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

!!UN:C5129744/2/?i^es_793_patch1^ C5129746/4/?i^es_793_patch2^;
!!UN:C5129744/2/177 C5129746/4/2425393296;  [если опция включена - отключаем стандартный доход от Поместий]

!?FU(OnGameLeave);
!!UN:P793/?(wogOption:y);     [проверяем включена ли опция 793]
!!FU&(wogOption)<>(TRUE):E;

!!UN:C5129744/2/i^es_793_patch1^ C5129746/4/i^es_793_patch2^;

!?FU7926;                     [получаем размер дневного дохода городов игрока x1 в v943]
!!UN:U98/-1/-1/1;             [v1..v3 - координаты города]
!!CA1:O?y1;                   [y1 - владелец города]
!!FU&y1<>x1:E;                [выход, если город принадлежит не текущему герою]

!!CA1:B3/10;                  [проверка наличия префектуры]
!!VRy1&1:S500;                [y1=500, если есть префектура]
!!CA1:B3/11;                  [проверка наличия городского холла]
!!VRy1&1:S1000;               [y1=1000, если есть городской холл]
!!CA1:B3/12;                  [проверка наличия муниципалитета]
!!VRy1&1:S2000;               [y1=2000, если есть муниципалитет]
!!CA1:B3/13;                  [проверка наличия капитолия]
!!VRy1&1:S4000;               [y1=4000, если есть капитолий]
!!CA1:B3/26;                  [проверка наличия Грааля]
!!VRy1&1:+5000;               [y1+=5000, если есть Грааль]
!!VRv943:+y1;                 [увеличиваем суммарный доход городов от строений]
!!UN:P785/?y1;                [проверяем включена ли опция 785 Батраки в y1]
!!FU&y1=0:E;                  [выход, если опция не активна]

!!CA1:P?y2/?y3/?y4;           [y2/y3/y4 - координаты города]
!!SN:W^peons_%Y2_%Y3_%Y4^/?y5;[y5 - ежедневный доход от батраков в городе]
!!VRv943:+y5;                 [увеличиваем суммарный доход городов на доход батраков]

!?FU(OnHeroScreenMouseClick); [клик в окне героя]
!!UN:P793/?y1;                [проверяем включена ли опция 793 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!CM:I?y1;                    [y1 - место нажатия]
!!FU|y1<79/y1>102:E;          [выход если нажатие не на втор. навыке]

!!VRy1:-79 %8 +1;             [y1 - номер слота втор.навыка у героя]
!!CM:H?y3/?y4;                [y3/y4 - левый/правый герои]
!!HEy3:Sy1/?y2/1;             [y2 - навык в слоте (13 - экономика)]
!!FU7960&y2=13:Py3;           [подмена описания, если нажатие на экономике]

!?FU(OnHeroesMeetScreenMouseClick); [клик в окне обмена между героями]
!!UN:P793/?y1;                [проверяем включена ли опция 793 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!CM:I?y1;                    [y1 - место нажатия]
!!FU|y1<200/y1>215:E;         [выход если нажатие не на втор. навыке]

!!CM:H?y3/?y4;                [y3/y4 - левый/правый герои]
!!VRy3&y1>207:Sy4;            [y3 - герой, на навык которого кликнули]
!!VRy1:-200 %8 +1;            [y1 - номер слота втор.навыка у героя]
!!HEy3:Sy1/?y2/1;             [y2 - навык в слоте (13 - экономика)]
!!FU7960&y2=13:Py3;           [подмена описания, если нажатие на экономике]

!?FU7960;                     [установка расширенного описания Экономики для героя x1]
!!HEx1:S13/?y1 X?y2/?y3/d/d/d/d/d O?y5; [y1 - Уровень Экономики, y2=0,y3=13 для специалистов по Экономике, y5 - владелец героя]
!!VRy1&y2=0/y3=13:+1;         [+1 уровень экономики, если герой - спец. по Экономике]
!!VRv942:Sy1;                 [v942 - уровень Экономики героя]
!!OW:Wy5/?y1;                 [y1 - количество городов текущего игрока]
!!VRy1:-1;                    [y1 - номер последнего города текущего игрока]
!!VRv943:S0;                  [инициализация v943]
!!UN:U98/-1/?v941;            [v941 Количество городов на карте]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7926/1/v941/1&v941>=0:Py5;[получаем размер дневного дохода городов текущего игрока в v943]
!!VRy1:Sv943 *v942 :20;       [y1 - реальный бонус экономики героя на текущий момент]
!!VRz1:Sz741 +^%Z179006%Z179188^; !!SN:H^secskill^/13/1/^%Z1^;[выставляем баз. описание навыка с текущим бонусом]
!!VRz1:Sz742 +^%Z179007%Z179188^; !!SN:H^secskill^/13/2/^%Z1^;[выставляем продв описание навыка с текущим бонусом]
!!VRz1:Sz743 +^%Z179008%Z179188^; !!SN:H^secskill^/13/3/^%Z1^;[выставляем эксп. описание навыка с текущим бонусом]

!?FU(OnHeroGainLevel)&999;
!!UN:P793/?y1;                [проверяем включена ли опция 793 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]

!!VRz1:Sz741 +^%Z179006^; !!SN:H^secskill^/13/1/^%Z1^;  [выставляем баз. описание навыка без бонуса]
!!VRz1:Sz742 +^%Z179007^; !!SN:H^secskill^/13/2/^%Z1^;  [выставляем продв описание навыка без бонуса]
!!VRz1:Sz743 +^%Z179008^; !!SN:H^secskill^/13/3/^%Z1^;  [выставляем эксп. описание навыка без бонуса]

** НАЧИСЛЕНИЯ

!?FU(Event_TownGoldIncomeCalculation); [расчет дохода города]
!!SN:W^tmp.y1^/y1 W^tmp.y2^/y2 W^tmp.y3^/y3 W^tmp.y4^/y4 W^tmp.y5^/y5 W^tmp.y6^/y6 W^tmp.y7^/y7; [сохранение значений y-переменных]
!!SN:W^tmp.y8^/y8 W^tmp.y9^/y9 W^tmp.y10^/y10 W^tmp.y11^/y11 W^tmp.y12^/y12 W^tmp.y13^/y13;      [сохранение значений y-переменных]
!!UN:P793/?y1;                [y1 - статус опции 793]

!!if&y1=0;                   [если опция не включена]
  !!SN:W^tmp.y1^/?y1;         [восстановление y1]
  !!FU:E;                     [выход]
!!en;

!!SN:X?y1; !!VRy2:Sy1+24;
!!UN:Cy2/4/?y3;               [структура текущего в функции города]
!!UN:Cy3/1/?y4;               [получить номер города на карте]
!!CA0/y4:P?y5/?y6/?y7 O?y8;   [y5/y6/y7 - координаты города, y8 - владелец]
!!SN:W^peons_%Y5_%Y6_%Y7^/?y10; [y10 - ежедневный доход от батраков в городе]
!!CA0/y4:B3/10; !!VRy9&1:S500;  [y9 - доход от строений Префектура..Капитолий + Грааль]
!!CA0/y4:B3/11; !!VRy9&1:S1000; [...]
!!CA0/y4:B3/12; !!VRy9&1:S2000; [...]
!!CA0/y4:B3/13; !!VRy9&1:S4000; [...]
!!CA0/y4:B3/26; !!VRy9&1:+5000; [...]
!!OW:Hy8/940/0;               [v940 - количество героев текущего игрока]
!!VRv942:S0;                  [v942 - суммарный уровень Экономики героев текущего игрока (обнуление)]
!!DO7925/1/v940/1&v940>0:Py8; [для каждого героя текущего игрока получаем сумму уровней Экономики в v942]
!!VRv942:*5;                  [v942 - процент увеличения дохода от Экономик героев]
!!VRy10:+y9 *v942 :100;       [y10 - бонус дохода города от Экономик героев]
!!VRy3:Sy1+28; !!UN:Cy3/4/?y1;[y1 - текущий доход города]
!!VRy1:+y10;                  [добавление дохода от батраков]
!!UN:Cy3/4/y1;                [обновление дохода]
!!SN:W^tmp.y1^/?y1 W^tmp.y2^/?y2 W^tmp.y3^/?y3 W^tmp.y4^/?y4 W^tmp.y5^/?y5 W^tmp.y6^/?y6 W^tmp.y7^/?y7; [восстановление значений y-переменных]
!!SN:W^tmp.y8^/?y8 W^tmp.y9^/?y9 W^tmp.y10^/?y10 W^tmp.y11^/?y11 W^tmp.y12^/?y12 W^tmp.y13^/?y13;       [восстановление значений y-переменных]

!?FU7925;                     [для каждого героя игрока x1 получаем сумму уровней Экономики в v942 а стандартный прирост золота от Поместий в v944]
!!OW:Hx1/945/x16;             [v945=номер героя]
!!HEv945:S13/?y11;            [y11 - Уровень Экономики]
!!HEv945:X?y12/?y13/d/d/d/d/d;[y12=0,y13=13 для специалистов по Экономике]
!!VRy11&y12=0/y13=13:+1;      [+1 уровень экономики, если герой - спец. по Экономике]
!!VRv942:+y11;                [увеличиваем сумму уровней Экономики]

** end
