ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Siege mastery
** Name rus.     : ќсадное дело
** Options       : 756


!?FU(OnAfterErmInstructions);           [пост-инструкци€: установка описаний]
!!UN:P756/?(wogOption:y);               [провер€ем включена ли опци€ 756]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!SN:H^secskill^/(SKILL_BALLISTICS)/0/^%T(es.756.name)^;

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_BASIC)/?(basicDesc:z);
!!VR(newBasicDesc:z):S(basicDesc) +^%T(es.756.basicDesc)^;
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_BASIC)/(newBasicDesc);

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_ADVANCED)/?(advancedDesc:z);
!!VR(newAdvancedDesc:z):S(advancedDesc) +^%T(es.756.advancedDesc)^;
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_ADVANCED)/(newAdvancedDesc);

!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_EXPERT)/?(expertDesc:z);
!!VR(newExpertDesc:z):S(expertDesc) +^%T(es.756.expertDesc)^;
!!SN:H^secskill^/(SKILL_BALLISTICS)/(SKILL_EXPERT)/(newExpertDesc);

!?FU(OnMonsterPhysicalDamage);          [при получении урона]
!!UN:P756/?(wogOption:y);               [провер€ем включена ли опци€ 756]
!!FU&(wogOption)<>(TRUE):E;             [выход если опци€ не включена]

!!MF:W?(atkType:y);

!!if&i^battle_hero_1^>(NO_HERO)/(atkType)>0;
  !!HEi^battle_hero_1^:S(SKILL_BALLISTICS)/?(lv:y); [навык ќсадного дела у геро€-защитника]

  !!if&(lv)>(SKILL_NOT_LEARNED);
    !!MF:F?(dmg:y);                     [получаемый урон]
    !!VR(newDmg:y):S(lv) *10 +100 *(dmg) :100; [получаемый урон +10% за уровень ќсадного дела]
    !!MF:F(newDmg);                     [обновление получаемого урона]
  !!en;
!!en;

**end
