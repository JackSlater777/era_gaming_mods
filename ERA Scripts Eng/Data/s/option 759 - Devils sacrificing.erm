ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Devils sacrificing
** Name rus.     : Дьявольское пожертвование
** Options       : 759

*** Архидьяволы и Бароны Ада получают способность 1 раз за бой (Бароны Ада - 2 раза)принести в жертву дружественный отряд
*** живых непризванных существ восстановив свое здоровье/численность на сумму здоровья принесенного в жертву отряда.
*** Количество Архидьяволов/Баронов Ада после восстановления не может превышать начальной численности отряда.

*** ИИ при возможности применяет новую способность если отряд Архидьяволов/Баронов Ада потерял не менее 30% численности.
*** При этом выбирается жертвенный отряд, здоровье которого не более чем на 10% превышает максимально необходимое здоровье
*** для восстановления всех Архидьяволов/Баронов Ада и не менее необходимого для восстановления хотя бы одного Архидьявола/Барона.
*** Если таких отрядов у ИИ несколько, выбирается отряд с наименьшим значением FV/восстанавливаемое существо.


// Initialise variables and set Up monster descriptions
!?FU(OnAfterErmInstructions);
; Initialise variables - This must to be execute regardless the option
!!VRi^es_759_devilStack^:S(NO_STACK);
!!VRi^es_759_victimStack^:S(NO_STACK);

!!UN:P759/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!MA:B(MON_ARCH_DEVIL)/d1 B(MON_HELL_BARON)/d2; 
!!SN:H^monname^/(MON_ARCH_DEVIL)/2/?(archDev:z) H^monname^/(MON_HELL_BARON)/2/?(hellBar:z);
!!VR(newArchDev:z):S(archDev) +^%T(es.759.desc)^;
!!VR(newHellBar:z):S(hellBar) +^%T(es.759.desc)^;

!!SN:H^monname^/(MON_ARCH_DEVIL)/2/(newArchDev) H^monname^/(MON_HELL_BARON)/2/(newHellBar);

; Set up descirption for fourth upgraded devil (if applicable)
!!FU(GetUpgradedMonster):P(MON_HELL_BARON)/?(upgHellBar:y);

!!if&(upgHellBar)>(NO_MON);
  !!MA:B(upgHellBar)/d3;
  !!SN:H^monname^/(upgHellBar)/2/?(upgHellBarDesc:z);
  !!VR(newUpgBaronDesc:z):S(upgHellBarDesc) +^%T(es.759.desc)^;
  !!SN:H^monname^/(upgHellBar)/2/(newUpgBaronDesc);
!!en;


// Check if it is any of the devils' turn
!?FU(OnBattleStackObtainsTurn)&i^battle_isVisible^;
!#VA(side:x) (stackOfSide:x);

!!UN:P759/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRi^es_759_devilStack^:S(NO_STACK);
!!VRi^es_759_victimStack^:S(NO_STACK);
!!VRi^ES_759_isDevilWithSpellsTurn^:S(FALSE);

!!VR(stack:y):S(side) *(BATTLE_STACKS_PER_SIDE) +(stackOfSide);
!!BM(stack):T?(type:y) E?(spell:y) N?(num:y) B?(startNum:y);

!!if&(spell)>0;
  !!if|(type)=(MON_ARCH_DEVIL)/(type)=(MON_HELL_BARON);
    !!VRi^ES_759_isDevilWithSpellsTurn^:S(TRUE);'
    !!VRi^es_759_devilStack^&(num)<(startNum):S(stack);
  !!en;

  ; Set the stack as the fourth upgraded devil (if applicable)
  !!FU(GetUpgradedMonster):P(MON_HELL_BARON)/?(upgHellBar:y);

  !!if&(type)=(upgHellBar)/(upgHellBar)>(NO_MON);
    !!VRi^ES_759_isDevilWithSpellsTurn^:S(TRUE);'
    !!VRi^es_759_devilStack^&(num)<(startNum):S(stack);
  !!en;
!!en;


// Recheck whether the devil is eligible to cast sacrifice (in case the hero's spell kills it)
!?FU(OnBattleActionEnd)&i^ES_759_isDevilWithSpellsTurn^;
!!BG:A?(action:y);

!!if&(action)=(BATTLE_ACTION_HERO_CAST);
  !!BMi^battle_current_stack^:N?(num:y) B?(startNum:y);
  !!VRi^es_759_devilStack^&(num)<(startNum)/(num)>0:Si^battle_current_stack^;
!!en;


// Change the cursor and show hint when hovering over victims
!?FU(OnBattleMouseHint)&i^es_759_devilStack^>(NO_STACK);  [при движении мыши по гексам поля боя, если ходит отряд Архидьяволов, который можно восстановить]
; Exit if not hovering one of the hex
!!VRi^es_759_victimStack^:S(NO_STACK);     [сброс номера жертвенного отряда]
!!MM:D?(pos:y);                            [позиция курсора]
!!FU|(pos)<0/(pos)>186:E;                  [выход, если курсор за полем]

; Exit if not no stack/same stack/not on the same side
!!BU:E(pos)/?(stack:y);                    [номер живого отряда в позиции]
!!FU|(stack)=(NO_STACK)/(stack)=i^es_759_devilStack^:E;   [выход, если живого отряда в позиции нет или курсор наведен на ходящий отряд Архидьяволов]
!!FU&(stack)<=(BATTLE_ATTACKER_STACK_LAST)/i^es_759_devilStack^>=(BATTLE_DEFENDER_STACK_FIRST):E; [выход, если курсор наведен на вражеский отряд]
!!FU&(stack)>=(BATTLE_DEFENDER_STACK_FIRST)/i^es_759_devilStack^<=(BATTLE_ATTACKER_STACK_LAST):E; [...]

; Exit if the victim is not a living creature/is summoned
!!BM(stack):F?(monFlags:y) T?(type:y) N?(num:y); [флаги/тип/количество существ отряда под курсором]
!!VR(isAlive:y):S(monFlags) &(MON_FLAG_ALIVE); [если существа отряда под курсором не живые]
!!VR(isSummoned:y):S(monFlags) &(MON_FLAG_SUMMONED); [если существа отряда под курсором призванные]
!!FU|(isAlive)=(FALSE)/(isSummoned)=(TRUE):E; [выход, если существа отряда под курсором не живые или призванные]

!!VRi^es_759_victimStack^:S(stack);        [установка номера жертвенного отряда]
!!UN:R5/2/18;                              [установка вида курсора "Жертва"]

!!if&(num)=1;
  !!SN:H^monname^/(type)/0/?(victimName:z);
!!el;
  !!SN:H^monname^/(type)/1/?(victimName);
!!en;

!!SN:T^es.759.hint^/?(hint:z)/^victim^/(victimName);
!!MM:M(hint);                              [подмена стандартной подсказки]


// Sacrifce the target when clicking on it
!?FU(OnBattleScreenMouseClick)&i^es_759_devilStack^>(NO_STACK)/i^es_759_victimStack^>(NO_STACK)/i^mouse_action^=(MOUSE_LMB_PRESSED); [при клике на жертвенном отряде, если ходит отряд Архидьяволов, который можно восстановить]
!!FU(ES_759_ReviveDevils):Pi^es_759_devilStack^/i^es_759_victimStack^;    [Восстановление Архидьяволов за счет жертвенного отряда]

!?FU(ES_759_ReviveDevils);                 [Восстановление Архидьяволов за счет жертвенного отряда: номер отряда Архидьяволов,Баронов/жертвенного отряда]
!#VA(devilStack:x) (victimStack:x);

; Calculate sacrificed HP (restoring HP for the devils)
!!BM(victimStack):N?(num:y) L?(lostHp:y) H?(hp:y) T?(victimType:y); [текущая численность/потерянное здоровье/макс.здоровье/тип существ в жертвенном отряде]
!!VR(sacrificedHp:y):S(num) *(hp) -(lostHp); ["пожертвованное" здоровье]

; Calculate the new HP/number of the devil
!!BM(devilStack):N?(num) L?(lostHp) H?(hp) B?(startNum:y) T?(devilType:y); [текущая численность/потерянное здоровье/макс.здоровье/начальная численность/тип существ в отряде Архидьяволов/Баронов]
!!VR(newTotalHp:y):S(num) *(hp) -(lostHp) +(sacrificedHp); [максимально возможное новое здоровье отряда Архидьяволов/Баронов]
!!VR(newNum:y):S(newTotalHp) :(hp);        [максимально возможное новое количество Архидьяволов/Баронов в отряде]
!!VR(lastUnitHp:y):S(newTotalHp) %(hp);    [количество здоровья у раненного Архидьявола/Барона]
!!VR(lastUnitHp)&(newNum)>=(startNum):S(hp); [...]
!!VR(newNum)&(lastUnitHp)>0:+1 F1/(startNum); [...]
!!VR(newLostHp:y):S(hp) -(lastUnitHp);     [потерянное здоровье нового отряда Архидьяволов/Баронов]

; Sacrifce the target, manage flags and memory
!!BM(victimStack):Fd(MON_FLAG_SACRIFICED) Fd(MON_FLAG_NO_COLORING) Fd(MON_FLAG_CLONE); [добавление флагов "Принесен в жертву", "Клон", "Не меняет цвет при клонироании" жертвенному отряду]
!!UN:C6919200/4/?(value:y);                [combatmanager]
!!VR(devilPtr:y):S(devilStack) *1352 +21708 +(value); [отряд Архидьяволов/Баронов для атаки]
!!VR(victimPtr:y):S(victimStack) *1352 +21708 +(value); [жертвенный отряд для атаки]

!!UN:C4462626/1/1;                         [анимациия атаки "подманивание"]
!!SN:P^SACRIF1.wav^;                       [воспроизведение звука]
!!BM(victimStack):V51;                     [анимация "Жертва" на жертвенном отряде]
!!SN:E4461360/2/(devilPtr)/(victimPtr)/2;  [Атака Архидьяволов/Баронов]
!!SN:P^RESURECT.wav^;                      [воспроизведение звука]
!!BM(devilStack):V50;                      [анимация на отряде Архидьяволов]

; Set up new number/hp of the devil
!!BM(devilStack):N(newNum) L(newLostHp) Ed-1; [восстановление Архидьяволов с уменьшением количества заклинаний отряда]

; Play the attack animation and msg on the battlefield
!!UN:C4462626/1/13;                        [анимациия атаки по умолчанию]
!!SN:H^monname^/(victimType)/1/?(victimName:z);
!!SN:H^monname^/(devilType)/1/?(devilName:z);
!!VR(restoredNum:y):S(newNum) -(num:y);    [not used for now]

!!SN:T^es.759.battleLog^/?(battleLog:z)/^devil^/(devilName)/^victim^/(victimName);
!!MM:S(battleLog);

; Skip this turn
!!BG:A(BATTLE_ACTION_SKIP);                [пропуск хода отрядом]

*** AI


// Revive devil for AI if the living number is lower than 70%
!?FU(OnBeforeBattleAction)&i^es_759_devilStack^>(NO_STACK)/i^battle_isVisible^; [перед действием отряда Архидяволов, который можно восстановить]
!!BMi^es_759_devilStack^:I?y1 G59/?y2/d G60/?y3/d N?y4 B?y5; [y1/y2/y3/y4/y5 - принадлежность отряда/продолжительность Берсерка/Гипноза/текущая/макс. численность]
!!VRy4:*100 :y5;                           [y4 - численность отряда в процентах от начальной]
!!FU&y4>70:E;                              [выход, если Архидьяволы/Бароны Ада понесли менее 30% потерь]

!!BA:O?y4/?y5;                             [y4/y5 - атакующий/защищающийся игроки]
!!VRy6&y1=0:Sy4;                           [y6 - номер игрока-владельца отряда]
!!VRy6&y1=1:Sy5;                           [...]
!!OW&y6>=0:Iy6/?y7;                        [y7=1, если отряд под контролем ИИ]
!!FU&y6>=0/y7=0:E;                         [выход, если отряд под контролем человека]

!!BMi^es_759_devilStack^:N?y11 B?y12 L?y13 H?y14;         [y11/y12/y13/y14 - текущее количество/начальное количество/потерянное здоровье/макс.здоровье отряда]
!!VRy10:Sy12 -y11 *y14 +y13 *11 :10;       [y10 - здоровье отряда, которое можно восстановить "жертвой" (+10% допустимый "перебор")]
!!VRv1:C-1/-1;                             [инициализация v1,v2]
!!DO(ES_759_SearchForAIVictim)/0/20/1&y1=0:Py13/y10/y14;         [поиск отряда наиболее подходящего для жертвы в v1]
!!DO(ES_759_SearchForAIVictim)/21/41/1&y1=1:Py13/y10/y14;        [...]
!!FU(ES_759_ReviveDevils)&v1>=0:Pi^es_759_devilStack^/v1; [Восстановление Архидьяволов за счет жертвенного отряда, если подходящая жертва найдена]

!?FU(ES_759_SearchForAIVictim);                                  [поиск наиболее подходящего отряда на роль жертвы]
** x16 - проверяемый отряд, x1/x2/x3 - минимальное здоровье жертвы/максимальное здоровье жертвы/здоровье восстанавливаемого существа
** v1 - прошлый найденный отряд, v2 - FV/кол-во восстановленных существ для прошлого найденного отряда
!!BMx16:T?y2 N?y3;                         [y2/y3 - тип/количество существ потенциального жертвенного отряда]
!!FU|x16=i^es_759_devilStack^/y2<0/y3<1:E;                [выход, если приверяемый и восстанавливаемый отряды совпадают или проверяемый отряд не существует]

!!BMx16:F?y1 H?y4 L?y5;                    [y1/y4/y5 - флаги/макс.здоровье/потерянное здоровье существ потенциального жертвенного отряда]
!!VRy6:Sy3 &16;                            [y6=0, если существа отряда под курсором не живые]
!!VRy7:Sy3 &4194304;                       [y7>0, если существа отряда под курсором призванные]
!!FU|y6=0/y7>0:E;                          [выход, если существа отряда не живые или призванные]

!!VRy6:Sy3 *y4 -y5;                        [y6 - общее здоровье жертвы]
!!FU|y6<x1/y6>x2:E;                        [выход, если здоровья не достаточно для восстановления хотя бы одного существа или более чем на 10% больше, чем необходимое для полного восстановления отряда]

!!VRy7:Sy6 -x1 :x3 +1;                     [y7 - количество восстанавливаемых существ]
!!VRy8:Sy6 -x1 %x3;                        [...]
!!VRy7&y8>0:+1;                            [...]
!!MA:Fy2/?y8;                              [y8 - значение "FV/кол-во восстановленных существ" для приносимого в жертву отряда]
!!VRy8:*y3 :y7;                            [...]
!!VRv1&y8<v2:Sx16;                         [если проверяемы отряд принести в жертву выгоднее чем предыдущий найденный...]
!!VRv2&y8<v2:Sy8;                          [...выбираем текущий отряд]
!!VRv2&v1=-1:Sy8;                          [если более подходящего отряда еще не было...]
!!VRv1&v1=-1:Sx16;                         [...выбираем текущий отряд]

*** end
