ZVSE

** Author orig.  : Algor (идея: igrik)
** Name          : trained AI
** Name rus.     : тренированный ИИ
** Options       : 731

** Каждый ИИ-герой при повышении уровня имеет шанс 0/25/50/75/100% (в зависимости от выбранного уровня сложности)
** дополнительно повысить на 1 профильный первичный параметр. Профильность параметров определяется классом героя.

!?HL-1&-1000;                           // при повышении уровня ИИ-героем
!!UN:P731/?y1; !!FU&y1<>1:E;            // выход, если не включена опция 731
!!VRy3:S3;                              // y3 - шанс получения доп.параметра за уровень сложности (3=25%(1/4), 4=20%(1/5), 7=12,5%(1/8), 9=10%(1/10)...)
!!UN:J2/?y1; !!VRy2:S0 Ry3; !!FU&y1<=y2:E; // выход, если тренировка не сработала
!!HE-1:O?y1; !!FU&y1<0:E;               // выход, если герой не нанят
!!HE-1:B2/?y1; !!VRy2:S0 R5;            // y1 - класс героя, y2 - случайное число (0..5)
!!VRy5:S0; !!VRy6:S0; !!VRy7:S0; !!VRy8:S0; // инициализация y5..y8
** выбор повышаемого параметра y5..y8 в зависимости от класса героя
!!if&y1=0:;  !!VRy5|y2=0/y2=1:S1; !!VRy6|y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 2 2 1 1 Рыцарь
!!if&y1=1:;  !!VRy5|y2=0:S1; !!VRy6|y2=1:S1; !!VRy7|y2=2/y2=3:S1; !!VRy8|y2=4/y2=5:S1; !!en:; // 1 1 2 2 Священник
!!if&y1=2:;  !!VRy5|y2=0:S1; !!VRy6|y2=1/y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 1 3 1 1 Рейнджер
!!if&y1=3:;  !!VRy6|y2=0/y2=1:S1; !!VRy7|y2=2:S1; !!VRy8|y2=3/y2=4/y2=5:S1;            !!en:; // 0 2 1 3 Друид
!!if&y1=4:;  !!VRy5|y2=0:S1; !!VRy6|y2=1:S1; !!VRy7|y2=2/y2=3:S1; !!VRy8|y2=4/y2=5:S1; !!en:; // 1 1 2 2 Алхимик
!!if&y1=5:;  !!VRy6|y2=0:S1; !!VRy7|y2=1/y2=2:S1; !!VRy8|y2=3/y2=4/y2=5:S1;            !!en:; // 0 1 2 3 Маг
!!if&y1=6:;  !!VRy5|y2=0/y2=1:S1; !!VRy6|y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 2 2 1 1 Демон
!!if&y1=7:;  !!VRy5|y2=0:S1; !!VRy6|y2=1:S1; !!VRy7|y2=2/y2=3:S1; !!VRy8|y2=4/y2=5:S1; !!en:; // 1 1 2 2 Еретик
!!if&y1=8:;  !!VRy5|y2=0:S1; !!VRy6|y2=1/y2=2:S1; !!VRy7|y2=3/y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 1 2 2 1 Рыцарь Смерти
!!if&y1=9:;  !!VRy5|y2=0:S1; !!VRy7|y2=1/y2=2:S1; !!VRy8|y2=3/y2=4/y2=5:S1;            !!en:; // 1 0 2 3 Некромант
!!if&y1=10:; !!VRy5|y2=0/y2=1:S1; !!VRy6|y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 2 2 1 1 Лорд
!!if&y1=11:; !!VRy5|y2=0:S1; !!VRy7|y2=1/y2=2/y2=3:S1; !!VRy8|y2=4/y2=5:S1;            !!en:; // 1 0 3 2 Чернокнижник
!!if&y1=12:; !!VRy5|y2=0/y2=1/y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1;            !!en:; // 4 0 1 1 Варвар
!!if&y1=13:; !!VRy5|y2=0/y2=1:S1; !!VRy6|y2=2:S1; !!VRy7|y2=3/y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 2 1 2 1 Боевой маг
!!if&y1=14:; !!VRy6|y2=0/y2=1/y2=2/y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1;            !!en:; // 0 4 1 1 Хозяин Зверей
!!if&y1=15:; !!VRy6|y2=0/y2=1:S1; !!VRy7|y2=2/y2=3:S1; !!VRy8|y2=4/y2=5:S1;            !!en:; // 0 2 2 2 Ведьма
!!if&y1=16:; !!VRy5|y2=0/y2=1/y2=2:S1; !!VRy6|y2=3:S1; !!VRy7|y2=4:S1; !!VRy8|y2=5:S1; !!en:; // 3 1 1 1 Путешественник
!!if&y1=17:; !!VRy7|y2=0/y2=1/y2=2:S1; !!VRy8|y2=3/y2=4/y2=5:S1;                       !!en:; // 0 0 3 3 Элементалист
** повышение первичного параметра
!!VRy5:*1; !!VRy6:*1; !!VRy7:*1; !!VRy8:*1; // множитель прироста первичных навыков, по умолчанию - 1
!!HE-1:Fdy5/dy6/dy7/dy8;                // повышение выбранного первичного параметра герою

** end