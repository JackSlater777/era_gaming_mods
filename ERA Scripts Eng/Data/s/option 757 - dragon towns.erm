ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Dragon towns
** Name rus.     : Драконьи города
** Options       : 757

** Разграбленную Утопию Драконов можно перестроить в город имеющий форт и ГМ 3-го уровня за 20 дерева и 20 руды.
** Тип нового города определяется расой героя-строителя. ИИ при наличии ресурсов всегда перестраивает разграбленную Утопию.

!$OB(OBJ_DRAGON_UTOPIA);
!!UN:P757/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Exit the function if there are more 48 or more towns on the map
!!UN:U(OBJ_TOWN)/(ANY_OBJ)/?(townNum:y);
!!FU&(townNum)>=48:E;

; Exit if the Utopia has not been cleared
!!CB998:T?(isCleared:y);
!!FU&(isCleared)<>(TRUE):E;
!!UN:V?(wogOption)/?(wogOption)/?(wogOption)/?(isRemoteGame:y)/?(wogOption);
!!FU&(isRemoteGame)<>(FALSE):E;

; Check the tile at the right bottom of the future town
!!VR(objX:y):Sv998 +1;
!!FU(ES_757_CheckTileEligibility):P(objX)/v999/v1000/?(result1:y);

!!FU&(result1)=0:E;

!!VR(objY:y):Sv999 -1;
!!FU(ES_757_CheckTileEligibility):P(objX)/(objY)/v1000/?(result2:y);

!!FU&(result2)=0:E;

!!VR(objLeftX:y):Sv998 -3;
!!FU(ES_757_CheckTileEligibility):P(objLeftX)/v999/v1000/?(result3:y);

!!FU&(result3)=0:E;

; Check the entrance of the future town (y-coordinate +1)
!!VR(heroX:y):S-1;
!!VR(heroResult:y):S0;
!!VR(entranceX:y):Sv998 -1;
!!VR(entranceY:y):Sv999 +1;
!!FU(ES_757_CheckTileEligibility):P(entranceX)/(entranceY)/v1000/?(entranceResultMid:y);

; Check for one tile to the right of the entrance, set heroX to the target's x coordinate if found
!!if&(entranceResultMid)=0;
  !!FU(ES_757_CheckTileEligibility):Pv998/(entranceY)/v1000/?(entranceResultRight:y);

  ; Check for one tile to the left
  !!if&(entranceResultRight)=0;
    !!VR(entranceLeftX:y):Sv998 -2;
    !!FU(ES_757_CheckTileEligibility):P(entranceLeftX)/(entranceY)/v1000/?(entranceResultLeft:y);

    !!if&(entranceResultLeft)<>0;
      !!VR(heroX):S(entranceLeftX);
      !!VR(heroResult):S(entranceResultLeft);
    !!en;
  !!el;
    !!VR(heroX):Sv998;
    !!VR(heroResult):S(entranceResultRight);
  !!en;
!!el;
  !!VR(heroX):S(entranceX);
  !!VR(heroResult):S(entranceResultMid);
!!en;

!!FU&(heroX)=-1:E;

; Check for the owner and type pf the hero to set the details of the new town
!!HE(CURRENT_HERO):O?(owner:y) B2/?(class:y);
!!VR(townType:y):S(class) :2;
; Show up dialogue for building a new town depending on whether having enought resoursces or if is AI
!!OW:R(owner)/(RES_WOOD)/?(wood:y) R(owner)/(RES_ORE)/?(ore:y);

!!IF:V1/1;
!!VR(flag:y):S(TRUE);
!!VR(flag)|(wood)<20/(ore)<20:S(FALSE);

!!if&1000;
  !!if&(flag);
    !!VR(townPicIndex:y):S(townType) +22;
    ; -100020 for showing -20 of the resource below the picture
    !!IF:Q1/(PIC_TYPE_RES_WOOD)/-100020/(townPicIndex)/7/(PIC_TYPE_RES_ORE)/-100020/(MSG_TYPE_QUESTION)/^%T(es.757.dlg1)%T(es.757.dlg2)^;
  !!el;
    !!IF:Q1/(PIC_TYPE_RES_WOOD)/-100020/(PIC_TYPE_RES_ORE)/-100020/(MSG_TYPE_MES)/^%T(es.757.dlg1)^;
  !!en;
!!en;

!!FU|(flag)=(FALSE)/-1:E;

; Reduce resources 
!!OW:R(owner)/(RES_WOOD)/d-20 R(owner)/(RES_ORE)/d-20;

; Remove obstacles if existing
!!UN:Ov998/v999/v1000/1;
!!UN&(result1)=2:O(objX)/v999/v1000/1;
!!UN&(result2)=2:O(objX)/(objY)/v1000/1;
!!UN&(result3)=2:O(objLeftX)/v999/v1000/1;
!!UN&(heroResult)=2:O(heroX)/(entranceY)/v1000/1;

; Teleport the hero to the entrance
!!HE(CURRENT_HERO):P(heroX)/v999/v1000;

; Build and set up the new town
; For some reason, the new town will only visually appear after showing up IF msg (from Achievements for example)
!!TR998:T?(terrain:y)/d/d/d/d/d/d/d;
!!UN:I(objX)/v999/v1000/(OBJ_TOWN)/(townType)/(OBJ_TOWN)/(townType)/(terrain)/0;

!!VR(random:y):R0/1/15;
!!SN:T^es.757.name%(random)^/?(name:z);
!!CA(objX)/v999/v1000:O(owner) N(name) B2/30 B2/31 B6/2 B6/9; [установка владельца, имени, разрушение жилищ и отстройка «амка и √ћ3 уровн€]
!!CA(objX)/v999/v1000:M2/0/-1/0 M2/1/-1/0 M2/2/-1/0 M2/3/-1/0 M2/4/-1/0 M2/5/-1/0 M2/6/-1/0; [нет стражников]
!!CA(objX)/v999/v1000:M1/0/0/0 M1/1/0/0 M1/2/0/0 M1/3/0/0 M1/4/0/0 M1/5/0/0 M1/6/0/0; [нет существ дл€ выкупа]

; Add in compatibility with Enhanced War Machines III
!!UN:P73/?(enhWMIII:y); 
!!PO(objX)/v999/v1000&(enhWMIII):H1 T1;

; Place stump and log to empty tiles
!!VR(stumpX:y):Sv998 -4;
!!VR(stumpY:y):Sv999 -1;
!!FU(ES_757_CheckTileEligibility):P(stumpX)/(stumpY)/v1000/?(result4:y);
!!UN&(result4:y)=1:I(stumpX)/(stumpY)/v1000/(OBJ_STUMP)/0/(OBJ_STUMP)/0/-1/1;

!!VR(logX:y):Sv998 +1;
!!VR(logY:y):Sv999 -2;
!!FU(ES_757_CheckTileEligibility):P(logX)/(logY)/v1000/?(result5:y);
!!UN&(result5)=1:I(logX)/(logY)/v1000/(OBJ_LOG)/0/(OBJ_LOG)/0/-1/1;
; Update the sreen
!!SN:D;
!!UN:R2;

// Check if the targeted tile is available
!?FU(ES_757_CheckTileEligibility);
!#VA(x:x) (y:x) (z:x) (result:x);

; Initialization. Result=0 means the tile is interactable or is part of an interactable object 
!!VR(result):S0;

; Check if the targeted tile is interactable and passable
!!TR(x)/(y)/(z):E?(notInteractable:y) P?(isPassable:y);
; Set the result to 1 if not interactable and passable (no object)
!!if&(notInteractable)/(isPassable);
  !!VR(result):S1;
; Check if the object is interractable by trying to find its yellow tile
!!el&(notInteractable)/(isPassable)<>(TRUE);
  !!VR(newX:y):S(x);
  !!VR(newY:y):S(y);
  !!VR(newZ:y):S(z);
  !!SN:O?(newX)/?(newY)/?(newZ);

  ; Set the result to 2 if the object is not interactable (normal road blocks)
  !!VR(result)&(x)=(newX)/(y)=(newY)/(z)=(newZ):S2;
!!en;

**end
