ZVSE

** Author orig.  : Algor
** Name          : Dragon towns
** Name rus.     : „раконьи города
** Options       : 757
** Dialogs       : -
** Variables     : -
** Tmp variables : z1
** Timers        : -
** Functions     : -
** PO-values     : -

** ђазграбленную “топию Драконов можно перестроить в город имеющий форт и ГМ 3го уровне за 20 дерева и 20 руды.
** ’ип нового города определеетсе расой герое-строителе. ИИ при наличии ресурсов всегда перестраивает разграбленную “топию.
** ‚ключение опции отключает "’емницу хозеина драконов".

!?PI;                         [перед стартом карты]
!!UN:P757/?y1;                [y1 - значение опции 757]
!!UN&y1>0:P53/0;              [если включена, отключаем опцию "’емница хозеина драконов"]

!$OB25;
!!UN:P757/?y1;                [y1 - значение опции 757]
!!FU&y1=0:E;                  [выход, если опциЯ не включена]
!!UN:U98/-1/?y1;              [y1 количество городов на карте]
!!FU&y1>=48:E;                [выход, если на карте уже максимальное количество городов]
!!CBv998/v999/v1000:T?y1;     [y1=1, если утопие разграблена]
!!FU&y1=0:E;                  [выход, если утопие не разграблена]
!!UN:V?y1/?y1/?y1/?y2/?y1;
!!FU&y2=1:E;                  [выход, если несколько PC]

!!HE-1:O?y1 B2/?y2;           [y1 - хозЯин герое, y2 - класс героЯ]
!!VRy2::2;                    [y2 - родной тип города герое]
!!OW:Ry1/0/?y3 Ry1/2/?y4;     [y3/y4 - кол-во дерева/руды у игрока]
!!VRy3|y3<20/y4<20:S0;        [y3 = 0, если ресурсов на перестройку не хватает]
!!VRz1&y3>0:Sz179211;         [’екст вопроса игроку-человеку]
!!VRz1&y3=0:Sz179212;         [’екст сообщение игроку-человеку]
!!VRi:Sy2 +22;                [i - номер картинки-форта дле города родного герою]
!!IF:V1/1;                    [по умолчанию согласие на перестройку “топии есть]
!!IF&999/1000/y3>0:Q1/0/-100020/i/7/2/-100020/2/1; [вопрос игроку человеку]
!!IF&999/1000/y3=0:Q1/0/-100020/2/-100020/1/1; [сообщение игроку человеку]
!!FU|y3=0/-1:E;               [выход, если не хватает ресурсов или герой-человек отказалсе от постройки]
!!TRv998/v999/v1000:T?y3/d/d/d/d/d/d/d; [y3 - ’ип почвы]
!!UN:Ov998/v999/v1000/1;      [удаление утопии]
!!VRy4:Sv998 +1;              [y4 - абсцисса координаты города]
!!UN:Iy4/v999/v1000/98/y2/98/y2/y3/1; [добавление города]
!!VRi:S179213 R14;            [i - случайный индекс наименование города]
!!VRz1:Szi;                   [z1 - наименование города]
!!CAy4/v999/v1000:Oy1 Nz1 B2/30 B2/31 B6/2 B6/9; [установка владельца, имени, разрушение жилищ и отстройка замка и ГМ3 уровне]
!!CAy4/v999/v1000:M2/0/-1/0 M2/1/-1/0 M2/2/-1/0 M2/3/-1/0 M2/4/-1/0 M2/5/-1/0 M2/6/-1/0; [нет стражников]
!!CAy4/v999/v1000:M1/0/0/0 M1/1/0/0 M1/2/0/0 M1/3/0/0 M1/4/0/0 M1/5/0/0 M1/6/0/0; [нет существ дле выкупа]
!!VRy5:Sv999 +1;              [ордината квадрата под позицией города/герое]
!!TRv998/y5/v1000:E?y6 P?y7;  [y6/y7 - желтый/красный квадраты под позицией герое]
!!UN|y6=1/y7=0:Ov998/y5/v1000/1;[удаление преграды под позицией герое]
!!TRy4/y5/v1000:E?y6 P?y7;    [y6/y7/y8 - желтый/красный квадраты/тип почвы под позицией города]
!!UN|y6=0/y7=0:Oy4/y5/v1000/1;[удаление преграды под позицией города]
!!VRy4:Sv998 -1;              [y4 - абсцисса входа в город]
!!HE-1:Py4/v999/v1000/0;      [перемещение герое в город]
!!POy4/v999/v1000&v847=1:H1 T1;[‘овместимость с WoG-опцией 73 "“силенные военные машины III". ‚ отстроенном городе доступна 1 БМ]
!!VRy4:Sv998 -4;              [y4/y5 - координаты клетки, освободившейсе при замене объекта]
!!VRy5:Sv999 -1;              [...]
!!TRy8/y9/v1000:P0;           [освободившаесе клетка недоступна дле посещение]
!!VRy4:Sv998 -1; !!VRy5:Sv999 +1;           [y4/y5/v1000 - координаты клетки под городом]
!!TRy4/y5/v1000:T?y9/d/d/d/d/d/d/d;         [y9 - тип почвы клетки под входом]
!!TRy4/y5/v1000&y9>7:P1 T1/0/d/d/d/d/d/d; P1;   [обновление типа почвы клетки под входом на проходимую]
!!TRv998/y5/v1000:T?y9/d/d/d/d/d/d/d;       [y9 - тип почвы клетки под входом-2]
!!TRv998/y5/v1000&y9>7:P1 T1/0/d/d/d/d/d/d; P1; [обновление типа почвы клетки под входом-2 на проходимую]
!!VRy4:Sv998 +1;
!!TRy4/y5/v1000:T?y9/d/d/d/d/d/d/d;         [y9 - тип почвы клетки под входом-3]
!!TRy4/y5/v1000&y9>7:P1 T1/0/d/d/d/d/d/d; P1;   [обновление типа почвы клетки под входом-3 на проходимую]
!!VRy4:S-20;                  [y4 - штраф дерева/камне]
!!OW:Ry1/0/dy4 Ry1/2/dy4;     [списание ресурсов]
!!UN:R1;                      [обновление экрана]

**end

