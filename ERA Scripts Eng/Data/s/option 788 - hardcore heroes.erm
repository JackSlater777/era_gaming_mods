ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30 and deamon_n
** Name          : Hardcore heroes
** Name rus.     : Смертные герои
** Options       : 788


!?FU(OnBeforeBattleAction);             // перед действием в бою
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BG:A?y1;
!!SN:W^opt788.a^/y1;                    // сохраняем последнее выполненное действие

!?FU(OnAfterBattleUniversal);           // после боя
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BA:H0/?y1 H1/?y2;                     // y1/y2 - атакующий/защищающийся герои, выход, если не битва двух героев
!!SN:W^opt788.a^/?y3;
!!FU|y3=4/y3=5:E;                       // выход, если герой сбежал/откупился

!!HEy1:O?y3;
!!HEy2&y2>=0:O?y4;                      // y3/y4 - владельцы атакующеего/защищающегося героя
!!FU(ES_788_Rebirth)&y1>=0/y3<0:Py1;    // перерождение атакующего героя, если он погиб
!!FU(ES_788_Rebirth)&y2>=0/y4<0:Py2;    // перерождение защищающегося героя, если он погиб

!?FU(ES_788_Rebirth);                   // Перерождение героя x1
!#VA(hero:x);

; Reset the hero
!!HE(hero):Z?(heroPtr:y);
!!SN:E5081488/(CALLCONV_THISCALL)/(heroPtr)/(hero);
; Restore the original starting army
!!UN:C(GAME_MANAGER)/(UNC_INT)/?(gameManager:y);
!!SN:E5018608/(CALLCONV_THISCALL)/(gameManager)/(hero)/0/0;
; Restore Movement Points
!!SN:E5132288/(CALLCONV_THISCALL)/(heroPtr);
!!HE(hero):Gv1 Wv1/1;

!!SN:W^opt788.h%X1r^/1;                 // установка герою признака перерожденного
!!HEx1:B2/?y2;
!!VRy1:Sy2 :2;                          // y1 - раса героя 0..8
!!VRy2:%2 *2 -1;
!!VRi:R0/0/2;
!!VRy2&i=2:S0;                          // y2 - класс героя -1/0/1 = Воин/Странник/Маг

** установка первичных параметров
!!VRy11:R0/1/2 -y2;
!!VRy12:R0/1/2 -y2;                      // y11/y12/y13/y14 - первичные параметры нового героя
!!VRy13:R0/2/3 +y2;
!!VRy14:R0/2/3 +y2;                      // ...
!!HEx1:Fy11/y12/y13/y14;                // Установка первичных параметров

; Add in spell book
!!HEx1:A1/(ART_SPELL_BOOK)/17;                 // выдам катапульту и книгу всем героям

** выбор специализации
!!VRy3:R0/0/1;
!!VRy4:R0/0/1;                           // y3 - тип специализации 0 - втор.навык, 1 - существо/ресурс/заклинание, y4=1 для альтернативной специализации (50% шанс)

; Warriors
!!if&y2=-1;

  !!if&y3=0;                        // воин со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3

    !!if&i=0;
      !!HEx1:X0/(SKILL_ARCHERY)/0/0/0/0/0;           // 0 - Стрельба
    !!el&i=1;
      !!HEx1:X0/(SKILL_OFFENCE)/0/0/0/0/0;           // 1 - Нападение
    !!el&i=2;
      !!HEx1:X0/(SKILL_ARMORER)/0/0/0/0/0;           // 2 - Доспехи
    !!el&i=3/y1<>(TOWN_NECROPOLIS);
      !!HEx1:X0/(SKILL_FIRST_AID)/0/0/0/0/0;         // 3 - First Aid
      !!HEx1:A1/(ART_FIRST_AID_TENT)/15;             [Give first aid tent]
    !!el&i=3/y1=(TOWN_NECROPOLIS);
      !!HEx1:X0/(SKILL_NECROMANCY)/0/0/0/0/0;        // 3 - Некромантия (Некрополь)
    !!en;

  !!el&y3=1;                            // воин с существом
    !!VRk:R0/0/7;

    ; 1/8 chance to become Ballista Specialist
    !!if&k<=6;             
      !!UN:Ty1/k/0/?i;                  // i - случайное негрейженное существо расы героя
    !!el;
      !!VRi:S(MON_BALLISTA);
      !!HEx1:A1/(ART_BALLISTA)/13;      [Give Ballista]
    !!en;

    !!HEx1:X1/i/0/0/0/0/0;                // специалист по существам

    !!if&y4=1;                            // специалисты по лучшению существ
      !!HEx1&i=(MON_MONK):X6/i/0/0/0/9/(MON_WAR_ZEALOT);        // Монахи и Фанатики -> Фанатики Войны
      !!HEx1&i=(MON_GREMLIN):X6/i/0/0/0/29/(MON_SANTA_GREMLIN);     // Гремлины и Мастер-гремлины -> Санта-гремлины
      !!HEx1&i=(MON_STONE_GOLEM):X6/i/0/0/0/33/(MON_GOLD_GOLEM);     // Каменные и Стальные големы -> Золотые големы
      !!HEx1&i=(MON_WALKING_DEAD):X6/i/0/0/0/59/(MON_MUMMY);     // Живые мертвецы и Зомби -> Мумии
      !!HEx1&i=(MON_CAVALIER):X6/i/0/0/0/11/(MON_DREAD_KNIGHT);      // Кавалеристы и Чемпионы -> Рыцари Смерти
      !!HEx1&i=(MON_UNICORN):X6/i/0/0/0/25/(MON_NIGHTMARE);     // Единороги и Боевые Единороги -> Кошмары
      !!HEx1&i=(MON_GREEN_DRAGON):X6/i/0/0/0/27/(MON_BLOOD_DRAGON);     // Зеленый и Золотой Драконы -> Кровавый Дракон
      !!HEx1&i=(MON_RED_DRAGON):X6/i/0/0/0/83/(MON_BLOOD_DRAGON);     // Красный и Черный Драконы -> Кровавый Дракон
    !!en; 
  !!en;

; Adventurers
!!el&y2=0;

  !!if&y3=0;                         // странник со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3
    !!HEx1&i=0:X0/(SKILL_LOGISTICS)/0/0/0/0/0;            // 0 - Логистика
    !!HEx1&i=1:X0/(SKILL_NAVIGATION)/0/0/0/0/0;            // 1 - Навигация
    !!HEx1&i=2:X0/(SKILL_EAGLE_EYE)/0/0/0/0/0;           // 2 - Зоркость
    !!HEx1&i=3:X0/(SKILL_ESTATES)/0/0/0/0/0;           // 3 - Имущество

  !!el&y3=1;                         // странник с ресурсом
    !!VRi:R0/0/6;                          // i - случайное число 0..6
    !!HEx1:X2/i/0/0/0/0/0;                // специалист по ресурсам
  !!en;

; Mages
!!el&y2=1;
  
  !!if&y3=0;                         // маг со вторичным навыком
    !!VRi:R0/0/3;                          // i - случайное число 0..3
    !!HEx1&i=0:X0/(SKILL_MYSTICISM)/0/0/0/0/0;            // 0 - Мистицизм
    !!HEx1&i=1:X0/(SKILL_INTELLIGENCE)/0/0/0/0/0;           // 1 - Интеллект
    !!HEx1&i=2:X0/(SKILL_SORCERY)/0/0/0/0/0;           // 2 - Волшебство
    !!HEx1&i=3:X0/(SKILL_RESISTANCE)/0/0/0/0/0;           // 3 - Сопротивление

  !!el&y3=1;                         // маг с заклинанием
    !!VRi:R0/0/10;                         // i - случайное число 0..10
    !!HEx1&i=0:X3/15/0/0/0/0/0;           // Волшебная стрела
    !!HEx1&i=1:X3/16/0/0/0/0/0;           // Ледяная молния
    !!HEx1&i=2:X3/17/0/0/0/0/0;           // Удар молнии
    !!HEx1&i=3:X3/20/0/0/0/0/0;           // Кольцо холода
    !!HEx1&i=4:X3/21/0/0/0/0/0;           // Огненный шар
    !!HEx1&i=5:X3/23/0/0/0/0/0;           // Метеоритный дождь
    !!HEx1&i=6:X3/22/0/0/0/0/0;           // Инферно
    !!HEx1&i=7:X3/18/0/0/0/0/0;           // Взрыв
    !!HEx1&i=8:X3/19/0/0/0/0/0;           // Цепная молния
    !!HEx1&i=9:X3/26/0/0/0/0/0;           // Армагеддон
    !!HEx1&i=10/y1<>4:X3/25/0/0/0/0/0;    // Уничтожение нежити (кроме Некрополя)
    !!HEx1&i=10/y1=4:X3/24/0/0/0/0/0;     // Волна смерти (Некрополь)

    !!if&y4=1;                            // специалисты по аурам-проклятиям
      !!if&i=7;
        !!HEx1:X3/45/0/0/0/0/0;         // Слабость
      !!el&i=8;
        !!HEx1:X3/47/0/0/0/0/0;         // Разрушение
      !!el&i=9;
        !!HEx1:X3/42/0/0/0/0/0;         // Проклятье
      !!el&i=10;
        !!HEx1:X3/54/0/0/0/0/0;        // Медлительность
      !!en;
    !!en; 
  !!en;
!!en;

// Set up spec descriptions
!!FU(ES_788_SetNewSpecDesc):Px1/y2/y3/x4/i;

** очистка/установка заклинаний
!!re i/(SPELL_FIRST)/(SPELL_LAST_WOG);  // для каждого заклинания
  !!HEx1:Mi/0;                          // удаляем заклинание
!!en; 

!!HEx1:X?j/?k/d/d/d/d/d;
!!HEx1&j=3:Mk/1;                        // даем герою спец.заклинание

** установка вторичных навыков
!!re i/(SEC_SKILL_FIRST)/(SEC_SKILL_LAST); // для каждого втор.навыка
  !!HEx1:Si/(SKILL_NOT_LEARNED);        // удаляем все вторичные навыки героя
!!en; 

!!HEx1:X?j/?k/d/d/d/d/d;                // j/k - параметры специализации героя
!!HEx1&j=0:Sk/(SKILL_ADVANCED);         // для спецов по втор.навыку даем этот навык на продвинутом уровне
!!HEx1&j=2:S(SKILL_ESTATES)/(SKILL_BASIC); // для спецов по ресурсам даем базовое Имущество

!!if&j=3;                               // для спецов по заклинаниям первым навыком даем профильную Школу Магии
  !!SSk:S?l;
  !!VRy10:S4;                           // y10 - навык профильной Школы. По умолчалию или для мультипрофильных заклинаний - Вода
  !!VRy11:Sl &1;
  !!VRy10&y11>0:S15;                    // ...
  !!VRy11:Sl &2;
  !!VRy10&y11>0:S14;                    // ...
  !!VRy11:Sl &8;
  !!VRy10&y11>0:S17;                    // ...
  !!VRy11:Sl &4;
  !!VRy10&y11>0:S16;                    // ...
  !!HEx1:Sy10/1;                        // даем базовый навык профильной Школы
!!en; 

!!if|j=1/j=6;                           // для спецов по существам первым навыком даем Нападение/Тактику/Стрельбу для пеших/летунов/стрелков
  !!MA:Xk/?l;
  !!VRy10:S22;                          // y10 - профильный навык. По-умолчанию - Нападение
  !!VRy11:Sl &2;
  !!VRy10&y11>0:S19;                    // для летающих существ - Тактика
  !!VRy11:Sl &4;
  !!VRy10&y11>0:S1;                     // для стреляющих существ - Стрельба
  !!HEx1:Sy10/1;                        // даем базовый профильный навык
!!en; 

!!if&j<>0;                              // для спецов не по втор.навыку подбираем второй базовый навык
  !!if&y2=-1;                           // для Воинов
    !!VRi:R0/0/5;
    !!HEx1&i=0/y1<>4:S6/1;              // Лидерство
    !!HEx1&i=0/y1=4:S12/1;              // Некромантия
    !!HEx1&i=1:S9/1;                    // Удача
    !!HEx1&i=2:S10/1;                   // Баллистика
    !!HEx1&i=3:S20/1;                   // Артиллерия
    !!HEx1&i=4:S23/1;                   // Доспехи
    !!HEx1&i=5:S27/1;                   // Первая помощь
  !!en; 

  !!if&y2=0;                            // для Странников
    !!VRi:R0/0/6;
    !!HEx1&i=0:S0/1;                    // Поиск Пути
    !!HEx1&i=1:S2/1;                    // Логистика
    !!HEx1&i=2:S3/1;                    // Разведка
    !!HEx1&i=3:S4/1;                    // Дипломатия
    !!HEx1&i=4:S11/1;                   // Зоркость
    !!HEx1&i=5:S18/1;                   // Грамотность
    !!HEx1&i=6:S21/1;                   // Обучение
  !!en; 

  !!if&y2=1;                            // для Магов
    !!VRi:R0/0/4;
    !!HEx1&i=0/y1<>4:S7/1;              // Мудрость
    !!HEx1&i=0/y1=4:S12/1;              // Некромантия
    !!HEx1&i=1:S8/1;                    // Мистицизм
    !!HEx1&i=2:S24/1;                   // Интеллект
    !!HEx1&i=3:S25/1;                   // Волшебство
    !!HEx1&i=4:S26/1;                   // Сопротивление
  !!en; 
!!en; 

** удаление достижений
!!re i/0/14;                            // для каждого достижения
  !!SN:W^opt760.h%X1a%Vir^/0;           // обнуляем достижение
  !!SN:W^opt760.h%X1a%Vic^/0;           // обнуляем счетчик достижения
!!en; 

** удаление ассасинов
!!SN:W^opt795.11.%X1^/?y15 W^opt795.11.%X1.rebirth^/y15 W^opt795.11.%X1^/0; // удаление ассасинов

** очистка командира/артефактов командира
!!UN:P3/?i P6/?j P51/?k;                // i/j/k - статус опций Командиры / Обязательный найм командиров / Измененные командиры

!!if&i=0;                               // если командиры разрешены
  !!COx1:A4/-1/0/-1/0/-1/0/-1/0/-1/0/-1/0;         // удаление артефактов командира
  !!COx1:B0/0 D0 Ty1 X0/1000 X1/0 X2/1;            // удаление способностей, уровня, опыта, оживление и сброс расы командира
  !!COx1&k=0:P0/5 P1/5 P2/40 P3/9 P4/1 P5/4 P6/5;  // сброс первичных параметров командира
  !!COx1&k=1:P0/5 P1/5 P2/30 P3/10 P4/3 P5/5 P6/0; // сброс первичных параметров для "измененного" командира
  !!COx1:S0/0 S1/0 S2/0 S3/0 S4/0 S5/0 S6/0;       // сброс вторичных навыков командира

  !!if&k=1;                             // если включены измененные командиры, раздаем расовые бонусные навыки
    !!COx1&y1=0:B1/5/0;                 // paladin         - endless retaliation 
    !!COx1&y1=1:B1/7/0;                 // heirophant      - magic mirror (fire shield)
    !!COx1&y1=2:B1/4/0;                 // temple guardian - shoot 
    !!COx1&y1=3:B1/14/0;                // succubus        - fly 
    !!COx1&y1=4:B1/1/0;                 // soul eater      - fear
    !!COx1&y1=5:B1/3/0;                 // brute           - no retaliation 
    !!COx1&y1=6:B1/8/0;                 // ogre leader     - block 
    !!COx1&y1=7:B1/12/0;                // shaman          - 50% poison (death stare)
    !!COx1&y1=8:B1/0/0;                 // astral spirit   - ignore 50% defense  
  !!en; 
!!en; 

!!COx1&j=1:E0;                          // если командиры должны быть наняты, удаляем командира

** очистка приспешников/бонусных БМ
!!IF:Wx1;                               // w-переменные текущего героя
; Warmachine Enhancements
!!VRw81:S0;
!!VRw82:S0;
!!VRw83:S0;                             // удаление бонусных БМ
; Henchmen
!!VRw117:S0;
!!VRw118:S-1;                           // удаление приспешника

** обнуление благословений/проклятий
!!HEx1:Y0/0/0/3;                        // удаление всех благословений/проклятий

// Set up special spec descriptions
!?FU(ES_788_SetNewSpecDesc);
!#VA(hero:x) (heroType:x) (random1:x) (random2:x) (param:x);

; Warriors
!!if&(heroType)=-1;
  ; Skill Master
  !!if&(random1)=0;
    !!SN:T^es.788.skillMaster^/?(spec:z);
  !!el&(random1)=1;
    ; Creature Specialist
    !!UN:P740/?(enhCreaSpec:y);

    !!if&(enhCreaSpec);
      !!SN:T^es.740.spec^/?(spec);
    !!el;
      !!SN:T^es.788.creaSpec^/?(spec);
    !!en;

    !!if&(random2);
      ; Creature Trainer
      !!if&(param)=(MON_MONK);
        !!SN:T^es.788.creaTrainer^/?(spec)/^mon1^/(param)/^mon2^/(MON_ZEALOT)/^upg^/(MON_WAR_ZEALOT);
      !!el&(param)=(MON_GREMLIN);
        !!SN:T^es.788.creaTrainer^/?(spec)/^mon1^/(param)/^mon2^/(MON_MASTER_GREMLIN)/^upg^/(MON_SANTA_GREMLIN);
      !!el&(param)=(MON_STONE_GOLEM);
        !!SN:T^es.788.creaTrainer^/?(spec)/^mon1^/(param)/^mon2^/(MON_IRON_GOLEM)/^upg^/(MON_GOLD_GOLEM);
      !!el&(param)=(MON_WALKING_DEAD);
        !!SN:T^es.788.creaTrainer^/?(spec)/^mon1^/(param)/^mon2^/(MON_ZOMBIE)/^upg^/(MON_MUMMY);
      ; Scholar of Death
      !!el&(param)=(MON_CAVALIER);
        !!SN:T^es.788.scholarOfDeath^/?(spec)/^mon1^/(param)/^mon2^/(MON_CHAMPION)/^upg^/(MON_DREAD_KNIGHT);
      !!el&(param)=(MON_UNICORN);
        !!SN:T^es.788.scholarOfDeath^/?(spec)/^mon1^/(param)/^mon2^/(MON_WAR_UNICORN)/^upg^/(MON_NIGHTMARE);
      !!el&(param)=(MON_GREEN_DRAGON);
        !!SN:T^es.788.scholarOfDeath^/?(spec)/^mon1^/(param)/^mon2^/(MON_GOLD_DRAGON)/^upg^/(MON_BLOOD_DRAGON);
      !!el&(param)=(MON_RED_DRAGON);
        !!SN:T^es.788.scholarOfDeath^/?(spec)/^mon1^/(param)/^mon2^/(MON_BLACK_DRAGON)/^upg^/(MON_BLOOD_DRAGON);
      !!en;
    !!en;
  !!en;
; Adventurers;
!!el&(heroType)=0;
  ; Skill Master
  !!if&(random1)=0;
    !!SN:T^es.788.skillMaster^/?(spec);
  ; Supplier
  !!el&(random1)=1;
    !!if&(param)<>(RES_GOLD);
      !!SN:T^es.788.supplier1^/?(spec);
    !!el;
      !!SN:T^es.788.supplier2^/?(spec);
    !!en;
  !!en;
; Mages
!!el&(heroType)=1;
  ; Skill Master
  !!if&(random1)=0;
    !!SN:T^es.788.skillMaster^/?(spec);
  !!el&(random1)=1;
    ; Aura of Curse
    !!if&(random2)=1/(param)>=7/(param)<=10;
      !!VR(type:y):S(param) -6;
      !!SN:T^es.788.auraOfCurse%(type)^/?(spec);
    ; Battlemage
    !!el;
      !!SN:T^es.788.battlemage^/?(spec);
    !!en;
  !!en;
!!en;

!!SN:H^spec^/(hero)/2/^%(spec)%T(es.788.reborn)^;

** Подержка специализаций на заклинаниях перерожденных героев
!?FU(OnMagicCorrectedResistance);       // при нанесении урона заклинанием
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BG:A?y1;
!!FU&y1<>1:E;                           // выход, если не каст героя

!!BG:H?y1;
!!SN:W^opt788.h%Y1r^/?y2;
!!FU&y2<1:E;                            // выход, если герой-кастер не перерожденный

!!HEy1:X?i/?j/d/d/d/d/d;
!!FU&i<>3:E;                            // выход, если герой не спец по заклинанию

!!MR:S?y2;
!!FU&y2<>j:E;                           // выход, если каст непрофильного заклинания

!!MR:F?y2;
!!HEy1:E?k/?l/1;                        // y2/l - наносимый урон/уровень героя
!!VRy3:Sy2 *l *5 :100 +y2;
!!MR:Fy3;                               // установка бонуса к урону в размере 5% за уровень героя

** Подержка аур-проклятий перерожденных героев
!?FU(OnBeforeBattleUniversal);
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!SN:W^opt788.a1^/0 W^opt788.a2^/0 W^opt788.a3^/0 W^opt788.a4^/0; // сброс флагов аур-проклятий

!?FU(OnSetupBattlefield);               // при настройке поля боя
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!BA:H0/?y1 H1/?y2;                     // y1/y2 - номера сражающихся героев
!!VRy11:C0/0/0/0;                       // y11..y14 - действующие ауры
!!HEy1:X?y3/?y4/d/d/d/d/d;              // y3/y4 - специализация атакующего героя
!!SN:W^opt788.h%Y1r^/?y5;               // y5 - флаг перерождения атакующего героя

!!if&y3=3/y5=1;
  !!VRy11&y4=45:S1;                // y11 - активация ауры Слабости
  !!VRy12&y4=47:S1;                // y11 - активация ауры Разрушения
  !!VRy13&y4=42:S1;                // y11 - активация ауры Проклятья
  !!VRy14&y4=54:S1;                // y11 - активация ауры Медлительности
!!en;

!!if&y2>=0;                             // если есть защищающийся герой
  !!HEy2:X?y3/?y4/d/d/d/d/d;            // y3/y4 - специализация защищающегося героя
  !!SN:W^opt788.h%Y2r^/?y5;             // y5 - флаг перерождения защищающегося героя

  !!if&y3=3/y5=1;
    !!VRy11&y4=45:S1;              // y11 - активация ауры Слабости
    !!VRy12&y4=47:S1;              // y11 - активация ауры Разрушения
    !!VRy13&y4=42:S1;              // y11 - активация ауры Проклятия
    !!VRy14&y4=54:S1;              // y11 - активация ауры Медлительности
  !!en;
!!en;                                   // 

!!FU&y11=0/y12=0/y13=0/y14=0:E;         // выход, если ни одна аура-проклятье не действует

!!BA:Q?f;                               // f-статус быстрой битвы

!!if&f=0;
  !!FU(Fun_SpellAnimationOnStacks)&y11=1:P-1/?y21; // y21 = слот для массовой анимации Слабости
  !!FU(Fun_SpellAnimationOnStacks)&y12=1:P-1/?y22; // y22 = слот для массовой анимации Разрушения
  !!FU(Fun_SpellAnimationOnStacks)&y13=1:P-1/?y23; // y23 = слот для массовой анимации Проклятия
  !!FU(Fun_SpellAnimationOnStacks)&y14=1:P-1/?y24; // y24 = слот для массовой анимации Медлительности
!!en;

!!re i/0/41;                            // для всех отрядов Защищающегося
  !!BMi:N?y3;
  !!co&y3<1;                            // пропуск пустых отрядов

  !!BMi:A?y3 D?y4 U1/?y5 U2/?y6 S?y7;   // y3/y4/y5/y6/y7 - атака/защита/мин.урон/макс.урон/скорость отряда
  !!VRy3&y11=1::2;                      // снижение атаки для Слабости
  !!VRy4&y12=1::2;                      // снижение защиты для Разрушения
  !!VRy6&y13=1:Sy5;                     // макс. урон снижается до мин.урона для Проклятия
  !!VRy7&y14=1/y7>1::2;                 // снижение скорости для Медлительности, если скорость больше 1

  !!if&f=0;
    !!FU(Fun_SpellAnimationOnStacks)&y11=1:Pi/y21; // добавление отряда в массовую анимацию Слабости
    !!FU(Fun_SpellAnimationOnStacks)&y12=1:Pi/y22; // добавление отряда в массовую анимацию Разрушения
    !!FU(Fun_SpellAnimationOnStacks)&y13=1:Pi/y23; // добавление отряда в массовую анимацию Проклятия
    !!FU(Fun_SpellAnimationOnStacks)&y14=1:Pi/y24; // добавление отряда в массовую анимацию Медлительности
  !!en;

  !!BMi:Ay3 Dy4 U2/y6 Sy7;              // обновляем параметры отряда
!!en;                                   // 

!!if&f=0;
  !!SN&y11=1:W^opt788.a1^/y21;        // выставление флага анимации Слабости
  !!SN&y12=1:W^opt788.a2^/y22;        // выставление флага анимации Разрушения
  !!SN&y13=1:W^opt788.a3^/y23;        // выставление флага анимации Проклятия
  !!SN&y14=1:W^opt788.a4^/y24;        // выставление флага анимации Медлительности
!!en;

!?FU(OnBattlefieldVisible);
!!UN:P788/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!SN:W^opt788.a1^/?y1;                  // y1 - аура Слабости

!!if&y1<>0;                             // если аура есть
  !!SN:T^es.788.battleLog^/?z1;
  !!MM:Sz1;                             // вывод текста в лог битвы
  !!SN:P^WEAKNESS^;                     // вывод звука слабости
  !!FU(Fun_SpellAnimationOnStacks):Py1/56/0/1; // проигрывание массовой амимации "Слабость" без анимации урона с очисткой памяти
!!en;                                   // 

!!SN:W^opt788.a2^/?y1;                  // y1 - аура Разрушения

!!if&y1<>0;                             // если аура есть
  !!SN:T^es.788.battleLog^/?z1;
  !!MM:Sz1;                             // вывод текста в лог битвы
  !!SN:P^DISRUPTR^;                     // вывод звука разрушения
  !!FU(Fun_SpellAnimationOnStacks):Py1/14/0/1; // проигрывание массовой амимации "Разрушение" без анимации урона с очисткой памяти
!!en;                                   // 

!!SN:W^opt788.a3^/?y1;                  // y1 - аура Проклятия

!!if&y1<>0;                             // если аура есть
  !!SN:T^es.788.battleLog^/?z1;
  !!MM:Sz1;                             // вывод текста в лог битвы
  !!SN:P^CURSE^;                        // вывод звука проклятия
  !!FU(Fun_SpellAnimationOnStacks):Py1/40/0/1; // проигрывание массовой амимации "Проклятие" без анимации урона с очисткой памяти
!!en;                                   // 

!!SN:W^opt788.a4^/?y1;                  // y1 - аура Медлительности

!!if&y1<>0;                             // если аура есть
  !!SN:T^es.788.battleLog^/?z1;
  !!MM:Sz1;                             // вывод текста в лог битвы
  !!SN:P^MUCKMIRE^;                     // вывод звука медлительности
  !!FU(Fun_SpellAnimationOnStacks):Py1/19/0/1; // проигрывание массовой амимации "Медлительность" без анимации урона с очисткой памяти
!!en;                                   // 

** end
