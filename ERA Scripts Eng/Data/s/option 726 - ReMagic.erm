ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : ReMagic
** Name rus.     : ReMagic
** Options       : -
** Dialogs       : -
** Variables     : -
** Tmp variables : z1-z3
** Timers        : -
** Functions     : FU79000-FU79006
** PO-values     : -

** Балансировка Знания/Силы героев под новые цены заклинаний
** Палач дает +8 к атаке против всех существ и позволяет добивать существо врага, у которого после атаки останется не более 20/30/40/50% здоровья.
** Специалист по Палачу повышает порог здоровья вражеского существа, которое можно добить дополнительно на 10%.
** Дополнительные участки Зыбучих песков
** Установка флага "заклинание с уроном" Огненной стене и Миному полю для отображения урона в описании
** Заклинания призыва:
** - дают посланников вместо элементалей.
** - имеют "стандартную" расчетную формулу (SP*a+b)
** - получают 50% бонус при наличии у героя сферы соответствующей стихии.
** - не имеют ограничение на призыв только одного типа существ в бою.
** Отключение заклинаний призыва у существ
** Городской Портал кроме маны расходует еще и очки передвижения (только для игрока-человека)
** Из подписей к заклинаниям в книге магии убраны уровни заклинания и школы (автор: feanor)
** Клоны при атаке игнорируют защиту противника
** Силовое поле имеет длительность A+SP/B
** Заклинания, колдуемых компьютерным игроком могут отражаться "Волшебным зеркалом"
** Волна Смерти наносит урон только "живым"" существам


** Балансировка Знания/Силы героев под новые цены заклинаний
!?FU(ES_726_Initialization);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!HE11:F0/0/2/3 I30;    [Аделаида (Кольцо льда) +1 Знания, -1 Атака]
!!HE14:F0/0/1/4 I40;    [Лоинс (Молитва) +2 Знания, -1 Атака, -1 Сила]
!!HE24:F0/0/2/3 I30;    [Корониус (Палач) +1 Сила +1 Знания, -2 Защита]
!!HE34:F0/1/2/3 I30;    [Жозефина (Ускорение) +1 Знания, -1 Атака]
!!HE37:F0/1/2/3 I30;    [Фафнер (Ускорение) +1 Знания, -1 Атака]
!!HE40:F0/0/1/4 I40;    [Астрал (Гипноз) +1 Знания, -1 Сила]
!!HE45:F0/0/1/4 I40;    [Солмир (Цепь молний) +1 Знания, -1 Сила]
!!HE57:F0/0/1/4 I40;    [Ксирон (Инферно) +3 Знания, -1 Атака, -1 Защита, -1 Сила]
!!HE59:F1/0/2/2 I20;    [Олема (Слабость) +1 Знания, -1 Защита]
!!HE60:F0/0/2/3 I30;    [Калид (Ускорение) +1 Знания, -1 Атака, -1 Защита]
!!HE62:F0/1/2/2 I20;    [Зидар (Каменная кожа) +1 Знания, -1 Атака]
!!HE63:F0/0/2/3 I30;    [Ксарфакс (Огненный шар) +2 Знания, -1 Атака, -1 Защита]
!!HE64:F1/0/2/3 I30;    [Стракер (Ускорение) +2 Знания, -2 Защита]
!!HE65:F1/1/2/2 I20;    [Вокиал (Каменная кожа) +1 Знания, -1 Защита]
!!HE66:F1/0/2/3 I30;    [Моандор (Медлительность) +2 Знания, -2 Защита]
!!HE72:F0/0/2/3 I30;    [Септиенна (Волна смерти) +1 Знания, -1 Атака]
!!HE73:F0/0/1/4 I40;    [Аислин (Шквал метеоритов) +2 Знания, -1 Атака, -1 Сила]
!!HE74:F0/0/2/3 I30;    [Сандро (Медлительность) +1 Знания, -1 Атака]
!!HE76:F0/0/1/4 I40;    [Тант (Оживление мертвецов) +2 Знания, -1 Атака, -1 Сила]
!!HE88:F0/0/1/4 I40;    [Аламар (Воскрешение) +2 Знания, -2 Сила]
!!HE91:F0/0/1/4 I40;    [Жеддит (Воскрешение) +2 Знания, -2 Сила]
!!HE92:F0/0/2/3 I30;    [Геон (Медлительность) +1 Знания, -1 Сила]
!!HE93:F0/0/1/4 I40;    [Деемер (Шквал Метеоритов) +2 Знания, -2 Сила]
!!HE106:F0/0/2/3 I30;   [Десса (Каменная кожа) +2 Знания, +1 Сила, -2 Атака, -1 Защита]
!!HE107:F1/0/1/3 I30;   [Терек (Ускорение) +2 Знания, -1 Атака, -1 Защита]
!!HE109:F1/0/1/3 I30;   [Гундула (Медлительность) +2 Знания, -1 Атака, -1 Защита]
!!HE122:F0/0/2/3 I30;   [Вой (Медлительность) +1 Знания, -1 Защита]
!!HE155:F0/0/2/3 I30;   [Лорд Хаарт (Медлительность) +2 Знания, -1 Защита]

!#FU(ES_726_Initialization):P;

** Подмена файла с параметрами заклинаний
; Note: SN:R can't be used here
!?FU(OnAfterLoadGame);
!!FU(ES_726_ReplaceSpellTable):P;

!?FU(ES_726_ReplaceSpellTable);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;   [выход, если опция не включена]

!!VRz1:S^opt726.txt^;         [z1 - полное название тектовика]
!!UN:C5890966/4/9597928;      [подмена ссылки на адрес переменной z1 "opt726.txt"]
!!SN:E5890960/1;              [загрузка "opt726.txt" в функции загрузки "sptraits.txt"]
!!UN:C5890966/4/6849372;      [возврат оригинальной ссылки на файл "sptraits.txt"]

!#FU(ES_726_ReplaceSpellTable):P;

** Палач дает +8 к атаке против всех существ.
!?FU(OnAfterBattleAction);                        [После любого действия]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!DO79000/0/41/1:P;           [Установка флага Король1 всем отрядам на поле]

!?FU79000;                    [Установка флага Король1 отряду]
!!BMx16:T?y1 F?y2;            [y1/y2 - тип/флаги отряда]
!!FU&y1<0:E;                  [выход, если отряда нет]

!!VRy2:|896 -768;             [добавление флага Король1, удаление Король2, Король3]
!!BMx16:Fy2;                  [обновление флагов отряда]

** Палач позволяет существам добивать существо врага, у которого после атаки останется не более 20/30/40/50% здоровья.
!?FU(OnBeforeBattleAction);                        [перед действием]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!SN:W^ReMagic.Slayer.Stack^/-1 W^ReMagic.Slayer.Effect^/-1 W^ReMagic.Slayer.Side^/-1; [сброс значений]
!!BG:A?y1 N?y2 Q?y6;          [y1/y2/y6 - тип действия/активный отряд/сторона]
!!FU|y1<6/y1>7:E;             [выход, если не атака/стрельба]

!!BMy2:G55/?y3/?y4;           [y3/y4 - продолжительность/сила заклинания Палач]
!!FU&y3<1:E;                  [выход, если заклинание не действует на отряд]

!!SS55:Ey4/?y5;               [y5 - эффект заклинания]
!!BA:Hy6/?y7;                 [y7 - номер героя атакующего существа]

!!if&y7>=0;                   [если герой есть]
  !!HEy7:X?y11/?y12/d/d/d/d/d;[y11=3, y12=55 для специалиста по палачу]
  !!VRy5&y11=3/y12=55:+10;    [специалист по Палачу повышает эффективность на 10]
!!en;                         [...]

!!SN:W^ReMagic.Slayer.Stack^/y2 W^ReMagic.Slayer.Effect^/y5 W^ReMagic.Slayer.Side^/y6; [сохранение значений]

!?FU(OnMonsterPhysicalDamage);[перед нанесением урона]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!MF:N?y1;                    [y1 - отряд, получающий урон]
!!BG:Q?y5;                    [y5 - сторона, наносящая урон]
!!SN:W^ReMagic.Slayer.Stack^/?y2 W^ReMagic.Slayer.Effect^/?y3 W^ReMagic.Slayer.Side^/?y4; [получение значений]
!!FU|y1=y2/y3<0/y5<>y4:E;     [выход, если урон получает зачарованный отряд, чар нет или вражеская ответка]

!!MF:F?y4;                    [y4 - получаемый урон]
!!BMy1:H?y5 L?y6;             [y5/y6 - макс./потерянное здоровье целевого отряда до получения урона]
!!VRy7:Sy4 :y5 +3 *y5 -y6 -y4 %y5; [y7 - % здоровье оставшееся после получения урона]
!!VRy8:Sy7 *100 :y5;          [y8 - % здоровья оставшегося после получения урона]

!!if&y8<=y3;                  [если эффективности "Палача" хватает для добивания существа]
  !!VRy4:+y7;                 [увеличение наносимого урона]
  !!MF:Fy4;                   [...]
  !!BMy2:N?y9 T?y10;          [y9/y10 - численность/тип атакующих существ]

  !!if&y9=1;
    !!UN:N3/2/y10/0;          [z2 - имя существа]
    !!SN:T^es.726.singSlayer^/?z1/^mon^/z2; [получаем текст для вывода в лог битвы из ert]
  !!el;
    !!UN:N3/2/y10/1;          [z2 - имя существ]
    !!SN:T^es.726.plurSlayer^/?z1/^mon^/z2;
  !!en;

  !!if&i^battle_isQuick^<>(TRUE);
    !!MM:Sz1;               [вывод текста в лог битвы]
    !!SN:P^SLAYER^;         [вывод звука Палача]
    !!BMy1:V28;             [анимация на целевом отряде]
  !!en;
!!en; 

** Установка флага "заклинание с уроном" Огненной стене и Миному полю для отображения урона в описании
!?FU(OnAfterErmInstructions); [перед загрузкой карты]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!SS11:Fd|512;                [y1/y2 - флаги минного поля и огненной стены]
!!SS13:Fd|512;                [добавляем флаг "заклинание с уроном"]

** установка нового описания специалисту "Палача"
!?FU(OnAfterErmInstructions);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Set the description of Slayer specialist
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);

  !!if&(spec)=3/(spec2)=(SPELL_SLAYER);
    !!SN:H^spec^/i/2/^%T(es.726.slayerSpec)^;
  !!en;
!!en;

** Дополнительные участки Зыбучих песков
!?FU(OnBeforeBattleAction);   [Зыбучие пески 8/10/12/14 участков]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1;                    [y1 - тип действия]
!!FU&y1<>1:E;                 [выход, если не колдовство героя]

!!BG:S?y1 Q?y2;               [y1 - номер колдующегося заклинания, y2 - текущая сторона]
!!FU&y1<>10:E;                [выход, если не каст Зыбучих песков]

!!SS10:C0/?y3 C2/?y6 C0/0 C2/0;[y3/y6 - стоимость базовых/продвинутых Зыбучих песков, обнуление стоимостей]
!!BA:Hy2/?y4;                 [y4 - номер колдующего героя]
!!HEy4:S17/?y5;               [y5 - уровень магии Земли]
!!HEy4&y5>0:S17/2;            [уровень магии Земли продвинутый, если школа есть(+6 участков)]
!!BHy2:C10/0/0/0;             [доп. каст Зыбучих песков]
!!HEy4:S17/y5;                [восстанавливаем уровень магии Земли]
!!SS10:C0/y3 C2/y6;           [восстанавливаем стоимости базовых/продвинутых Зыбучих песков]
!!HEy4:Id1/1;                 [возвращаем 1 маны, которая все равно тратится при нулевой стоимости заклианния]

** Заклинания призыва:
** - дают посланников вместо элементалей.
** - имеют "стандартную" расчетную формулу (SP*a+b)
** - получают 50% бонус при наличии у героя сферы соответствующей стихии.
** - не имеют ограничение на призыв только одного типа существ в бою.
!?FU(OnStartOrLoad);                  [после загрузки]
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

!!UN:C5896327/(UNC_INT8)/?i^es_726_patch1^;
!!UN:C5936848/(UNC_INT8)/?i^es_726_patch2^;

!!UN:C5896327/(UNC_INT8)/235;                  [Отключение проверки типа призываемых элементалей]
!!UN:C5936848/(UNC_INT8)/235;                  [большое спасибо Sav'у за пример...]

!?FU(OnGameLeave);
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

!!UN:C5896327/(UNC_INT8)/i^es_726_patch1^;
!!UN:C5936848/(UNC_INT8)/i^es_726_patch2^;

!?FU(OnBeforeBattleAction);                                [действие в бою]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!BG:A?y1;                            [y1 - тип действия]
!!FU&y1<>1:E;                         [выход, если не колдовство героя]

!!BG:S?y1 Q?y2;                       [y1 - номер колдующегося заклинания, y2 - текущая сторона]
!!FU|y1<(SPELL_FIRE_ELEMENTAL)/y1>(SPELL_AIR_ELEMENTAL):E; [выход, если не призыв элементалей]

!!VRy4&y1=(SPELL_FIRE_ELEMENTAL):S(MON_FIRE_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_EARTH_ELEMENTAL):S(MON_EARTH_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_WATER_ELEMENTAL):S(MON_WATER_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_AIR_ELEMENTAL):S(MON_AIR_MESSENGER); [y4 - номер призываемого существа]
!!BA:Hy2/?y5;                         [y5 - колдующий герой]
!!FU79006:Py1/0/y5/?y7;               [y7 - уровень Школы для заклианния]
!!SSy1:Cy7/?y9 Ey7/?y10 P?y11;        [y9/y10/y11 цена/бонус/эффект заклинания]
!!VRy9:*-1;                           [...]
!!HEy5:Fd/d/?y12/d;                   [y12 - Сила героя, уменьшаем ману]
!!VRy13:Sy12 *y11 +y10;               [y13 - HP призванных существ (SP*P+E)]
!!HEy5:A2/(ART_ORB_OF_THE_FIRMAMENT)/?i/?y14 A2/(ART_ORB_OF_DRIVING_RAIN)/?i/?y15 A2/(ART_ORB_OF_TEMPESTUOUS_FIRE)/?i/?y16 A2/(ART_ORB_OF_SILT)/?i/?y17; [y14-y17 Сферы стихий у героя]
!!VRy13&y1=(SPELL_FIRE_ELEMENTAL)/y16>0:*3 :2; [+50% к HP призываемых посланников, если на герое надета Сфера соотв. стихии]
!!VRy13&y1=(SPELL_EARTH_ELEMENTAL)/y17>0:*3 :2; [...]
!!VRy13&y1=(SPELL_WATER_ELEMENTAL)/y15>0:*3 :2; [...]
!!VRy13&y1=(SPELL_AIR_ELEMENTAL)/y14>0:*3 :2; [...]
!!MA:Py4/?y20;                        [y20 - базовое здоровье призываемого существа]
!!VRy21:Sy13 :y20 +1;                 [y21 - количество призываемых существ]

; Fix quantity/lost HP in particular cases
!!VRy22:Sy13 %y20 -y20 *-1;           [y22 - "потерянное здоровье" призываемых существ]

!!if&y20=y22;
  !!VRy21:-1;                         
  !!VRy22:S0;
!!el&y22=0;
  !!VRy21:-1;                         [уменьшение числа призванных, если нет "раненого" существа]
!!en;

!!FU79001:Py4/y2/y21/?y18/y22;        [призываем посланников]

!!if&y18=0;                           [если призыв удачен]
  !!BHy2:M1;                          [запрещаем колдовство в этом раунде]
  !!HEy5:Idy9/1 B0/?z1;               [уменьшаем ману, z1 - имя героя]

  !!if&i^battle_isQuick^<>(TRUE);
    !!UN:N1/2/y1;                     [z2 - Название заклинания]
    !!SN:T^es.726.summon1^/?z3/^hero^/z1/^spell^/z2;       [z3 сообщение для вывода в лог боя]         
    !!MM:Sz3;                         [вывод сообщения в лог боя]
  !!en;
!!en;

!!FU(Fun_RedrawShadowAfterAction):P;
!!BG:A0;                              [отменяем штатный призыв с перерисовкой тени]

!?FU79001;                            [добавление отряда x1 на поле боя, сторона x2 (0/1), количество x3, потер. здоровье x5]
!!VRy1:Sx2 *21;                       [y1 - первый отряд для поиска (0/21)]
!!VRy2:Sy1 +20;                       [y2 - последний отряд для поиска (20/41)]
!!VRy3:S-1;                           [y3 - результат поиска]
!!DO79002/y1/y2/1:P?y3;               [y3>=0, если есть свободный отряд]

!!if&y3=-1;                           [если нет свободного отряда]
  !!if&i^battle_isQuick^<>(TRUE);
    !!SN:T^es.726.summon2^/?z3;       [z3 сообщение для вывода в лог боя]
    !!MM:Sz3;                         [вывод сообщения в лог боя]
  !!en;

  !!VRx4:S1;                          [возврат кода неудачного призыва]
  !!FU:E;                             [выход]
!!en;

!!VRv1:S0;                            [v1 - результат поиска]
!!DO79003/0/10/1:P0/x2;               [v1 - количество свободных позиций]

!!if&v1>0;                            [если есть свободная позиция]
  !!VRv1:-1;                          [y4 - порядковый номер свободной позиции для призыва]
  !!VRy4:S1 Rv1;                      [...]
  !!VRv1:S0;                          [v1 - результат поиска]
  !!DO79003/0/10/1:Py4/x2;            [v1 - позиция для призыва]
  !!BU:Sx1/x3/v1/x2/-1/1 Ev1/?y4;     [призыв нового отряда и получение его номера в y4]
  !!BMy4:Lx5;                         [установка "потерянного здоровья"]

  !!SN&i^battle_isQuick^<>(TRUE):P^SUMNELM^;                      [вывод звука, если не быстрая битва]
  !!VRx4:S0;                          [возврат кода удачного призыва]
!!el;
  !!if&i^battle_isQuick^<>(TRUE);
    !!SN:T^es.726.summon3^/?z3;       [z3 сообщение для вывода в лог боя]
    !!MM:Sz3;                         [вывод сообщения в лог боя]
  !!en;

  !!VRx4:S2;                          [возврат кода неудачного призыва]
  !!FU:E;                             [выход]
!!en;

!?FU79002;                            [возвращает в x1 номер первого свободного отряда]
!!BMx16:T?y1 N?y2;                    [y1 - тип отряда, y2 - кол-во существ в отряде]
!!FU|y1<>-1/y2<>0:E;                  [выход, если отряд занят]

!!VRx1:Sx16;                          [если отряда свободен, возвращаем его номер]
!!VRx16:S100500;                      [прерываем цикл]

!?FU79003;                            [поиск количества (x1=0) или возврат в x3 x1-й свободной позиции для стороны x2. x16 - ряд.]
!!VRi:Sx2 *13;                        [i - сдвиг позиции для защищающегося игрока]
!!VRy1:Sx16 *17 +1 +i;                [y1 - проверяемая позиция (1й ряд)]
!!BU:Dy1/?y2 Oy1/?y3 Ey1/?y4;         [y2=-1, y3=0, y4=-1 если на позиции нет отрядов и препятствий]
!!VRv1&y2=-1/y3=0/y4=-1:+1;           [v1 - счетчик свободных позиций]

!!if&x1>0/x1=v1;                      [если найдена позиция для призыва]
  !!VRv1:Sy1;                         [возвращаем ее]
  !!VRx16:S100500;                    [и прерываем цикл]
  !!FU:E;                             [выход]
!!en; 

!!VRy1:+1;                            [y1 - проверяемая позиция (2й ряд)]
!!BU:Dy1/?y2 Oy1/?y3 Ey1/?y4;         [y2=-1, y3=0, y4=-1 если на позиции нет отрядов и препятствий]
!!VRv1&y2=-1/y3=0/y4=-1:+1;           [v1 - счетчик свободных позиций]

!!if&x1>0/x1=v1;                      [если найдена позиция для призыва]
  !!VRv1:Sy1;                         [возвращаем ее]
  !!VRx16:S100500;                    [и прерываем цикл]
  !!FU:E;                             [выход]
!!en; 

// Adapt the Summon Elementals Option
// The following lines must be executed later than the functions with the same name in 4 wog - summon elementals.erm
!?FU(OnAfterErmInstructions)&i^wog_74_enabled^;
!!UN:P726/?(eraOption:y); 
!!FU&(eraOption)<>(TRUE):E;

!!VRi^wog_74_mon_%(SPELL_AIR_ELEMENTAL)^:S(MON_AIR_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_EARTH_ELEMENTAL)^:S(MON_EARTH_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_WATER_ELEMENTAL)^:S(MON_WATER_MESSENGER);
!!VRi^wog_74_mon_%(SPELL_FIRE_ELEMENTAL)^:S(MON_FIRE_MESSENGER);

!?FU(WOG_74_GetSummonQty);
!#VA(hero:x) (spell:x) (level:x) (qty:x);

!!UN:P726/?(eraOption:y); 
!!FU&(eraOption)<>(TRUE):E;

!!SS(spell):E(level)/?(effect:y) P?(spellPwr:y);
!!HE(hero):Fd/d/?(pwr:y)/d;
!!VR(totalHp:y):S(pwr) *(spellPwr) +(effect);

; Calculate bonus from Artifacts
!!HE(hero):A2/(ART_ORB_OF_THE_FIRMAMENT)/?i/?(hasAirOrb:y) A2/(ART_ORB_OF_DRIVING_RAIN)/?i/?(hasWaterOrb:y) A2/(ART_ORB_OF_TEMPESTUOUS_FIRE)/?i/?(hasFireOrb:y) A2/(ART_ORB_OF_SILT)/?i/?(hasEarthOrb:y);
!!VR(totalHp)&(spell)=(SPELL_FIRE_ELEMENTAL)/(hasAirOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_EARTH_ELEMENTAL)/(hasEarthOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_WATER_ELEMENTAL)/(hasWaterOrb)>0:*3 :2;
!!VR(totalHp)&(spell)=(SPELL_AIR_ELEMENTAL)/(hasAirOrb)>0:*3 :2;

!!MA:Pi^wog_74_mon_%(spell)^/?(hp:y);
!!VR(qty):S(totalHp) :(hp) +1;

; Fix quantity in particular cases
!!VR(lostHp:y):S(totalHp) %(hp) -(hp) *-1;
!!VR(qty)|(hp)=(lostHp)/(lostHp)=0:-1;

** Отключение заклинаний призыва у существ
!?FU(OnAfterErmInstructions);         [опыт существ: удаление заклинаний призыва у существ]
!!UN:P726/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;           [выход, если опция не включена]

!!DO79004/0/196/1:P;                  [перебор существ]

!?FU79004;
!!DO79005/0/19/1:Px16;                [перебор линий опыта]

!?FU79005;
!!EAx1:Bx16/?y2/?y3/?y4/d/d/d/d/d/d/d/d/d/d/d; [получение параметров линии опыта]

!!if|y3=74/y3=97/y3=99/y3=106/y3=112/y3=115;   [если линия связана с сотворением заклинания призыва]
  !!EAx1&y4>65/y4<70:Bx16/0/d/d/0/0/0/0/0/0/0/0/0/0/0; [отключение линии опыта]
!!en; 

** Призыв посланников командиром
!?FU(OnBeforeBattleAction);                                [действие в бою]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!BG:A?y1;                            [y1 - тип действия]
!!FU&y1<>10:E;                        [выход, если не колдовство существа]

!!BG:Q?y2 N?y3;                       [y2 - текущая сторона, y3 - активный отряд]
!!BMy3:T?y4 U4/?y1 E?y30;             [y4 - тип существ активного отряда, y1 - номер колдующегося заклинания, y30 - кол-во заклинаний]
!!FU|y1<(SPELL_FIRE_ELEMENTAL)/y1>(SPELL_AIR_ELEMENTAL)/y4<(MON_COMMANDER_FIRST_A)/y4>(MON_COMMANDER_LAST_D)/y30<1:E;[выход, если не призыв элементалей командиром]

!!VRy4&y1=(SPELL_FIRE_ELEMENTAL):S(MON_FIRE_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_EARTH_ELEMENTAL):S(MON_EARTH_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_WATER_ELEMENTAL):S(MON_WATER_MESSENGER); [y4 - номер призываемого существа]
!!VRy4&y1=(SPELL_AIR_ELEMENTAL):S(MON_AIR_MESSENGER); [y4 - номер призываемого существа]
!!BA:Hy2/?y5;                         [y5 - герой]
!!COy5:S4/?y6;                        [y6 - сила магии командира]
!!VRy7:S2;                            [y7 - множитель призыва элементалей]
!!BA:P?y11/?y12/?y13;                 [y11-y13 - координаты битвы]
!!TRy11/y12/y13:G?y8;                 [y8 - тип бонусной земли]
!!VRy7&y8=46:S(SKILL_EXPERT);         [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=(SPELL_AIR_ELEMENTAL)/y8=229:S(SKILL_EXPERT);  [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=(SPELL_FIRE_ELEMENTAL)/y8=226:S(SKILL_EXPERT); [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=(SPELL_WATER_ELEMENTAL)/y8=228:S(SKILL_EXPERT); [множитель призыва на соотв. бонусной земле]
!!VRy7&y1=(SPELL_EARTH_ELEMENTAL)/y8=231:S(SKILL_EXPERT); [множитель призыва на соотв. бонусной земле]
!!HEy5:A2/(ART_ORB_OF_THE_FIRMAMENT)/?i/?y14 A2/(ART_ORB_OF_SILT)/?i/?y15 A2/(ART_ORB_OF_TEMPESTUOUS_FIRE)/?i/?y16 A2/(ART_ORB_OF_DRIVING_RAIN)/?i/?y17; [y14-y17 Сферы стихий у героя]
!!VRy9:Sy6 +1 *y7;                    [y9 - количество призызываемых элементалей]
!!VRy9&y1=(SPELL_FIRE_ELEMENTAL)/y16>0:*3 :2; [+50% к количеству призываемых, если на герое надета Сфера соотв. стихии]
!!VRy9&y1=(SPELL_EARTH_ELEMENTAL)/y17>0:*3 :2; [...]
!!VRy9&y1=(SPELL_WATER_ELEMENTAL)/y15>0:*3 :2; [...]
!!VRy9&y1=(SPELL_AIR_ELEMENTAL)/y14>0:*3 :2; [...]
!!FU79001:Py4/y2/y9/?y18/0;           [призываем посланников]

!!if&y18=0;                           [если призыв удачен]
  !!BG:A12;                           [тратим ход командира]
  !!VRy30:-1;                         [уменьшаем кол-во заклинаний командира]
  !!BMy3:Ey30;                        [обновляем кол-во заклинаний командира]
!!en;

** Городской Портал кроме маны расходует еще и очки передвижения (только для игрока-человека)
!?FU(OnBeforeAdventureMagic);
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!OW:A-1/?h;                          [h - номер выбранного героя]
!!HEh:P?y1/?y2/?y3 W?y4;              [сохраняем координаты и ОД героя при открытии книги заклинаний]
!!FU79006:P9/1/h/?y10;                [y10 - уровень Школы для заклианния]
!!SN:W^ReMagicTPx^/y1 W^ReMagicTPy^/y2 W^ReMagicTPz^/y3 W^ReMagicTPw^/y4 W^ReMagicTPs^/y10; [...]

!?FU(OnAfterAdventureMagic);
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]
!!FU&v997<>9:E;                       [выход, если кастован не городской портал]

!!OW:A-1/?h;                          [h - номер выбранного героя]
!!HEh:P?y1/?y2/?y3 G?y11;             [проверяем, изменились ли координаты героя]
!!SN:W^ReMagicTPx^/?y4 W^ReMagicTPy^/?y5 W^ReMagicTPz^/?y6 W^ReMagicTPw^/?y12 W^ReMagicTPs^/?y10; [...]
!!FU&y1=y4/y2=y5/y3=y6:E;             [выход, если герой не телепортировался]

!!SS9:Ey10/?y13;                      [y13 - штраф передвижения (%), y11 - начальные ОД, y12 -  текущие ОД]
!!VRy11:*y13 :100;                    [y11 - штраф ОД]
!!VRy12:-y11;                         [уменьшаем текущие ОД героя]
!!VRy12&y12<0:S0;                     [...]
!!HEh:Wy12;                           [...]

** Клоны при атаке игнорируют защиту противника
!?FU(OnBeforeBattleUniversal);        [перед боем]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!UN:P900/?y1 P900/1;                 [сохраняем состояние опыта отрядов, включаем опыт]
!!SN:W^ReMagicStackExp^/y1;           [...]

!?FU(OnAfterBattleUniversal);         [после боя]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!SN:W^ReMagicStackExp^/?y1;          [восстанавливаем состояние опыта отрядов]
!!UN:P900/y1;                         [...]

!?FU(OnBattleStackObtainsTurn);       [при получении хода отрядом]
!!UN:P726/?y1;
!!FU&y1=0:E;                          [выход, если опция не включена]

!!SN:X?y1/?y2;                        [y1/y2 - сторона и номер отряда]
!!VRy3:Sy1 *20 +y2;                   [y3 - номер отряда на поле боя]
!!BMy3:F?y4;                          [y4 - флаги атакующего отряда]
!!VRy4:&8388608;                      [y4>0 для атакующего "клона"]
!!FU&y4=0:E;                          [выход, если атакует не клон]

!!VRy5:Sy3 *-1 -1;                    [y5 - номер отряда для EA]
!!EAy5:B0/1/98/61/100/100/100/100/100/100/100/100/100/100/100; [установка атакующему отряду способности игнорировать защиту цели]

!?FU79006;                            [возврат в x4 действующего уровня Школы заклинания x1 в бою (x2=0) или на карте (x2=1) для героя x3]
!!VRy10:S0;                           [обнуляем искомое значение]
!!HEx3&x2=1:P?y1/?y2/?y3;             [y1/y2/y3 - координаты места колдовства]
!!BA&x2=0:P?y1/?y2/?y3;               [...]
!!TRy1/y2/y3:G?y4;                    [y4 - тип бонусной земли]
!!SSx1:S?y5;                          [y5 - биты школ заклинания]
!!VRy6:Sy5 &1;                        [y6>0 - заклинание Воздуха]
!!VRy7:Sy5 &4;                        [y7>0 - заклинание Воды]
!!VRy8:Sy5 &2;                        [y8>0 - заклинание Огня]
!!VRy9:Sy5 &8;                        [y9>0 - заклинание Земли]
!!HEx3:S15/?y11 S16/?y12 S14/?y13 S17/?y14; [y11-y14 Уровни навыков Школ героя]
!!VRy10&y6>0/y10<y11:Sy11;            [y10 - макс. уровень по Школам героя и бонусам террирорий]
!!VRy10&y7>0/y10<y12:Sy12;            [...]
!!VRy10&y8>0/y10<y13:Sy13;            [...]
!!VRy10&y9>0/y10<y14:Sy14;            [...]
!!VRy10&y4=46:S3;                     [маг. земля]
!!VRy10&y6>0/y4=229:S3;               [маг. облака]
!!VRy10&y7>0/y4=228:S3;               [прозрачные пруды]
!!VRy10&y8>0/y4=226:S3;               [огненные поля]
!!VRy10&y9>0/y4=231:S3;               [скалистая земля]
!!VRx4:Sy10;                          [Возвращаем макс. значение Школы]

*** Силовое поле имеет длительность A+SP/B
!?FU(OnBeforeBattleAction);                        [Перед действием в бою]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1 S?y2 D?y3 Q?y4;     [y1/y2/y3/y4 - действие/заклинание/место/сторона]
!!VRy5:Sy4 +1 *-10;           [y5 - герой]
!!FU|y1<>1/y2<>12:E;          [выход, если не колдовство "Силового поля"]

!!FU79006:P12/0/y5/?y6;       [y6 - уровень Школы]
!!HEy5:Fd/d/?y7/d;            [y7 - Сила героя]
!!SS12:P?y8 Ey6/?y9;          [y8/y9 - Сила/Эффект заклинания]
!!VRy10:Sy7 :y9 +y8;          [y10 - длительность Силового поля]
!!UN:C5901022/4/y10;          [устанавливаем длительность (спс. gamecreator'у)]

*** Заклинания, колдуемых компьютерным игроком могут отражаться "Волшебным зеркалом"
!?FU(OnBeforeBattleAction);                        [триггер: действия в битве]
!!UN:P726/?y1;
!!FU&y1=0:E;                  [выход, если опция не включена]

!!BG:A?y1 S?y2 H?y3 E?y4 Q?y5;[y1-y5 - параметры действия]
!!FU&y1<>1|y4<0:E;            [выход, если не каст героем или не целенаправленное заклинание]

!!HEy3:O?y31;                 [y31 - получить цвет кастующего героя]
!!OW:Iy31/?y32;               [y32 - ИИ или человек]
!!BMy4:G36/?y41/?y42;         [y41/y42 - продолжительность/сила волшебного зеркала]
!!FU|y32<>1/y41<1/y42<0:E;    [выход, если не ИИ, или нет зеркала на цель-стеке]

!!VRy7:S1 R99;                [y7 - случайное число 1..100]
!!SS36:Ey42/?y43;             [y43 - шанс отражения заклинания]
!!FU&y7>y43:E;                [выход, если заклинание не отражено]

!!VRy1:Sy5 *21 R20;           [генерация случайного стека своей стороны (SN:G сюда)]
!!BMy1:N?y2 P?y3 T?y6;        [узнать кол-во существ в этом стеке и его позицию, и тип монстра]
!!SN&y2<1|y6=149:G9;          [если монстров нет или убиты, или это башня, вызваем генерацию заново]
!!UN:C6919200/4/?y1;          [y1 - комбат мэнеджер]
!!VRy1:+72;                   [переход к номеру позиции цели при отражении зеркалом]
!!UN:Cy1/4/y3;                [установить позицию цели]

** Волна Смерти наносит урон только "живым"" существам
!?FU(OnDwarfMagicResistance);           // проверка сопротивлений
!!UN:P726/?y1;
!!FU&y1=0:E;                            // выход, если опция не включена

!!MR:S?y1;
!!FU&y1<>(SPELL_DEATH_RIPPLE):E;                          // выход, если не Волна смерти

!!MR:N?y1;
!!BMy1:F?y2;
!!VRy2:&16;                             // y2=0, если у отряда нет флага "живой"
!!MR&y2=0:F100;                         // иммунитет к Волне смерти для существ без флага "живой"

** end
