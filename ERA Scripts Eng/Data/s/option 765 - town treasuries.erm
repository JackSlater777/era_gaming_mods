ZVSE
ERMS_ScriptDate=2.12(December).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Timothy Pulver (Idea by RedMage)
** Name          : Town Treasuries
** Name rus.     : Городские сокровищницы
** Changes rus.  : [Algor] вынос опции в отдельные erm- и ert-файлы для мода ERA
**               : [Algor] скрипт переписан, изменены номера переменных и функций, оптимизирована скорость выполнения
**               : [Algor] добавлено срабатывание опции когда герой-гость в воротах города, без героя в гарнизоне
**               : [Algor] убрано ошибочное срабатывание опции когда побежден герой-гость при живом гарнизонном герое
**               : [Algor] количество ресурсов, захватываемых игроком-человеком привязано к уровню сложности, соответствующие коэффициенты вынесены в ini-файл
** Options       : 765
** Dialogs       : -
** Variables     : v7927
** Tmp variables : z1-z3
** Timers        : -
** Functions     : FU7954-FU7955
** PO-values     : -

!?PI;                         [пост-инструкция: определение коэффициента захватываемых ресурсов]
!!UN:P765/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!UN:J2/?y1;                  [y1 - выбранный уровень сложности (0..4)]
!!VRy1:+1;                    [y1 - номер параметра в ini-файле]
!!VRz1:S^Data\s\option 765 - town treasuries.ini^; [z1 - имя ini-файла]
!!VRz2:S^option 765^;         [z2 - секция ini-файла]
!!UN:N6/3/y1/2/1;             [z3 - текстровые значение коэффициента захватываемых ресурсов]
!!VRv7927:Vz3;                [v7927 - коэффициент захватываемых ресурсов]

!?OB98;                       [при захвате города без охраны передаем ресурсы]
!!UN:P765/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!CA998:O?y1;                 [y1 - текущий владелец города]
!!FU&y1<0:E;                  [выход, если город нейтральный]
!!HE-1:O?y2;                  [y2 - игрок-посетитель]
!!OW:Ty1/?y3 Ty2/?y4;         [y3/y4 - команды владельца/посетителя]
!!FU&y3=y4:E;                 [выход, если город свой/союзный]
!!CA998:H0/?y3 H1/?y4 M2/0/?y5/d M2/1/?y6/d M2/2/?y7/d M2/3/?y8/d M2/4/?y9/d M2/5/?y10/d M2/6/?y11/d; [y3/y4 - номера героев в гарнизоне/воротах города, y5-y11 - существа-защитники]
!!FU7954&y3=-1/y4=-1/y5=-1/y6=-1/y7=-1/y8=-1/y9=-1/y10=-1/y11=-1:Py1/y2/v998/v999/v1000; [если защиты у города нет, передаем ресурсы]

!?BA53;                       [в конце осадной битвы передаем ресурсы, если владелец города сменился]
!!UN:P765/?y1;                [проверяем включена ли опция в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!BA:H0/?y1;                  [y1 - нападавший герой]
!!HEy1:O?y2;                  [y2 - владелец нападавшего героя]
!!BA:P?y11/?y12/?y13;         [y11-y13 - координаты битвы]
!!TRy11/y12/y13:E?y4;         [y4=0, если в координатах "желтый квадрат"]
!!FU&y4=1:E;                  [выход, если нет "желтого квадрата"]
!!OBy11/y12/y13:T?y3;         [y3 - тип объекта в координатах битвы]
!!FU&y3<>98:E;                [выход, если битва не в городе]
!!CAy11/y12/y13:O?y1;         [y1 - текущий владелец города]
!!CAy11/y12/y13:H0/?y4;       [y4 - гарнизонный герой]
!!FU7954&y2>=0/y1>=0/y4=-1:Py1/y2/y11/y12/y13;[если нападавший герой жив, город не был нейтральным и пал, передаем ресурсы]

!?FU7954;                     [передача ресурсов игрока x1 игроку x2 при захвате города в координатах x3/x4/x5]
!!CAx3/x4/x5:B3/10;           [f1 - наличие Сельской управы]
!!VRy1&1:S1;                  [y1=1 часть при наличии Сельской управы]
!!CAx3/x4/x5:B3/11;           [f1 - наличие Префектуры]
!!VRy1&1:S2;                  [y1=2 части при наличии Префектуры]
!!CAx3/x4/x5:B3/12;           [f1 - наличие Муниципалитета]
!!VRy1&1:S3;                  [y1=3 части при наличии Муниципалитета]
!!CAx3/x4/x5:B3/13;           [f1 - наличие Капитолия]
!!VRy1&1:S4;                  [y1=4 части при наличии Капитолия]
!!OW:Wx1/?y2;                 [y2 - количество городов у прежнего владельца]
!!VRy2:-1;                    [y2 - номер последнего города у прежнего владельца]
!!VRv1:S0;                    [инициализация v1]
!!DO7955/0/y2/1:Px1;          [подсчет в v1 количества "частей" в городах игрока x1]
!!VRy1:*100 :v1;              [y1 - процент от ресурсов, который получает новый владелец]
!!OW:Ix2/?y4;                 [y4=0/1 для человека/ИИ]
!!VRy1&y4=0:*v7927 :100;      [y1 - процент от ресурсов, который получает новый владелец, с учетом коэффициента для игрока-человека]
!!OW:Rx1/0/?y10 Rx1/1/?y11 Rx1/2/?y12 Rx1/3/?y13 Rx1/4/?y14 Rx1/5/?y15 Rx1/6/?y16 Rx1/7/?y17; [y10-y17 - ресурсы побежденного игрока]
!!VRy20:Sy10 *y1 :100;        [y20-y27 - доля ресурсов, достающаяся победителю]
!!VRy21:Sy11 *y1 :100;        [...]
!!VRy22:Sy12 *y1 :100;        [...]
!!VRy23:Sy13 *y1 :100;        [...]
!!VRy24:Sy14 *y1 :100;        [...]
!!VRy25:Sy15 *y1 :100;        [...]
!!VRy26:Sy16 *y1 :100;        [...]
!!VRy27:Sy17 *y1 :100;        [...]
!!VRy30:Sy20 *-1;             [y30-y37 - доля ресурсов, забираемая у проигравшего]
!!VRy31:Sy21 *-1;             [...]
!!VRy32:Sy22 *-1;             [...]
!!VRy33:Sy23 *-1;             [...]
!!VRy34:Sy24 *-1;             [...]
!!VRy35:Sy25 *-1;             [...]
!!VRy36:Sy26 *-1;             [...]
!!VRy37:Sy27 *-1;             [...]
!!if&y4=0:;                   [если победитель - игрок-человек]
  !!CAx3/x4/x5:N?z1;          [z1 - название захваченного города]
  !!IF:N0/y20/1/y21/2/y22/3/y23/4/y24/5/y25/6/y26/7/y27; [установка количества захваченных ресурсов для диалога]
  !!VRz1:Sz179115;            [z1 - текст сообщения из ert-файла}
  !!IF:N1/1;                  [отображаем диалог]
!!en:;
!!OW:Rx1/0/dy30 Rx1/1/dy31 Rx1/2/dy32 Rx1/3/dy33 Rx1/4/dy34 Rx1/5/dy35 Rx1/6/dy36 Rx1/7/dy37; [обновляем ресурсы побежденного игрока]
!!OW:Rx2/0/dy20 Rx2/1/dy21 Rx2/2/dy22 Rx2/3/dy23 Rx2/4/dy24 Rx2/5/dy25 Rx2/6/dy26 Rx2/7/dy27; [обновляем ресурсы победедителя]

!?FU7955;                     [подсчет в v1 количества "частей" в городах игрока x1]
!!OW:Wx1/x16/?y2;             [y2 - номер города на карте]
!!CA0/y2:B3/10;               [f1 - наличие Сельской управы]
!!VRy1&1:S1;                  [y1=1 часть при наличии Сельской управы]
!!CA0/y2:B3/11;               [f1 - наличие Префектуры]
!!VRy1&1:S2;                  [y1=2 части при наличии Префектуры]
!!CA0/y2:B3/12;               [f1 - наличие Муниципалитета]
!!VRy1&1:S3;                  [y1=3 части при наличии Муниципалитета]
!!CA0/y2:B3/13;               [f1 - наличие Капитолия]
!!VRy1&1:S4;                  [y1=4 части при наличии Капитолия]
!!VRv1:+y1;                   [увеличиваем v1 на количество "частей" в городе]

** end
