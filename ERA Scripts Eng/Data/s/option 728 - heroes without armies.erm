ZVSE2
** Author orig.  : igrik, Algor
** Update        : daemon_n and Archer30
** Name          : Heroes without armies
** Name rus.     : Герои без армий
** Options       : 728

** Включение опции разрешает передавать/увольнять все войска и оставлять героев без армии
** Герои без армий в начале дня получают 25% штраф передвижения (аналог штрафу за наличие в армии существ со скоростью 3)


!?FU(OnAfterErmInstructions);           [при старте игры]
; Check if one of the addresses has been modified, exit if positive
!!UN:C5102718/(UNC_INT)/?(dismissLast:y);[Dismiss last stack]
!!UN:C5968130/(UNC_INT)/?(tranferLast:y);[Transfer last stack]
!!FU|(dismissLast)<>2114058371/(tranferLast)<>14844943:E;

!!UN:P728/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

; Store the current values
!!UN:C5102718/(UNC_INT)/?i^es_728_patch1^ C5102722/(UNC_INT8)/?i^es_728_patch2^ C5961680/(UNC_INT16)/?i^es_728_patch3^ C5961682/(UNC_INT8)/?i^es_728_patch4^;
!!UN:C6115569/(UNC_INT)/?i^es_728_patch5^ C6115573/(UNC_INT8)/?i^es_728_patch6^ C6115641/(UNC_INT)/?i^es_728_patch7^ C6115645/(UNC_INT8)/?i^es_728_patch8^;

!!UN:C5968130/(UNC_INT)/?i^es_728_patch9^ C5968134/(UNC_INT16)/?i^es_728_patch10^ C5968202/(UNC_INT8)/?i^es_728_patch11^ C6060664/(UNC_INT8)/?i^es_728_patch12^;
!!UN:C6117225/(UNC_INT8)/?i^es_728_patch13^ C7703532/(UNC_INT16)/?i^es_728_patch14^ C7703637/(UNC_INT16)/?i^es_728_patch15^ C7705318/(UNC_INT16)/?i^es_728_patch16^;
!!UN:C7705430/(UNC_INT16)/?i^es_728_patch17^ C6060065/(UNC_INT16)/?i^es_728_patch18^;

** разрешение роспуска последнего отряда
!!UN:C5102718/(UNC_INT)/2425393296 C5102722/(UNC_INT8)/144 C5961680/(UNC_INT16)/434 C5961682/(UNC_INT8)/144;
!!UN:C6115569/(UNC_INT)/2425393296 C6115573/(UNC_INT8)/144 C6115641/(UNC_INT)/2425393296 C6115645/(UNC_INT8)/144;
** разрешение передачи последнего отряда
!!UN:C5968130/(UNC_INT)/2425393296 C5968134/(UNC_INT16)/37008 C5968202/(UNC_INT8)/235 C6060664/(UNC_INT8)/235;
!!UN:C6117225/(UNC_INT8)/235 C7703532/(UNC_INT16)/3307 C7703637/(UNC_INT16)/3307 C7705318/(UNC_INT16)/3307;
!!UN:C7705430/(UNC_INT16)/3307 C6060065/(UNC_INT16)/27320;

!?FU(OnGameLeave);                      [при загрузке игры]
; Check if one of the addresses has been modified, exit if negative
!!UN:C5102718/(UNC_INT)/?(dismissLast:y);[Dismiss last stack]
!!UN:C5968130/(UNC_INT)/?(tranferLast:y);[Transfer last stack]
!!FU|(dismissLast)<>2425393296/(tranferLast)<>2425393296:E;

!!UN:P728/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

** разрешение роспуска последнего отряда
!!UN:C5102718/(UNC_INT)/i^es_728_patch1^ C5102722/(UNC_INT8)/i^es_728_patch2^ C5961680/(UNC_INT16)/i^es_728_patch3^ C5961682/(UNC_INT8)/i^es_728_patch4^;
!!UN:C6115569/(UNC_INT)/i^es_728_patch5^ C6115573/(UNC_INT8)/i^es_728_patch6^ C6115641/(UNC_INT)/i^es_728_patch7^ C6115645/(UNC_INT8)/i^es_728_patch8^;
** разрешение передачи последнего отряда
!!UN:C5968130/(UNC_INT)/i^es_728_patch9^ C5968134/(UNC_INT16)/i^es_728_patch10^ C5968202/(UNC_INT8)/i^es_728_patch11^ C6060664/(UNC_INT8)/i^es_728_patch12^;
!!UN:C6117225/(UNC_INT8)/i^es_728_patch13^ C7703532/(UNC_INT16)/i^es_728_patch14^ C7703637/(UNC_INT16)/i^es_728_patch15^ C7705318/(UNC_INT16)/i^es_728_patch16^;
!!UN:C7705430/(UNC_INT16)/i^es_728_patch17^ C6060065/(UNC_INT16)/i^es_728_patch18^;

!?FU(OnEveryDay)&i^timerDay^>1;         [каждый день для всех игроков начиная со 2го дня]
!!UN:P728/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!OW:Hi^timerOwner^/2;
!!VR(lastHeroPtr:y):Sv2 +2;

!!if&v2>0;
  ; Loop through all the hero of the current player
  !!re i/3/(lastHeroPtr);
    !!VR(hero:y):Svi;

    ; Check if the hero has no army
    !!re (slot:y)/(ARMY_SLOT_FIRST)/(ARMY_SLOT_LAST);
      !!HE(hero):C0/(slot)/?(type:y)/?(num:y);

      !!br&(type)>(NO_MON)/(num)>0;
    !!en;

    !!if&(slot)>(ARMY_SLOT_LAST);
      !!HE(hero):P?(x:y)/?(y:y)/?(z:y); [координаты героя]
      !!TR(x)/(y)/(z):T?(terrain:y)/d/d/d/d/d/d/d; [тип почвы под героем]

      ; Reduce the movement points if the hero is on water
      !!if&(terrain)<>8;
        !!HE(hero):G?(maxMovement:y) W?(currMovement:y)/1;
        !!VR(newCurrMovement:y):S(currMovement) *3 :4 F0/(INT_MAX); [(newCurrMovement) - новый текущий запас хода]
        !!HE(hero):W(newCurrMovement)/1;
        !!HE(hero)&(maxMovement)=(currMovement):G(newCurrMovement); [установка нового текущего запаса, и, если ходы не тратились, начального, чтобы герой без армии мог копать]
      !!en;
    !!en;
  !!en;
!!en;

** end
