ZVSE2
** Author orig.  : Algor
** Name          : Neutral neighborhood
** Name rus.     : Нейтралы-соседи
** Options       : 778

; This script must be executed after WoG object repalcements


!?FU(OnEveryDay)&i^timerOnce^/i^timerDay^=1;
!!UN:P778/?(wogOption:y);               [проверяем включена ли опция 778]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!UN:U(OBJ_CREATURE_GENERATOR_1)/(ANY_OBJ)/?(dwellNum:y); [Количество жилищ в y2]
!!VRv10:S-1;                            [инициализация v10 для быстрого поиска]
!!DO(ES_778_AddInNeighbors)/1/(dwellNum)/1:P; [Для каждого жилища подселяем нейтралов-соседей FU(ES_778_AddInNeighbors)]

!?FU(ES_778_AddInNeighbors);            [подселение соседей]
!!UN:U(OBJ_CREATURE_GENERATOR_1)/(ANY_OBJ)/-1/10; [v10..v12 - координаты жилища]
!!DW10:M0/?y1/?y2;            [y1-тип существа, y2-количество существ в слоте 0]
!!VRv1:C0/0/0/0/0/0/0/0/0;    [По умолчанию соседей и охранников нет]
!!VRv1&y1=138:C139/0/0/25/0/0/0/0/0;  [Хоббиты 138/15/0, Крестьяне 139/25/0]
!!VRv1&y1=139:C138/0/0/15/0/0/0/0/0;  [Хоббиты 138/15/0, Крестьяне 139/25/0]
!!VRv1&y1=140:C142/143/0/7/8/0/0/0/0; [Кабаны 140/8/0, Кочевники 142/7/0, Воры 143/8/0]
!!VRv1&y1=142:C140/143/0/8/8/0/0/0/0; [Кабаны 140/8/0, Кочевники 142/7/0, Воры 143/8/0]
!!VRv1&y1=143:C140/142/0/8/7/0/0/0/0; [Кабаны 140/8/0, Кочевники 142/7/0, Воры 143/8/0]
!!VRv1&y1=141:C194/0/0/4/0/0/0/0/0;   [Оборотни 194/4/0, Мумии 141/7/0]
!!VRv1&y1=194:C141/0/0/7/0/0/0/0/0;   [Оборотни 194/4/0, Мумии 141/7/0]
!!VRv1&y1=137:C170/171/0/3/3/0/9/9/0; [Снайперы 137/4/0, Закаленные снайперы 171/3/9, Арктические снайперы 170/3/9]
!!VRv1&y1=170:C137/171/0/4/3/0/0/9/0; [Снайперы 137/4/0, Закаленные снайперы 171/3/9, Арктические снайперы 170/3/9]
!!VRv1&y1=171:C137/170/0/4/3/0/0/9/0; [Снайперы 137/4/0, Закаленные снайперы 171/3/9, Арктические снайперы 170/3/9]
!!VRv1&y1=136:C193/169/0/2/2/0/6/6/0; [Чародеи 136/2/6, Волшебницы 193/2/6, Фанатики фойны 169/2/6]
!!VRv1&y1=193:C136/169/0/2/2/0/6/6/0; [Чародеи 136/2/6, Волшебницы 193/2/6, Фанатики фойны 169/2/6]
!!VRv1&y1=169:C136/193/0/2/2/0/6/6/0; [Чародеи 136/2/6, Волшебницы 193/2/6, Фанатики фойны 169/2/6]
!!VRv1&y1=195:C172/0/0/2/0/0/6/0/0;   [Пожары 195/2/6, Кошмары 172/2/6]
!!VRv1&y1=172:C195/0/0/2/0/0/6/0/0;   [Пожары 195/2/6, Кошмары 172/2/6]
!!VRv1&y1=168:C144/0/0/3/0/0/9/0/0;   [Горынычи 168/1/3, (Тролли 144/3/9)]
!!DW10&v4>0:M1/v1/v4;         [устанавливаем v4 соседей типа v1 в слот 1]
!!DW10&v5>0:M2/v2/v5;         [устанавливаем v5 соседей типа v2 в слот 2]
!!DW10&v6>0:M3/v3/v6;         [устанавливаем v6 соседей типа v3 в слот 3]
!!DW10&v7>0:G1/v1/v7;         [устанавливаем v7 охранников типа v1 в слот 1]
!!DW10&v8>0:G2/v2/v8;         [устанавливаем v8 охранников типа v2 в слот 2]
!!DW10&v9>0:G3/v3/v9;         [устанавливаем v9 охранников типа v3 в слот 3]

** end
