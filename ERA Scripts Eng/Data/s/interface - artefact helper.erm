ZVSE2
** Author orig.  : Algor
** Updated by    : Archer30
** Name          : Artefact helper

** ПКМ по артефакту на карте приключений выводит его развернутое описание.
** Полезно как новичкам, так и для просмотра артефактов с измененными или случайными сойствами.

!#SN:W^artefact_helper^/1;

!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^=37;
!!CM:P?y1/?y2/?y3 ;             [y1-y3 - координаты клика]
!!OW:C?y4/?y30;                 [текущий игрок]

!!VRy5:S1 Sd<<y30;              [y5 - бит.маска текущего игрока]

!!TRy1/y2/y3:V?y6;              [y6 - бит.маска видимости квадрата]
!!VRy6:&y5;                     [y6=0, если квадрат не видим для игрока]
!!FU&y6=0:E;                    [выход, елси квадрат невидим]

!!OBy1/y2/y3:T?y4 U?y5;         [y4/y5 - тип/подтип объекта]
!!FU&y4<>5:E;                   [выход, если клик не на артефакте]

!!UN:C6687592/4/?y12;           [y12 - ссылка на таблицу артефактов [спс => igrik]]
!!VRy13:Sy5 *32 +y12 +16;       [y13 - адрес указателя с описанием артефакта]
!!UN:Cy13/4/?y14;               [y14 - получить указатель на описание артефакта]
!!SN:X?y9 Xy14 X?z1 Xy9;        [z1 - хранит описание артефакта]
!!IF:Q1/8/y5/4^%Z1^;            [вывод окна с картинкой и названием артефакта]
!!CM:R0;                        [отмена стандартной реакции на клик]

** end
