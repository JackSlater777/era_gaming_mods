ZVSE2
** Author orig.  : Algor
** Update        : Archer30
** Name          : Strong resistance
** Name rus.     : Усиленное сопротивление
** Options       : 777

; Enhanced Resistance disable Resistance II from WoG Options


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P777/?(enhRes:y);                  [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;                [выход если опция не включена]

; Set up new descriptions
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_BASIC)/?(basicDesc:z);
!!VR(newBasicDesc:z):S(basicDesc) +^%T(es.777.basicDesc)^;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_BASIC)/(newBasicDesc);

!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_ADVANCED)/?(advancedDesc:z);
!!VR(newAdvancedDesc:z):S(advancedDesc) +^%T(es.777.advancedDesc)^;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_ADVANCED)/(newAdvancedDesc);

; Manage the percentage change in Expert Resistance
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_EXPERT)/?(expertDesc:z);

!!SN:K(expertDesc)/?(length:y);

; Loop through the orignal skill description and look for the first "20"
!!re i/0/(length)/1/-1;
  !!SN:K(expertDesc)/i/?(str:z);

  ; Replace the fisst "20" with "15"
  !!if&(str)=^2^;
    !!VRj:Si+1;

    !!if&j<(length);
      !!SN:K(expertDesc)/j/?(str2:z);
      
      !!if&(str2)=^0^;    
        !!SN:K(expertDesc)/i/^1^;
        !!SN:K(expertDesc)/j/^5^;

        !!br;
      !!en;
    !!en;
  !!en;
!!en;

!!VR(newExpertDesc:z):S(expertDesc) +^%T(es.777.expertDesc)^;
!!SN:H^secskill^/(SKILL_RESISTANCE)/(SKILL_EXPERT)/(newExpertDesc);

// Set Expert Resistance to 15% (was 20%)
!?FU(OnStartOrLoad);
!!UN:P777/?(enhRes:y);        [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;      [выход если опция не включена]

!!UN:C6548052/(UNC_INT)/?i^es_777_patch^;
!!VRe1:S15 :100;              [Шанс срабатывания экспертного сопротивления - 15%]
!!SN:X?(value:y) Xe1 X?(value2:y) X(value); [...]
!!UN:C6548052/(UNC_INT)/(value2); [...]

!?FU(OnGameLeave);
!!UN:P777/?(enhRes:y);        [проверяем включена ли опция 777]
!!FU&(enhRes)<>(TRUE):E;      [выход если опция не включена]

!!UN:C6548052/(UNC_INT)/i^es_777_patch^;

!?FU(OnMagicCorrectedResistance); [перед расчетом сопротивления]
!!UN:P777/?y1;                [проверяем включена ли опция 777 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!MR:D?y1 F?y6;               [y1/y6 - базовый/конечный урон заклинания]
!!VRy6:-y1 *-1;               [y6 - кол-во урона, поглощенное естественным и магическим сопротивлениями]
!!UN:C42231940/4/?y11;        [y2 - номер отряда-цели заклинания]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y12;             [y12 - гекс отряда, получающего урон]
!!FU|y12<0/y12>186:E;         [выход, если гекс за пределами поля]
!!BU:Ey12/?y2;                [y2 - номер отряда на позиции]
!!VRy2::21;                   [y2 - сторона отряда-цели заклинания]
!!BA:Hy2/?y2;                 [y2 - номер героя-владельца]
!!FU&y2<0:E;                  [выход если героя-владельца нет]
!!HEy2:S26/?y3;               [y3 - уровень Сопротивления (0..3) героя]
!!HEy2:X?y4/?y5/d/d/d/d/d;    [y4=0, y5=26 для специалиста по сопротивлению]
!!VRy3&y4=0/y5=26:+1;         [y3 - уровень Сопротивления героя с учетом специализации(0..4)]
!!HEy2:A2/57/d/?y4;           [y4 - кол-во надетых на героя Колье неприступности]
!!VRy3&y4>0:+1;               [y3 - уровень Сопротивления с учетом Колье]
!!HEy2:A2/58/d/?y4;           [y4 - кол-во надетых на героя Мантия равновесия]
!!VRy3&y4>0:+2;               [y3 - уровень Сопротивления с учетом Мантии]
!!HEy2:A2/59/d/?y4;           [y4 - кол-во надетых на героя Сапоги противодействия]
!!VRy3&y4>0:+3;               [y3 - уровень Сопротивления с учетом Сапог]
!!VRy3:*5 *y1 :100;           [y3 - количество урона поглощеннго голем-like сопротивлением]
!!VRy1:-y3 -y6;               [y1 - новый конечный урон заклинания]
!!VRy1&y1<0:S0;               [...]
!!MR:Fy1;                     [устанавливаем новый конечный урон заклинания]

** end
