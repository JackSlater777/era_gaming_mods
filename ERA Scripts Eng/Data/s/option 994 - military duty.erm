ZVSE
ERMS_ScriptDate=19.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Military duty
** Name rus.     : Воинская обязанность
** Options       : 994
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : TM61(standard)
** Functions     : FU7887-FU7889
** PO-values     : -

** Каждое воскресенье невыкупленные существа из захваченных внешних жилищ прибывают для найма в первый город игрока, с подходящим отстроенным зданием.
** Если города с подходящим отстроенным зданием у игрока нет, существа остаются во внешних жилищах.
** Порядок выбора города из нескольких подходящих, определяется порядком в списке городов игрока,
** который может быть изменен при использовании WoG-опци 244 "Сортировка списка героев и городов"
** Существа "8го уровня" воинской обязанности не подлежат и попадают в города героя стандартным для них путем.

!#TM61:S7/999/7/255;                   [переопределение стандартного таймера - каждое воскресерье для всех игроков]

!?TM61;                                [каждое воскресенье]
!!UN:P994/?y1; !!FU&y1<>1:E;           [выход, если опция 994 не включена]
!!OW:C?y2;                             [y2 - текущий игрок]
!!UN:U17/-1/?y1; !!VRv10:S-1;          [y1 - количество жилищ на карте (тип 17), инициализация v10 для быстрого поиска]
!!DO7889/1/y1/1&y1>0:Py2/17;           [для каждого жилища призываем обитателей в подходящие города игрока]
!!UN:U20/-1/?y1; !!VRv10:S-1;          [y1 - количество жилищ на карте (тип 20), инициализация v10 для быстрого поиска]
!!DO7889/1/y1/1&y1>0:Py2/20;           [для каждого жилища призываем обитателей в подходящие города игрока]

!?FU7889;
!!UN:Ux2/-1/-1/10;                     [v10..v12 - координаты жилища]
!!DW10:O?y1/1; !!FU&y1<>x1:E;          [выход, если жилище не принадлежит текущему игроку]
!!DO7887/0/3/1:Px1;                    [перебор слотов жилища]

!?FU7887;                              [проверка слота жилища]
!!DW10:Mx16/?y1/?y2;                   [y1/y2 - тип и кол-во доступных существ в 1м слоте жилища]
!!if&y1>=0/y2>0:;                      [если в слоте жилища есть существа]
  !!VRy5:S-2; !!VRy6:S-2;              [инициализация переменных]
  !!MA:Oy1/?y3 Ly1/?y4;                [y3/y4 - тип города, которому принадлежит существо / уровень существа]
  !!UN&y3>=0:Ty3/y4/0/?y5;             [y5 - тип существа для найма в подходящем городе (баз.)]
  !!UN&y3>=0:Ty3/y4/1/?y6;             [y6 - тип существа для найма в подходящем городе (улучш.)]
  !!if|y5=y1/y6=y1:;                   [если существо возможно нанять в городе (не нейтрал)]
    !!OW:Wx1/?y7; !!VRy7:-1;           [y7 - количество городов у игрока-1]
    !!VRv13:S-1;                       [инициализация v13 - номер подходящего города]
    !!VRy8:Sy4 +30; !!VRy8&y6=y1:+7;   [y8 - номер необходимого здания для перемещения существ]
    !!DO7888/0/y7/1&y7>=0:Px1/y3/y8;   [v13 - номер подходящего города для перемещения существ]
    !!if&v13>=0:;                      [если город найден]
      !!VRy8&y8<37:+7; !!CA0/v13:B3/y8;[f1=1, если отстроено улучшенное жилище]
      !!if&1:;                         [если отстроено улучшенное жилище]
        !!CA0/v13:M1/y4/d/dy2;         [увеличиваем кол-во доступных улучшенных существ]
      !!el:;                           [иначе]
        !!CA0/v13:M1/y4/dy2/d;         [увеличиваем кол-во доступных неулучшенных существ]
      !!en;
      !!DW10:Mx16/d/0;                 [удаляем существ из слота внешнего жилища]
    !!en:;
  !!en:;
!!en:;

!?FU7888;                              [поиск подходящего города]
!!OW:Wx1/x16/?y1;                      [y1 - номер города на карте]
!!CA0/y1:T?y2 B3/x3; !!FU|-1/y2<>x2:E; [выход, если город не подходящего типа или в нем не отстроено подходящее жилище]
!!VRv13:Sy1; !!VRx16:S99;              [v13 - номер (на карте) подходящего города, прерывание поиска]

** end
