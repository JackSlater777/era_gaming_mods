ZVSE

** Author orig.  : Algor
** Name          : Cutthroats
** Name rus.     : Головорезы
** Options       : 758
** Dialogs       : -
** Variables     : -
** Tmp variables : z1-z2
** Timers        : -
** Functions     : -
** PO-values     : -

** Воры становятся хладнокровными головорезами не зависящими от боевого духа,
** но каждый раз после убийства отряда воодушевляются получая возможность совершить еще одно действие.

!?FU(OnAfterErmInstructions); [перед началом игры]
!!UN:P758/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!FU(Fun_SetDescriptionFromERT):P1/143/0/179206 P1/143/1/179207 P1/143/2/179208; [установка новых названий и описаний Головорезов]
!!MA:F143/?y1 X143/?y2;       [y1/y2 - FV/флаги Головорезов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!VRy2:|131072;               [даем флаг "нет пенальти морали"]
!!MA:F143/y1 X143/y2;         [обновляем FV/флаги Головорезов]

!?FU(OnBeforeBattleUniversal);[перед боем]
!!UN:P758/?y1;                [проверяем включена ли опция 758 в y1]
!!SN&y1>0:W^opt758s^/-1 W^opt758d^/-1 W^opt758m^/-1; [сбрасываем номера атакующего отряда и цели]

!?FU(OnBeforeBattleAction);   [перед действием]
!!UN:P758/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!BG:A?y1 N?y2 E?y3;          [y1/y2/y3 - тип действия/отряд/цель]
!!FU|y1<>6/y2<0/y3<0:E;       [выход, если не атака ближнего боя, нет отряда или цели]
!!BMy2:T?y4;                  [y4 - тип атакующего отряда]
!!SN&y4=143:W^opt758s^/y2 W^opt758d^/y3; [если атака Головорезов, сохраняем номера атакующего отряда и цели]

!?FU(OnBattleRegeneratePhase);[фаза регенерации]
!!UN:P758/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!SN&y1>0:W^opt758s^/?y1 W^opt758d^/?y2; [y1/y2 - атакующий отряд/цель]
!!FU&y1<0/y2<0:E;             [выход, если не было атаки Головорезов]
!!BMy1:N?y3;                  [y3/y4 - количество существ в отрядах]
!!BMy2:N?y4;                  [...]
!!if|y3<1/y4>0:;              [если убиты головорезы или не убит целевой отряд]
  !!SN:W^opt758s^/-1 W^opt758d^/-1; [обнуляем номера]
  !!FU:E;                     [и выходим]
!!en:;
!!SN&y1<21:W^opt758m^/0;      [устанавливаем признак стороны Головорезов]
!!SN&y1>20:W^opt758m^/1;      [...]
!!SN:X?i/?j/1;                [блокируем фазу регенерации]

!?FU(OnBattleStackObtainsTurn); [при получении хода в бою]
!!UN:P758/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!SN&y1>0:W^opt758s^/?y1 W^opt758d^/?y2 W^opt758m^/?y3; [y1/y2/y3 отряд/цель/сторона]
!!FU|y1<0/y2<0/y3<0:E;        [выход, если одно из значений не установлено]
!!SN:W^opt758s^/-1 W^opt758d^/-1 W^opt758m^/-1; [обнуляем номера]
!!BMy1:N?y5;                  [y5 - количество головорезов]
!!FU&y5<1:E;                  [выход, если головорезов нет]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!if&f=0:;                    [для не-быстрой битвы]
  !!VRz1&y5=1:Sz179209;       [z1 - текст сообщения для вывода в лог]
  !!VRz1&y5>1:Sz179210;       [...]
  !!MM:Sz1;                   [вывод текста в лог битвы]
  !!VRz1:S^MIRTH^;            [z1 - звуковой файл морали]
  !!SN:P1;                    [вывод звука морали]
  !!BMy1:V20;                 [анимация морали на отряде]
!!en:;
!!VRy1&y1>20/y3=1:-21;        [y1 - номер отряда (0..20)]
!!SN:Xy3/y1;                  [передаем ход головорезам]

** end