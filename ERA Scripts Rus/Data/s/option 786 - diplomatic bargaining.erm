ZVSE
ERMS_ScriptDate=18.11(November).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Diplomatic bargaining
** Name rus.     : Дипломатические торги
** Options       : 786
** Dialogs       : -
** Variables     : v7900-v7906, v7910-v7916, z802-z803, z892-z894, z182
** Tmp variables :
** Timers        :
** Functions     : FU7937
** PO-values     : -

!?PI;                         [пост-инструкция]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz182:Sz91 +^%Z179065^;    [получаем базовое описание навыка]
!!UN:G0/4/1/182;              [Устанавливаем базовое описание навыка]
!!VRz892:Sz92 +^%Z179066^;    [получаем продвинутое описание навыка]
!!UN:G0/4/2/892;              [Устанавливаем продвинутое описание навыка]
!!VRz893:Sz93 +^%Z179067^;    [получаем экспертное описание навыка]
!!UN:G0/4/3/893;              [Устанавливаем экспертное описание навыка]
!!VRz894:Sz179068;            [получаем описание Медали Дипломата]
!!UN:A66/10/894;              [устанавливаем описание Медали Дипломата]
!!VRz802:Sz179069;            [получаем описание Кольца Дипломата]
!!UN:A67/10/802;              [устанавливаем описание Кольца Дипломата]
!!VRz803:Sz179070;            [получаем описание Ленты Посла]
!!UN:A68/10/803;              [устанавливаем описание Ленты Посла]

!?FU7937;                     [установка оригинальных цен существ]
!!MA&v7900>-1:Cv7900/6/v7910; [восстанавливаем оригинальную стоимость монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/v7911; [восстанавливаем оригинальную стоимость монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/v7912; [восстанавливаем оригинальную стоимость монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/v7913; [восстанавливаем оригинальную стоимость монстра 4го типа]
!!MA&v7904>-1:Cv7904/6/v7914; [восстанавливаем оригинальную стоимость монстра 5го типа]
!!MA&v7905>-1:Cv7905/6/v7915; [восстанавливаем оригинальную стоимость монстра 6го типа]
!!MA&v7906>-1:Cv7906/6/v7916; [восстанавливаем оригинальную стоимость монстра 7го типа]
!!VRv7900:C-1/-1/-1/-1/-1/-1/-1; [устанавливаем признак оригинальной цены монстрам 1го-7го типов]
!!UN:P786/1;                  [устанавливаем опцию 786 в 1 (цены не снижены)]

!?OB54;                       [при атаке монстра]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!MO998:R10/1;                [устанавливаем монстру максимальную агрессию]

!?OB17;                       [посещение внешнего жилища тип 17]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!FU7937&y1=2:P;              [если цены изменены, устанавливаем оригинальные]
!!HE-1:S4/?y1 A2/66/d/?y2 A2/67/d/?y3 A2/68/d/?y4; [y1 - навык дипломатии, y2-y4 - кол-во надетых артефактов дипломата медаль/кольцо/лента]
!!VRy3&y3>0:S1;               [учитываем только одно кольцо]
!!VRy1:+y2 +y3 +y4;           [y1 - эффективный уровень Дипломатии (0..6)]
!!FU&y1=0:E;                  [выход, если уровень дипломатии нулевой]
!!VRy1:*-1 +10;               [y1 - коэффициент снижения стоимости]
!!VRv7900:C-1/-1/-1/-1/-1/-1/-1; [инициализация переменных с номерами обиталетей]
!!DW998:M0/?v7900/d M1/?v7901/d M2/?v7902/d M3/?v7903/d; [v7900..v7903 - типы 1-4го нанимаемого монстра (-1 - нет монстра)]
!!MA&v7900>-1:Cv7900/6/?v7910;[v7910 - оригинальная стоимость монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/?v7911;[v7911 - оригинальная стоимость монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/?v7912;[v7912 - оригинальная стоимость монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/?v7913;[v7913 - оригинальная стоимость монстра 4го типа]
!!VRy2&v7900>-1:Sv7910 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 1го типа]
!!MA&v7900>-1:Cv7900/6/y2;    [устанавливаем стоимость монстра 1го типа]
!!VRy2&v7901>-1:Sv7911 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 2го типа]
!!MA&v7901>-1:Cv7901/6/y2;    [устанавливаем стоимость монстра 2го типа]
!!VRy2&v7902>-1:Sv7912 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 3го типа]
!!MA&v7902>-1:Cv7902/6/y2;    [устанавливаем стоимость монстра 3го типа]
!!VRy2&v7903>-1:Sv7913 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 4го типа]
!!MA&v7903>-1:Cv7903/6/y2;    [устанавливаем стоимость монстра 4го типа]
!!UN:P786/2;                  [выставляем флаг сниженной цены (опция 786=2)]

!$OB17;                       [посещение внешнего жилища тип 17, посттриггер]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1<2:E;                  [выход если опция не включена или цены не снижались]
!!FU7937:P;                   [если цены изменены, устанавливаем оригинальные]

!?OB20;                       [посещение внешнего жилища тип 20]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!FU7937&y1=2:P;              [если цены изменены, устанавливаем оригинальные]
!!HE-1:S4/?y1 A2/66/d/?y2 A2/67/d/?y3 A2/68/d/?y4; [y1 - навык дипломатии, y2-y4 - кол-во надетых артефактов дипломата медаль/кольцо/лента]
!!VRy3&y3>0:S1;               [учитываем только одно кольцо]
!!VRy1:+y2 +y3 +y4;           [y1 - эффективный уровень Дипломатии (0..6)]
!!FU&y1=0:E;                  [выход, если уровень дипломатии нулевой]
!!VRy1:*-1 +10;               [y1 - коэффициент снижения стоимости]
!!VRv7900:C-1/-1/-1/-1/-1/-1/-1; [инициализация переменных с номерами обиталетей]
!!DW998:M0/?v7900/d M1/?v7901/d M2/?v7902/d M3/?v7903/d; [v7900..v7903 - типы 1-4го нанимаемого монстра (-1 - нет монстра)]
!!MA&v7900>-1:Cv7900/6/?v7910;[v7910 - оригинальная стоимость монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/?v7911;[v7911 - оригинальная стоимость монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/?v7912;[v7912 - оригинальная стоимость монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/?v7913;[v7913 - оригинальная стоимость монстра 4го типа]
!!VRy2&v7900>-1:Sv7910 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 1го типа]
!!MA&v7900>-1:Cv7900/6/y2;    [устанавливаем стоимость монстра 1го типа]
!!VRy2&v7901>-1:Sv7911 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 2го типа]
!!MA&v7901>-1:Cv7901/6/y2;    [устанавливаем стоимость монстра 2го типа]
!!VRy2&v7902>-1:Sv7912 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 3го типа]
!!MA&v7902>-1:Cv7902/6/y2;    [устанавливаем стоимость монстра 3го типа]
!!VRy2&v7903>-1:Sv7913 *y1 :10;[y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 4го типа]
!!MA&v7903>-1:Cv7903/6/y2;    [устанавливаем стоимость монстра 4го типа]
!!UN:P786/2;                  [выставляем флаг сниженной цены (опция 786=2)]

!$OB20;                       [посещение внешнего жилища тип 20, посттриггер]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1<2:E;                  [выход если опция не включена или цены не снижались]
!!FU7937:P;                   [если цены изменены, устанавливаем оригинальные]

!?OB35;                       [посещение форта-на-холме]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!FU7937&y1=2:P;              [если цены изменены, устанавливаем оригинальные]
!!HE-1:S4/?y1 A2/66/d/?y2 A2/67/d/?y3 A2/68/d/?y4; [y1 - навык дипломатии, y2-y4 - кол-во надетых артефактов дипломата медаль/кольцо/лента]
!!VRy3&y3>0:S1;               [учитываем только одно кольцо]
!!VRy1:+y2 +y3 +y4;           [y1 - эффективный уровень Дипломатии (0..6)]
!!FU&y1=0:E;                  [выход, если уровень дипломатии нулевой]
!!VRv7900:C-1/-1/-1/-1/-1/-1/-1; [инициализация переменных с номерами существ в армии героя]
!!HE-1:C0/0/?v7900/d C0/1/?v7901/d C0/2/?v7902/d C0/3/?v7903/d C0/4/?v7904/d C0/5/?v7905/d C0/6/?v7906/d; [v7900..v7906 - типы 1-7го монстра в армии героя(-1 - нет монстра)]
!!MA&v7900>-1:Cv7900/6/?y10;  [y10 - оригинальная стоимость монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/?y11;  [y11 - оригинальная стоимость монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/?y12;  [y12 - оригинальная стоимость монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/?y13;  [y13 - оригинальная стоимость монстра 4го типа]
!!MA&v7904>-1:Cv7904/6/?y14;  [y14 - оригинальная стоимость монстра 5го типа]
!!MA&v7905>-1:Cv7905/6/?y15;  [y15 - оригинальная стоимость монстра 6го типа]
!!MA&v7906>-1:Cv7906/6/?y16;  [y16 - оригинальная стоимость монстра 7го типа]
!!FU7938:P7900 P7901 P7902 P7903 P7904 P7905 P7906; [v7900..v7906 - номера апгрейдов для 1-7го монстров в армии героя (-1 - нет монстра)]
!!MA&v7900>-1:Cv7900/6/?v7910;[v7910 - оригинальная стоимость улучшенного монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/?v7911;[v7911 - оригинальная стоимость улучшенного монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/?v7912;[v7912 - оригинальная стоимость улучшенного монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/?v7913;[v7913 - оригинальная стоимость улучшенного монстра 4го типа]
!!MA&v7904>-1:Cv7904/6/?v7914;[v7914 - оригинальная стоимость улучшенного монстра 5го типа]
!!MA&v7905>-1:Cv7905/6/?v7915;[v7915 - оригинальная стоимость улучшенного монстра 6го типа]
!!MA&v7906>-1:Cv7906/6/?v7916;[v7916 - оригинальная стоимость улучшенного монстра 7го типа]
!!VRy10&v7900>-1:-v7910 *y1 :10 +v7910;[y10 - стоимость улучшенного монстра 1го типа со скидкой]
!!VRy11&v7901>-1:-v7911 *y1 :10 +v7911;[y11 - стоимость улучшенного монстра 2го типа со скидкой]
!!VRy12&v7902>-1:-v7912 *y1 :10 +v7912;[y12 - стоимость улучшенного монстра 3го типа со скидкой]
!!VRy13&v7903>-1:-v7913 *y1 :10 +v7913;[y13 - стоимость улучшенного монстра 4го типа со скидкой]
!!VRy14&v7904>-1:-v7914 *y1 :10 +v7914;[y14 - стоимость улучшенного монстра 5го типа со скидкой]
!!VRy15&v7905>-1:-v7915 *y1 :10 +v7915;[y15 - стоимость улучшенного монстра 6го типа со скидкой]
!!VRy16&v7906>-1:-v7916 *y1 :10 +v7916;[y16 - стоимость улучшенного монстра 7го типа со скидкой]
!!VRy10|y10<0/y10>v7910:Sv7910;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy11|y11<0/y11>v7911:Sv7911;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy12|y12<0/y12>v7912:Sv7912;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy13|y13<0/y13>v7913:Sv7913;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy14|y14<0/y14>v7914:Sv7914;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy15|y15<0/y15>v7915:Sv7915;[если скидка отрицательная, выставляем оригинальную стоимость]
!!VRy16|y16<0/y16>v7916:Sv7916;[если скидка отрицательная, выставляем оригинальную стоимость]
!!MA&v7900>-1:Cv7900/6/y10;   [устанавливаем стоимость монстра 1го типа]
!!MA&v7901>-1:Cv7901/6/y11;   [устанавливаем стоимость монстра 2го типа]
!!MA&v7902>-1:Cv7902/6/y12;   [устанавливаем стоимость монстра 3го типа]
!!MA&v7903>-1:Cv7903/6/y13;   [устанавливаем стоимость монстра 4го типа]
!!MA&v7904>-1:Cv7904/6/y14;   [устанавливаем стоимость монстра 5го типа]
!!MA&v7905>-1:Cv7905/6/y15;   [устанавливаем стоимость монстра 6го типа]
!!MA&v7906>-1:Cv7906/6/y16;   [устанавливаем стоимость монстра 7го типа]
!!UN:P786/2;                  [выставляем флаг сниженной цены опция 786=2]

!$OB35;                       [посещение форта-на-холме, посттриггер]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1<2:E;                  [выход если опция не включена или цены не снижались]
!!FU7937:P;                   [если цены изменены, устанавливаем оригинальные]

!?OB78;                       [посещение лагеря беженцев]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!FU7937&y1=2:P;              [если цены изменены, устанавливаем оригинальные]
!!HE-1:S4/?y1 A2/66/d/?y2 A2/67/d/?y3 A2/68/d/?y4; [y1 - навык дипломатии, y2-y4 - кол-во надетых артефактов дипломата медаль/кольцо/лента]
!!VRy3&y3>0:S1;               [учитываем только одно кольцо]
!!VRy1:+y2 +y3 +y4;           [y1 - эффективный уровень Дипломатии (0..6)]
!!FU&y1=0:E;                  [выход, если уровень дипломатии нулевой]
!!VRy1:*-1 +10;               [y1 - коэффициент снижения стоимости]
!!VRv7900:C-1/-1/-1/-1/-1/-1/-1; [инициализация переменных с номерами обиталетей]
!!OB998:U?v7900;              [v7900 - тип нанимаемого монстра]
!!MA&v7900>-1:Cv7900/6/?v7910;[v7910 - оригинальная стоимость монстра]
!!VRy2:Sv7910 *y1 :10;        [y2 - сниженная на 10%*уровень Дипломатии стоимость монстра 1го типа]
!!MA&v7900>-1:Cv7900/6/y2;    [устанавливаем стоимость монстра 1го типа]
!!UN:P786/2;                  [выставляем флаг сниженной цены (опция 786=2)]

!$OB78;                       [посещение лагеря беженцев, посттриггер]
!!UN:P786/?y1;                [проверяем включена ли опция 786 в y1]
!!FU&y1<2:E;                  [выход если опция не включена или цены не снижались]
!!FU7937:P;                   [если цены изменены, устанавливаем оригинальные]

!?FU7938;                     [получение номера апгрейда в для номера монстра x1 - номер v-переменной с номером монстра]
!!VRy1:Svx1;                  [y1 - номер исходного существа]
!!VRy2:S-1;                   [по умолчанию нет апгрейда (y2=-1)]
!!VRy2&y1<=111:Sy1 |1;        [y2 - номер улучшенного существа для RoE-Существ]
!!VRy2&y1=118:S119;           [Pixie]
!!VRy2&y1=112:S127;           [Air Elemental]
!!VRy2&y1=115:S123;           [Water Elemental]
!!VRy2&y1=114:S129;           [Fire Elemental]
!!VRy2&y1=113:S125;           [Earth Elemental]
!!VRy2&y1=120:S121;           [Psychic Elemental]
!!VRy2&y1=130:S131;           [Firebird]
!!VRy2&y1=116:S117;           [Gold golem]
!!MA&y1>-1:Uy1/?y3;           [y3 - номер улучшенного монстра, если переопределено стандартное улучшение (y3=-1/-2, если стандартно/не улучшается)]
!!VRy2&y1>-1/y3>=0:Sy3;       [y2 - номер улучшенного монстра]
!!VRy2&y1=y2:S-1;             [y2 = -1, если исходный монстр не улучшается]
!!VRvx1:Sy2;                  [изменяем исходную переменную vx1]

** end
