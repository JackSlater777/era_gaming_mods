ZVSE2
** Author orig.  : Algor
** Name          : Bandit ambush
** Name rus.     : Засады бандитов
** Options       : 791
** Dialogs       : -
** Variables     : -
** Tmp variables : z1,v1,v2
** Timers        : -
** Functions     : FU7922
** PO-values     : -


!?OB12;                       [Кострище]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB22;                       [Труп]
!!UN:P791/?y1 P882/?y2;       [проверяем включены ли опция 791 и 882 в y1/y2]
!!FU|y1=0/y2<>0:E;            [выход, если опция не включена или включены "скелеты с артефактами"]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB39;                       [Навес]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB53;                       [Шахта]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB55;                       [Мистический сад]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB64;                       [Флаг единства]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB79;                       [Ресурсы]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB101;                      [Сундук сокровищ]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB109;                      [Водяное колесо]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB112;                      [Мельница]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?OB220;                      [Рудник]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!OW:C?y10;
!!OB998:Ey10;                 [разрешаем посещение объекта]
!!FU7922:P;                   [если опция включена вызываем функцию]
!!HE-1:O?y1;                  [y1 - принадлежность героя, посетившего объект (-1 - нет)]
!!OB998&y1=-1:S;              [запрещаем посещение объекта, если герой не победил в бою с бандитами]
!!SN&y1=-1:Q;                 [прерываем триггеры объекта, если герой не победил в бою с бандитами]

!?FU7922;                     [Засады бандитов - организация засады]
!!VRy1:R0/0/19 %20;           [Засада есть в каждом 20м случае (шанс 5%)]
!!FU&y1>0:E;                  [Выход, если нет засады]
!!OW:C?y1 Iy1/?y2;            [y1/y2 - номер игрока/признак ИИ]
!!VRy11:Sc +1 *10 +y1;        [y11 - контрольное число посещения объекта (10 х (день+1) + игрок)]
!!SN:W^opt791_%V998_%V999_%V1000^/?y12; [y12 - прежнее контрольное число посещения объекта]
!!SN:W^opt791_%V998_%V999_%V1000^/y11;  [обновление контрольного числа посещения объекта]
!!FU&y12=y11:E;               [выход, если данный игрок сегодня уже посещал данный объект]
!!HE-1:N?y3 Ed/?y4/1;         [y3/y4 - номер/уровень героя]
!!UN:J2/?y5;                  [y5=0..4 уровень сложности для человека]
!!VRy5&y2=1:S0;               [для ИИ уровень сложности всегда "легкий"]
** старый алгоритм
!_!VRv1:S0;                    [инициализация v1 для подсчета FV армии героя]
!_!DO7961/0/6/1:Py3;           [v1 - FV армии героя]
!_!VRv1&v1<100:S100;           [мин. FV армии для расчета - 100]
!_!UN:P3/?y6;                  [y6=0, если включена опция "командиры"]
!_!COy3:D?y7 X2/?y8;           [y7=0, если командир жив. y9 - уровень командира]
!_!VRy8&y6=0/y7=0:*100;        [y8  - FV командира = 100*уровень]
!_!VRv1&y2=0:+y8;              [v1 - FV армии героя с учетом командира (для ИИ командира не учитываем)]
!_!VRy6:S10 *y5 +40 +y4 *v1 :100; [y6 - FV отряда бандитов (40..80% от FV армии героя, в зависимости от уровня сложности +1% за каждый уровень героя)]
** новый алгоритм
!!VRy6:Sy4 *y5 +c *200;       [FV банды = 200 за каждый прошедший день и уровень_героя*сложность]
**
!!VRy6&y6<0:S2000000000;      [специально для читеров и прочих редкостных извращенцев]

!!UN:P791/y6;                 [значение опции равно FV засады бандитов]

!!IF&y2=0:M^%T(es.791.dlg)^;  [Вывод сообщения о засаде только для игрока человека]

!!UN:C4600476/2/37008 C4600637/4/95721; [Бой как в банке и без машин]
!!HE-1:Tv998/v999/v1000/143/1;[Начало боя]
!!UN:C4600476/2/5236 C4600637/4/24413455; [Возвращаем стандартный бой]
!!UN:P791/1;                  [значение опции равно 1]

!?FU7961;                     [увеличение v1 на FV x61-го отряда героя x1]
!!HEx1:C0/x16/?y1/?y2;        [y1/y2 - тип и количество существ в отряде]
!!FU|y1<0/y1<=0:E;            [выход, если отряда нет]
!!MA:Fy1/?y3;                 [y3 - FV существа]
!!VRy3:*y2;                   [y3 - FV отряда]
!!VRv1:+y3;                   [увеличение v1]

!?BF;                         [Засады бандитов - подготовка поля боя]
!!UN:P791/?y1;                [y1 - статус опции]
!!BF&y1<0:C;                  [Убираем препятствия, если бой с бандитами]

!?BA52;                       [Засады бандитов - бой]
!!UN:P791/?y1;                [y1 - статус опции]
!!FU&y1<=1:E;                 [Выход если не засада]
!!VRy2:S5 R2;                 [y2 - кол-во отрядов бандитов(5..7)]
!!VRy3:Sy1 :y2;               [y3 - примерное FV каждого отряда бандитов]
!!FU7967&y2>=7:Py3/?y17;      [v1/v2 - параметры 7го отряда]
!!BA&y2>=7:M1/6/v1/v2;        [установка 7го отряда]
!!FU7967&y2>=6:Py3/?y16;      [v1/v2 - параметры 6го отряда]
!!BA&y2>=6:M1/5/v1/v2;        [установка 6го отряда]
!!FU7967&y2>=5:Py3/?y15;      [v1/v2 - параметры 5го отряда]
!!BA&y2>=5:M1/4/v1/v2;        [установка 5го отряда]
!!FU7967&y2>=4:Py3/?y14;      [v1/v2 - параметры 4го отряда]
!!BA&y2>=4:M1/3/v1/v2;        [установка 4го отряда]
!!FU7967&y2>=3:Py3/?y13;      [v1/v2 - параметры 3го отряда]
!!BA&y2>=3:M1/2/v1/v2;        [установка 3го отряда]
!!FU7967&y2>=2:Py3/?y12;      [v1/v2 - параметры 2го отряда]
!!BA&y2>=2:M1/1/v1/v2;        [установка 2го отряда]
!!VRy3:Sy1 -y17 -y16 -y15 -y14 -y13 -y12; [y3 - откорректированное FV 1го отряда]
!!FU7967&y2>=1:Py3/?y11;      [v1/v2 - параметры 1го отряда]
!!BA&y2>=1:M1/0/v1/v2;        [установка 1го отряда]
!!BA:H0/?y1;                  [y1 - номер героя]
!!HEy1:R4/?y2 R4/0;           [y2 - состояние тактического формирования, отключение расстановки войск]
!!VRy2:*-1000 -y1 -1;         [y2 - номер героя и состояние тактического формирования]
!!UN:P791/y2;                 [сохранение номера героя и состояния тактического формирования в статус опции]

!?BA53;                       [Засады бандитов - окончание боя]
!!UN:P791/?y1;                [y1 - статус опции]
!!FU&y1>=0:E;                 [выход, если бой не с бандитами или опция отключена]
!!VRy2:Sy1 +1 %1000 *-1;      [y2 - номер сражавшегося героя]
!!VRy3:Sy1 +1 :-1000;         [y3 - состояние тактического формирования]
!!HEy2:R4/y3;                 [восстановление состояния тактического формирования]

!?FU7967;                     [установка в v1/v2 - типа/количества бандитов (~FV отряда = x1), в х2 возвращается точное FV]
!!VRy10:Sc;                   [y10 - номер дня]
!!VRy9:S8;                    [после 5й недели полый набор бандитов]
!!VRy9&y10<36:S5;             [4-5 неделя: + сильваны и оборотни]
!!VRy9&y10<22:S3;             [2-3 неделя: + кабаны, кочевники]
!!VRy9&y10<8:S1;              [1 неделя: только хоббиты и воры]
!!VRy1:S0 Ry9;                [y1 - случайное число (0..y9)]
!!VRv1&y1=0:S138;             [v1 - Хоббиты]
!!VRv1&y1=1:S143;             [v1 - Воры]
!!VRv1&y1=2:S140;             [v1 - Кабаны]
!!VRv1&y1=3:S142;             [v1 - Кочевники]
!!VRv1&y1=4:S192;             [v1 - Сильв.кентавры]
!!VRv1&y1=5:S194;             [v1 - Оборотни]
!!VRv1&y1=6:S144;             [v1 - Тролли]
!!VRv1&y1=7:S195;             [v1 - Пожары]
!!VRv1&y1=8:S168;             [v1 - Горынычи]
!!MA:Fv1/?y2;                 [y2 - FV одного существа]
!!VRv2:Sx1 :y2;               [v2 - кол-во существ...]
!!VRv2&v2<1:S1;               [...но не менее 1]
!!VRx2:Sv2 *y2;               [возвращаем в x2 уточненное FV отряда]

!?GM0;                        [Для случая загрузки из боя во время засады]
!!UN:P791/?y1;                [проверяем включена ли опция 791 в y1]
!!UN&y1<0:P791/1;             [если опция включена и выставлена засада - убираем засаду]
!!UN:C4600476/2/5236 C4600637/4/24413455; [Возвращаем стандартный бой]

** end
