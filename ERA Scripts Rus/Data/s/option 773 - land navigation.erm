ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Land Navigation
** Name rus.     : Сухопутная навигация
** Options       : 773
** Dialogs       : -
** Variables     : w101, z898-z901
** Tmp variables : v1-v8
** Timers        : TM50 (standard)
** Functions     : FU7906-FU7909,FU7956-FU7957,FU7959
** PO-values     : -

!#TM50:S2/999/1/255;          [каждый день для всех игроков начиная со 2го дня]

!?FU7906;                     [подсчет водных квадратов поверхности в v1. v2-размер, v5-квадрат]
!!VRv6:Sv5 %v4;               [v6 - абсцисса]
!!VRv7:Sv5 :v4;               [v7 - ордината]
!!TRv6/v7/0:T?v8/d/d/d/d/d/d/d;[y3=8, если квадрат водный]
!!VRv1&v8=8:+1;               [увеличиваем счетчик водных квадратов]
!!VRv5:+1;                    [увеличиваем счетчик квадратов]

!?OB8;                        [Посадка в лодку]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!HE-1:W?y1;                  [y1 - ОД героя]
!!IF:W-1;                     [выбор текущего героя]
!!VRw101:Sy1;                 [y1 - ОД героя при посадке(высадке) в лодку]

!$OB8;                        [Посадка в лодку - пост-триггрер]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!HE-1:S5/?y1 X?y2/?y3/d/d/d/d/d; [y1 - Уровень навигации героя (0..3), y2=0 и y3=5 для специалиста по Навигации]
!!VRy1&y2=0/y3=5:+1;          [y1 - Уровень навигации героя с учетом специализации (0..4)
!!VRy2:S6 -y1 *100;           [y2 - стоимость посадки(высадки) на лодку в ОД (Базовая стоимость 600 ОД и минус 100 ОД за каждый уровень Навигации/Специализацию Навигации)]
!!HE-1:A2/123/d/y3;           [y3=1, если на герое Шляпа Морского Капитана]
!!VRy2&y3=1:S0;               [обнуляем стоимость посадки(высадки) если на герое Шляпа Морского Капитана]
!!IF:W-1;                     [выбор текущего героя]
!!VRw101:-y2;                 [w101 - расчетные ОД героя после посадки]
!!HE-1&w101>0:Ww101;          [возвращаем герою неиспользованные ОД. Если ОД не хватило, в w101 остается "долг ОД" для списания с следующем дне]

!?OB3;                        [Высадка из лодки]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!HE-1:W?y1;                  [y1 - ОД героя]
!!IF:W-1;                     [выбор текущего героя]
!!VRw101:Sy1;                 [y1 - ОД героя при посадке(высадке) в лодку]

!$OB3;                        [Высадка из лодки - пост-триггрер]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!HE-1:S5/?y1 X?y2/?y3/d/d/d/d/d; [y1 - Уровень навигации героя (0..3), y2=0 и y3=5 для специалиста по Навигации]
!!VRy1&y2=0/y3=5:+1;          [y1 - Уровень навигации героя с учетом специализации (0..4)
!!VRy2:S6 -y1 *100;           [y2 - стоимость посадки(высадки) на лодку в ОД (Базовая стоимость 600 ОД и минус 100 ОД за каждый уровень Навигации/Специализацию Навигации)]
!!HE-1:A2/123/d/y3;           [y3=1, если на герое Шляпа Морского Капитана]
!!VRy2&y3=1:S0;               [обнуляем стоимость посадки(высадки) если на герое Шляпа Морского Капитана]
!!IF:W-1;                     [выбор текущего героя]
!!VRw101:-y2;                 [w101 - расчетные ОД героя после посадки]
!!HE-1&w101>0:Ww101;          [возвращаем герою неиспользованные ОД. Если ОД не хватило, в w101 остается "долг ОД" для списания с следующем дне]

!?TM50:S2/999/1/255;          [каждый день для всех игроков начиная со 2го дня списываем долги по посадке на лодку]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW:H-1/1/0;                 [v1 - количество героев текущего игрока]
!!DO7907/1/v1/1&v1>0:P;       [для каждого героя текущего игрока списываем долги по посадке на лодку]

!?FU7907;                     [списываем долги по посадке на лодку]
!!OW:H-1/2/x16;               [v2-номер героя текущего игрока]
!!IF:Wv2;                     [выбор v2-го героя]
!!VRw101&w101>0:S0;           [обнуляем w101, если долга нет]
!!FU&w101>-1:E;               [выход, если долга нет]
!!HEv2:W?y1;                  [y1 - ОД героя]
!!VRy2:Sy1 +w101;             [  y2 - ОД героя ]
!!VRy2&y2<0:S0;               [ после списания ]
!!HEv2:Wy2;                   [устанавливаем ОД героя]
!!VRw101:+y1;                 [списываем долг (w101)]
!!VRw101&w101>-1:S0;          [обнуляем w101, если долга нет]

!?TM50:S2/999/1/255;          [каждый день для всех игроков начиная со 2го пересчитываем бонус Навигации (замена на стандартные для Логистики 10/20/30%)]
!!UN:P773/?y1;                [проверяем включена ли опция 773 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW:H-1/1/0;                 [v1 - количество героев текущего игрока]
!!DO7908/1/v1/1&v1>0:P;       [для каждого героя текущего игрока пересчитываем бонус Навигации]

!?FU7908;                     [пересчитываем бонус Навигации]
!!OW:H-1/2/x16;               [v2-номер героя текущего игрока]
!!HEv2:P?y1/?y2/?y3;          [y1-y3 - координаты героя]
!!TRy1/y2/y3:T?y4/d/d/d/d/d/d/d; [y4=8, если герой на воде (в лодке)]
!!FU&y4<>8:E;                 [выход, если герой не на воде]
!!HEv2:S5/?y1 X?y2/?y3/d/d/d/d/d Ed/?y4; [y1 - Уровень навигации героя (0..3), y2=0 и y3=5 для специалиста по Навигации, y4 - уровень героя]
!!FU&y1=0:E;                  [выход, если навигации у героя нет]
!!VRy5:Sy1 *600;              [y5 - пересчет от уровня Навигации]
!!VRy6&y2=0/y3=5:Sy1 *30 *y4; [y6 - пересчет от специализации Навигации]
!!VRy5&y2=0/y3=5:+y6;         [y5 - общий пересчет от уровня Навигации и специализации]
!!HEv2:W?y6;                  [y6 - текущие ОД героя]
!!VRy6:-y5;                   [y6 - пересчитанные ОД героя]
!!HEv2:Wy6;                   [обновляем ОД героя]

!?FU7909;                      [замена вторичных навыков или специализаций героев]
** x1 - навык, который нужно заменить или специализация на вторичном навыке
** x2 - уровень навыка, который нужно заменить (0-любой)
** x3 - навык, НА который нужно заменить или специализация
** x4 - уровень навыка, НА который нужно заменить (0-то же, что и у заменяемого навыка) или тип специализации (0-вторичный навык)
** x5 - 1 - заменить специализацию по вторичному навыку, другое - заменить вторичный навык
** x6 - номер z-переменной с новым описанием специализации (0-стандартное описание)
** x16 - номер героя
** ПРИМЕР: DO7909/0/155/1:P4/0/6/0/0   [для каждого героя меняем навык Дипломатия на Лидерство]
!!HEx16&x5=1:X?y2/?y3/d/d/d/d/d; [y2=0,y3=номер навыка для специалистов по вторичному навыку]
!!FU&y2<>0/x5=1:E;               [выход, если герой не спец. по вторичному навыку]
!!FU&y3<>x1/x5=1:E;              [выход, если герой не спец. по заменяемому вторичному навыку]
!!HEx16&x5=1:Xx4/x3/d/d/d/d/d;   [замена специализации героя]
!!UN&x5=1:G2/x16/2/x6;           [установить описание специализации после замены]
!!FU&x5=1:E;                     [окончание замены специализации]
!!HEx16:Sx1/?y1;                 [y1 - уровень заменяемого навыка у героя]
!!FU&y1=0:E;                     [выход, если заменяемый навык отсутствует]
!!FU&y1<>x2/x2>0:E;              [выход, если уровень заменяемого навыка не соответствует требуемуму]
!!HEx16:Sx3/?y2;                 [y2 - уровень навыка у героя на который нужно заменить]
!!VRy2&x4=0:+y1;                 [y2 - новый уровень навыка у героя]
!!VRy2&x4>0:+x4;                 [        на который нужно заменить]
!!VRy2&y2>3:S3;                  [        не более 3 (эксперт)     ]
!!HEx16:Sx1/0;                   [убираем заменяемый навык]
!!HEx16:Sx3/y2;                  [добавляем новый навык]

** Замена Навигации на Логистику и бан водных заклинаний/артефактов, если воды на карте менее 15%
!#UN:P773/?v9;                        [проверяем включена ли опция 773 в v2]
!#VRz901&v9>0:Sz750 +^%Z179060^;      [получаем базовое описание навыка Навигация]
!#UN&v9>0:G0/5/1/901;                 [Устанавливаем базовое описание навыка Навигация]
!#VRz900&v9>0:Sz751 +^%Z179061^;      [получаем продвинутое описание навыка Навигация]
!#UN&v9>0:G0/5/2/900;                 [Устанавливаем продвинутое описание навыка Навигация]
!#VRz899&v9>0:Sz752 +^%Z179062^;      [получаем экспертное описание навыка Навигация]
!#UN&v9>0:G0/5/3/899;                 [Устанавливаем экспертное описание навыка Навигация]
!#VRz898&v9>0:Sz179063;               [получаем описание специализации Навигация]
!#DO7909/0/155/1&v9>0:P5/0/5/0/1/898; [Для героев со специализацией Навигация меняем описание специализации]
!#UN&v9>0:X?v3/?v4;                   [v3,v4 - размер карты]
!#VRv4&v9>0:Sv3;                      [v4 - ширина карты]
!#VRv3&v9>0:*v3 -1;                   [v3 - макс номер квадрата]
!#VRv1&v9>0:S0;                       [обнуляем счетчик водных квадратов]
!#VRv5&v9>0:S0;                       [обнуляем счетчик квадратов]
!#DO7906/0/v3/1&v9>0:P;               [перебираем все "строки" поверхности, v4-размер, v5-квадрат]
!#VRv3&v9>0:+1;                       [v3 - количество квадратов карты]
!#VRv4&v9>0:Sv1 *100 :v3;             [v4 - процент воды на поверхности]
!#VRv9&v9>0/v4>14:S0;                 [если воды на карте не менее 15%, не заменяем Навигацию и не ограничиваем водные заклинания/артефакты]
!#UN&v9>0:P773/2;                     [если карта не водная, значение опции 2]
!#VRz898&v9>0:Sz179064;               [получаем описание специализации Логистика]
!#DO7909/0/155/1&v9>0:P5/0/2/0/1/898; [для каждого героя меняем специализацию Навигация на Логистика]
!#DO7909/0/155/1&v9>0:P5/0/2/0/0/0;   [для каждого героя меняем навык Навигация на Логистика]
!#UN&v9>0:A71/1 A90/1 A123/1 A136/1;  [запрет водных артефактов]
!#UN&v9>0:P152/1 P153/1 P221/1;       [включение опций Бана водных заклинаний]
!#UN&v9>0:J0/0/1 J0/1/1 J0/7/1;       [Бан водных заклинаний в Гильдиях Магов и Пирамидах]
!#UN&v9>0:P148/?v1;                   [v1=1, если включен бан заклинаний у ученых]
!#FU10805&v9>0/v1=1:P;                [бан заклинаний у ученых]
!#UN&v9>0:P150/?v1;                   [v1=1, если включен бан заклинаний в свитках]
!#FU10809&v9>0/v1=1:P;                [бан заклинаний в свитках]

!?PI;                                   [пост-инструкция]
!!UN:P773/?y1;                          [y1=2 для безводной карты]
!!FU&y1<>2:E;                           [выход, если карта водная]
!!UN:U113/-1/?y1;                       [Количество Хижин Ведьмы в y1]
!!VRv1:S-1;                             [инициализация v1 для быстрого поиска]
!!DO7956/1/y1/1&y1>0:P;                 [Для каждой Хижины Ведьм меняем Навигацию на Логистику]
!!UN:U104/-1/?y1;                       [Количество Университетов в y1]
!!VRv1:S-1;                             [инициализация v1 для быстрого поиска]
!!DO7957/1/y1/1&y1>0:P;                 [Для каждого Университета меняем Навигацию на другой навык]
!!UN:U81/-1/?y1;                        [Количество Ученых в y1]
!!VRv1:S-1;                             [инициализация v1 для быстрого поиска]
!!DO7959/1/y1/1&y1>0:P;                 [Для каждого Ученого меняем Навигацию на Логистику]
!!UN:P151/?y1;                          [y1=1, если включен бан заклинаний в стартовых у героев]
!!FU10811&y1=1:P;                       [бан заклинаний в стартовых у героев]

!?FU7956;                               [Замена Навигации на Логистику в Хижинах Ведьм]
!!UN:U113/-1/-1/1;                      [v1..v3 - координаты хижины]
!!WH1:S?y1;                             [y1 - навык ведьмы]
!!WH1&y1=5:S2;                          [меняем Навигацию на Логистику]

!?FU7957;                               [Замена Навигации на Логистику/Поиск Пути/Разведку/Обучение в Университетах]
!!UN:U104/-1/-1/1;                      [v1..v3 - координаты Университета]
!!UR1:S?y1/?y2/?y3/?y4;                 [y1-y4 - навыки Университета]
!!FU&y1<>5/y2<>5/y3<>5/y4<>5:E;         [Выход, если Навигация не предлагается]
!!VRy5&y1<>21/y2<>21/y3<>21/y4<>21:S21; [y5 - навык "Обучение" на замену, если не предлагается в другом слоте]
!!VRy5&y1<>3/y2<>3/y3<>3/y4<>3:S3;      [y5 - навык "Разведка" на замену, если не предлагается в другом слоте]
!!VRy5&y1<>0/y2<>0/y3<>0/y4<>0:S0;      [y5 - навык "Поиск Пути" на замену, если не предлагается в другом слоте]
!!VRy5&y1<>2/y2<>2/y3<>2/y4<>2:S2;      [y5 - навык "Логистика" на замену, если не предлагается в другом слоте]
!!UR1&y1=5:Sy5/d/d/d;                   [замена Навигации, если она в 1м слоте Университета]
!!UR1&y2=5:Sd/y5/d/d;                   [замена Навигации, если она во 2м слоте Университета]
!!UR1&y3=5:Sd/d/y5/d;                   [замена Навигации, если она в 3м слоте Университета]
!!UR1&y4=5:Sd/d/d/y5;                   [замена Навигации, если она в 4м слоте Университета]

!?FU7959;                               [Замена Навигации на Логистику/Поиск Пути/Разведку/Обучение в Университетах]
!!UN:U81/-1/-1/1;                       [v1..v3 - координаты Ученого]
!!SCv1/v2/v3:T?y1 S?y2;                 [y1/y2 тип/навык Ученого]
!!SCv1/v2/v3&y1=1/y2=5:S2;              [если Ученый предлагает "Навигацию" заменяем ее на "Логистику"]

** end
