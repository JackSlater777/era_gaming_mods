ZVSE
ERMS_ScriptDate=28.11(November).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : Misfortune and demoralization
** Name rus.     : Неудача и деморализация
** Options       : 767
** Dialogs       : -
** Variables     : v7907-v7908
** Tmp variables : z1
** Timers        : -
** Functions     : -
** PO-values     : -

** Добавляет в игру отрицательную удачу (неудачу).
** Шанс срабатывания положительной удачи - 5% за уровень удачи отряда.
** Шанс срабатывания отрицательной удачи (неудачи) - 10% за уровень неудачи отряда.
** устанавливает ограничения на максимльную/минимальную мораль отрядов в -10..+20.
** Шанс срабатывания положительной морали - 5% за уровень морали отряда.
** Шанс срабатывания отрицательной морали - 10% за уровень деморалиации отряда.

!?FU(OnAfterErmInstructions);           // при входе в игру - установка параметров морали (спс. Igor)
!!UN:P767/?y1;                          // выход если опция не включена
!!if&y1<>0:;                            // если опция включена
  !!UN:C4605318/1/20;                   // максимум морали
  !!UN:C4605344/1/20;                   // ...
  !!UN:C4605354/1/20;                   // делитель шанса выпадения положительной морали - 1/20
  !!UN:C4605324/1/-10;                  // минимум морали
  !!UN:C4605331/1/-10;                  // ...
  !!UN:C4605854/1/10;                   // делитель шанса выпадения отрицательной морали- 1/10
!!el:;                                  // если выключена, выставляем значения по-умолчанию
  !!UN:C4605318/1/3;                    // ...
  !!UN:C4605344/1/3;                    // ...
  !!UN:C4605354/1/24;                   // ...
  !!UN:C4605324/1/-3;                   // ...
  !!UN:C4605331/1/-3;                   // ...
  !!UN:C4605854/1/12;                   // ...
!!en:;

!?FU(OnAfterLoadGame);                  // после загрузки - установка параметров морали (спс. Igor)
!!UN:P767/?y1;                          // выход если опция не включена
!!if&y1<>0:;                            // если опция включена
  !!UN:C4605318/1/20;                   // максимум морали
  !!UN:C4605344/1/20;                   // ...
  !!UN:C4605354/1/20;                   // делитель шанса выпадения положительной морали - 1/20
  !!UN:C4605324/1/-10;                  // минимум морали
  !!UN:C4605331/1/-10;                  // ...
  !!UN:C4605854/1/10;                   // делитель шанса выпадения отрицательной морали- 1/10
!!el:;                                  // если выключена, выставляем значения по-умолчанию
  !!UN:C4605318/1/3;                    // ...
  !!UN:C4605344/1/3;                    // ...
  !!UN:C4605354/1/24;                   // ...
  !!UN:C4605324/1/-3;                   // ...
  !!UN:C4605331/1/-3;                   // ...
  !!UN:C4605854/1/12;                   // ...
!!en:;

!?BG0;                        [перед действием]
!!UN:P767/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!BG:N?y1 A?y2 E?y3;          [y1/y2/y3 - активный отряд/действие/целевой отряд]
!!FU&y3=-1:E;                 [выход, если целевой отряд не существует (например, убит абилкой перед основной атакой)]
!!if|y2=6/y2=7:;              [если отряд атакует]
  !!BMy1:G213/?v7907/d G213/0/d;[сохранение в v7907 и обнуление текущей (не)удачи атакующего отряда]
  !!BMy3:G213/?v7908/d G213/0/d;[сохранение в v7908 и обнуление текущей (не)удачи целевого отряда]
!!en:;

!?MF1;                        [перед нанесением физического урона]
!!UN:P767/?y1; !!FU&y1=0:E;   [выход если опция не включена]
!!MF:W?y1 N?y3;               [y1/y3 - тип атаки, отряд получающий урон]
!!FU&y1>0:E;                  [выход, если урон от рва или башни]
!!BG:N?y1 E?y4;               [y1/y4 - ходящий отряд/цель]
!!FU|y1<0/y4<0:E;             [выход, если одного из взаимодействующих отрядов нет (например, огненный щит от трупа)]
!!VRy2:Sv7907;                [y2 - (не)удача атакующего отряда]
!!if&y3=y1:;                  [если ответный удар]
  !!VRy1:Sy4;                 [y1 - отряд, наносящий урон - отряд-цель]
  !!VRy2:Sv7908;              [y2 - текущая (не)удача отряда-цели]
!!en:;
!!FU&y2=0:E;                  [выход если (не)удача нулевая]
!!BMy1:G213/0/d;              [обнуляем текущую (не)удачу отряда]
!!VRy5:Sy2 *5;                [y5 - шанс выпадения удачи - 5% за уровень]
!!VRy5&y2<0:*-2;              [y5 - шанс выпадения НЕудачи - 10% за уровень]
!!VRy4:S0 R99;                [y4 - случайное число 0.99]
!!FU&y5<y4:E;                 [выход, если (не)удача не выпала]
!!MF:F?y6;                    [y6 - урон, который получит отряд]
!!BMy1:T?y7;                  [y7 - тип существ в отряде наносящем урон]
!!UN:N3/2/y7/1;               [Z2 - название существ наносящих урон (мн.число)]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!if&y2>0:;                   [срабатывание удачи]
  !!VRy6:*3 :2;               [урон 150%]
  !!VRz1&f=0:Sz179186;        [получаем текст для вывода в лог битвы из ert]
  !!MM&f=0:Sz1;               [вывод текста в лог битвы]
  !!VRz1&f=0:S^GOODLUCK^;     [z1 - звуковой файл удачи]
  !!SN&f=0:P1;                [вывод звука удачи]
  !!BMy1&f=0:V18;             [анимация удачи на отряде]
!!el:;                        [срабатывание неудачи]
  !!VRy6::2;                  [урон 50%...]
  !!VRy6&y6=0:S1;             [...но не менее 1]
  !!VRz1&f=0:Sz179187;        [получаем текст для вывода в лог битвы из ert]
  !!MM&f=0:Sz1;               [вывод текста в лог битвы]
  !!VRz1&f=0:S^BADLUCK^;      [z1 - звуковой файл неудачи]
  !!SN&f=0:P1;                [вывод звука удачи]
  !!BMy1&f=0:V48;             [анимация неудачи на отряде]
!!en:;
!!MF:Fy6;                     [корректировка урона]

** end
