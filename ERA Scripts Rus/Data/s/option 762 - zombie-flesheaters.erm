ZVSE
ERMS_ScriptDate=12.4(April).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor (Idea: Ivor)
** Name          : zombie-flesheaters
** Name rus.     : зомби-трупоеды
** Options       : 762
** Dialogs       : -
** Variables     : v7920-v7922,z877-z878
** Tmp variables : z1
** Timers        : -
** Functions     : -
** PO-values     : -

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz877:Sz179180;            [получаем из ert новое название существа (ед.ч.)]
!!VRz878:Sz179181;            [получаем из ert новое название существа (мн.ч.)]
!!VRz879:Sz179183;            [получаем из ert новую подсказку по существу]
!!UN:G1/59/0/877 G1/59/1/878 G1/59/2/879; [установка новых текстов]
!!MA:F59/?y1;                 [y1 - FV зомби]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F59/y1;                  [обновляем FV зомби]

!?BG0;                        [перед действием]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRv7920:C-1/-1/-1;          [обнуляем v-переменные]
!!BG:N?y1 A?y2;               [y1 - активный отряд, y2 - действие (6 - атака)]
!!BMy1:T?y3;                  [y3 - тип атакующих существ (59 - зомби)]
!!FU|y3<>59/y2<>6:E;          [выход если не атака зомби]
!!BG:E?y2;                    [y2 - целевой отряд]
!!BMy2:F?y3;                  [y3 - флаги существ целевого отряда]
!!VRy4:Sy3 &16;               [y4=16, если существа целевого отряда живые]
!!FU&y4=0:E;                  [выход если зомби атакуют не отряд живых существ]
!!BMy2:N?y5 H?y6 L?y7;        [y5/y6/y7 - количество существ/макс. здоровье/потер. здоровье целевого отряда]
!!VRy5:*y6 -y7;               [y5 - суммарное здоровье целевого отряда перед атакой]
!!VRv7920:Cy1/y2/y5;          [v7920/v7921/v7922 - номер атакующего отряда/номер целевого отряда/здоровье целевого отряда]

!?MF1&v7920>=0;               [при нанесении урона]
!!UN:P762/?y1;                [проверяем включена ли опция 762 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!MF:F?y1;                    [y1 - урон, который получит целевой отряд]
!!if&y1<v7922:;               [если атака не уничтожит целевой отряд полностью]
  !!VRv7920:C-1/-1/-1;        [обнуляем v-переменные]
  !!FU:E;                     [выход]
!!en:;
!!BMv7920:N?y1 H?y2 L?y3 B?y4;[y1/y2/y3/y4 - количество существ/макс. здоровье/потер. здоровье/начальное количество отряда зомби]
!!if&y1=y4/y3=0:;               [если атака не уничтожит целевой отряд полностью]
  !!VRv7920:C-1/-1/-1;          [обнуляем v-переменные]
  !!FU:E;                       [выход если у отряда зомби нет потерь и раненных]
!!en:;
!!VRy5:Sy1 *y2 -y3 +v7922;    [y5 - новое суммарное здоровье отряда зомби]
!!VRy6:Sy4 *y2;               [y6 - максимально допустимое здоровье отряда зомби]
!!VRy5&y5>y6:Sy6;             [y5 - фактическое здоровье отряда зомби после трупоедства]
!!VRy6:Sy5 :y2;               [y6 - количество нераненых сомби после трупоедства]
!!VRy7:Sy5 %y2;               [y7 - здоровье раненого зомби]
!!VRy6&y7>0:+1;               [добавляем раненого зомби к общему количеству]
!!VRy8&y7>0:Sy2 -y7;          [y8 - потерянное здоровье раненого зомби]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!VRz1&f=0:Sz179182;          [получаем текст для вывода в лог битвы из ert]
!!MM&f=0:Sz1;                 [вывод текста в лог битвы]
!!VRz1&f=0:S^ANIMDEAD^;       [z1 - звуковой файл поднятия нежити]
!!SN&f=0:P1;                  [вывод звука поднятия нежити]
!!BMv7920&f=0:V74;            [анимация вампирской регенерации]
!!BMv7920:Ny6 Ly8;            [и восстановление отряда зомби]
!!BMv7921:F?y1;               [y1 - флаги целевого отряда]
!!VRy1:|813694976;            [добавляем цели флаги жертвы/клона/неизменяемости цвета]
!!BMv7921:Fy1 E0 K1;          [обновляем флаги и убиваем цель без возможности воскрешения]
!!VRv7920:C-1/-1/-1;          [обнуляем v-переменные]

** end
