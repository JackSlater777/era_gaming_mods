ZVSE2
** Author        : igrik
** Name          : 28 secondary skills
** Name rus.     : максимум вторичных навыков
** Options       : 824
fixed typo
** Опция устанавливает максимальное количество вторичных навыков, доступных героям
** По-умолчанию количество доступных навыков повышено до 10, значение настраивается в ini-файле

!?FU(ES_824_SetSecSkillLimit);
!!UN:P824/?(wogOption:y);               [включен ли скрипт]

!!if&(wogOption);
  !!UN:C4881872/(UNC_UINT8)/?(ssLimit:y);
  !!VRz1:S^Data\s\option 824 - 28 secondary skills.ini^; [z1 - имя ini-файла]
  !!VRz2:S^option 824^;                 [z2 - секция ini-файла]
  !!UN:N6/3/0/2/1;
  !!VR(value:y):Vz3 F8/28;              [кол-во втор.навыков из ini-файла]

  ; Store the current values
  !!UN:C4881872/(UNC_INT8)/?i^es_824_patch1^ C5091278/(UNC_INT8)/?i^es_824_patch2^ C5121386/(UNC_INT8)/?i^es_824_patch3^ C5718399/(UNC_INT8)/?i^es_824_patch4^;
  !!UN:C6229127/(UNC_INT8)/?i^es_824_patch5^ C6229840/(UNC_INT8)/?i^es_824_patch6^ C7621300/(UNC_INT8)/?i^es_824_patch7^;

  ; Set up new values
  !!UN:C4881872/(UNC_INT8)/(value) C5091278/(UNC_INT8)/(value) C5121386/(UNC_INT8)/(value) C5718399/(UNC_INT8)/(value);
  !!UN:C6229127/(UNC_INT8)/(value) C6229840/(UNC_INT8)/(value) C7621300/(UNC_INT8)/(value);
!!en;

!#FU(ES_824_SetSecSkillLimit):P;

!?FU(OnAfterLoadGame);
!!UN:P824/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!VRi^es_824_isLoadGame^:S(TRUE);

!?FU(OnGameEnter)&i^es_824_isLoadGame^;
!!FU(ES_824_SetSecSkillLimit):P;
!!VRi^es_824_isLoadGame^:S(FALSE);

!?FU(OnGameLeave);
!!UN:P824/?(wogOption:y);               [включен ли скрипт]

!!if&(wogOption);
  ; restore default values
  !!UN:C4881872/(UNC_INT8)/i^es_824_patch1^ C5091278/(UNC_INT8)/i^es_824_patch2^ C5121386/(UNC_INT8)/i^es_824_patch3^ C5718399/(UNC_INT8)/i^es_824_patch4^;
  !!UN:C6229127/(UNC_INT8)/i^es_824_patch5^ C6229840/(UNC_INT8)/i^es_824_patch6^ C7621300/(UNC_INT8)/i^es_824_patch7^;
!!en;

** end