ZVSE
ERMS_ScriptDate=2.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Size-limited cities
** Name rus.     : Города не резиновые
** Options       : 780
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : TM51(standard)
** Functions     : FU7915
** PO-values     : -

!#TM51:S8/999/7/255;          [каждую неделю для всех игроков начиная с 8го дня]

!?TM51;                       [каждую неделю начиная со второй]
!!UN:P780/?y1;                [проверяем включена ли опция 780 в y1]
!!FU&y1<>1:E;                 [выход если опция не включена(<>1)]
!!UN:U98/-1/?y1;              [Количество городов в y1]
!!VRy1:-1;                    [y1 - максимальный номер города на карте]
!!OW:C?y2;                    [y2 - текущий игрок]
!!DO7915/0/y1/1:Py2;          [проверяем все города на превышение лимита доступных для найма существ]

!?FU7915;                     {x1 - текущий игрок, x16 - номер города на карте]
!!CA0/x16:O?y1;               [y1 - владелец города]
!!FU&y1<>x1/y1>-1:E;          [выход если город не принадлежит текущему игроку и не нейтральный]
!!CA0/x16:T?y1;               [y1 - тип города]
!!UN:Ty1/0/0/?y11 Ty1/0/1/?y21 Ty1/1/0/?y12 Ty1/1/1/?y22 Ty1/2/0/?y13 Ty1/2/1/?y23 Ty1/3/0/?y14; [y1x/2x - тип неулучшенного/улучшенного]
!!UN:Ty1/3/1/?y24 Ty1/4/0/?y15 Ty1/4/1/?y25 Ty1/5/0/?y16 Ty1/5/1/?y26 Ty1/6/0/?y17 Ty1/6/1/?y27; [     монстра x-го уровня в городе     ]
!!MA:Gy11/?y31 Gy21/?y41 Gy12/?y32 Gy22/?y42 Gy13/?y33 Gy23/?y43 Gy14/?y34; [y3x/4x - базовый прирост неулучшенного/]
!!MA:Gy24/?y44 Gy15/?y35 Gy25/?y45 Gy16/?y36 Gy26/?y46 Gy17/?y37 Gy27/?y47; [    улучшенного монстра x-го уровня    ]
!!VRy2:S2;                    [y2=2 для базового прироста]
!!CA0/x16:B3/8;               [проверка на цитадель]
!!VRy2&1:S3;                  [y2=3 при построенной цитадели]
!!CA0/x16:B3/9;               [проверка на замок]
!!VRy2&1:S4;                  [y2=4 при построенном замке]
!!VRy51:Sy31 *y2 :2 *3;       [-----------------------------------------------]
!!VRy52:Sy32 *y2 :2 *3;       [                                               ]
!!VRy53:Sy33 *y2 :2 *3;       [        y5х - лимит существ х-го уровня        ]
!!VRy54:Sy34 *y2 :2 *3;       [(3*прирост от городских строений, кроме грааля)]
!!VRy55:Sy35 *y2 :2 *3;       [           без учета "зданий орды"             ]
!!VRy56:Sy36 *y2 :2 *3;       [                                               ]
!!VRy57:Sy37 *y2 :2 *3;       [-----------------------------------------------]
!!VRy2:S0;                    [обнуление y2]
!!CA0/x16:B3/18;              [------------------------------]
!!VRy2&1:S1;                  [проверка на первое здание орды]
!!CA0/x16:B3/19;              [  y2=1 если здание построено  ]
!!VRy2&1:S1;                  [------------------------------]
!!VRy53&y1=0/y2=1:+9;         [увеличенный лимит при отстроенной орде: Грифоны]
!!VRy52&y1=1/y2=1:+12;        [увеличенный лимит при отстроенной орде: Гномы]
!!VRy52&y1=2/y2=1:+12;        [увеличенный лимит при отстроенной орде: Гаргульи]
!!VRy51&y1=3/y2=1:+24;        [увеличенный лимит при отстроенной орде: Бесы]
!!VRy51&y1=4/y2=1:+18;        [увеличенный лимит при отстроенной орде: Скелеты]
!!VRy51&y1=5/y2=1:+21;        [увеличенный лимит при отстроенной орде: Троглодиты]
!!VRy51&y1=6/y2=1:+24;        [увеличенный лимит при отстроенной орде: Гоблины]
!!VRy51&y1=7/y2=1:+18;        [увеличенный лимит при отстроенной орде: Гноллы]
!!VRy51&y1=8/y2=1:+30;        [увеличенный лимит при отстроенной орде: Феи]
!!VRy2:S0;                    [обнуление y2]
!!CA0/x16:B3/24;              [------------------------------]
!!VRy2&1:S1;                  [проверка на второе здание орды]
!!CA0/x16:B3/25;              [  y2=1 если здание построено  ]
!!VRy2&1:S1;                  [------------------------------]
!!VRy55&y1=1/y2=1:+6;         [увеличенный лимит при отстроенной орде: Дендроиды]
!!VRy53&y1=3/y2=1:+9;         [увеличенный лимит при отстроенной орде: Церберы]
***
!!UN:J2/?y60;                 [y60 - уровень сложности]
!!VRy60:*-1 +5;               [y60 - уровень сложности (5/4/3/2/1)]
!!UN:P785/?y70;               [y70 - опция "батраки"]
!!CA0/x16:P?y71/?y72/?y73;    [y71/y72/y73 - координаты города]
!!SN:W^peons_%Y71_%Y72_%Y73^/?y74;[y74 - ежедневный доход от батраков в городе]
!!OW:Ix1/?y75;                [y75=1 для ИИ]
***
!!CA0/x16:M1/0/?y3/?y4;       [y3/y4 - кол-во доступных монстров 1го уровня]
!!CA0/x16:B3/30;              [f1 - отстроено жилище 1го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y51:M1/0/y51/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/0/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/37;            [f1 - отстроено жилище 1го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y51/1:M1/0/?i/y51;[удаляем существ сверх лимита]
    !!UN:Ty1/0/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y51:Sy3 -y51 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/1/?y3/?y4;       [y3/y4 - кол-во доступных монстров 2го уровня]
!!CA0/x16:B3/31;              [f1 - отстроено жилище 2го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y52:M1/1/y52/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/1/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/38;            [f1 - отстроено жилище 2го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y52/1:M1/1/?i/y52;[удаляем существ сверх лимита]
    !!UN:Ty1/1/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y52:Sy3 -y52 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/2/?y3/?y4;       [y3/y4 - кол-во доступных монстров 3го уровня]
!!CA0/x16:B3/32;              [f1 - отстроено жилище 3го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y53:M1/2/y53/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/2/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/39;            [f1 - отстроено жилище 3го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y53/1:M1/2/?i/y53;[удаляем существ сверх лимита]
    !!UN:Ty1/2/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y53:Sy3 -y53 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/3/?y3/?y4;       [y3/y4 - кол-во доступных монстров 4го уровня]
!!CA0/x16:B3/33;              [f1 - отстроено жилище 4го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y54:M1/3/y54/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/3/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/40;            [f1 - отстроено жилище 4го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y54/1:M1/3/?i/y54;[удаляем существ сверх лимита]
    !!UN:Ty1/3/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y54:Sy3 -y54 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/4/?y3/?y4;       [y3/y4 - кол-во доступных монстров 5го уровня]
!!CA0/x16:B3/34;              [f1 - отстроено жилище 5го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y55:M1/4/y55/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/4/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/41;            [f1 - отстроено жилище 5го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y55/1:M1/4/?i/y55;[удаляем существ сверх лимита]
    !!UN:Ty1/4/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y55:Sy3 -y55 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/5/?y3/?y4;       [y3/y4 - кол-во доступных монстров 6го уровня]
!!CA0/x16:B3/35;              [f1 - отстроено жилище 6го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y56:M1/5/y56/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/5/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/42;            [f1 - отстроено жилище 6го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y56/1:M1/5/?i/y56;[удаляем существ сверх лимита]
    !!UN:Ty1/5/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y56:Sy3 -y56 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!CA0/x16:M1/6/?y3/?y4;       [y3/y4 - кол-во доступных монстров 7го уровня]
!!CA0/x16:B3/36;              [f1 - отстроено жилище 7го уровня (downgrade)]
!!if&1:;
  !!CA0/x16&y3>y57:M1/6/y57/?i;[удаляем существ сверх лимита]
  !!UN:Ty1/6/0/?y61;          [y61 - тип монстра]
  !!CA0/x16:B3/43;            [f1 - отстроено жилище 7го уровня (upgrade)]
  !!if&1:;
    !!CA0/x16&y4>y57/1:M1/6/?i/y57;[удаляем существ сверх лимита]
    !!UN:Ty1/6/1/?y61;        [y61 - тип монстра]
  !!en:;
  !!MA:Fy61/?y62;             [y62 - FV одного распускаемого существа]
  !!VRy3&y4>y4:Sy4;           [y3 - кол-во распускаемых существ]
  !!VRy63:S0;                 [y63 - доход от перевода распускаемых]
  !!VRy63&y3>y57:Sy3 -y57 *y62 :50 :y60; [...существ в батраки]
  !!VRy74&y70>0/y75=1:+y63;   [увеличиваем доход от батраков если опция "батраки" включена и город принадлежит ИИ]
!!en:;
***
!!SN&y70>0/y75=1:W^peons_%Y71_%Y72_%Y73^/y74;[обновление ежедневного дохода от батраков в городе]

** end
