ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Gamecreator
** Name          : Disable Necromancy
** Name rus.     : Отключение Некромантии

!?FU(OnAfterErmInstructions);
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN&y2<>0:Ey2/1/5128006/(ES.DisableNecromancy); 0x4E3F40 + 0x6 - necromancy power (after esp/ebp and locals)

!?FU(OnAfterLoadGame);
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN&y2<>0:Ey2/1/5128006/(ES.DisableNecromancy); 0x4E3F40 + 0x6 - necromancy power (after esp/ebp and locals)

!?CM2; click in hero screen
!!CM:F?y1 I?y2 S?y3;
!!FU|y1<>4/y2<79/y2>86/y3<>12:E; exit if not Ctrl+LMB on skill
!!VRy2:-78; get skill slot from dialog element
!!HE-1:Sy2/?y1/1 N?y3;
!!FU&y1<>12:E; exit if not Necromancy was clicked
!!CM:R0; disable default action
!!SN:W^es.interface.dn.%Y3^/?y4; check state for this hero
!!UN:C6919480/4/?y2;
!!VRy5:Sy3 *1170 +136736 +y2; hero struct = [0x699538]+0x21620+0x492*num
!!SN:E5127888/2/y5; get necro creature
!!VRy5:Sv1; save result
!!IF&y4=0:Q1/21/y5/2^{%T(es.interface.dn1)}^; ask
!!IF&y4<>0:Q1/21/y5/2^{%T(es.interface.dn2)}^; ask
!!VRy4&1:*-1 +1; 0<->1
!!SN:W^es.interface.dn.%Y3^/y4; save
!!SN:W^NecromancyDisabled%Y3^/y4;

!?CM3;                        [клик в окне встречи героев]
!!CM:F?y1 I?y2 S?y3;
!!FU|y1<>4/y2<200/y2>215/y3<>12:E; exit if not Ctrl+LMB on skill
!!CM&y2<208:H?y3/?i;          [y3 - номер героя]
!!CM&y2>207:H?i/?y3;          [...]
!!VRy2:-199;                  [y2 - номер слота втор. навыка]
!!VRy2&y2>8:-8;               [...]
!!OW:C?y6;                    [y6 - текущий игрок]
!!HEy3:Sy2/?y1/1 O?y7;        [y7 - хозяин героя]
!!FU|y1<>12/y6<>y7:E; exit if not Necromancy was clicked или клик на навыке союзного героя
!!CM:R0; disable default action
!!SN:W^es.interface.dn.%Y3^/?y4; check state for this hero
!!UN:C6919480/4/?y2;
!!VRy5:Sy3 *1170 +136736 +y2; hero struct = [0x699538]+0x21620+0x492*num
!!SN:E5127888/2/y5; get necro creature
!!VRy5:Sv1; save result
!!IF&y4=0:Q1/21/y5/2^{%T(es.interface.dn1)}^; ask
!!IF&y4<>0:Q1/21/y5/2^{%T(es.interface.dn2)}^; ask
!!VRy4&1:*-1 +1; 0<->1
!!SN:W^es.interface.dn.%Y3^/y4; save
!!SN:W^NecromancyDisabled%Y3^/y4;

!?FU(ES.DisableNecromancy);
; this = hero struct
; arg1 = capAt1 (not used)
!!SN:X?y1/?y2; context, execOrigCode
!!VRy2:Sy1 +24; ptr to ecx(=this)
!!UN:Cy2/4/?y3; get this
!!VRy3:+26; this->number
!!UN:Cy3/4/?y4; get hero number
!!SN:W^NecromancyDisabled%Y4^/?y3; check state for this hero
!!if&y3=0:; necromancy enabled
    !!SN:X?y1/1; make sure it executes original code
    !!UN:C5128603/4/1065353216; immed = 1.0 (restore default)
!!el:; necromancy disabled
    !!SN:X?y1/0; do not execute
    !!VRy2:Sy1 +32; return addr
    !!UN:Cy2/4/5128600; return to 0x4E4198 (alternate path: immediate returned to FPU stack)
    !!UN:C5128603/4/0; immed = 0.0
!!en:;
**end
