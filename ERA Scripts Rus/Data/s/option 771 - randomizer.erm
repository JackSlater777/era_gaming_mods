ZVSE
ERMS_ScriptDate=20.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Randomizer
** Name rus.     : Рандомизатор
** Options       : 771
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v3,z1-z2,z30-z36
** Timers        : -
** Functions     : FU7900-FU7903,FU7952,FU7958,FU7965-FU7966
** PO-values     : -

!?PI;                         [события в начале игры]
!!UN:P771/?y1;                [проверяем включена ли опция 771 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!VRz1:S^Data\s\option 771 - randomizer.ini^; [z1 - имя ini-файла]
!!VRz2:S^option 771^;         [z2 - секция ini-файла]
!!UN:N6/30/100/2/1 N6/31/1/2/1 N6/32/2/2/1 N6/33/3/2/1 N6/34/4/2/1 N6/35/5/2/1 N6/36/6/2/1; [z30-z36 - текстровые значения опций 0..6]
!!VRy100:Vz30;                [y100 - числовое значение опции 100]
!!VRy1:Vz31;                  [y1 - числовое значение опции 1]
!!VRy2:Vz32;                  [y2 - числовое значение опции 2]
!!VRy3:Vz33;                  [y3 - числовое значение опции 3]
!!VRy4:Vz34;                  [y4 - числовое значение опции 4]
!!VRy5:Vz35;                  [y5 - числовое значение опции 5]
!!VRy6:Vz36;                  [y6 - числовое значение опции 6]
** На старте каждый герой получает до 3х проклятий
** x1 - корреляция с уровнем сложности, x2 - шанс для человека, x3 - шанс для нейтрала/ИИ
!!DO7901/0/155/1|y1>0/y2>0:Py100/y1/y2; [вызов функции, если шансы не нулевые]
!!DO7901/0/155/1|y1>0/y2>0:Py100/y1/y2; [вызов функции, если шансы не нулевые]
!!DO7901/0/155/1|y1>0/y2>0:Py100/y1/y2; [вызов функции, если шансы не нулевые]
** На старте каждый город получает до 3х ограничений на улучшенное жилище, уровень гильдии магов или хранилище ресурсов
** x1 - корреляция с уровнем сложности, x2 - шанс для человека, x3 - шанс для нейтрала/ИИ
!!UN:U98/-1/?y99; !!VRy99:-1; [y99 - максимальный номер города на карте]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7902/0/y99/1|y3>0/y4>0:Py100/y3/y4; [вызов функции, если шансы не нулевые]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7902/0/y99/1|y3>0/y4>0:Py100/y3/y4; [вызов функции, если шансы не нулевые]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7902/0/y99/1|y3>0/y4>0:Py100/y3/y4; [вызов функции, если шансы не нулевые]
** На старте каждая шахта (кроме дерева и руды) получает охрану
** x1 - шанс получения шахтой охраны, x2 - коэффициент количества охранников (1..9) для (S..2XL)
!!UN:U53/-1/?y99;             [Количество шахт в y99]
!!VRy99:-1;                   [y99 - максимальный номер шахты]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!UN:X?y98/?y97;              [y97,98 - этажность/размер карты]
!!VRy97:+1 *y98 :32;          [y97 - коэффициент количества охранников (1..9) для (S..2XL)]
!!DO7903/0/y99/1|y5>0:Py5/y97;[вызов функции, если шанс не нулевой]
** разброс приростов существ
!!DO7952/0/196/1|y6>0:Py6;    [случайные изменения приростов существ, если разброс не нулевой]
!!DO7958/0/62/1|y6>0:P;       [выравнивание приростов up- и downgrade'ов в городах]

!?FU7900;                     [расчет вероятности для игрока x1 - корреляция, x2 - шанс для человека, x3 - шанс для ИИ, x4 - игрок, x5 - итог. шанс срабатывания]
!!VRx5&x4=-1:Sx3;             [возвращаем шанс ИИ, если игрока нет (-1)]
!!FU&x4=-1:E;                 [выход, если игрока нет (-1)]
!!OW:Ix4/?y1;                 [y1=0 для человека, 1 для ИИ]
!!VRx5&y1=1:Sx3;              [возвращаем шанс ИИ, если игрок - ИИ]
!!FU&y1=1:E;                  [выход, если игрок - ИИ]
!!UN:J2/?y1;                  [y1 - уровень сложности для человека (0..4)]
!!VRx5:Sx2;                   [x5 - шанс для человека без учета уровня сложности]
!!VRy1&y1=0:S80;              [80% для уровня "Легко"]
!!VRy1&y1=1:S100;             [100% для уровня "Норма"]
!!VRy1&y1=2:S130;             [130% для уровня "Тяжело"]
!!VRy1&y1=3:S160;             [160% для уровня "Эксперт"]
!!VRy1&y1=4:S200;             [200% для уровня "Невозможно"]
!!VRx5&x1>0:*x1 *y1 :100;     [y2 - шанс срабатывания для человека]

!?FU7901;                     [На старте каждый герой получает проклятие]
!!HEx16:O?y1;                 [y1 - хозяин героя (-1 - нет)]
!!FU7900:Px1/x2/x3/y1/?y2;    [y2 - шанс срабатывания]
!!VRy1:S0 R999;               [y1 - случайное число (0..999)]
!!FU&y1>=y2:E;                [выход, если событие не случилось]
!!VRy1:S22 R38;               [y1 - случайное проклятие (22..60)]
!!VRy1&y1=36:S2;              [Запрет посещения сундуков заменяем на блокировку слота артефакта]
!!VRy1&y1=38:S61;             [Запрет подбора ресурсов заменяем на запрет посещения колодца]
!!VRy1&y1=27:S6;              [запрет посещения камней знания заменяем на потерю маны]
!!VRy1&y1=49:S10;             [запрет посещения порталов заменяем на потерю ОД]
!!VRy1&y1=35:S4;              [запрет посещения подземных врат заменяем на потерю золота]
!!VRy2:S0;                    [по умолчанию сила проклятия 0]
!!VRy2&y1=2:S-1;              [случайный слот для блокировки слота артефакта]
!!VRy2&y1=6:S3;               [3 единицы для проклятия потери маны]
!!VRy2&y1=10:S50;             [50 единиц для проклятия потери ОД]
!!VRy2&y1=4:S50;              [50 единиц для проклятия потери золота]
!!HEx16:Yy1/y2/999/1;         [Установка проклятия игроку (если такое уже есть, не суммируется)]

!?FU7902;                     [На старте каждый город получает ограничение на улучшенное жилище или уровень гильдии магов]
!!UN:U98/-1/-1/1;             [получаем координаты города в v1-v3]
!!CA1:O?y1;                   [y1 - хозяин города (-1 - нет)]
!!FU7900:Px1/x2/x3/y1/?y2;    [y2 - шанс срабатывания]
!!VRy1:S0 R999;               [y1 - случайное число (0..999)]
!!FU&y1>=y2:E;                [выход, если событие не случилось]
!!VRy1:S0 R8;                 [y1 - случайное число (0..8)]
!!CA1:T?y2;                   [y2 - тип города]
** 0 - запрет последнего уровня магии
** 1 - запрет хранилища ресурсов (для Крепости - Кровавый обелиск)
** 2-8 - запрет улучшенного жилища существ соотв. уровня
!!VRy1:+35;                   [улучшенное жилище 1-7 уровня]
!!VRy1&y1=35/y2=0:S3;         [4й уровень гильдии для Замка]
!!VRy1&y1=35/y2=6:S2;         [3й уровень гильдии для Цитадели]
!!VRy1&y1=35/y2=7:S2;         [3й уровень гильдии для Крепости]
!!VRy1&y1=35/y2<>0/y2<>6/y2<>7:S4; [5й уровень гильдии для остальных городов]
!!VRy1&y1=36/y2=7:S22;        [Кровавый обелиск для Крепости, вместо хранилища ресурсов]
!!VRy1&y1=36:S15;             [Хранилище ресурсов, для замков кроме Крепости]
!!CA1:B5/y1;                  [Запрет отстройки здания]

!?FU7903;                     [На старте каждая шахта (кроме дерева и руды) получает охрану]
!!UN:U53/-1/-1/1;             [получаем координаты шахты в v1-v3]
!!VRy1:S0 R999;               [y1 - случайное число (0..999)]
!!MN1:R?y2;                   [y2 - тип ресурса]
!!FU|y1>=x1/y2=0/y2=2:E;      [выход, если событие не случилось, или дерево/руда]
!!VRy7:Sx2 *8;                [Базовое количество охранников 2го уровня]
!!VRy3:Sy7 Ry7;               [y3 - количество охранников 1го уровня]
!!VRy7:Sx2 *4;                [Базовое количество охранников 2го уровня]
!!VRy4:Sy7 Ry7;               [y4 - количество охранников 2го уровня]
!!VRy7:Sx2 *2;                [Базовое количество охранников 3го уровня]
!!VRy5:Sy7 Ry7;               [y5 - количество охранников 3го уровня]
!!VRy6:S0 R1;                 [y6-0..1 тип набора охранников]
!!MN1&y2=1/y6=0:M0/44/y4 M1/46/y5 M2/42/y3;   [Охрана для ртути: бесы, гоги, гончие]
!!MN1&y2=1/y6=1:M0/58/y4 M1/60/y5 M2/56/y3;   [Охрана для ртути: скелеты, мертвяки, стражи]
!!MN1&y2=3/y6=0:M0/72/y4 M1/74/y5 M2/70/y3;   [Охрана для серы: троглодиты, гарпии, бехолдеры]
!!MN1&y2=3/y6=1:M0/100/y4 M1/104/y5 M2/98/y3; [Охрана для серы: гноллы, ящеры, змии]
!!MN1&y2=4/y6=0:M0/16/y4 M1/18/y5 M2/14/y3;   [Охрана для кристаллов: кентавры, гномы, эльфы ]
!!MN1&y2=4/y6=1:M0/86/y4 M1/88/y5 M2/84/y3;   [Охрана для кристаллов: гоблины, наездники, орки]
!!MN1&y2=5/y6=0:M0/30/y4 M1/32/y5 M2/28/y3;   [Охрана для драг.камней: гремлины, гаргульи, големы]
!!MN1&y2=5/y6=1:M0/2/y4 M1/4/y5 M2/0/y3;      [Охрана для драг.камней: копейщики, арбалетчики, грифоны]
!!MN1&y2=6/y6=0:M0/127/y4 M1/119/y3 M2/123/y5;[Охрана для золота: феи, штормовые эл, ледяные эл.]
!!MN1&y2=6/y6=1:M0/143/y4 M1/138/y3 M2/142/y5;[Охрана для золота: хоббиты, воры, кочевники]

!?FU7952;                     [изменение прироста существа x16 на случайное числов диапазоне от (100-x1/10)% до (100+x1/10)% базового прироста]
!!MA:Gx16/?y1;                [y1 - базовый прирост существа]
!!VRy2:Sy1 *x1 :1000;         [y2 - изменение прироста при максимальном отклонении]
!!FU&y2=0:E;                  [выход, если прирост гарантированно не изменится]
!!VRy2:Sx1 *2;                [y2 - удвоенное отклонение]
!!VRy3:S0 Ry2 -x1;            [y3 - случайное отклонение в диапазоне от -x1 до x1]
!!VRy2:Sy1 *y3 :1000 +y1;     [y2 - новый базовый прирост]
!!MA:Gx16/y2;                 [обновляем базовый прирост существа]

!?FU7958;                     [выравнивание приростов up- и downgrade'ов в городах]
!!VRy1:Sx16 :7;               [y1 - номер города (0..8)]
!!VRy2:Sx16 %7;               [y2 - уровень существа в городе (0..6)]
!!UN:Ty1/y2/0/?y3 Ty1/y2/1/?y4;[y3/y4 - номер down-/upgrade существа]
!!MA:Gy3/?y5 Gy4/y5;          [прирост upgrade-существа равен приросту downgrade-существа]

!?FU7966;                     [возвращает в x2 случайный навык класса x1 (1 - боевой, 2 - вспомогательный, 3 - магический), x3 - город героя]
**                            [возвращает в x2 класс навыка x3 если x1=0]
!!VRy1:S0 R8;                 [y1 - случайное число (0..8)]
!!if&x1=0:;                   [определение класса навыка x3]
  !!VRx2|x3=1/x3=6/x3=10/x3=19/x3=20/x3=22/x3=23/x3=9/x3=26:S1;    [боевой навык]
  !!VRx2|x3=0/x3=2/x3=3/x3=4/x3=5/x3=13/x3=18/x3=21/x3=27/x3=12:S2;[вспомогательный навык]
  !!VRx2|x3=7/x3=8/x3=11/x3=15/x3=16/x3=14/x3=17/x3=24/x3=25:S3;   [магический навык]
!!en:;
!!if&x1=1:;                   [боевые навыки]
  !!VRx2&y1=0:S1;             [Стрельба]
  !!VRx2&y1=1:S6;             [Лидерство]
  !!VRx2&y1=2:S10;            [Баллистика]
  !!VRx2&y1=3:S19;            [Тактика]
  !!VRx2&y1=4:S20;            [Артиллерия]
  !!VRx2&y1=5:S22;            [Нападение]
  !!VRx2&y1=6:S23;            [Доспехи]
  !!VRx2&y1=7:S9;             [Удача]
  !!VRx2&y1=8:S27;            [Первая помощь]
!!en:;
!!if&x1=2:;                   [вспомогательные навыки]
  !!VRx2&y1=0:S0;             [Поиск Пути]
  !!VRx2&y1=1:S2;             [Логистика]
  !!VRx2&y1=2:S3;             [Разведка]
  !!VRx2&y1=3:S4;             [Дипломатия]
  !!VRx2&y1=4:S5;             [Навигация]
  !!VRx2&y1=5:S11;            [Зоркость]
  !!VRx2&y1=6:S13;            [Имущество]
  !!VRx2&y1=7:S18;            [Грамотность]
  !!VRx2&y1=8:S21;            [Обучение]
!!en:;
!!if&x1=3:;                   [магические навыки]
  !!VRx2&y1=0:S7;             [Мудрость]
  !!VRx2&y1=1:S8;             [Мистицизм]
  !!VRx2&y1=2:S15;            [Магия Воздуха]
  !!VRx2&y1=3:S16;            [Магия Воды]
  !!VRx2&y1=4:S14;            [Магия Огня]
  !!VRx2&y1=5:S17;            [Магия Земли]
  !!VRx2&y1=6:S24;            [Интеллект]
  !!VRx2&y1=7:S25;            [Волшебство]
  !!VRx2&y1=8:S26;            [Сопротивление]
!!en:;
!!if&x1>0/x3=4:;              [для героев Некрополя заменяем некоторые навыки на Некромантию]
  !!VRx2|x2=6/x2=4/x2=7:S12;  [Лидерство,Дипломатия,Мудрость > Некромантия]
!!en:;

!?FU7965;                     [изменение вторичных навыков героя x16, v1 - режим определения случайного навыка]
!!HEx16:S0/0 S1/0 S2/0 S3/0 S4/0 S5/0 S6/0 S7/0 S8/0 S9/0 S10/0 S11/0 S12/0 S13/0 S14/0; [обнуление вторичных навыков героя]
!!HEx16:S15/0 S16/0 S17/0 S18/0 S19/0 S20/0 S21/0 S22/0 S23/0 S24/0 S25/0 S26/0 S27/0;   [...]
!!HEx16:X?y1/?y2/?y3/?y4/?y5/?y6/?y7; [y1-y7 - параметры специализации героя]
!!HEx16:B2/?y8;               [y8 - класс героя]
!!VRy8::2;                    [y8 - тип города героя (4 - Некрополь)]
!!VRy9:S2;                    [по умолчанию класс случайного навыка - вспомогательные]
!!FU7966:P2/?y10/y8;          [по умолчанию первый навык - случайный навык вспомогательного класса]
!!VRy9&v1=3:S1 R2;            [для 3-го режима выбора случайного навыка второй навык выбирается из случайного класса]
!!if&y1=0:;                   [для специалистов по вторичному навыку]
  !!VRy10:Sy2;                [y10 - первый навык совпадает со специальностью]
  !!FU7966:P0/?y9/y2;         [y9 - основной класс навыков]
  !!if&v1=2:;                 [для 2-го режима выбора случайного навыка]
    !!VRy11:S0 R1;            [y11 - случайное число (0..1)]
    !!VRy9&y11=1:S2;          [в 50% случаях второй навык выбирается их класса вспомогательных]
  !!en:;
  !!VRy9&v1=3:S1 R2;          [для 3-го режима выбора случайного навыка второй навык выбирается из случайного класса]
!!en:;
!!if|y1=1/y1=4/y1=6:;         [для специалистов по существам, экстраспециалистам по существам и специалистам по улучшениям]
  !!VRy2&y1=6:Sy7;            [для специалистам по улучшениям расчет производится по улучшенному существу]
  !!MA:Ay2/?y11 Dy2/?y12 Xy2/?y13; [y11/y12/y13 - атака/защита/флаги спец.существа]
  !!VRy10&y11>y12:S22;        [если атака спец.существа больше его защиты, первый навык - Нападение]
  !!VRy10&y11<y12:S23;        [если атака спец.существа меньше его защиты, первый навык - Доспехи]
  !!VRy10&y11=y12:S19;        [если атака спец.существа равна его защите, первый навык - Тактика]
  !!VRy13:&4;                 [y13=4, если спец.существо - стрелок]
  !!VRy10&y13=4:S1;           [если спец.существо - стрелок, первый навык - Стрельба]
  !!VRy10&y1=1/y2=146:S20;    [если спец.существо - Баллиста, первый навык - Артиллерия]
  !!VRy10&y1=6/y7=146:S20;    [если спец.существо - Баллиста, первый навык - Артиллерия]
  !!VRy10&y1=1/y2=147:S27;    [если спец.существо - Палатка, первый навык - Первая Помощь]
  !!VRy10&y1=6/y7=147:S27;    [если спец.существо - Палатка, первый навык - Первая Помощь]
  !!VRy9:S1;                  [y9 - основной класс навыков - боевые]
  !!if&v1=2:;                 [для 2-го режима выбора случайного навыка]
    !!VRy11:S0 R1;            [y11 - случайное число (0..1)]
    !!VRy9&y11=1:S2;          [в 50% случаях второй навык выбирается их класса вспомогательных]
  !!en:;
  !!VRy9&v1=3:S1 R2;          [для 3-го режима выбора случайного навыка второй навык выбирается из случайного класса]
!!en:;
!!if&y1=3:;                   [для специалистов по Заклинанию]
  !!SSy2:S?y11;               [y11 - биты школ магии спец.заклинания]
  !!VRy14:Sy11 &8;            [y14=8 для Школы Земли]
  !!VRy13:Sy11 &4;            [y13=4 для Школы Воды]
  !!VRy12:Sy11 &2;            [y12=2 для Школы Огня]
  !!VRy11:&1;                 [y11=1 для Школы Воздуха]
  !!VRy10&y14=8:S17;          [y10 - первый навык - Школа спец.заклинания (приоритет: вода-огонь-воздух-земля)]
  !!VRy10&y11=1:S15;          [...]
  !!VRy10&y12=2:S14;          [...]
  !!VRy10&y13=4:S16;          [...]
  !!VRy9:S3;                  [y9 - основной класс навыков - магические]
  !!if&v1=2:;                 [для 2-го режима выбора случайного навыка]
    !!VRy11:S0 R1;            [y11 - случайное число (0..1)]
    !!VRy9&y11=1:S2;          [в 50% случаях второй навык выбирается их класса вспомогательных]
  !!en:;
  !!VRy9&v1=3:S1 R2;          [для 3-го режима выбора случайного навыка второй навык выбирается из случайного класса]
!!en:;
!!FU7966:Py9/?y11/y8;         [y11 - второй навык]
!!HEx16&y10<>y11:Sy10/1 Sy11/1; [устанавливаем выбранные вторичные навыки герою на базовом уровне]
!!HEx16&y10=y11:Sy10/2;       [если первый и второй навыки совпадают устанавливаем навык герою на продвинутом уровне]
!!HEx16:S18/?y20 A2/0/?y21/?y21; [y20/y21 - наличие грамотности/книги заклинаний]
!!HEx16&y20>0/y21=0:A1/0/17;  [герой с грамотностью получает книгу заклинаний, если ее нет]

!#UN:P771/?v1;                [проверяем включена ли опция 771 в v1]
!#VRz1&v1>0:S^Data\s\option 771 - randomizer.ini^; [z1 - имя ini-файла]
!#VRz2&v1>0:S^option 771^;    [z2 - секция ini-файла]
!#UN&v1>0:N6/3/7/2/1;         [z3 - текстровое значения подопции 7]
!#VRv1&v1>0:Vz3;              [v1 - значение подопции 7]
!#DO7965/0/155/1&v1>0:P;      [изменение вторичных наывков героев]

** end
