ZVSE
ERMS_ScriptDate=12.11(November).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Tyrant
** Name rus.     : Тиран
** Options       : 770
** Dialogs       : -
** Variables     : -
** Tmp variables : v1-v4
** Timers        : -
** Functions     : FU7904
** PO-values     : -

!#UN:P770/?v1;                [проверяем включена ли опция 770 в y1]
!#UN&v1=1:P100/0;             [если да, отключаем легендарных героев]

!?PI;                         [события в начале игры]
!!UN:P770/?y1;                [проверяем включена ли опция 770 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW:I7/?y2/?y3;              [y2=0, если розовый - человек. y3=0, если жив]
!!FU&y2=0/y3=0:E;             [выход, если розовый - человек]
!!OW:I7/1/0 T7/-1;            [активируем розового ИИ-игрока без союзов]
!!VRv4:S145;                  [v4 - номер первого героя розового (145..152)]
!!UN:U98/-1/?y1;              [Количество городов в y1]
!!VRy1:-1;                    [y1 - максимальный номер города на карте]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7904/0/y1/1:P98;          [передача ничейных городов розовому игроку]
!!OW&v4=145/y3=1:I7/1/1;      [деактивируем розового ИИ-игрока, если Тиран не получил ни одного города и на старте не было розового игрока]
!!FU&v4=145/y3=1:E;           [Выход, если Тиран не получил ни одного города и на старте не было розового игрока]
!!UN:U53/-1/?y1;              [Количество шахт в y1]
!!VRy1:-1;                    [y1 - максимальный номер шахты]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7904/0/y1/1:P53;          [передача ничейных шахт розовому игроку]
!!UN:U17/-1/?y1;              [Количество жилищ (тип 17) в y1]
!!VRy1:-1;                    [y1 - максимальный номер жилища]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7904/0/y1/1:P17;          [передача ничейных жилищ розовому игроку]
!!UN:U20/-1/?y1;              [Количество жилищ (тип 20) в y1]
!!VRy1:-1;                    [y1 - максимальный номер жилища]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!!DO7904/0/y1/1:P20;          [передача ничейных жилищ розовому игроку]
!!UN:J2/?y1;                  [y1 - уровень сложности (0..4)]
!!VRy1:+v4 -144;              [y1 - уровень сложности (0..4) + количество городов Тирана]
!!VRy2:Sy1 *5;                [y2 - дерево/руда 5 за каждый стартовый город и уровень сложности]
!!VRy3:Sy1 *3;                [y3 - минералы 3 за каждый стартовый город и уровень сложности]
!!VRy4:Sy1 *10000;            [y4 - золото 10000 за каждый стартовый город и уровень сложности]
!!OW&y6>0:R/7/0/y2 R/7/2/y2 R/7/1/y3 R/7/3/y3 R/7/4/y3 R/7/5/y3 R/7/6/y4; [даем Тирану ресурсы]

!?FU7904;                     [передача ничейного объекта розовому игроку. х1 - тип объекта]
!!UN:Ux1/-1/-1/1;             [получаем координаты объекта в v1-v3]
!!CA1&x1=98:O?y1;             [y1 - текущий хозяин города]
!!CA1&x1=98/y1=-1:O7;         [смена хозяина ничейного города]
!!CA1&x1=98/y1=-1:B1/5;       [постройка таверны в городе]
!!HEv4:O?y2 P?y3/?y4/?y5;     [y2 - текущий владелец героя #v4, y3/y4/y5 - координаты героя #v4]
!!HEv4&x1=98/y1=-1/y2=-1/y3=-1/y4=-1/y5=-1:Pv1/v2/v3 O7; [Помещаем героя в последний присвоенный город, если он не нанят или не в тюрьме]
!!OW&x1=98/y1=-1:A7/v4;       [Передаем героя Тирану]
!!VRv4&x1=98/v4<152/y1=-1:+1; [переключение на следующего героя]
!!MN1&x1=53:O?y1;             [y1 - текущий хозяин шахты]
!!MN1&x1=53/y1=-1:O7;         [смена хозяина ничейной шахты]
!!DW1&x1=17:O?y1 G0/d/?y2;    [y1 - текущий хозяин жилища (тип 17), y2 - количество охранников]
!!DW1&x1=17/y1=-1/y2=0:O7;    [смена хозяина ничейного жилища  без охраны (1-4 уровни)]
!!DW1&x1=20:O?y1 G0/d/?y2;    [y1 - текущий хозяин жилища (тип 20), y2 - количество охранников]
!!DW1&x1=20/y1=-1/y2=0:O7;    [смена хозяина ничейного жилища без охраны (1-4 уровни)]

** end
