ZVSE

** Author        : igrik, Algor (sponsor: swallowmee)
** Name          : Enhanced creature specialists
** Name rus.     : Измененные специалисты по существам
** Options       : 740
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7992
** PO-values     : -

** Существа под командованием соответствующего специалиста получают единовременный бонус +1 к скорости,
** а также +1 к атаке за каждый нечётный уровень героя и +1 к защите за каждый чётный.

!?PI;
!!UN:P740/?y1;                           [проверяем включена ли опция 740 в y1]
!!FU&y1=0:E;                             [выход если опция не включена]
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN:Ey2/1/5137688/7992;                 [0x004E6518]
!!DO7993/0/143/1:P;                      [изменение специализаций некампанейских героев]

!?GM0;
!!UN:P740/?y1;                           [проверяем включена ли опция 740 в y1]
!!FU&y1=0:E;                             [выход если опция не включена]
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN:Ey2/1/5137688/7992;                 [0x004E6518]
!!DO7993/0/143/1:P;                      [изменение специализаций некампанейских героев]

!?FU7992;
!!SN:X?y1/0;
; [получаем параметры героя]
!!VRy2:Sy1 +28; [EAX]    !!UN:Cy2/4/?y3;  [структура героя]
!!VRy3:+85; [EAX+0x55]   !!UN:Cy3/2/?y4;  [узнать уровень героя]
; [получаем параметры монстра]
!!VRy5:Sy1 +4;  [ESI]    !!UN:Cy5/4/?y6;  [структура монстра]
!!VRy7:Sy6 +4;  [ESI+4]  !!UN:Cy7/4/?y7;  [уровень монстра]
!!VRy7:+11;                               [y7 - индекс в таблице уровней героя, с которых начинают даваться бонусы]
!!VRv11:C2/3/4/5/6/7/8;                   [таблица уровней героя, с которых начинают даваться бонусы существам - оригинал]
!!VRv11:C1/1/1/1/1/1/1;                   [таблица уровней героя, с которых начинают даваться бонусы существам - тест]
; [бонус от специальности]
!!VRy10:Sy4 -vy7 +1;                      [y10 - текущее количество бонусных уровней]
!!VRy10&y10<0:S0;                         [проверка на отрицательное значение]
!!VRy13:Sy10 :2;                          [y13 - бонус атаки (+1 за каждый четный уровень)]
!!VRy14:Sy10 +1 :2;                       [y14 - бонус защиты (+1 за каждый нечетный уровень)]
; [устанавливаем бонусы]
!!VRy11:Sy6 +84;  !!UN:Cy11/4/dy13;       [бонус к атаке]
!!VRy12:Sy6 +88;  !!UN:Cy12/4/dy14;       [бонус к защите]
!!VRy99:Sy1 +32;  !!UN:Cy99/4/5137828;    [возврат в 0x004E65A4]

!?FU7993;                                 [смена экстраспециальности по существам на обычную специальность, установка описания]
!!HEx16:X?y1/?y2/d/d/d/d/d;               [y1 - тип специальности героя]
!!HEx16&y1=4:X1/y2/d/d/d/d/d;             [смена экстраспециальности по существам на обычную специальность]
!!FU&y1<>1/y1<>4:E;                       [выход, если специальность не по существам]
!!VRz1:Sz179283; !!SN:H^spec^/x16/2/^%Z1^;[установка описания специальности из z179283]

** end
