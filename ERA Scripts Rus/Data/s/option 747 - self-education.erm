ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Fully Rewritten by  : daemon_n 08 Aug 2022
** Name          : Self-education
** Name rus.     : Самообучение
** Options       : 747

** Герои с неизрасходованными очками передвижения тратят их на самообучение, получая опыт.
** Самообучение возможно при нахождении в гарнизоне города (не для гостевого героя) с отстроенными "образовательными учреждениями"
** К "образовательным учреждениям" оносятся:
** Гильдия магии (каждый уровень отдельно) - 5% неизрасходованных ОД переходит в опыт
** Библиотека (Башня) - 5%
** Академия боевых искусств (Темница) - 5%
** Университет магии (Сопряжение) - 5%
** Все проценты суммируются.
** Вторичный навык "Обучение" позволяет герою заниматься образованием даже вне города, переводит 10/20/30% ОД в опыт
** и увеличивает эффективность "образовательных зданий" до 6/7/8% (на 20/40/60%).

** Прим: самообучение отключается, если в игре всего один игрок

// Initialization
!?FU(OnAfterErmInstructions);
  !!UN:P747/?(wogOption:y);
  !!FU&(wogOption)<>(TRUE):E;

  ; Change the description of Learning
  !!re i/(SKILL_BASIC)/(SKILL_EXPERT);
    !!SN:H^secskill^/(SKILL_LEARNING)/i/?(desc:z);
    !!VR(percent:y):Si *10;
    !!SN:T^es.747.desc^/?(newDesc:z)/^percent^/(percent);
    !!SN:H^secskill^/(SKILL_LEARNING)/i/^%(desc)%(newDesc)^;
  !!en;

// Calculate and give exp to heroes on every turn of every player
!?FU(ES_EndOfTurn);
  ; Exit if the option self-learning is not enabled
  !!UN:P747/?(wogOption:y);
  !!FU&(wogOption)<>(TRUE):E;

  !!VRf:S0;
  !!re i/0/(PLAYER_LAST);
    !!OW:Ii/d/?(isDead:y);
    !!VRf&(isDead)=0:+1;
  !!en;

  !!if&f>1;

    !!OW:Wi^timerOwner^/?(townsNum:y);

    !!if&(townsNum);

      !!re i/0/(townsNum)/1/-1;
        !!OW:Wi^timerOwner^/i/?(townId:y);
        !!CA0/(townId):H0/?(hero:y) T?(townType:y);

        !!if&(hero)<>(NO_HERO);
          !!HE(hero):S(SKILL_LEARNING)/?(skill:y) W?(movement:y)/1;

          !!if&(movement);
            !!VR(buildEfficiency:y):S(skill) +5;  [5% to *%8 per building depending on learning level]
            !!VR(totalBuildEffect:y):S0;

            !!re j/0/4;
              !!CA0/(townId):B3/j;
              !!if&1;
                !!VR(totalBuildEffect):+(buildEfficiency);
              !!el;
                !!br;
              !!en;
            !!en;
            ; Check Library for Tower
            !!if&(townType)=(TOWN_TOWER);
              !!CA0/(townId):B3/22;
              !!VR(totalBuildEffect)&1:+(buildEfficiency);
            ; Check Battle Scholar Academy for Dungeon
            !!el&(townType)=(TOWN_DUNGEON);
              !!CA0/(townId):B3/23;
              !!VR(totalBuildEffect)&1:+(buildEfficiency);
            ; Check Magic University for Conflux
            !!el&(townType)=(TOWN_CONFLUX);
              !!CA0/(townId):B3/21;
              !!VR(totalBuildEffect)&1:+(buildEfficiency);
            !!en;

            !!VR(skill):*10 +(buildEfficiency);
            !!VR(bonusExp:y):S(movement) :100 *(skill);
            !!HE(hero):Ed(bonusExp) W0/1;
          !!en;
        !!en;
      !!en;
    !!en;

    !!OW:Hi^timerOwner^/1;
    !!if&v1;
      !!re i/2/v1/1/1;
        !!VR(hero):Svi;
        !!HE(hero):S(SKILL_LEARNING)/?(skill:y) W?(movement:y)/1;
        !!if&(skill)/(movement);
          !!VR(skill):*10;
          !!VR(bonusExp:y):S(movement) :100 *(skill);
          !!HE(hero):Ed(bonusExp) W0;
        !!en;
      !!en;
    !!en;
  !!en;
