ZVSE
ERMS_ScriptDate=12.4(April).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor
** Name          : battle dwarves reinforcement
** Name rus.     : укрепление боевых гномов
** Options       : 761
** Dialogs       : -
** Variables     : v7923,z880
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P761/?y1;                [проверяем включена ли опция 761 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz880:Sz179184;            [получаем из ert новую подсказку по существу]
!!UN:G1/17/2/880;             [установка новой подсказки]
!!MA:F17/?y1;                 [y1 - FV боевых гномов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F17/y1;                  [обновляем FV боевых гномов]
!!VRv7923:S-1;                [инициализация v7923]

!?MF1&v7923=-1;               [при нанесении урона]
!!UN:P761/?y1;                [проверяем включена ли опция 761 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!MF:N?y1;                    [y1 - номер отряда, получающего урон]
!!BMy1:T?y2;                  [y2 - тип отряда, получающего урон (17 - боевые гномы)]
!!VRv7923&y2=17:Sy1;          [v7923 - номер отряда боевых гномов, получающих физ. урон]

!?BG1&v7923>-1;               [после действия]
!!UN:P761/?y1;                [проверяем включена ли опция 761 в y1]
!!BU:C?y2;                    [y2=1, если бой закончен]
!!VRv7923|y1=0/y2=1:S-1;      [сброс v7923, если опция не включена или бой закончен]
!!FU|y1=0/y2=1:E;             [выход если опция не включена или бой закончен]
!!BMv7923:N?y1;               [y1 - текущее количество гномов в отряде]
!!VRv7923&y1<1:S-1;           [сброс v7923 если отряд уничтожен]
!!FU&y1<1:E;                  [выход, если отряд уничтожен]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!VRz1&f=0:Sz179185;          [получаем текст для вывода в лог битвы из ert]
!!MM&f=0:Sz1;                 [вывод текста в лог битвы]
!!VRz1&f=0:S^SHIELD^;         [z1 - звуковой файл щита]
!!SN&f=0:P1;                  [вывод звука щита]
!!BMv7923&f=0:V27;            [анимация]
!!BMv7923:Dd2;                [укрепление отряда]
!!VRv7923:S-1;                [сброс v7923]

** end
