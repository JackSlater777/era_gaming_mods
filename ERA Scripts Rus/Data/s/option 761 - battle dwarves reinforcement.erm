ZVSE2
** Author orig.  : Algor
** Rewritten by  : Archer30
** Name          : battle dwarves reinforcement
** Name rus.     : укрепление боевых гномов
** Options       : 761


!?FU(OnAfterErmInstructions);           [пост-инструкция: установка описаний]
!!UN:P761/?(wogOption:y);               [проверяем включена ли опция 761]
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Set up new description
!!SN:H^monname^/(MON_BATTLE_DWARF)/2/?(desc:z);
!!SN:H^monname^/(MON_BATTLE_DWARF)/2/^%(desc)%T(es.761.desc)^;

!!MA:F(MON_BATTLE_DWARF)/?(fv:y);       [FV боевых гномов]
!!VR(fv):*105 :100;                     [увеличиваем FV на 5%]
!!MA:F(MON_BATTLE_DWARF)/(fv);          [обновляем FV боевых гномов]

!?FU(OnBattleActionEnd);
!!BG:E?(stack:y) A?(action:y);

; Increase the defense of Battle Dwarves if they are hit by melee attack
!!if&(stack)>(NO_STACK)/(action)=(BATTLE_ACTION_WALK_AND_ATTACK);
  !!BM(stack):T?(type:y) N?(num:y);
  !!FU(ES_761_CheckIfMonIsEligible):P(type)/?(result:y);

  !!if&(result);

    ; Show battle log and play animation/sound
    !!if&i^battle_isVisible^;
      !!if&(num)=1;
        !!SN:H^monname^/(type)/0/?(battleDwarfName:z);
      !!el;
        !!SN:H^monname^/(type)/1/?(battleDwarfName);
      !!en;

      !!SN:T^es.761.battleLog^?(battleLog:z)/^battleDwarf^/(battleDwarfName);
      !!MM:S(battleLog);

      !!SN:P^SHIELD^;                   [вывод звука щита]
      !!BM(stack):V27;                  [анимация]
    !!en;

    ; Increase defence by 2
    !!BM(stack):Dd2;                    [укрепление отряда]
  !!en;
!!en;

// Funciton to check if the targeted monster is eligible for reinforcement
!?FU(ES_761_CheckIfMonIsEligible);
!#VA(mon:x) (result:x);

!!VR(result):S(FALSE);
!!VR(result)&(mon)=(MON_BATTLE_DWARF):S(TRUE);

** end
