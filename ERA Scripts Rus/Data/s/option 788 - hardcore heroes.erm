ZVSE

** Author orig.  : Algor
** Name          : Hardcore heroes
** Name rus.     : Смертные герои
** Options       : 788

!?FU(OnBeforeBattleAction);             // перед действием в бою
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!BG:A?y1; !!SN:W^opt788.a^/y1;         // сохраняем последнее выполненное действие

!?FU(OnAfterBattleUniversal);           // после боя
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!BA:H0/?y1 H1/?y2;                     // y1/y2 - атакующий/защищающийся герои, выход, если не битва двух героев
!!SN:W^opt788.a^/?y3; !!FU|y3=4/y3=5:E; // выход, если герой сбежал/откупился
!!HEy1:O?y3;!!HEy2&y2>=0:O?y4;          // y3/y4 - владельцы атакующеего/защищающегося героя
!!FU(ES.opt788.Rebirth)&y1>=0/y3<0:Py1; // перерождение атакующего героя, если он погиб
!!FU(ES.opt788.Rebirth)&y2>=0/y4<0:Py2; // перерождение защищающегося героя, если он погиб

!?FU(OnAfterErmInstructions);           // перед началом игры
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!SN:M-1/454/0/1 W^opt788.h^/v1;        // Mi^opt788.h^ - сохраняемый массив новых героев

!?FU(ES.opt788.Rebirth);                // Перерождение героя x1
!!SN:W^opt788.h%X1r^/1;                 // установка герою признака перерожденного
!!HEx1:B2/?y2;   !!VRy1:Sy2 :2;         // y1 - раса героя 0..8
!!VRy2:%2 *2 -1; !!VRi:S0 R2; !!VRy2&i=2:S0; // y2 - класс героя -1/0/1 = Воин/Странник/Маг
** установка первичных параметров
!!VRy11:S1 R2 -y2; !!VRy12:S1 R2 -y2;   // y11/y12/y13/y14 - первичные параметры нового героя
!!VRy13:S2 R1 +y2; !!VRy14:S2 R1 +y2;   // ...
!!HEx1:Fy11/y12/y13/y14;                // Установка первичных параметров
** выбор специализации
!!VRy3:S0 R1; !!VRy4:S0 R1;             // y3 - тип специализации 0 - втор.навык, 1 - существо/ресурс/заклинание, y4=1 для альтернативной специализации (50% шанс)
!!if&y2=-1/y3=0:;                       // воин со вторичным навыком
  !!VRy10:S179230;                      // y10 - номер z-переменной с описанием специализации
  !!VRi:S0 R5;                          // i - случайное число 0..5
  !!HEx1&i=0:X0/1/0/0/0/0/0;            // 0 - Стрельба
  !!HEx1&i=1:X0/9/0/0/0/0/0;            // 1 - Удача
  !!HEx1&i=2:X0/22/0/0/0/0/0;           // 2 - Нападение
  !!HEx1&i=3:X0/23/0/0/0/0/0;           // 3 - Доспехи
  !!HEx1&i=4/y1<>4:X0/20/0/0/0/0/0;     // 4 - Артиллерия (кроме Некрополя)
  !!HEx1&i=4/y1=4:X0/12/0/0/0/0/0;      // 4 - Некромантия (Некрополь)
  !!HEx1&i=5:X0/27/0/0/0/0/0;           // 5 - Первая помощь
!!en:;
!!if&y2=-1/y3=1:;                       // воин с существом
  !!VRy10:S179229;                      // y10 - номер z-переменной с описанием специализации
  !!UN:P740/?y9;!!VRy10&y9>0:S179283;   // y10 - номер z-переменной если включена опция "Измененные специалисты по существам"
  !!VRj:Sy1 *14; !!VRi:S0 R6 *2 +j;     // i - случайное негрейженное существо расы героя
  !!VRj:Si; !!UN:P67/?k;                // k - состояние WoG-опции "Нейтральный город"
  !!VRi&j=112/k=0:S118;                 // М.Феи
  !!VRi&j=114/k=0:S112;                 // Возд.эл.
  !!VRi&j=116/k=0:S115;                 // Водн.эл.
  !!VRi&j=118/k=0:S114;                 // Огн.эл.
  !!VRi&j=120/k=0:S113;                 // Земл.эл.
  !!VRi&j=122/k=0:S120;                 // Псих.эл.
  !!VRi&j=124/k=0:S130;                 // Огн.птица
  !!VRi&j=112/k=0:S138;                 // Хоббиты
  !!VRi&j=114/k=0:S140;                 // Боровы
  !!VRi&j=116/k=0:S164;                 // Посл. огня
  !!VRi&j=118/k=0:S164;                 // Оборотни
  !!VRi&j=120/k=0:S193;                 // Волшебницы
  !!VRi&j=122/k=0:S165;                 // Пожары
  !!VRi&j=124/k=0:S168;                 // Горынычи
  !!HEx1:X1/i/0/0/0/0/0;                // специалист по существам
  !!if&y4=1:;                           // специалисты по лучшению существ
    !!HEx1&i=8:X6/8/0/0/0/9/169;        // Монахи и Фанатики -> Фанатики Войны
    !!VRy10&i=8:S179231;                // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=28:X6/28/0/0/0/29/173;     // Гремлины и Мастер-гремлины -> Санта-гремлины
    !!VRy10&i=28:S179232;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=32:X6/32/0/0/0/33/116;     // Каменные и Стальные големы -> Золотые големы
    !!VRy10&i=32:S179233;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=58:X6/58/0/0/0/59/141;     // Живые мертвецы и Зомби -> Мумии
    !!VRy10&i=58:S179234;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=10:X6/10/0/0/0/11/67;      // Кавалеристы и Чемпионы -> Рыцари Смерти
    !!VRy10&i=10:S179237;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=24:X6/24/0/0/0/25/172;     // Единороги и Боевые Единороги -> Кошмары
    !!VRy10&i=24:S179238;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=26:X6/26/0/0/0/27/154;     // Зеленый и Золотой Драконы -> Кровавый Дракон
    !!VRy10&i=26:S179239;               // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=82:X6/82/0/0/0/83/154;     // Красный и Черный Драконы -> Кровавый Дракон
    !!VRy10&i=82:S179240;               // y10 - номер z-переменной с описанием специализации
  !!en:;
!!en:;
!!if&y2=0/y3=0:;                        // странник со вторичным навыком
  !!VRy10:S179230;                      // y10 - номер z-переменной с описанием специализации
  !!VRi:S0 R3;                          // i - случайное число 0..3
  !!HEx1&i=0:X0/2/0/0/0/0/0;            // 0 - Логистика
  !!HEx1&i=1:X0/5/0/0/0/0/0;            // 1 - Навигация
  !!HEx1&i=2:X0/11/0/0/0/0/0;           // 2 - Зоркость
  !!HEx1&i=3:X0/13/0/0/0/0/0;           // 3 - Имущество
!!en:;
!!if&y2=0/y3=1:;                        // странник с ресурсом
  !!VRy10:S179235;                      // y10 - номер z-переменной с описанием специализации
  !!VRi:S0 R6;                          // i - случайное число 0..6
  !!HEx1:X2/i/0/0/0/0/0;                // специалист по ресурсам
  !!VRy10&i=6:S179236;                  // y10 - номер z-переменной с описанием специализации
!!en:;
!!if&y2=1/y3=0:;                        // маг со вторичным навыком
  !!VRy10:S179230;                      // y10 - номер z-переменной с описанием специализации
  !!VRi:S0 R3;                          // i - случайное число 0..3
  !!HEx1&i=0:X0/8/0/0/0/0/0;            // 0 - Мистицизм
  !!HEx1&i=1:X0/24/0/0/0/0/0;           // 1 - Интеллект
  !!HEx1&i=2:X0/25/0/0/0/0/0;           // 2 - Волшебство
  !!HEx1&i=3:X0/26/0/0/0/0/0;           // 3 - Сопротивление
!!en:;
!!if&y2=1/y3=1:;                        // маг с заклинанием
  !!VRy10:S179228;                      // y10 - номер z-переменной с описанием специализации
  !!VRi:S0 R10;                         // i - случайное число 0..10
  !!HEx1&i=0:X3/15/0/0/0/0/0;           // Волшебная стрела
  !!HEx1&i=1:X3/16/0/0/0/0/0;           // Ледяная молния
  !!HEx1&i=2:X3/17/0/0/0/0/0;           // Удар молнии
  !!HEx1&i=3:X3/20/0/0/0/0/0;           // Кольцо холода
  !!HEx1&i=4:X3/21/0/0/0/0/0;           // Огненный шар
  !!HEx1&i=5:X3/23/0/0/0/0/0;           // Метеоритный дождь
  !!HEx1&i=6:X3/22/0/0/0/0/0;           // Инферно
  !!HEx1&i=7:X3/18/0/0/0/0/0;           // Взрыв
  !!HEx1&i=8:X3/19/0/0/0/0/0;           // Цепная молния
  !!HEx1&i=9:X3/26/0/0/0/0/0;           // Армагеддон
  !!HEx1&i=10/y1<>4:X3/25/0/0/0/0/0;    // Уничтожение нежити (кроме Некрополя)
  !!HEx1&i=10/y1=4:X3/24/0/0/0/0/0;     // Волна смерти (Некрополь)
  !!if&y4=1:;                           // специалисты по аурам-проклятиям
    !!HEx1&i=7:X3/45/0/0/0/0/0;         // Слабость
    !!VRy10&i=7:S179241;                // y10 - номер z-переменной с описанием специализации
    !!HEx1&i=8:X3/47/0/0/0/0/0;         // Разрушение
    !!VRy10&i=8:S179242;                // y10 - номер z-переменной с описанием специализации    
    !!HEx1&i=9:X3/42/0/0/0/0/0;         // Проклятье
    !!VRy10&i=9:S179243;                // y10 - номер z-переменной с описанием специализации    
    !!HEx1&i=10:X3/54/0/0/0/0/0;        // Медлительность
    !!VRy10&i=10:S179244;               // y10 - номер z-переменной с описанием специализации    
  !!en:;
!!en:;
!!VRz1:Szy10; !!SN:H^spec^/x1/2/^%Z1^;
** очистка/установка заклинаний
!!re i/0/69:;                           // для каждого заклинания
  !!HEx1:Mi/0;                          // удаляем заклинание
!!en:;
!!HEx1:X?j/?k/d/d/d/d/d; !!HEx1&j=3:Mk/1; // даем герою спец.заклинание
** установка вторичных навыков
!!re i/0/27:;                           // для каждого втор.навыка
  !!HEx1:Si/0;                          // удаляем все вторичные навыки героя
!!en:;
!!HEx1:X?j/?k/d/d/d/d/d;                // j/k - параметры специализации героя
!!HEx1&j=0:Sk/2;                        // для спецов по втор.навыку даем этот навык на продвинутом уровне
!!HEx1&j=2:S13/1;                       // для спецов по ресурсам даем базовое Имущество
!!if&j=3:;                              // для спецов по заклинаниям первым навыком даем профильную Школу Магии
  !!SSk:S?l; !!VRy10:S4;                // y10 - навык профильной Школы. По умолчалию или для мультипрофильных заклинаний - Вода
  !!VRy11:Sl &1; !!VRy10&y11>0:S15;     // ...
  !!VRy11:Sl &2; !!VRy10&y11>0:S14;     // ...
  !!VRy11:Sl &8; !!VRy10&y11>0:S17;     // ...
  !!VRy11:Sl &4; !!VRy10&y11>0:S16;     // ...
  !!HEx1:Sy10/1;                        // даем базовый навык профильной Школы
!!en:;
!!if|j=1/j=6:;                          // для спецов по существам первым навыком даем Нападение/Тактику/Стрельбу для пеших/летунов/стрелков
  !!MA:Xk/?l; !!VRy10:S22;              // y10 - профильный навык. По-умолчанию - Нападение
  !!VRy11:Sl &2; !!VRy10&y11>0:S19;     // для летающих существ - Тактика
  !!VRy11:Sl &4; !!VRy10&y11>0:S1;      // для стреляющих существ - Стрельба
  !!HEx1:Sy10/1;                        // даем базовый профильный навык
!!en:;
!!if&j<>0:;                             // для спецов не по втор.навыку подбираем второй базовый навык
  !!if&y2=-1:;                          // для Воинов
    !!VRi:S0 R5;
    !!HEx1&i=0/y1<>4:S6/1;              // Лидерство
    !!HEx1&i=0/y1=4:S12/1;              // Некромантия
    !!HEx1&i=1:S9/1;                    // Удача
    !!HEx1&i=2:S10/1;                   // Баллистика
    !!HEx1&i=3:S20/1;                   // Артиллерия
    !!HEx1&i=4:S23/1;                   // Доспехи
    !!HEx1&i=5:S27/1;                   // Первая помощь
  !!en:;
  !!if&y2=0:;                           // для Странников
    !!VRi:S0 R6;
    !!HEx1&i=0:S0/1;                    // Поиск Пути
    !!HEx1&i=1:S2/1;                    // Логистика
    !!HEx1&i=2:S3/1;                    // Разведка
    !!HEx1&i=3:S4/1;                    // Дипломатия
    !!HEx1&i=4:S11/1;                   // Зоркость
    !!HEx1&i=5:S18/1;                   // Грамотность
    !!HEx1&i=6:S21/1;                   // Обучение
  !!en:;
  !!if&y2=1:;                           // для Магов
    !!VRi:S0 R4;
    !!HEx1&i=0/y1<>4:S7/1;              // Мудрость
    !!HEx1&i=0/y1=4:S12/1;              // Некромантия
    !!HEx1&i=1:S8/1;                    // Мистицизм
    !!HEx1&i=2:S24/1;                   // Интеллект
    !!HEx1&i=3:S25/1;                   // Волшебство
    !!HEx1&i=4:S26/1;                   // Сопротивление
  !!en:;
!!en:;
** удаление достижений
!!re i/0/14:;                           // для каждого достижения
  !!SN:W^opt760.h%X1a%Vir^/0;           // обнуляем достижение
  !!SN:W^opt760.h%X1a%Vic^/0;           // обнуляем счетчик достижения
!!en:;
** удаление ассасинов
!!SN:W^opt795.11.%X1^/?y15 W^opt795.11.%X1.rebirth^/y15 W^opt795.11.%X1^/0; // удаление ассасинов
** удаление БМ и артефактов
!!re i/0/170:;                          // для каждого артефакта/БМ
  !!HEx1:A2/i/?j/?k A3/i/j/1;           // удаление артефакта/БМ
!!en:;
!!HEx1:A1/3/16 A1/0/17;                 // выдам катапульту и книгу всем героям
** обнуление уровня и опыта
!!HEx1:E0/1/1;                          // обнуление уровня и опыта
** очистка командира/артефактов командира
!!UN:P3/?i P6/?j P51/?k;                // i/j/k - статус опций Командиры / Обязательный найм командиров / Измененные командиры
!!if&i=0:;                              // если командиры разрешены
  !!COx1:A4/-1/0/-1/0/-1/0/-1/0/-1/0/-1/0;         // удаление артефактов командира
  !!COx1:B0/0 D0 Ty1 X0/1000 X1/0 X2/1;            // удаление способностей, уровня, опыта, оживление и сброс расы командира
  !!COx1&k=0:P0/5 P1/5 P2/40 P3/9 P4/1 P5/4 P6/5;  // сброс первичных параметров командира
  !!COx1&k=1:P0/5 P1/5 P2/30 P3/10 P4/3 P5/5 P6/0; // сброс первичных параметров для "измененного" командира
  !!COx1:S0/0 S1/0 S2/0 S3/0 S4/0 S5/0 S6/0;       // сброс вторичных навыков командира
  !!if&k=1:;                            // если включены измененные командиры, раздаем расовые бонусные навыки
    !!COx1&y1=0:B1/5/0;                 // paladin         - endless retaliation 
    !!COx1&y1=1:B1/7/0;                 // heirophant      - magic mirror (fire shield)
    !!COx1&y1=2:B1/4/0;                 // temple guardian - shoot 
    !!COx1&y1=3:B1/14/0;                // succubus        - fly 
    !!COx1&y1=4:B1/1/0;                 // soul eater      - fear
    !!COx1&y1=5:B1/3/0;                 // brute           - no retaliation 
    !!COx1&y1=6:B1/8/0;                 // ogre leader     - block 
    !!COx1&y1=7:B1/12/0;                // shaman          - 50% poison (death stare)
    !!COx1&y1=8:B1/0/0;                 // astral spirit   - ignore 50% defense  
  !!en:;
!!en:;
!!COx1&j=1:E0;                          // если командиры должны быть наняты, удаляем командира
** очистка приспешников/бонусных БМ
!!IF:Wx1;                               // w-переменные текущего героя
!!VRw81:S0; !!VRw82:S0; !!VRw83:S0;     // удаление бонусных БМ
!!VRw118:S-1;                           // удаление приспешника
** обнуление благословений/проклятий
!!HEx1:Y0/0/0/3;                        // удаление всех благословений/проклятий

** Подержка специализаций на заклинаниях перерожденных героев
!?MR1;                                  // при нанесении урона заклинанием
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!BG:A?y1; !!FU&y1<>1:E;                // выход, если не каст героя
!!BG:H?y1; !!SN:W^opt788.h%Y1r^/?y2; !!FU&y2<1:E; // выход, если герой-кастер не перерожденный
!!HEy1:X?i/?j/d/d/d/d/d; !!FU&i<>3:E;   // выход, если герой не спец по заклинанию
!!MR:S?y2; !!FU&y2<>j:E;                // выход, если каст непрофильного заклинания
!!MR:F?y2; !!HEy1:E?k/?l/1;             // y2/l - наносимый урон/уровень героя
!!VRy3:Sy2 *l *5 :100 +y2; !!MR:Fy3;    // установка бонуса к урону в размере 5% за уровень героя

** Подержка аур-проклятий перерожденных героев
!?FU(OnBeforeBattleUniversal);
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!SN:W^opt788.a1^/0 W^opt788.a2^/0 W^opt788.a3^/0 W^opt788.a4^/0; // сброс флагов аур-проклятий

!?FU(OnSetupBattlefield);               // при настройке поля боя
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!BA:H0/?y1 H1/?y2;                     // y1/y2 - номера сражающихся героев
!!VRy11:C0/0/0/0;                       // y11..y14 - действующие ауры
!!HEy1:X?y3/?y4/d/d/d/d/d;              // y3/y4 - специализация атакующего героя
!!SN:W^opt788.h%Y1r^/?y5;               // y5 - флаг перерождения атакующего героя
!!VRy11&y3=3/y4=45/y5=1:S1;             // y11 - активация ауры Слабости
!!VRy12&y3=3/y4=47/y5=1:S1;             // y11 - активация ауры Разрушения
!!VRy13&y3=3/y4=42/y5=1:S1;             // y11 - активация ауры Проклятья
!!VRy14&y3=3/y4=54/y5=1:S1;             // y11 - активация ауры Медлительности
!!if&y2>=0:;                            // если есть защищающийся герой
  !!HEy2:X?y3/?y4/d/d/d/d/d;            // y3/y4 - специализация защищающегося героя
  !!SN:W^opt788.h%Y2r^/?y5;             // y5 - флаг перерождения защищающегося героя
  !!VRy11&y3=3/y4=45/y5=1:S1;           // y11 - активация ауры Слабости
  !!VRy12&y3=3/y4=47/y5=1:S1;           // y11 - активация ауры Разрушения
  !!VRy13&y3=3/y4=42/y5=1:S1;           // y11 - активация ауры Проклятия
  !!VRy14&y3=3/y4=54/y5=1:S1;           // y11 - активация ауры Медлительности
!!en:;                                  // 
!!FU&y11=0/y12=0/y13=0/y14=0:E;         // выход, если ни одна аура-проклятье не действует
!!BA:Q?f;                               // f-статус быстрой битвы
!!FU(Fun_SpellAnimationOnStacks)&f=0/y11=1:P-1/?y21; // y21 = слот для массовой анимации Слабости
!!FU(Fun_SpellAnimationOnStacks)&f=0/y12=1:P-1/?y22; // y22 = слот для массовой анимации Разрушения
!!FU(Fun_SpellAnimationOnStacks)&f=0/y13=1:P-1/?y23; // y23 = слот для массовой анимации Проклятия
!!FU(Fun_SpellAnimationOnStacks)&f=0/y14=1:P-1/?y24; // y24 = слот для массовой анимации Медлительности
!!re i/0/41:;                           // для всех отрядов Защищающегося
  !!BMi:N?y3; !!co&y3<1:;               // пропуск пустых отрядов
  !!BMi:A?y3 D?y4 U1/?y5 U2/?y6 S?y7;   // y3/y4/y5/y6/y7 - атака/защита/мин.урон/макс.урон/скорость отряда
  !!VRy3&y11=1::2;                      // снижение атаки для Слабости
  !!VRy4&y12=1::2;                      // снижение защиты для Разрушения
  !!VRy6&y13=1:Sy5;                     // макс. урон снижается до мин.урона для Проклятия
  !!VRy7&y14=1/y7>1::2;                 // снижение скорости для Медлительности, если скорость больше 1
  !!FU(Fun_SpellAnimationOnStacks)&f=0/y11=1:Pi/y21; // добавление отряда в массовую анимацию Слабости
  !!FU(Fun_SpellAnimationOnStacks)&f=0/y12=1:Pi/y22; // добавление отряда в массовую анимацию Разрушения
  !!FU(Fun_SpellAnimationOnStacks)&f=0/y13=1:Pi/y23; // добавление отряда в массовую анимацию Проклятия
  !!FU(Fun_SpellAnimationOnStacks)&f=0/y14=1:Pi/y24; // добавление отряда в массовую анимацию Медлительности
  !!BMi:Ay3 Dy4 U2/y6 Sy7;              // обновляем параметры отряда
!!en:;                                  // 
!!SN&f=0/y11=1:W^opt788.a1^/y21;        // выставление флага анимации Слабости
!!SN&f=0/y12=1:W^opt788.a2^/y22;        // выставление флага анимации Разрушения
!!SN&f=0/y13=1:W^opt788.a3^/y23;        // выставление флага анимации Проклятия
!!SN&f=0/y14=1:W^opt788.a4^/y24;        // выставление флага анимации Медлительности

!?FU(OnBattlefieldVisible);
!!UN:P788/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!SN:W^opt788.a1^/?y1;                  // y1 - аура Слабости
!!if&y1<>0:;                            // если аура есть
  !!VRz1:Sz179245; !!MM:Sz1;            // вывод текста в лог битвы
  !!VRz2:S^WEAKNESS^; !!SN:P2;           // вывод звука слабости
  !!FU(Fun_SpellAnimationOnStacks):Py1/56/0/1; // проигрывание массовой амимации "Слабость" без анимации урона с очисткой памяти
!!en:;                                  // 
!!SN:W^opt788.a2^/?y1;                  // y1 - аура Разрушения
!!if&y1<>0:;                            // если аура есть
  !!VRz1:Sz179245; !!MM:Sz1;            // вывод текста в лог битвы
  !!VRz2:S^DISRUPTR^; !!SN:P2;          // вывод звука разрушения
  !!FU(Fun_SpellAnimationOnStacks):Py1/14/0/1; // проигрывание массовой амимации "Разрушение" без анимации урона с очисткой памяти
!!en:;                                  // 
!!SN:W^opt788.a3^/?y1;                  // y1 - аура Проклятия
!!if&y1<>0:;                            // если аура есть
  !!VRz1:Sz179245; !!MM:Sz1;            // вывод текста в лог битвы
  !!VRz2:S^CURSE^; !!SN:P2;             // вывод звука проклятия
  !!FU(Fun_SpellAnimationOnStacks):Py1/40/0/1; // проигрывание массовой амимации "Проклятие" без анимации урона с очисткой памяти
!!en:;                                  // 
!!SN:W^opt788.a4^/?y1;                  // y1 - аура Медлительности
!!if&y1<>0:;                            // если аура есть
  !!VRz1:Sz179245; !!MM:Sz1;            // вывод текста в лог битвы
  !!VRz2:S^MUCKMIRE^; !!SN:P2;          // вывод звука медлительности
  !!FU(Fun_SpellAnimationOnStacks):Py1/19/0/1; // проигрывание массовой амимации "Медлительность" без анимации урона с очисткой памяти
!!en:;                                  // 

** end
