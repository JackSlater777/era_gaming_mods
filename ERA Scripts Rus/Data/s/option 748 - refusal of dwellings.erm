ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor (Idea: Antib)
** Name          : Refusal of dwellings
** Name rus.     : Отказ от жилищ
** Options       : 748
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** ПКМ на принадлежащем герою внешнем жилище вызывает диалог,в котором от данного жилища можно отказаться,
** сделав его нейтральным и переведя всех невыкупленных существ в охрану (существа 1-4 уровней только до конца недели).
** Отказ от жилищ существ "8го уровня" (3й грейд замковых существ 7го уровня) запрещен,
** если жилища 8го уровня функционируют не как обычные.

!?CM0;
!!UN:P748/?y1;                [проверяем включена ли опция 748 в y1]
!!FU&y1=0:E;                  [выход, елси опция не включена]
!!CM:P?y1/?y2/?y3 F?y4;       [y1/y2/y3 - координаты клика, y4 - тип клика]
!!FU|y4<>512:E;               [выход, если не ПКМ]
!!TRy1/y2/y3:E?y4;            [y4 - Признак "желтого квадрата"]
!!FU&y4<>0:E;                 [выход, если под кликом не желтый квадрат]
!!OBy1/y2/y3:T?y4 U?y5;       [y4/y5 - тип/подтип объекта под кликом]
!!FU&y4<>17/y4<>20:E;         [выход, если под кликом не жилище]
!!UN:P0/?y6;                  [y6=0, если жилища 8го уровня функционируют не как обычные]
!!FU&y4=17/y5>79/y5<89/y6=0:E;[выход, если жилище существ "8го уровня" и функционирует не как обычные]
!!DWy1/y2/y3:O?y5;            [y5 - владелец жилища]
!!OW:C?y6;                    [y6 - текущий игрок]
!!FU&y5<>y6:E;                [выход, если жилище не принадлежит текущему игроку]
!!VRz1:Sz179268;              [z1 - текст вопроса об отказе]
!!IF:Q1/1;                    [вопрос об отказе]
!!FU&-1:E;                    [выход, если игрок не отказался от жилища]
!!DWy1/y2/y3:O-1/1;             [удаление владельца жилища]
!!DWy1/y2/y3:M0/?y10/?y20 M1/?y11/?y21 M2/?y12/?y22 M3/?y13/?y23; [y10-y13/y20-23 - типы/кол-ва существ доступных для найма]
!!DWy1/y2/y3:G0/?y30/?y40 G1/?y31/?y41 G2/?y32/?y42 G3/?y33/?y43; [y30-y33/y40-43 - типы/кол-ва существ в охране]
!!VRy40|y30=y10/y30=-1:+y20;  [y40-y43 - новые кол-ва охранников Жилища]
!!VRy41|y31=y11/y31=-1:+y21;  [...]
!!VRy42|y32=y12/y32=-1:+y22;  [...]
!!VRy43|y33=y13/y33=-1:+y23;  [...]
!!VRy30&y30=-1/y20>0:Sy10;    [обновление типов охраны, если не было]
!!VRy31&y31=-1/y21>0:Sy11;    [...]
!!VRy32&y32=-1/y22>0:Sy12;    [...]
!!VRy33&y33=-1/y23>0:Sy13;    [...]
!!DWy1/y2/y3:M0/d/0 M1/d/0 M2/d/0 M3/d/0; [обнуление кол-ва существ доступных для найма]
!!DWy1/y2/y3:G0/y30/y40 G1/y31/y41 G2/y32/y42 G3/y33/y43; [установка нового кол-ва охранников]

**end

