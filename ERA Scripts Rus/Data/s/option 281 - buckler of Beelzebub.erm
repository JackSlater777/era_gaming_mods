ZVSE2
** Author orig.  : Jim Vogan
** Rewritten by  : Archer30 and daemon_n
** Name          : Buckler of Beelzebub
** Name rus.     : Щит Вельзевула
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
**               : [Algor] вынос текстов в ert-файл
**               : [Algor] перенумерация переменных
** Options       : 281


!?FU(OnAfterErmInstructions);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:A15/(ART_BLANK_SHIELD)/(ART_SWORD_OF_HELLFIRE)/(ART_SHIELD_OF_THE_DAMNED)/(ART_HELLSTORM_HELMET)/(ART_BREASTPLATE_OF_BRIMSTONE); new combo #15

!!SN:H^art^/(ART_BLANK_SHIELD)/0/^%T(es.281.name)^;
!!SN:H^art^/(ART_BLANK_SHIELD)/1/^%T(es.281.desc)^;

; Enable spell on the artifact
!!UN:A(ART_BLANK_SHIELD)/8/(TRUE);

// Change the spell of AB depending on the actual artifact equipped
!?FU(ES_OnArtifactGiveSpell);
!#VA(address:x);

!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/?(art:y);

!!if&(art)=(ART_BLANK_SHIELD);
  !!UN:C5085052/1/(SPELL_DIMENSION_DOOR);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!el&(art)=(ART_ARMAGEDDONS_BLADE);
  !!UN:C5085052/1/(SPELL_ARMAGEDDON);
  !!UN:C(address)/(STRUCT_HOOK_CONTEXT_EDX)/(UNC_INT)/(ART_ARMAGEDDONS_BLADE);
!!en;

// Add in no retaliation flag for the current stack with the owner having a Buckler of Beelzebub
!?FU(OnBeforeBattleAction);
!!UN:P281/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BG:H?(hero:y);

!!if&(hero)>(NO_HERO);
  !!HE(hero):A2/(ART_BLANK_SHIELD)/?(backpacked:y)/?(equipped:y);
  !!BMi^battle_acting_stack^&(equipped)>0:Fd|(MON_FLAG_NO_RETALIATION);
!!en;

** end
