ZVSE
ERMS_ScriptDate=14.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Jim Vogan
** Name          : Buckler of Beelzebub
** Name rus.     : Щит Вельзевула
** Changes rus.  : [Algor] вынос опции в отдельный erm-файл для мода ERA
**               : [Algor] вынос текстов в ert-файл
**               : [Algor] перенумерация переменных (w95->w94)
** Options       : 281
** Dialogs       : -
** Variables     : w94, z180-181
** Tmp variables : -
** Timers        : -
** Functions     : FU10939
** PO-values     : -

!?PI;
!!UN:P281/?y1; check ERM Options flag
!!FU&y1=0:E;
!!UN:A15/163/11/17/23/29; new combo #15 is formed of Sword of Hellfire (11), Shield of the Damned (17), Hellstorm Helmet (23), and Breastplate of Brimstone (29), and represented by shield #163
!!VRz180:Sz179153;
!!VRz181:Sz179154;
!!UN:A163/9/180;
!!UN:A163/10/181;
!!DO10939/0/155/1&v2=1:P;

** Function to reset variable w94 at start
!?FU10939;
!!IF:Wx16;
!!VRw94:S0;

!?AE1&v998=163; artifact #163 put on
!!UN:P281/?y1; check ERM Options flag
!!FU&y1<1:E; disable this script if option disabled
** increase primary skills by 2 if v998=163
!!HE-1:Fd2/d2/d2/d2;
!!HE-1:M8/?v1; check for spell known (1) or unknown (0)
!!HE-1&v1=0:M8/3;
!!IF:W-1;
!!VRw94:S1+v1;

!?AE0&v998=163; artifact #163 taken off
!!UN:P281/?y1; check ERM Options flag
!!FU&y1<1:E; disable this script if option disabled
** decrease primary skills by 2 if v998=163
!!HE-1:Fd-2/d-2/d-2/d-2;
!!IF:W-1;
!!HE-1&w94=1:M8/0;
!!VRw94:S0;

!?BG0;
!!UN:P281/?y1; check ERM Options flag
!!FU&y1<1:E; disable this script if option disabled
!!BG:H?v1 N?v2;
!!IF&v1<>-1:Wv1;
!!BMv2&v1<>-1/w94>0:F?v3;
!!VRv3&v1<>-1/w94>0:|65536;
!!BMv2&v1<>-1/w94>0:Fv3;

** end
