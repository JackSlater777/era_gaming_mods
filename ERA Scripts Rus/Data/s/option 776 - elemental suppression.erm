ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Elemental suppression
** Name rus.     : Подавление стихий
** Options       : 776
** Dialogs       : -
** Variables     : z902-z903, z909
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:C5129872/1/233;          [отключаем стандартное действие навыка Зоркость (спс. feanor'у)]
!!UN:C5129873/4/340;          [отключаем стандартное действие артефактов Зоркости (спс. feanor'у)]
!!VRz1:Sz179041; !!SN:H^secskill^/11/0/^%Z1^; [Устанавливаем название навыка]
!!VRz1:Sz270 +^%Z179042^; !!SN:H^secskill^/11/1/^%Z1^; [Устанавливаем базовое описание навыка]
!!VRz1:Sz271 +^%Z179043^; !!SN:H^secskill^/11/2/^%Z1^; [Устанавливаем продвинутое описание навыка]
!!VRz1:Sz272 +^%Z179044^; !!SN:H^secskill^/11/3/^%Z1^; [Устанавливаем экспертное описание навыка]
!!VRz1:Sz179045;              [получаем описание специализации Подавление]
!!SN:H^spec^/13/2/^%Z1^;      [Устанавливаем описание специализации герою Саня]
!!SN:H^spec^/28/2/^%Z1^;      [Устанавливаем описание специализации герою Малькольм]
!!SN:H^spec^/42/2/^%Z1^;      [Устанавливаем описание специализации герою Серена]
!!SN:H^spec^/75/2/^%Z1^;      [Устанавливаем описание специализации герою Нимбус]
!!SN:H^spec^/92/2/^%Z1^;      [Устанавливаем описание специализации герою Геон]
!!SN:H^spec^/110/2/^%Z1^;     [Устанавливаем описание специализации герою Орис]
!!SN:H^spec^/127/2/^%Z1^;     [Устанавливаем описание специализации герою Тива]
!!VRz909:Sz420 +^%Z179046^;   [получаем описание Птица познания]
!!UN:A63/10/909;              [устанавливаем описание Птица познания]
!!VRz902:Sz421 +^%Z179047^;   [получаем описание Бесстрашный хранитель]
!!UN:A64/10/902;              [устанавливаем описание Бесстрашный хранитель]
!!VRz903:Sz422 +^%Z179048^;   [получаем описание Символ Знаний]
!!UN:A65/10/903;              [устанавливаем описание Символ Знаний]

!?GM0;                        [после загрузки сохраненной игры]
!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:C5129872/1/233;          [отключаем стандартное действие навыка Зоркость (спс. feanor'у)]
!!UN:C5129873/4/340;          [отключаем стандартное действие артефактов Зоркости (спс. feanor'у)]

!?BA52;                       [Начало боя: сохранение параметров школ маги героев и их подавление]
!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BA:H0/?y1;                  [y1 - атакующий герой]
!!BA:H1/?y2;                  [y2 - защищающийся герой]
!!FU&y2<0:E;                  [выход если битва против монстров без героя]
!!IF:Wy1;                     [выбор атакующего героя для установки w95-w98]
!!HEy1:S14/?w95;              [w95 - уровень магии огня (0..3) атакующего героя]
!!HEy1:S15/?w96;              [w96 - уровень магии воздуха (0..3) атакующего героя]
!!HEy1:S16/?w97;              [w97 - уровень магии воды (0..3) атакующего героя]
!!HEy1:S17/?w98;              [w98 - уровень магии земли (0..3) атакующего героя]
!!IF:Wy2;                     [выбор защищающегося героя для установки w95-w98]
!!HEy2:S14/?w95;              [w95 - уровень магии огня (0..3) защищающегося героя]
!!HEy2:S15/?w96;              [w96 - уровень магии воздуха (0..3) защищающегося героя]
!!HEy2:S16/?w97;              [w97 - уровень магии воды (0..3) защищающегося героя]
!!HEy2:S17/?w98;              [w98 - уровень магии земли (0..3) защищающегося героя]
!!HEy1:S11/?y3;               [y3 - уровень СП (0..3) атакующего героя]
!!HEy1:X?y5/?y6/d/d/d/d/d;    [y5=0, y6=11 для специалиста по ПС]
!!VRy3&y5=0/y6=11:+1;         [y3 - уровень СП атакующего героя с учетом специализации(0..4)]
!!HEy1:A2/63/d/?y5;           [y5 - кол-во надетых на героя Птица познания]
!!VRy3&y5>0:+1;               [y3+=1, если надета Птица познания]
!!HEy1:A2/64/d/?y5;           [y5 - кол-во надетых на героя Бесстрашных хранителей]
!!VRy3&y5>0:+1;               [y3+=1, если надет Бесстрашный хранитель]
!!HEy1:A2/65/d/?y5;           [y5 - кол-во надетых на героя Символов Знаний]
!!VRy3&y5>0:+2;               [y3+=2, если надет Символ знаний]
!!HEy2:S11/?y4;               [y4 - уровень СП (0..3) защищающегося героя]
!!HEy2:X?y5/?y6/d/d/d/d/d;    [y5=0, y6=11 для специалиста по ПС]
!!VRy4&y5=0/y6=11:+1;         [y4 - уровень СП атакующего героя с учетом специализации(0..4)]
!!HEy2:A2/63/d/?y5;           [y5 - кол-во надетых на героя Птица познания]
!!VRy4&y5>0:+1;               [y4+=1, если надета Птица познания]
!!HEy2:A2/64/d/?y5;           [y5 - кол-во надетых на героя Бесстрашных хранителей]
!!VRy4&y5>0:+1;               [y4+=1, если надет Бесстрашный хранитель]
!!HEy2:A2/65/d/?y5;           [y5 - кол-во надетых на героя Символов Знаний]
!!VRy4&y5>0:+2;               [y4+=2, если надет Символ знаний]
!!FU&y3=y4:E;                 [выход если суммарные уровни ПС у противников равные (y3=y4)]
!!VRy1&y3>y4:Sy2;             [y1 - номер героя с более слабым ПС]
!!VRy2:Sy4 -y3;               [y2 - разница уровней ПС героев]
!!VRy2&y3>y4:*-1;             [y2 - уровень результирующего подавления]
!!IF:Wy1;                     [выбор героя для чтения w95-w98]
!!VRy3:Sw95 -y2;              [y3 - новый уровень магии огня]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S14/y3;                [подавляем магию огня]
!!VRy3:Sw96 -y2;              [y3 - новый уровень магии воздуха]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S15/y3;                [подавляем магию воздуха]
!!VRy3:Sw97 -y2;              [y3 - новый уровень магии воды]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S16/y3;                [подавляем магию воды]
!!VRy3:Sw98 -y2;              [y3 - новый уровень магии земли]
!!VRy3&y3<0:S0;               [...неотрицательный]
!!HEy1:S17/y3;                [подавляем магию земли]

!?BG1;                        [восстановление параметров школ маги героев по окончании боя]
!!BU:C?y1;                    [y1=1, если бой закончен]
!!FU&y1=0:E;                  [выход, если бой не закончен]
!!UN:P776/?y1;                [проверяем включена ли опция 776 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BA:H0/?y1;                  [y1 - атакующий герой]
!!BA:H1/?y2;                  [y2 - защищающийся герой]
!!FU&y2<0:E;                  [выход если битва против монстров без героя]
!!IF:Wy1;                     [выбор атакующего героя для чтения w95-w98]
!!HEy1:S14/w95;               [w95 - уровень магии огня (0..3) атакующего героя]
!!HEy1:S15/w96;               [w96 - уровень магии воздуха (0..3) атакующего героя]
!!HEy1:S16/w97;               [w97 - уровень магии воды (0..3) атакующего героя]
!!HEy1:S17/w98;               [w98 - уровень магии земли (0..3) атакующего героя]
!!IF:Wy2;                     [выбор защищающегося героя для чтения w95-w98]
!!HEy2:S14/w95;               [w95 - уровень магии огня (0..3) защищающегося героя]
!!HEy2:S15/w96;               [w96 - уровень магии воздуха (0..3) защищающегося героя]
!!HEy2:S16/w97;               [w97 - уровень магии воды (0..3) защищающегося героя]
!!HEy2:S17/w98;               [w98 - уровень магии земли (0..3) защищающегося героя]

** end
