ZVSE
ERMS_ScriptDate=10.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : External upgrades
** Name rus.     : Внешние улучшения
** Options       : 772
** Dialogs       : -
** Variables     : -
** Tmp variables : z1
** Timers        : -
** Functions     : FU7905
** PO-values     : -

!?OB17;                       [Посещение внешнего жилища тип 17]
!!UN:P772/?y1;                [проверяем включена ли опция 772 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW-1:C?y5 Iy5/?y6;          [y6=1 для ИИ, 0 для человека]
!!DW998:O?y7 G0/d/?y8;        [y7 - владелец жилища, y8 - охрана в 0м слоте]
!!FU&y5<>y7/y8>0:E;           [выход, если жилище не принадлежит игроку и есть охрана]
!!DW998:M0/?y1/?y11 M1/?y2/?y12 M2/?y3/?y13 M3/?y4/?y14; [y1..y4/y11-y14 - типы/количества 1-4го нанимаемого монстра (-1 - нет монстра)]
!!DO7905/0/6/1&y1>-1/y11>0:Py1/y6;[Проверка улучшения существ в армии героя до 1го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y2>-1/y12>0:Py2/y6;[Проверка улучшения существ в армии героя до 2го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y3>-1/y13>0:Py3/y6;[Проверка улучшения существ в армии героя до 3го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y4>-1/y14>0:Py4/y6;[Проверка улучшения существ в армии героя до 4го нанимаемого монстра, если такой есть]

!?OB20;                       [Посещение внешнего жилища тип 20]
!!UN:P772/?y1;                [проверяем включена ли опция 772 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW-1:C?y5 Iy5/?y6;          [y6=1 для ИИ, 0 для человека]
!!DW998:O?y7 G0/d/?y8;        [y7 - владелец жилища, y8 - охрана в 0м слоте]
!!FU&y5<>y7/y8>0:E;           [выход, если жилище не принадлежит игроку и есть охрана]
!!DW998:M0/?y1/?y11 M1/?y2/?y12 M2/?y3/?y13 M3/?y4/?y14; [y1..y4/y11-y14 - типы/количества 1-4го нанимаемого монстра (-1 - нет монстра)]
!!DO7905/0/6/1&y1>-1/y11>0:Py1/y6;[Проверка улучшения существ в армии героя до 1го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y2>-1/y12>0:Py2/y6;[Проверка улучшения существ в армии героя до 2го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y3>-1/y13>0:Py3/y6;[Проверка улучшения существ в армии героя до 3го нанимаемого монстра, если такой есть]
!!DO7905/0/6/1&y4>-1/y14>0:Py4/y6;[Проверка улучшения существ в армии героя до 4го нанимаемого монстра, если такой есть]

!?OB78;                       [Посещение лагеря беженцев]
!!UN:P772/?y1;                [проверяем включена ли опция 772 в y1]
!!FU&y1=0:E;                  [Выход, если опция не включена]
!!OW-1:C?y5 Iy5/?y6;          [y6=1 для ИИ, 0 для человека]
!!OB998:U?y7 C?y8;            [y7/y8 - тип/количество существ в лагере]
!!FU&y8<1:E;                  [выход, если лагерь пуст]
!!DO7905/0/6/1&y7>-1:Py7/y6;  [Проверка улучшения существ в армии героя до нанимаемого монстра, если такие есть]

!?FU7905;                     [Проверка и улучшение существ героя. x16 - слот в армии, x1 - улучшенное существо, x2=1 для ИИ]
!!HE-1:C0/x16/?y1/?y2;        [y1,y2 - тип и кол-во монстров в слоте]
!!FU&y1=-1:E;                 [выход если монстров нет]
!!MA:Uy1/?y3;                 [y3 - тип существа, в которое может улучшаться монстров в слоте, -1 - по умолчанию]
!!VRy3&y3=-1/y1<=111:Sy1 %2 -1 *-1 +y1;  [y3 - номер улучшенного существа, кроме существ конфлюкса]
!!VRy3&y3=-1/y1=118:S119;     [Pixie]
!!VRy3&y3=-1/y1=112:S127;     [Air Elemental]
!!VRy3&y3=-1/y1=115:S123;     [Water Elemental]
!!VRy3&y3=-1/y1=114:S129;     [Fire Elemental]
!!VRy3&y3=-1/y1=113:S125;     [Earth Elemental]
!!VRy3&y3=-1/y1=120:S121;     [Psychic Elemental]
!!VRy3&y3=-1/y1=130:S131;     [Firebird]
!!VRy3&y3=-1/y1=116:S117;     [Gold golem]
!!FU|y3<>x1/y1=x1:E;          [выход если существо не может быть улучшено до обитателя жилища или уже улучшено]
!!HE-1&x2=1:C0/x16/x1/d;      [автоматическое бесплатное улучшение для ИИ]
!!FU&x2=1:E;                  [выход, если ИИ]
!!MA:Cx1/0/?y10 Cx1/1/?y11 Cx1/2/?y12 Cx1/3/?y13 Cx1/4/?y14 Cx1/5/?y15; [y10-y15 стоимость улучшенного существа (ресурс)]
!!VRy16:S-1;                  [по умолчанию ресурса нет]
!!VRy16&y10>0:S0;             [доп ресурс - дерево]
!!VRy16&y11>0:S1;             [доп ресурс - ртуть]
!!VRy16&y12>0:S2;             [доп ресурс - руда]
!!VRy16&y13>0:S3;             [доп ресурс - сера]
!!VRy16&y14>0:S4;             [доп ресурс - кристаллы]
!!VRy16&y15>0:S5;             [доп ресурс - драг.камни]
!!MA&y16>-1:Cy1/y16/?y17;     [y17 - цена 1го неулучшенного существа (ресурс)]
!!VRy15&y16=0:Sy10 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y16=1:Sy11 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y16=2:Sy12 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y16=3:Sy13 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y16=4:Sy14 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y16=5:Sy15 -y17 *y2;  [y15 - базовая стоимость улучшения (ресурс)
!!VRy15&y15<0:S0;             [нулевая стоимость (ресурс), если улучшенное существо дешевле]
!!MA:Cy1/6/?y4 Cx1/6/?y5;     [y4,y5 - цена 1го неулучшенного/улучшенного существа (золото)]
!!VRy5:-y4 *y2;               [y5 - базовая стоимость улучшения (золото)]
!!VRy5&y5<0:S0;               [нулевая стоимость, если улучшенное существо дешевле]
!!UN:J2/?y4;                  [y4=0..4 уровень сложности]
!!VRy4:-1;                    [y4=-1..3 - коэффициент повышения цены в зависимости от уровня сложности]
!!UN:P772/?y10;               [проверяем включена ли опция 786 Дипломатические торги в y10]
!!HE-1&y10=1:S4/?y6 A2/66/d/?y7 A2/67/d/?y8 A2/68/d/?y9; [y6 - навык дипломатии, y7-y9 - кол-во надетых артефактов дипломата медаль/кольцо/лента]
!!VRy8&y8>0/y10=1:S1;         [учитываем только одно кольцо]
!!VRy6&y10=1:+y7 +y8 +y9 *-1; [y6=-6..0 - коэффициент снижения цены в зависимости от навыка/артефактов Дипломатии]
!!VRy4&y10=1:+y6;             [y4=-7..3 - результирующий коэффициент изменения цены]
!!VRy4:*y5 :10;               [y4 - скидка/надбавка 10% за уровень сложности/Диплотамии]
!!VRy5:+y4;                   [y5 - итоговая стоимость улучшения]
!!OW:R-1/6/?y4;               [y4 - текущее золото игрока]
!!OW&y16>-1:R-1/y16/?y14;     [y14 - текущие ресурсы игрока]
!!FU&y4<y5:E;                 [выход, если не хватает золота]
!!FU&y16>-1/y14<y15:E;        [выход, если не хватает ресурсов]
!!UN&y2=1:N3/2/y1/0;          [z2 - название улучшаемого существа (ед.ч.)]
!!UN&y2>1:N3/2/y1/1;          [z2 - название улучшаемого существа (мн.ч.)]
!!UN&y2=1:N3/3/x1/0;          [z2 - название получаемого существа (ед.ч.)]
!!UN&y2>1:N3/3/x1/1;          [z2 - название получаемого существа (мн.ч.)]
!!VRz1:Sz179071;              [получаем текст вопроса в z1]
!!VRy20:Sy5 *-1 -100000;      [y20 - отрицательное количество золота для отображения в диалоге]
!!VRy20&y5=0:S0;              [нулевая стоимость улучшения не отображается в диалоге]
!!VRy21:Sy15 *-1 -100000;     [y21 - отрицательное количество ресурса для отображения в диалоге]
!!IF&y16>-1:Q1/21/x1/6/y20/y16/y21/2/z1;[задаем вопрос: улучшить ли стек (золото и ресурс)]
!!IF&y16=-1:Q1/21/x1/6/y20/2/z1;[задаем вопрос: улучшить ли стек (только золото)]
!!FU&-1:E;                    [выход, если ответ отрицательный]
!!HE-1:C0/x16/x1/d/0/13;      [улучшаем существа в стеке с потерей опыта при улучшении]
!!VRy5:*-1;
!!VRy15:*-1;
!!OW:R-1/6/dy5;               [уменьшаем золото игрока]
!!OW&y16>-1:R-1/y16/dy15;     [уменьшаем ресурс игрока]
!!UN:R1;                      [обновляем экран]

** end
