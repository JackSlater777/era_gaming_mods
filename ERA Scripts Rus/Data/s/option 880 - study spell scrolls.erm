ZVSE
ERMS_ScriptDate=22.9(September).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : DracoLich
** Name          : Study spell scrolls
** Name rus.     : ”чеба свитков заклинаний
** Changes rus.  : [Algor] вынос опции в отдельный файл дл€ мода ERA, построчные комментарии
**                 [Algor] вынос текстов в ert-файл
**                 [Algor] уровень заклинани€ определ€етс€ через SS:L, а не "hardcoded"
**                 [Algor] проверка то, что герой уже изучил заклинание
**                 [Algor] сообщение о том, что гильди€ недостаточно высокого уровн€ не выводитс€
**                 [Algor] цена за обучение прив€зана к базовой стоимости заклинани€ S (S^2 * 5)
**                 [Algor] перенумераци€ функций/переменных и пр.
** Options       : 880
** Dialogs       : -
** Variables     : -
** Tmp variables : z1
** Timers        : -
** Functions     : FU7935

!?CM1;                        [клик мышкой в окне города]
!!UN:P880/?y1;                [провер€ем включена ли опци€ в y1]
!!CM:I?y2 F?y3;               [y1/y2 - параметры клика]
!!FU|y1=0/y2>5/y2<0/y3<>1:E;  [выход, если опци€ не включена или не Shift+Ћ ћ на гильдии магов]
!!CA-1:H1/?y1 B3/y2;          [y1 - номер геро€-гост€, флаг 1 ложь, если здание не построено]
!!FU|y1=-1/-1:E;              [выход, если нет геро€-гост€ или гильдии]
!!DO7935/0/69/1:Py2/y1;       [дл€ каждого заклинани€ провер€ем наличие свитка и предлагаем изучение]
!!VRz1:Sz179095;              [сообщение "больше нет свитков"]
!!IF:M^%Z1^;                  [...]

!?FU7935;                     [x1 - уровень √ћ, x2 - герой, x16 - номер заклинани€]
!!VRy1:Sx16 +1001;            [y1 - номер артефакта-свитка]
!!HEx2:A2/y1/?y2/?y3;         [y2/y3 - общее/одетое количество свитков у геро€]
!!FU&y2=0/y3=0:E;             [выход, если у геро€ нет свитков]
!!HEx2:M=x16/1;               [флаг 1 истина, если герой уже изучил заклинание]
!!FU&1:E;                     [выход, если заклинание уже изучено]
!!SSx16:L?y4;                 [y4 - уровень заклинани€ (1..5)]
!!VRy5:Sx1 +1;                [y5 - уровень гильдии (1..5)]
!!FU&y4>y5:E;                 [выход, если гильди€ недостаточно высокого уровн€]
!!SSx16:C0/?y6;               [y6 - базова€ стоимость заклинани€ S]
!!VRy6:*y6 *5;                [y6 - стоимость изучени€ в гильди магов S^2*5]
!!VRz1:Sz179093;              [сообщение "хотите переписать заклинани€?"]
!!IF:Q1/9/x16/2^%Z1^;         [...]
!!FU&-1:E;                    [выход, если ответ отрицателен]
!!OW:R-1/6/?y7;               [y7 - количество золота у игрока]
!!VRz1&y7<y6:Sz179094;        [сообщение "недостаточно золота"]
!!IF&y7<y6:M^%Z1^;            [...]
!!FU&y7<y6:E;                 [выход, если золота недостаточно]
!!VRy6:*-1;                   [y6 - отрицательное количество золота дл€ списани€]
!!OW:R-1/6/dy6;               [списание оплаты]
!!UN:R2 R4;                   [обновление строки ресурсов и экрана города]
!!HEx2:Mx16/1 A3/y1/1/1;      [изучение заклинани€ и удаление свитка]

** end
