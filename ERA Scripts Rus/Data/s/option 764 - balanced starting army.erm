ZVSE
ERMS_ScriptDate=12.2(February).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : balanced starting army
** Name rus.     : Сбалансированные стартовые армии
** Options       : 764
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

!#UN:P764/?v1;                          // проверяем включена ли опция 764 в v1
!#UN&v1>0:P199/0;                       // отключаем опцию 199 "Измененные стартовые армии героев"
!#VRv1402&v1>0:S0;                      // ...

!?FU(OnAfterErmInstructions);           // перед показом карты
!!UN:P764/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!UN:C6919480/4/?y10; !!VRy11:Sy10 +128980; !!UN:Cy11/1/?y11; [y11=114 для рандомной карты (каталог [r]andom_maps) thanks to feanor] 
!!UN&y11<>114:P764/0;                   // отключение опции для нерандомной карты

!?FU(OnEveryDay);                       // ежедневно
!!UN:P764/?y1; !!FU&y1=0:E;             // выход, если опция не включена
!!SN:W^opt764.day^/?y1 W^opt764.day^/1; // y1 признак выполнения балансировки стартовых героев
!!DO(ES.opt764.BSA)/0/155/1&y1=0:P0/0/y11; // если балансировка еще не выполнялась, выполняем
!!VRy1:Sc %7; !!FU&y1<>1:E;             // выход, если не понедельник
!!OW:C?y3 Vy3/?y1/?y2;                  // y1/y2 - герои в таверне текущего игрока
!!FU(ES.opt764.BSA)&y1>=0:P1/y1/0;      // балансировка стартовых армий героев таверны
!!FU(ES.opt764.BSA)&y2>=0:P1/y2/0;      // ...

!?FU(ES.opt764.BSA);                    // Балансировка стартовой армии героя
!!VRx16&x1=1:Sx2;            [проверяется герой из x2 для героев в тавернах]
!!HEx16&x1=0:O?y1 P?y2/?y3/?y4 E?y5/?y6/1; [y1/y2/y6 - владелец/абсцисса/уровень героя]
!!FU&x1=0/y1<0:E;            [выход, если начало игры и герой не нанят]
!!FU&y1<0/y2>=0/x3<>114:E;   [выход, если герой в тюрьме на авторской (не рандомной) карте]
!!FU&y6>1:E;                 [выход, если герой не первого уровня]
!!SN&x1=1:W^opt764.H%X16.BDay^/?y7; [y7 - мин. дата балансировки для героя]
!!VRy8&x1=1:Sc; !!FU&x1=1/y7>y8:E;  [выход, если герой вчера сбегал/откупался]
!!VRy10:S4000;               [y10 - макс. FV армии героя]
!!HEx16:B2/?y1 X?y2/?y3/d/d/d/d/?i; [y1 - класс, y2/y3 - специальность]
!!VRy2&y2=4:S1;              [Учет специальности "Экстраспец по существам"]
!!VRy3&y2=6:Si;              [Учет специальности "Спец по улучшениям"]
!!VRy2&y2=6:S1;              [...]
!!VRy1::2;                   [y1 - тип родного города]
!!UN:Ty1/0/0/?y11 Ty1/1/0/?y12 Ty1/2/0/?y13; [y11-y13 типы существ по умолчанию для 1-3 слота]
!!if&y2=1/y3<>145/y3<>146/y3<>147/y3<>148/y3<>149:; [если герой спец по существам (кроме БМ), переопределяем типы существ в слотах]
  !!MA:Ly3/?y4;              [y4 - уровень спец.существа (0..6)]
  !!VRy3&y4>2/y4<6/y3<112:Sy3 :2 *2 +1; // используем улучшенных спец.существ для 4-6 уровней
  !!VRy3&y3=114:S129;        [... энергетический эл.]
  !!VRy3&y3=113:S125;        [... эл. магмы]
  !!VRy3&y3=120:S121;        [... эл. магии]
  !!UN:P67/?k;               [Совместимость с WoG-опцией "Нейтральный город"]
  !!if&k>0:;                 [если опция включена, переопределяем апгрейды существ]
    !!VRy3&y3=138:S143;      [Halfling-Rogue]
    !!VRy3&y3=140:S142;      [Boar-Nomad]
    !!VRy3&y3=164:S166;      [Fire messenger-Air messenger]
    !!VRy3&y3=194:S144;      [Werewolf-Troll]
    !!VRy3&y3=193:S136;      [Sorceress-Enchanter]
    !!VRy3&y3=195:S172;      [Hell Steed-Nightmare]
    !!VRy3&y3=168:S135;      [Gorynych-Rust Dragon]
  !!en;
  !!VRy12&y4<3:Sy3;          [спецы по существам 1-3 уровней имеют их во 2 слоте]
  !!VRy12&y4>2/y4<6:Sy11;    [спецы по существам 4-6 уровней имеют во 2 слоте существ 1го уровня]
  !!VRy13&y4<6:Sy3;          [спецы по существам 1-6 уровней имеют их в 3 слоте]
!!en:;
** Герои с машинным навыком получают соотв. машину
!!HEx16:S10/?y51 S20/?y52 S27/?y53; [y51/y52/y53 - навыки Баллистики/Артиллерии/ПП у героя]
!!VRy51&y2=1/y3=148:S1;      [Тележка положена спецам по тележкам (а вдруг!)]
!!VRy51&y2=0/y3=10:S1;       [Тележка положена спецам по Баллистике/Осадному делу]
!!VRy52&y2=1/y3=146:S1;      [Баллиста положена спецам по баллистам]
!!VRy52&y2=0/y3=20:S1;       [Баллиста положена спецам по Артиллерии]
!!VRy53&y2=1/y3=147:S1;      [Палатка положена спецам по Палаткам]
!!VRy53&y2=0/y3=27:S1;       [Палатка положена спецам по Первой помощи/Медицине]
!!HEx16&y51>0:A1/5/14;       [даем Тележку]
!!VRy10&y51>0:-500;          [уменьшаем макс. FV армии на FV боевой машины]
!!HEx16&y52>0:A1/4/13;       [даем Баллисту]
!!VRy10&y52>0:-1500;         [уменьшаем макс. FV армии на FV боевой машины]
!!HEx16&y53>0:A1/6/15;       [даем Палатку]
!!VRy10&y53>0:-750;          [уменьшаем макс. FV армии на FV боевой машины]
** Распределение сил по слотам:
** 3й слот - 30-60% FV
** 2й слот - 40-80% оставшегося FV
** 1й слот - оставшееся FV
!!MA:Fy11/?y31 Fy12/?y32 Fy13/?y33; [y31-y33 -  FV существ в 1-3 слотах]
!!VRy30:Sy10 *3 :5 :y33;     [y30 - макс. кол-во существ в 3 слоте]
!!VRy40:Sy30 :2; !!VRy40&y40<1:S1; [y40 - мин кол-во существ (50% от максимума, но не менее 1)]
!!VRy30:-y40; !!VRy30&y30<0:S0;    [y30 - доп. кол-во существ (не меньше 0)]
!!VRy43:Sy40 Ry30;           [y43 - кол-во существ в 3 слоте]
!!VRy30:Sy43 *y33;           [y30 - FV 3го слота]
!!VRy10:-y30;                [y10 - оставшееся FV]
!!VRy10&y10<0:S1;            [...но не меньше 1]
!!VRy30:Sy10 *4 :5 :y32 -1;  [y30 - макс. кол-во существ во 2 слоте -1]
!!VRy40:Sy30 :2; !!VRy40&y40<1:S1; [y40 - мин кол-во существ (50% от максимума, но не менее 1)]
!!VRy30:-y40; !!VRy30&y30<0:S0;    [y30 - доп. кол-во существ (не меньше 0)]
!!VRy42:Sy40 Ry30;           [y43 - кол-во существ во 2 слоте]
!!VRy30:Sy42 *y32;           [y30 - FV 2го слота]
!!VRy10:-y30;                [y10 - оставшееся FV]
!!VRy10&y10<0:S1;            [...но не меньше 1]
!!VRy41:Sy10 :y31;           [y41 - кол-во существ в 1 слоте]
!!VRy41&y41<1:S1;            [...но не меньше 1]
!!HEx16:C0/0/y11/y41 C0/1/y12/y42 C0/2/y13/y43; [установка текущей армии героя]

!?BG0;                       [перед действием в бою]
!!UN:P764/?y1;  !!FU&y1=0:E; [Выход, если опция не включена]
!!BG:A?y1; !!FU|y1<4/y1>5:E; [выход, если не сдача/откуп]
!!BG:Q?y1; !!BA:Hy1/?y2;     [y2 - сбегающий/откупающийся герой]
!!VRy3:Sc +2;                [y3 - день, ранее которого армию героя не балансировать]
!!SN:W^opt764.H%Y2.BDay^/y3; [установка минимального дня для балансировки стартовой армии героя]

** end
