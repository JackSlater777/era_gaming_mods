ZVSE

** Author        : Algor
** Name          : Zealots interceptors
** Name rus.     : Фанатики-перехватчики
** Options       : 741
** Dialogs       : -
** Variables     : z189
** Tmp variables : -
** Timers        : -
** Functions     : FU7990-FU7991
** PO-values     : -

** Фанатики Войны получают шанс перехватить (перенаправить на себя) целевое вражеское заклинание
** Шанс перехвата = (Кол-во фанатиков в отряде)%
** Перехваченное заклинание далее может быть отражено во врага со стандартной вероятностью.
** FV Фанатиков повышено на 5%

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P741/?y1;                [проверяем включена ли опция 741 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz304:Sz179282;            [получаем из ert новую подсказку по существу]
!!UN:G1/169/2/304;            [установка новой подсказки]
!!MA:F169/?y1;                [y1 - FV Фанатиков]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F169/y1;                 [обновляем FV Фанатиков]


!?BG0;                        [перед действием в бою]
!!UN:P741/?y1;                [проверяем включена ли опция 741 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BG:A?y1 Q?y2 E?y3;          [y1/y2/y3 - тип действия/сторона/целевой отряд]
!!FU&y1<>1/y1<>10:E;          [выход, если не каст героя/монстра]
!!FU&y3=-1:E;                 [выход, ecли не целевой каст]
!!FU&y3<21/y2=0:E;            [выход, если не каст на вражеский отряд]
!!FU&y3>20/y2=1:E;            [...]
!!VRy4:Sy2 -1 *-21;           [y4-y5 - диапазон вражеских отрядов]
!!VRy5:Sy4 +20;               [...]
!!VRv1:S-1;                   [инициализация v1]
!!DO7990/y4/y5/1:Py3;         [поиск отряда-перехватчика]
!!FU&v1=-1:E;                 [выход, если отряд-перехватчик не найден]
!!VRy6:Sv1;                   [y6 - отряд-перехватчик]
!!BA:Q?f;                     [f-статус быстрой битвы]
!!VRz1&f=0:S^MIRRORIM^;       [z1 - звуковой файл]
!!SN&f=0:P1;                  [вывод звука бешенства]
!!BMy6:P?y7;                  [y7 - позиция отряда]
!!BMy6&f=0:V75;               [анимация Поглощения маны на отряде Фанатиков]
!!BG:Ey6 Dy7;                 [переопределение цели и позиции]
!!SS36:E3/?y8;                [y8 - шанс срабатывания экспертного Волшебного зеркала]
!!VRy9:S1 R99;                [y9 - случайное число (1..100)]
!!FU&y8<y9:E;                 [выход, если отражение не сработало]
!!VRy4:Sy2 *21;               [y4-y5 - диапазон отрядов колдующей стороны]
!!VRy5:Sy4 +20;               [...]
!!VRv1:S-1;                   [инициализация v1]
!!DO7991/y4/y5/1:P-1;         [v1 - кол-во отрядов для перенаправления]
!!FU&v1=-1:E;                 [выход, если отрядов нет, что очень маловероятно ;)]
!!VRy9:S0 Rv1;                [y9 - случайный отряд колдующей стороны]
!!VRv1:S-1;                   [инициализация v1]
!!DO7991/y4/y5/1:Py9;         [v1 - отряд для перенаправления]
!!VRy10:Sv1;                  [y10 - отряд для перенаправления]
!!BMy6&f=0:V3;                [Анимация Волшебное зеркало на отряде Фанатиков]
!!BMy10:P?y11;                [y11 - позиция отряда для перенаправления]
!!BG:Ey10 Dy11;               [переопределение цели и позиции]

!?FU7990;                     [возврат в v1 номера перехватившего заклинание отряда Фанатиков]
!!BMx16:T?y1 N?y2;            [y1/y2 - тип/численность проверяемого отряда]
!!FU|y1<>169/y2<0/x1=x16:E;   [выход, если отряд не Фанатиков или отряд мертв или отряд является исходной целью]
!!VRy3:S0 R99;                [y3 - случайное число (0..99)]
!!VRv1&y2>y3:Sx16;            [если перехват сработал, возврат номера отряда]
!!VRx16&y2>y3:S50;            [прерывание цикла]

!?FU7991;                     [возврат в v1 количества (x1=-1) или номера x1-го отряда]
!!BMx16:T?y1 N?y2;            [y1/y2 - тип/численность проверяемого отряда]
!!VRv1&y1>=0/y2>0:+1;         [v1 - кол-во/номер (-1) найденных отрядов]
!!if&x1>=0/x1=v1:;            [если найден номер отряда]
  !!VRv1:Sx16;                [возврат номера отряда]
  !!VRx16:S50;                [прерывание цикла]
!!en:;

** end
