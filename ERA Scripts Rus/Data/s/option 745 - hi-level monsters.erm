ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor [Idea: Ivor]
** Name          : Hi-level monsters
** Name rus.     : Высокоуровневые монстры
** Options       : 745
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7985
** PO-values     : -

** Отряды существ на карте могут заменяться аналогичными по силе и расе отрядами существ более высокого уровня.
** Отряды нейтральных существ не заменяются.
** Новая численность округляется вверх от 1/3.

!?TM1;
!!UN:P745/?y1;                [проверяем включена ли опция 745 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!UN:U54/-1/?y1;              [y1 - количество существ на карте]
!!VRv1:S-1;                   [инициализация v1 для быстрого поиска]
!_!VRv4:S0;                   [DEBUG]
!!DO7985/1/y1/1:P;            [перебираем существ]
!_!UN:U54/-1/?y2;             [DEBUG]
!_!IF:M^Монстров на карте: %Y1->%Y2, Усилено: %V4^; [DEBUG]
!!UN:P745/0;                  [отключаем опцию 745]

!?FU7985;                     [замена существ]
!!UN:U54/-1/-1/1;             [получаем координаты существ в v1-v3]
!!OB1:U?y1;                   [y1 - тип существа]
!!FU|y1>129/y1=116/y1=117:E;  [выход, если существо - нейтрал]
!!MO1:G?y2;                   [y2 - кол-во существ]
!!MA:Ly1/?y3;                 [y3 - уровень существа (0..6)]
!!VRy3:*-1 +6;                [y3 - уровень существа (6..0)]
!!VRy3&y3>3:S3;               [y3 - макс. кол-во уровней для повышения (0..3)]
!!VRy4:S0 Ry3 *2;             [y4 - кол-во "номеров" для повышения (0/2/4/6)]
!!FU&y4=0:E;                  [выход, если уровень существ не повышеается]
!!VRy3:Sy1 +y4;               [y3 - новый тип существа]
!!if&y1>97/y1<130:;           [нестандартные номера существ]
  !!VRy4&y1=98/y3=102:S106;   [...]
  !!VRy4&y1=99/y3=103:S107;   [...]
  !!VRy4&y1=104/y3=110:S102;  [...]
  !!VRy4&y1=105/y3=111:S103;  [...]
  !!VRy4&y1=106/y3=112:S102;  [...]
  !!VRy4&y1=107/y3=113:S103;  [...]
  !!VRy4&y1=102/y3=104:S108;  [...]
  !!VRy4&y1=102/y3=106:S110;  [...]
  !!VRy4&y1=103/y3=105:S109;  [...]
  !!VRy4&y1=103/y3=107:S111;  [...]
  !!VRy4&y1=118/y3=120:S112;  [...]
  !!VRy4&y1=118/y3=122:S115;  [...]
  !!VRy4&y1=118/y3=124:S114;  [...]
  !!VRy4&y1=119/y3=121:S127;  [...]
  !!VRy4&y1=119/y3=123:S123;  [...]
  !!VRy4&y1=119/y3=125:S129;  [...]
  !!VRy4&y1=112/y3=114:S115;  [...]
  !!VRy4&y1=112/y3=116:S114;  [...]
  !!VRy4&y1=112/y3=118:S113;  [...]
  !!VRy4&y1=127/y3=129:S123;  [...]
  !!VRy4&y1=127/y3=131:S129;  [...]
  !!VRy4&y1=127/y3=133:S125;  [...]
  !!VRy4&y1=115/y3=117:S114;  [...]
  !!VRy4&y1=115/y3=119:S113;  [...]
  !!VRy4&y1=115/y3=121:S120;  [...]
  !!VRy4&y1=123/y3=125:S129;  [...]
  !!VRy4&y1=123/y3=127:S125;  [...]
  !!VRy4&y1=123/y3=129:S121;  [...]
  !!VRy4&y1=114/y3=116:S113;  [...]
  !!VRy4&y1=114/y3=118:S120;  [...]
  !!VRy4&y1=114/y3=120:S130;  [...]
  !!VRy4&y1=129/y3=131:S125;  [...]
  !!VRy4&y1=129/y3=133:S121;  [...]
  !!VRy4&y1=129/y3=135:S131;  [...]
  !!VRy4&y1=113/y3=115:S120;  [...]
  !!VRy4&y1=113/y3=117:S130;  [...]
  !!VRy4&y1=125/y3=127:S121;  [...]
  !!VRy4&y1=125/y3=129:S131;  [...]
  !!VRy4&y1=120/y3=122:S130;  [...]
  !!VRy4&y1=121/y3=123:S131;  [...]
  !!VRy3:Sy4;                 [...]
!!en:;                        [...]
!!MA:Fy1/?y5 Fy3/?y6;         [y5/y6 - FV старого и нового существа]
!!VRy4:Sy2 *y5 :y6;           [y4 - кол-во новых существ]
!!VRy7:Sy2 *y5;               [y7 - FV старого отряда]
!!VRy8:Sy4 *y6;               [y8 - FV нового отряда]
!!VRy9:Sy7 -y8 *3;            [y9 - утроенное "потерянное" FV]
!!VRy4&y9>=y6:+1;             [y4 - кол-во новых существ, с округлением вверх от 1/3]
!!VRy4&y4<1:S1;               [минимум 1 новое существо]
!!OB1:C?y10;                  [y10 - контрольное число монстров]
!!UN:Ov1/v2/v3/1;             [удаление старого существа]
!!UN:Iv1/v2/v3/54/y3/54/y3/-1/0; [установка нового существа]
!!VRy11:Sy10 -y2 +y4;         [y11 - новое контрольное число монстров]
!!OB1:Cy11;                   [обновление контрольного числа]
!_!VRv4:+1;                   [DEBUG]

**end
