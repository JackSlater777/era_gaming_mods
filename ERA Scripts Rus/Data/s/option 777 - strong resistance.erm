ZVSE
ERMS_ScriptDate=13.10(October).2012
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2004.10.5.945
** Author orig.  : Algor
** Name          : Strong resistance
** Name rus.     : Усиленное сопротивление
** Options       : 777
** Dialogs       : -
** Variables     : z958, z994-z995

!#UN:P777/?v1;                [проверяем включена ли опция 777 в y1]
!#UN&v1>0:P210/0;             [если включена, отключаем опцию Сопротивление II]
!#VRv7195&v1>0:S0;            [...]

!?PI;                         [пост-инструкция: установка описаний]
!!UN:P777/?y1;                [проверяем включена ли опция 777 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRe1:S15 :100;              [Шанс срабатывания экспертного сопротивления - 15%]
!!SN:X?y2 Xe1 X?y3 Xy2;       [...]
!!UN:C6548052/4/y3;           [...]
!!VRz1:Sz276 +^%Z179034^; !!SN:H^secskill^/26/1/^%Z1^; [Устанавливаем базовое описание навыка]
!!VRz1:Sz277 +^%Z179035^; !!SN:H^secskill^/26/2/^%Z1^; [Устанавливаем продвинутое описание навыка]
!!VRz1:Sz278 +^%Z179036^; !!SN:H^secskill^/26/3/^%Z1^; [Устанавливаем экспертное описание навыка]
!!VRz1:Sz179037; !!SN:H^secskill^/20/2/^%Z1^;          [Устанавливаем описание специализации герою Торгрим]
!!VRz994:Sz417 +^%Z179038^;   [получаем описание Колье неприступности]
!!UN:A57/10/994;              [устанавливаем описание Колье неприступности]
!!VRz995:Sz418 +^%Z179039^;   [получаем описание Мантия равновесия]
!!UN:A58/10/995;              [устанавливаем описание Мантия равновесия]
!!VRz958:Sz419 +^%Z179040^;   [получаем описание Сапоги противодействия]
!!UN:A59/10/958;              [устанавливаем описание Сапоги противодействия]

!?GM0;                        [после загрузки игры]
!!VRe1:S15 :100;              [Шанс срабатывания экспертного сопротивления - 15%]
!!SN:X?y2 Xe1 X?y3 Xy2;       [...]
!!UN:C6548052/4/y3;           [...]

!?MR1;                        [перед расчетом сопротивления]
!!UN:P777/?y1;                [проверяем включена ли опция 777 в y1]
!!FU&y1=0:E;                  [выход, если опция не включена]
!!MR:D?y1 F?y6;               [y1/y6 - базовый/конечный урон заклинания]
!!VRy6:-y1 *-1;               [y6 - кол-во урона, поглощенное естественным и магическим сопротивлениями]
!!UN:C42231940/4/?y11;        [y2 - номер отряда-цели заклинания]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y12;             [y12 - гекс отряда, получающего урон]
!!FU|y12<0/y12>186:E;         [выход, если гекс за пределами поля]
!!BU:Ey12/?y2;                [y2 - номер отряда на позиции]
!!VRy2::21;                   [y2 - сторона отряда-цели заклинания]
!!BA:Hy2/?y2;                 [y2 - номер героя-владельца]
!!FU&y2<0:E;                  [выход если героя-владельца нет]
!!HEy2:S26/?y3;               [y3 - уровень Сопротивления (0..3) героя]
!!HEy2:X?y4/?y5/d/d/d/d/d;    [y4=0, y5=26 для специалиста по сопротивлению]
!!VRy3&y4=0/y5=26:+1;         [y3 - уровень Сопротивления героя с учетом специализации(0..4)]
!!HEy2:A2/57/d/?y4;           [y4 - кол-во надетых на героя Колье неприступности]
!!VRy3&y4>0:+1;               [y3 - уровень Сопротивления с учетом Колье]
!!HEy2:A2/58/d/?y4;           [y4 - кол-во надетых на героя Мантия равновесия]
!!VRy3&y4>0:+2;               [y3 - уровень Сопротивления с учетом Мантии]
!!HEy2:A2/59/d/?y4;           [y4 - кол-во надетых на героя Сапоги противодействия]
!!VRy3&y4>0:+3;               [y3 - уровень Сопротивления с учетом Сапог]
!!VRy3:*5 *y1 :100;           [y3 - количество урона поглощеннго голем-like сопротивлением]
!!VRy1:-y3 -y6;               [y1 - новый конечный урон заклинания]
!!VRy1&y1<0:S0;               [...]
!!MR:Fy1;                     [устанавливаем новый конечный урон заклинания]

** end
