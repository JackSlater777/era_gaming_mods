ZVSE
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814

** Author        : Algor
** Name          : Halflings
** Name rus.     : Хоббиты-уклонисты
** Options       : 752
** Dialogs       : -
** Variables     : z188
** Tmp variables : -
** Timers        : -
** Functions     : -
** PO-values     : -

** Ловкость Хоббитов позволяет им уклоняться от атаки
** или заклинания урона не направленного непосредственно на них.
** Эффекты обездвиживания или изменения сознания отменяют способность к уклонению.
** FV Хоббитов повышено на 5%.

!?PI;
!!UN:P752/?y1;                [проверяем включена ли опция 752 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!VRz188:Sz179261;            [получаем из ert новую подсказку по существу]
!!UN:G1/138/2/188;            [установка новой подсказки]
!!MA:F138/?y1;                [y1 - FV хоббитов]
!!VRy1:*105 :100;             [увеличиваем FV на 5%]
!!MA:F138/y1;                 [обновляем FV]

!?MF1;
!!UN:P752/?y1;                [проверяем включена ли опция 752 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!BG:D?y1;                    [y1 - атакуемый гекс]
!!MF:W?y2 N?y3;               [y2/y3 - тип урона/отряд получающий урон]
!!BMy3:T?y4 P?y5 N?y6;        [y4/y5/y6 - тип/позиция/численность отряда, получающего урон]
!!BG:N?y7;                    [y7 - активный отряд]
!!FU|y2<>0/y4<>138/y1=y5/y3=y7:E; [выход, если урон рва/башен, урон получают не Хоббиты, урон прямой или ответная атака]
!!BMy3:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживания/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на Хоббитах есть заклинание обездвиживания/разума]
!!MF:E0;                      [отмена урона]
!!BMy3:V86;                   [анимация блока урона на отряде]
!!UN&y6=1:N3/2/138/1;         [z2 - название Хоббита]
!!UN&y6>1:N3/2/138/1;         [z2 - название Хоббитов]
!!VRz1&y6=1:Sz179262;         [z1 - текст сообщения для вывода в лог]
!!VRz1&y6>1:Sz179263;         [...]
!!MM:Sz1;                     [вывод текста в лог битвы]

!?MR1;
!!UN:P752/?y1;                [проверяем включена ли опция 752 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!MR:M?y1;                    [y1 - тип существа получающего урон]
!!FU&y1<>138:E;               [выход, если урон получают не Хоббиты]
!!BG:A?y1 D?y2;               [y1/y2 - действие/атакуемый гекс]
!!FU&y1<>1/y1<>10:E;          [выход, если не каст заклинания]
!!UN:C42231940/4/?y11;        [спасибо за код Berserker'у]
!!VRy11:+56;                  [...]
!!UN:Cy11/4/?y3;              [y3 - гекс отряда, получающего урон]
!!FU|y3<0/y3>186:E;           [выход, если гекс за пределами поля]
!!BU:Ey3/?y4;                 [y4 - номер отряда на позиции]
!!FU&y4<0:E;                  [выход, если живого отряда нет]
!!BMy4:N?y6;                  [y6 - численность отряда, получающего урон]]
!!FU|y2=y3/y6<1:E;            [выход, если урон прямой или отряд мертв]
!!BMy4:G59/?y11/d G60/?y12/d G61/?y13/d G62/?y14/d G70/?y15/d G72/?y16/d G74/?y17/d; [y11-y17 - длительность заклинаний обездвиживания/разума]
!!FU|y11>0/y12>0/y13>0/y14>0/y15>0/y16>0/y17>0:E; [выход, если на Хоббитах есть заклинание обездвиживания/разума]
!!MR:F0;                      [обнуление магического урона]
!!BA:Q?f; !!FU&f=1:E;         [выход, если быстрая битва]
!!BMy4:V86;                   [анимация блока урона на отряде]
!!UN&y6=1:N3/2/138/1;         [z2 - название Хоббита]
!!UN&y6>1:N3/2/138/1;         [z2 - название Хоббитов]
!!VRz1&y6=1:Sz179262;         [z1 - текст сообщения для вывода в лог]
!!VRz1&y6>1:Sz179263;         [...]
!!MM:Sz1;                     [вывод текста в лог битвы]

**end
