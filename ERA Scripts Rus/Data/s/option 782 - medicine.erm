ZVSE2
** Author orig.  : Algor
** Name          : Medicine
** Name rus.     : Медицина
** Options       : 782

** Внимание! Для работы требуется "FUN.erm".


!?FU(OnAfterErmInstructions);           [перед началом новой игры]
!!UN:P782/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

; Set up new name
!!SN:H^secskill^/(SKILL_FIRST_AID)/0/^%T(es.782.name)^;

; Set up new skill description
!!VR(basicDesc:z):S^%T(es.782.desc1)^;
!!VR(advancedDesc:z):S^%T(es.782.desc2)^;
!!VR(expertDesc:z):S^%T(es.782.desc3)^;

; Add in First Aid I description if enabled
!!UN:P204/?(firstAidI:y);

!!if&(firstAidI);
  !!VR(basicDesc):+z175257;
  !!VR(advancedDesc):+z175258;
  !!VR(expertDesc):+z175259;
!!en;

; Add in First Aid II description if enabled
!!UN:P190/?(firstAidII:y);

!!if&(firstAidII);
  !!VR(basicDesc):+z175260;
  !!VR(advancedDesc):+z175261;
  !!VR(expertDesc):+z175262;
!!en;

; Change the descriptions
!!SN:H^secskill^/(SKILL_FIRST_AID)/(SKILL_BASIC)/(basicDesc);
!!SN:H^secskill^/(SKILL_FIRST_AID)/(SKILL_ADVANCED)/(advancedDesc);
!!SN:H^secskill^/(SKILL_FIRST_AID)/(SKILL_EXPERT)/(expertDesc);

; Set up new spec description
!!re i/(HERO_FIRST)/(HERO_LAST_WOG);
  !!HEi:X?(spec:y)/?(spec2:y);
  !!SN&(spec)=0/(spec2)=(SKILL_FIRST_AID):H^spec^/i/2/^%T(es.782.spec)^;
!!en;

!?FU(OnStartOrLoad);
!!UN:P782/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;             [выход если опция не включена]

!!SN:L^EraPlugins\erm_hooker.era^/?(lib:y) A(lib)/^SetHook^/?(add:y);
!!SN:E(add)/1/5137296/(ES_782_Hook);    [установка хука на событие]

!?FU(OnBeforeBattleAction);   [новые параметры лечения Палатки]
!!UN:P782/?y1;
!!FU&y1=0:E;                  [выход если опция не включена]

!!UN:P73/?y1;
!!FU&y1<>0:E;                 [выход если включена опция Усиленные военные машины III]

!!BG:N?y1 E?y2 A?y6;          [y1/y2 - номер ходящего отряда / целевой отряд, y6 - действие]
!!BMy1:T?y3 I?y4;             [y3/y4 - тип / принадлежность ходящего отряда]
!!FU|y3<>147/y6<>11/y2=-1:E;  [выход, если не лечение палатки]

!!BMy2:H?y11 L?y12;           [y11/y12 - максимальное/потерянное здоровье исцеляемого отряда]
!!BMy1:I?y5;                  [y5 - номер героя-владельца Палатки]
!!VRy5:+1 *-10;               [...]
!!HEy5:S27/?y13;              [y13 - уровень навыка Медицина]
!!HEy5:X?y14/?y15/d/d/d/d/d;  [y13=0,y14=27 для специалистов по Медицине]
!!VRy13&y14=0/y15=27:+1;      [Специальность добавляет +1 к навыку]
!!VRy14:Sy13 *25 +1;          [y14 - минимальное лечение Палатками (1/26/51/76)*кол-во Палаток]
!!VRy15:Sy13 *25 +25;         [y15 - максимальное лечение Палатками (25/50/75/100)*кол-во Палаток]
!!VRy15:-y14;                 [y15 - разброс между минимальным и максимальным лечением Палаток]
!!VRy14:Ry15;                 [y14 - случайный в пределах разброса объем лечения ]
!!UN:P767/?i;                 [проверяем включена ли опция 767 (Не)удача в i]
!!BA:Q?f;                     [f-статус быстрой битвы]

!!if&i>0;                     [если (Не)удача включена]
  !!BMy1:G213/?y20/d G213/0/d;[y20 - (не)удача палаток]
  !!VRy21:Sy20 *5;            [y21 - шанс выпадения удачи - 5% за уровень]
  !!VRy21&y21<0:*-2;          [y21 - шанс выпадения НЕудачи - 10% за уровень]
  !!VRy22:S0 R99;             [y22 - случайное число 0.99]

  !!if&y22<y21/y20<0;         [если выпала неудача]
    !!VRy14::2;               [сила лечения 50%]
    !!VRz1&f=0:S^BADLUCK^;    [z1 - звук "Неудачи"]
    !!SN&f=0:Pz1;             [воспроизведение звука]
    !!BMy1&f=0:V48;           [анимация неудачи]
  !!en;

  !!if&y22<y21/y20>0;         [если выпала удача]
    !!VRy14:*3 :2;            [сила лечения 150%]
    !!VRz1&f=0:S^GOODLUCK^;   [z1 - звук "Удачи"]
    !!SN&f=0:Pz1;             [воспроизведение звука]
    !!BMy1&f=0:V18;           [анимация удачи]
  !!en;
!!en;

!!VRy14&y14>y12:Sy12;         [сила лечения не более, чем потерянное здоровье исцеляемого отряда]
!!VRy12:-y14;                 [y12 - обновленное "потерянное здоровье"]

!!if&y14>0;                   [если эффект лечения есть]
  !!BMy2:Ly12;                [восполнение здоровья]

  !!if&f=0;
    !!SN:P^REGENER.wav^;      [воспроизведение звука]
    !!BMy2:V79;               [анимация на отряде]
    !!SN:T^es.782.battleLog^/?z1/^hp^/y14;
    !!MM:Sz1;                 [вывод сообщения в лог битвы]
  !!en;
!!en;

!!BG:A(BATTLE_ACTION_SKIP);   [отмена стандартного лечения]

!?FU(ES_782_Hook);
!!UN:P782/?y1;
!!FU&y1=0:E;                  [выход если опция не включена]

!!SN:X?y1;                    [получить системный параметр хука]
!!VRy2:Sy1 +24;               [перейти к [ECX]
!!UN:Cy2/4/?y3;               [прочитать содержимое [ECX]
!!VRy4:Sy3 +26;               [перейти к номеру героя [ECX +26]
!!UN:Cy4/4/?y5;               [узнать номер героя]
!!HEy5:S27/?y11;              [y11 - уровень Медицины героя [0..3]]
!!HEy5:X?y13/?y14/d/d/d/d/d;  [y13=0,y14=номер навыка для специалистов по вторичному навыку]
!!VRy11&y13=0/y14=27:+1;      [y11 - бонус Медицины героя]
!!VRy2:Sy1 +12;               [перейти к [ESP]
!!UN:Cy2/4/?y3;               [прочитать содержимое [ESP]
!!VRy4:Sy3 +8;                [перейти к номеру монстра [ESP +8]
!!UN:Cy4/4/?y5;               [узнать номер монстра
!!MA:Xy5/?y22;                [y22 - флаг монстра]
!!VRy5:Sy3 +12;               [перейти к структуре монстра [ESP +12]
!!UN:Cy5/4/?y6;               [прочитать содержимое [ESP +12]
!!VRy7:Sy6 +76;               [перейти к HP монстра]
!!UN:Cy7/4/dy11;              [добавить +1/+2/+3/+4 к его HP]

!?FU(OnDwarfMagicResistance); [Проверка сопротивлений]
!!UN:P782/?y1 P767/?y2;       [проверяем включены ли опция 782 и 767 в y1/y2]
!!FU|y1=0/y2=0:E;             [выход если хотя бы одна из опций не включена]

!!MR:S?y1 M?y2;               [y1/y2 - заклинание/существо]
!!MR&y1>50/y1<53/y2=147:F0;   [Палатка восприимчива к заклинаниям Удача/Неудача]

** end
