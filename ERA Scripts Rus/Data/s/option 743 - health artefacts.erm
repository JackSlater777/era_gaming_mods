ZVSE
ERMS_ScriptDate=2.1(January).2013
_WARNING_#1=IMPORTANT! This file is not in a plain text format. NEVER use any editor except ERM_S for making any kind of changes!
ERMS_PoweredBy=ERM Scripter v. 2005.9.7.814
** Author orig.  : Algor, igrik
** Name          : Health artefacts
** Name rus.     : Артефакты здоровья
** Options       : 743
** Dialogs       : -
** Variables     : -
** Tmp variables : -
** Timers        : -
** Functions     : FU7986
** PO-values     : -

** Внимание! Для работы требуется "FUN.erm".

!?FU(OnAfterErmInstructions); [перед началом новой игры]
!!UN:P743/?y1;                [проверяем включена ли опция 743 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN:Ey2/1/5137296/7986;      [установка хука на событие]
!!FU(Fun_SetDescriptionFromERT):P3/94/1/179029;      [устанавливаем описание Кольца живучести]
!!FU(Fun_SetDescriptionFromERT):P3/95/1/179032;      [устанавливаем описание Кольца жизни]
!!FU(Fun_SetDescriptionFromERT):P3/96/1/179030;      [устанавливаем описание Виала жизненной силы]

!?FU(OnAfterLoadGame);        [после загрузки]
!!UN:P743/?y1;                [проверяем включена ли опция 743 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!SN:L^EraPlugins\erm_hooker.era^/?y1 Ay1/^SetHook^/?y2;
!!SN:Ey2/1/5137296/7986;      [установка хука на событие]
!!FU(Fun_SetDescriptionFromERT):P3/94/1/179029;      [устанавливаем описание Кольца живучести]
!!FU(Fun_SetDescriptionFromERT):P3/95/1/179032;      [устанавливаем описание Кольца жизни]
!!FU(Fun_SetDescriptionFromERT):P3/96/1/179030;      [устанавливаем описание Виала жизненной силы]

!?FU7986;                     [вызов функции из хука]
!!UN:P743/?y1;                [проверяем включена ли опция 743 в y1]
!!FU&y1=0:E;                  [выход если опция не включена]
!!SN:X?y1;                    [получить системный параметр хука]
!!VRy2:Sy1 +24;               [перейти к [ECX]
!!UN:Cy2/4/?y3;               [прочитать содержимое [ECX]
!!VRy4:Sy3 +26;               [перейти к номеру героя [ECX +26]
!!UN:Cy4/4/?y5;               [знать номер героя]
!!VRy20:S0;                   [переменная о наличии надетых артов]
!!HEy5:A2/94/d/?y21;          [y21 - количество надетых Колец живучести у героя]
!!VRy20&y21>0:+5;             [y20=1, если надето Кольца живучести (соответствует 5%)]
!!HEy5:A2/95/d/?y21;          [y21 - количество надетых Колец жизни у героя]
!!VRy20&y21>0:+5;             [y20=1, если надето Кольцо жизни (соответствует 5%)]
!!HEy5:A2/96/d/?y21;          [y21 - количество надетых Виалов жизненной силы у героя]
!!VRy20&y21>0:+10;            [y20=1, если надето Кольцо жизни (соответствует 5%)]
!!HEy5:A2/131/d/?y21;         [y21 - переменная о наличии надетого Элексира жизни у героя]
!!VRy2:Sy1 +12;               [перейти к [ESP]
!!UN:Cy2/4/?y3;               [прочитать содержимое [ESP]
!!VRy4:Sy3 +8;                [перейти к номеру монстра [ESP +8]
!!UN:Cy4/4/?y5;               [узнать номер монстра
!!MA:Xy5/?y22;                [y22 - флаг монстра]
!!VRy22:&16;                  [y22=16 если монстр живой, y22=0 если нет]
!!VRy5:Sy3 +12;               [перейти к структуре монстра [ESP +12]
!!UN:Cy5/4/?y6;               [прочитать содержимое [ESP +12]
!!VRy7:Sy6 +76;               [перейти к HP монстра]
!!UN:Cy7/4/?y8;               [прочитать его HP]
!!VRy9&y21=0/y22<>0:Sy8 *y20 :100 +y8;[получаем бонусные HP + стандартные HP]
!!VRy9&y21<>0|y22=0:Sy8;      [если элексир или нежить, то стандартный бонус]
!!VRy9&y20=5:-1;              [вычитываем стандартные бонусы артефактов]
!!VRy9&y20=10:-2;             [вычитываем стандартные бонусы артефактов]
!!VRy9&y20=15:-3;             [вычитываем стандартные бонусы артефактов]
!!VRy9&y20=20:-4;             [вычитываем стандартные бонусы артефактов]
!!VRy9&y21<>0:-4;             [вычитываем стандартный бонус Элексира жизни]
!!UN:Cy7/4/y9;                [установить новые HP]

** end
