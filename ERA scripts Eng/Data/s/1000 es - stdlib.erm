ZVSE2
; Author:   V.A.
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework


********************************
**** Option Compatibilities ****
********************************
// Get the number of War Machines of Enhanced War Machines III at the end of battle
!?FU(OnAfterBattleAction);
!!UN:P73/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!BU:C?(battleIsEnded:y);

!!if&(battleIsEnded);

  ; Get the hero who won the battle, exit if no one won
  !!VR(wonHero:y):S(NO_HERO);

  !!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
    !!HEi^battle_hero_%i^:O?(owner:y);

    !!if&(owner)>(NO_OWNER);
      !!VR(wonHero):Si^battle_hero_%i^;
      !!VR(lostSide:y):Si X1;
      !!VR(lostHero:y):Si^battle_hero_%(lostSide)^;

      !!br;
    !!en;
  !!en;

  !!FU&(wonHero)=(NO_HERO):E;

  ; Get the number of the remaining War Machines of the lost hero
  !!IF:W(lostHero);
  !!VRi^es_737_capturedWM1^:Sw81;
  !!VRi^es_737_capturedWM2^:Sw82;
  !!VRi^es_737_capturedWM3^:Sw83;
!!en;

// Disable Rebalanced Starting Armies (WoG) if Balanced Starting Army (ES) is enabled
!?FU(ES_764_Initialization);
!!UN:P764/?(wogOption:y);               [проверяем включена ли опция 764 в v1]

; Check if the current map is a random map, disable Balanced Starting Army (ES) if not
!!if&(wogOption);
  !!UN:C6919480/4/?(value:y);
  !!VR(add:y):S(value) +128980;
  !!UN:C(add)/(UNC_INT8)/?(character:y);[для рандомной карты (каталог [r]andom_maps) thanks to feanor]

  !!if&(character)<>114;
    !!UN:P764/(FALSE);                  [отключение опции для нерандомной карты]
  ; Disable Rebalanced Starting Armies (WoG) if the map is a random map
  !!el;
    !!UN:P199/(FALSE);                  [отключаем опцию 199 "Измененные стартовые армии героев"]
    !!VRv1402:S(FALSE);
  !!en;
!!en;

!#FU(ES_770_Intialization):P;

// Temporally disable Extention Heroes if Tyrant is enabled, would be re-enabled again when Tyrant is set up
!?FU(ES_770_DisableExtentionHeroes);
!!UN:P100/?i^ES_100_enabled^;
!!UN:P770/?(wogOption:y);
!!UN&(wogOption):P100/0;

!#FU(ES_770_DisableExtentionHeroes):P;

// Determine the mode of Landing Navigation by checking the percentage of water on the surface
!?FU(ES_770_Intialization);
!!UN:P773/?(landNavi:y);                [проверяем включена ли опция 773]
!!FU&(landNavi)<>(TRUE):E;

; Loop through all the tiles on the surface to get the percentage of water
!!VR(waterTiles:y):S0;
!!UN:X?(width:y)/?(level:y);

!!re (x:y)/0/(width)/1/-1;
  !!re (y:y)/0/(width)/1/-1;
    !!TR(x)/(y)/0:T?(terrain:y);
    !!VR(waterTiles)&(terrain)=8:+1;    [8 = water]
  !!en;
!!en;

!!VR(totalTiles:y):S(width) *(width);
!!VR(waterPercent:y):S(waterTiles) *100 :(totalTiles);

!!if&(waterPercent)<15;
  ; Set the global variable for land mode
  !!UN:P773/2;
  !!VRi^es_770_landMode^:S(TRUE);

  ; Ban water artifacts and spells
  !!UN:A(ART_NECKLACE_OF_OCEAN_GUIDANCE)/1 A(ART_BOOTS_OF_LEVITATION)/1 A(ART_SEA_CAPTAINS_HAT)/1 A(ART_ADMIRALS_HAT)/1; [запрет водных артефактов]
  !!UN:P152/1 P153/1 P221/1;            [включение опций Бана водных заклинаний]
  !!UN:J0/(SPELL_SUMMON_BOAT)/1 J0/(SPELL_SCUTTLE_BOAT)/1 J0/(SPELL_WATER_WALK)/1; [Бан водных заклинаний в Гильдиях Магов и Пирамидах]
!!en;

!#FU(ES_770_Intialization):P;

// The following changes are critical to the initialization of secondary skill text
// Disable Resistance II if Enhanced Resistance is enabled
!?FU(ES_777_DisableResistanceII);
!!UN:P777/?(enhRes:y);                  [проверяем включена ли опция 777]

!!if&(enhRes);
  !!UN:P210/(FALSE);                    [если включена, отключаем опцию Сопротивление II]
  !!VRv7195:S(FALSE);                   [...]
!!en;

!#FU(ES_777_DisableResistanceII):P;

// Disable Sorcery II if Critical Sorcery is re-enabled
!?FU(ES_789_DisableSorceryII);
!!UN:P789/?(critSorcery:y);             [проверяем включена ли опция 789]

!!if&(critSorcery);
  !!UN:P213/0;                          [если включена, отключаем опцию Волшебство II]
  !!VRv7198:S0;                         [...]
!!en;

!#FU(ES_789_DisableSorceryII):P;

// Ask to write a spell to the spell book if condition has been meet
// Execute sooner than daemon_n's Spell Research
!?FU(OnTownMouseClick)&i^mouse_item^>=0/i^mouse_item^<=4/i^mouse_action^=(MOUSE_LMB_PRESSED)/i^key_shift^;
!!UN:P880/?(wogOption:y);
!!FU&(wogOption)<>(TRUE):E;

!!FU(ES_880_WriteSpellToSpellBook):P;

**********************
**** Set Up hooks ****
**********************

!?FU(OnStartOrLoad);
!!SN:L^erm_hooker.era^/?(ermHooker:y);
!!FU&(ermHooker)=0:E;

!!SN:A(ermHooker)/^SetHook^/?(address:y);
!!FU(ES_CreateERMHook):P(address:y);

!?FU(ES_CreateERMHook);
!#VA(address:x);

!!SN:E(address)/1/4462998/(ES_HOOK_AfterAttackMainFunc); [Trigger after melee attack, before retaliation]
!!SN:E(address)/1/4455129/(ES_HOOK_AfterAttackMainFunc); [Trigger after shooting]
!!SN:E(address)/1/5085308/(ES_OnArtifactGiveSpell); [Trigger when artifact with spell given is about to be equipped]

!?FU(ES_HOOK_AfterAttackMainFunc);      [by igrik]
!!SN:X?(hook:y);
!!UN:C(hook)/0/(UNC_INT)/?(stackStrucktDefender:y);   
!!UN:C(hook)/4/(UNC_INT)/?(stackStrucktAttacker:y);  
!!BG:A?(typeAttack:y);
!!VR(stackAttackerID:y):S(NO_MON); 
!!VR(stackDefenderID:y):S(NO_MON); 

!!if&(stackStrucktAttacker)<>(FALSE); 
  !!UN:C(stackStrucktAttacker)/244/(UNC_INT)/?(attakerSide:y); 
  !!UN:C(stackStrucktAttacker)/248/(UNC_INT)/?(attakerStackIdInSide:y);
  !!VR(stackAttackerID):S(attakerSide) *(BATTLE_STACKS_PER_SIDE) +(attakerStackIdInSide); 
!!en;

!!if&(stackStrucktDefender)<>(FALSE);
  !!UN:C(stackStrucktDefender)/244/(UNC_INT)/?(defenderSide:y);
  !!UN:C(stackStrucktDefender)/248/(UNC_INT)/?(defenderStackIdInSide:y);
  !!VR(stackDefenderID):S(defenderSide) *(BATTLE_STACKS_PER_SIDE) +(defenderStackIdInSide);
!!en;

!!FU(ES_OnAfterMelee)&(typeAttack)=(BATTLE_ACTION_WALK_AND_ATTACK):P(stackAttackerID)/(stackDefenderID);
!!FU(ES_OnAfterShoot)&(typeAttack)=(BATTLE_ACTION_SHOOT):P(stackAttackerID)/(stackDefenderID);

************************************************************
**** Function for getting artifacts of different levels ****
************************************************************
; A better way than UN:J6 as it includes new artifacts supported by Emerald
; Warning: This must be executed sooner than Set up object repalcements
; Disable for now
; Create artifact arrays of all the levels
*?FU(OnAfterErmInstructions);
*!FU(GetMaxArtifactId):P?(lastArt:y);

*!re (index:y)/1/4;
  *!VR(artLevel:y):S1 Sd<<(index);
  ; Create the array
  *!FU(NewIntArray):P?i^es_artifactArray_%(artLevel)^/(M_TEMP);

  *!FU(ES_FillInArtArrays):P(lastArt)/(artLevel);
*!en;

; Fiil in the array upon loading
*?FU(OnAfterLoadGame);
*!FU(GetMaxArtifactId):P?(lastArt:y);

*!re (index:y)/1/4;
  *!VR(artLevel:y):S1 Sd<<(index);
  ; Remove all the elements from the array
  *!SN:Mi^es_artifactArray_%(artLevel)^/0;

  *!FU(ES_FillInArtArrays):P(lastArt)/(artLevel);
*!en;

*?FU(ES_FillInArtArrays);
*#VA(lastArt:x) (artLevel:x);

*!re i/(ART_CENTAUR_AXE)/(lastArt);

  *!if&i<>(ART_HIGHLIGHTED_SLOT)/i<>(ART_ARTIFACT_LOCK);
    *!UN:Ai/?(isDisabled:y) Ai/3/?(level:y) Ai/4/?(combiId:y);

    *!IF&i>170:M^%i %(isDisabled) %(level) %(combiId)^;
    *!if&(isDisabled)<>(TRUE)/(combiId)=(NO_ART)/(level)=(artLevel);
      *!FU(Array_Push):Pi^es_artifactArray_%(artLevel)^/i;
    *!en;
  *!en;
*!en;

; The action function for getting a random artifact ID with artifact level
*?FU(ES_GetRandomArtifactId);
*#VA(artLevel:x);                       [level of the artifact]
*#VA(artifact:x);                       [returned artifact ID]

*!SN:Mi^es_artifactArray_%(artLevel)^/?(size:y);
*!VR(size):-1;
*!VR(index:y):R0/0/(size);
*!SN:Mi^es_artifactArray_%(artLevel)^/(index)/?(artifact);

************************************
**** Set up object repalcements ****
************************************
; Warning: This must be executed later than Function for getting artifacts of different levels
!?FU(OnAfterErmInstructions);
; deleting the sound in the delete object function
; because it causes a crash
!!UN:C4893167/2/?(patch:y);
!!UN:C4893167/2/24555;
; get size of the map
!!UN:X?(mapSize:y)/?(hasUnderground:y);

; pass once through all cells of the map
!!re l/0/(hasUnderground);    coord z
  !!re k/0/(mapSize)/1/-1;    coord y
    !!re i/0/(mapSize)/1/-1;  coord x
      !!OBi/k/l:T?(objType:y) U?(objSubtype:y);
      !!TRi/k/l:E?(isYellowSquare:y) P?(isPassable:y); 
      !!VR(isYellowSquare):X1;  reverse param isYellowSquare
      !!FU(ES_OnIterateAllMapObjects):Pi/k/l/(objType)/(objSubtype)/(isYellowSquare)/(isPassable);
    !!en;
  !!en;
!!en;
; restoring the source code of the sound
; in the delete objects function
!!UN:C4893167/2/(patch);

*******************
**** Functions ****
*******************

// Get strings from text resources
!?FU(ES_GetStringByFilename);
!#VA(txtFilename:x);                    [filename of the text]
!#VA(lineIndex:x);                      [number of line of the string, starts from 0]
!#VA(string:x);                         [returned string]

; Standarize the filename
!!FU(StrToLower):Pz(txtFilename)/?(text:z);
; Remove filename extension (if applicable)
!!SN:K(text)/?(size:y);
!!VR(index:y):S(size) -4;
!!SN:K(text)/(index)/?(character:z);
!!FU(Substr)&(character)=^.^:P(text)/0/-4/?(text);

!!if&(text)=^artevent^;
  !!VR(address:y):S6909120;             [artifact events]
!!el&(text)=^minename^;
  !!VR(address):S6968376;               [mine names]
!!el&(text)=^objnames^;
  !!VR(address):S6974640;               [object names]
!!el&(text)=^plcolors^;
  !!VR(address):S6976900;               [player colour]
!!el&(text)=^priskill^;
  !!VR(address):S6974556;               [primary skills]
!!el&(text)=^restypes^;
  !!VR(address):S6968208;               [resources]
!!el;
  !!VR(string):Z^Wrong txt filename!^;
  !!FU:E;
!!en;

; @Master Of Puppets
!!UN:C(address)/(UNC_INT)/?(value:y);            
!!VR(address2:y):S(value) +32;
!!UN:C(address2)/(UNC_INT)/?(value3:y);
!!VR(address3:y):S(lineIndex) *4 +(value3);
!!UN:C(address3)/(UNC_INT)/?(value4:y);
!!SN:X?(value5:y) X(value4) X?z1 X(value5);
!!VR(string):Z^%z1^;

// Set up IF:N dialogue with array
!?FU(ES_SetupMultiPicDlg);              [by Berserker]
!#VA(argsArr:x); SN:M array ID with picture type and subtype pairs.

!!SN:M(argsArr)/?(numArgs:y);
!!VR(numArgPairs:y):S(numArgs) :2;
!!VR(numArgPairs)&(numArgPairs)>8:S8;

!#VA(params[16]:y);

!!re i/0/(params[SIZE])/1/-1;
  !!VR(params[i]):S(NO_PIC_TYPE);
!!en;

!!VR(paramPtr:y):S(@params);
!!VR(argInd:y):S0;

!!re i/0/(numArgPairs)/1/-1;
  !!SN:V(argsArr)/(argInd)/?(picType:y)/?(picValue:y);

  !!if&(picType)<>(NO_PIC_TYPE);

    !!if|(picType)<>(PIC_TYPE_MONSTER)/(picValue)>=(MON_FIRST);
      !!VRy(paramPtr):S(picType);
      !!VR(paramPtr):+1;
      !!VRy(paramPtr):S(picValue);
      !!VR(paramPtr):+1;
    !!en;
  !!en;

  !!VR(argInd):+2;
!!en;

!!IF:N(params[0])/(params[1])/(params[2])/(params[3])/(params[4])/(params[5])/(params[6])/(params[7])/
      (params[8])/(params[9])/(params[10])/(params[11])/(params[12])/(params[13])/(params[14])/(params[15]);

// Get the degraded monster of the input monster, or output the input monster if not available
// This function is used to get the weakest monster of a upgrade chain
!?FU(ES_GetDegradedMonOrSelf);          [by Archer30]
!#VA(monId:x) (degradedMon:x);

!!VR(degradedMon):S(monId);
!!FU(NewIntArray):P?(degradedMons:y);

; Check for the degraded creature and push to the array if degrade is possible. Check for 5 times at most
!!re i/0/4;
  !!FU(GetDegradedMonCandidates):P(monId:x)/?(degradesList:y);
  !!SN:M(degradesList)/?(size:y);

  !!if&(size)<>0;
    !!SN:M(degradesList)/0/?(monId);
    !!FU(Array_Push):P(degradedMons)/(monId);
  !!el;
    !!br;
  !!en;
!!en;

; Loop through the array and get the creature with the lowest fight value as the most degraded form
!!SN:M(degradedMons)?(size);

!!if&(size)>0;
  !!VR(lowestFv:y):S(INT_MAX);

  !!re (index:y)/0/(size)/1/-1;
    !!SN:M(degradedMons)/(index)/?(mon:y);
    !!MA:F(mon)/?(fv:y);

    !!if&(fv)<=(lowestFv);
      !!VR(lowestFv):S(fv);
      !!VR(degradedMon):S(mon);
    !!en;
  !!en;
!!en;

// Get a monster's rank with its experience
; Returns R11 as R10
!?FU(ES_GetMonRankWithExp);             [by Hawaiing]
!#VA(mon:x) (exp:x) (rank:x);

; Get the exp of R11
!!EA(mon):L?(monExpR10:y) P?(monExpTop:y);
!!VR(monExpR11:y):S(monExpR10) +(monExpTop);

!!if&(exp)=(monExpR11);
  !!VR(rank):S11;
!!el;
  !!SN:E7503648/1/(mon)/(exp);
  !!VR(rank):Sv1;
!!en;

// Get the coordinates by battle position
!?FU(ES_GetBattleHexCoordinates);       [by Hawaiing]
!#VA(position:x) (x:x) (y:x);

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(value:y);
!!VR(value2:y):S(position) *112;
!!VR(value3:y):S(value2) +(value) +452; [coordinate structure]
!!VR(xAddress:y):S(value3) +4; 
!!UN:C(xAddress)/(UNC_INT16)/?(x);      [x coordinate]
!!VR(yAddress:y):S(value3) +6; 
!!UN:C(yAddress)/(UNC_INT16)/?(y);      [y coordinate]

// Calculate distance between two hexes
!?FU(ES_CalcDistanceBetweenHexes);      [by wessonsm]
!#VA(hex1:x) (hex2:x) (distance:x);

!!VR(x1:y):S(hex1) %17;
!!VR(y1:y):S(hex1) :17;
!!VR(x2:y):S(hex2) %17;
!!VR(y2:y):S(hex2) :17;
!!VR(distanceY:y):S(y1) -(y2);
!!VR(distanceY)&(distanceY)<0: *-1;
!!VR(x1):*2;
!!VR(y1):&1;
!!VR(x2):*2;
!!VR(y2):&1;
!!VR(distanceX:y):S(x1) -(y1) -(x2) +(y2);
!!VR(distanceX)&(distanceX)<0:*-1;
!!VR(distanceX):-(distanceY);
!!VR(distance):S(distanceX);
!!VR(distance)&(distance)<0:S0;
!!VR(distanceY):*2;
!!VR(distance):+(distanceY) :2;

// Turn a stack around
!?FU(ES_TurnStackAround);              [by Hawaiing]
!#VA(stack:x);

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(value:y);
!!VR(value2:y):S(stack) *1352 +21708 +(value);
!!SN:E4482112/2/(value2)/1;

// Initiate a melee attack
!?FU(ES_InitiateMeleeAttack);           [by Hawaiing]
!#VA(atkStack:x) (defStack:x);
!#VA(direction:x);                      [0 for upper, 1 for forward and 2 for lower]

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(value:y);
!!VR(value2:x):S(atkStack) *1352 +21708 +(value);
!!VR(value3:x):S(defStack) *1352 +21708 +(value);
!!SN:E4461360/2/(value2)/(value3)/(direction);
