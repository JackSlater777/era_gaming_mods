ZVSE2

**********************************
**** Ring of Machine Affinity ****
**********************************
// Script by JackSlater

; TODO: replace b a h(side)/?(heroNum) with function from EEF when it is released
; In it Hypnotize is checked


; This is for displaying bonus in non-combat dlg 
; Bug with crInfoPtr = null if click on creature specialization!
*?FU(tum_OnAfterHeroSpecCreatureInfo)&i^tum_js_InCombat^=(FALSE);
*!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/?(hero:y);
*!UN:C(ebp)/12/4/?(crInfoPtr:y) C(crInfoPtr)/16/4/?f;
*!UN:C(hero)/26/4/?(heroId:y) C(ebp)/8/4/?(monID:y);

*!VRf:&(MON_FLAG_SHOOTER);
*!HE(heroId):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(equipped:y);
*!HE(heroId):A2/(ART_AMMO_CART)/d/?(equipped2:y);

*!if&f/(equipped:y)/(equipped2:y);
  *!UN:C(crInfoPtr)/84/4/d4;
*!en;


; Adding bonus in battle
!?FU(tum_GiveAttackAsPrecision);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(monStack:y); 
!!UN:C(monStack:y)/244/4/?(side:y); F4
!!BA:H(side:y)/?(heroNum:y);
!!FU&(heroNum:y)<0:E;
!!HE(heroNum:y):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(equipped:y);
!!HE(heroNum:y):A2/(ART_AMMO_CART)/d/?(equipped2:y);
!!UN&(equipped:y)/(equipped2:y):Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/d4;


!?FU(tum_OnBeforeTentHeal);
!!BG:N?(stackNum:y);
!!BM(stackNum:y):I?(side:y);
!!BA:H(side:y)/?(heroNum:y);
!!FU&(heroNum:y)<0:E;
!!HE(heroNum:y):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(equipped:y);
!!UN&(equipped:y):Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/d<<1; = x2


!?FU(tum_OnBeforeCatapultActionType);
!!BG:N?(stackNum:y);
!!BM(stackNum:y):I?(side:y) T?(monType:y);
!!BA:H(side:y)/?(heroNum:y);
!!FU&(heroNum:y)<0:E;
!!HE(heroNum:y):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(equipped:y);

!!if&(monType:y)=(MON_CATAPULT)/(equipped:y);
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp:y)/-20/4/d1; 
  !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/d1;
!!en;


!?FU(tum_OnBeforeShootActionType);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_ESI)/4/?(shooterStack:y);
!!UN:C(shooterStack:y)/52/4/?(monType:y); 34
!!UN:C(shooterStack:y)/244/4/?(side:y); F4
!!BA:H(side:y)/?(heroNum:y); // TODO - use func from EEF when it is ready and released 0043FE60
!!FU&(heroNum:y)<0:E;
!!HE(heroNum:y):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(equipped:y);

!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/?(targetStack:y);

!!if&(monType:y)=(MON_BALLISTA)/(equipped:y);
  !!FU(tum_DoAdditionalShot):P(shooterStack:y)/(targetStack:y);
!!en;
