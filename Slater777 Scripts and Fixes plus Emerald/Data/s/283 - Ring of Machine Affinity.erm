ZVSE2

**********************************
**** Ring of Machine Affinity ****
**********************************

!?FU(gem_CreateERMHook);
!!SN:Ex1/1/4687160/(js_DoubleTentHeal); 478538
!!SN:Ex1/1/5138208/(js_GetExtraAttackToShooters); 4E6720
!!SN:Ex1/1/4456346/(js_SetAdditionalShots); 43FF9A
!!SN:Ex1/1/4479733/(js_GetExtraShotToCatapult); 445AF5


!?FU(OnSetupBattlefield);
; Check Heroes
!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!HEi^battle_hero_%i^:A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(isEquipped:y);
  !!if&(isEquipped);
    !!VRi^js_roma_equipped_%i^:S(TRUE);
  !!en;
!!en;


!?FU(OnAfterBattle)|i^js_roma_equipped_0^/i^js_roma_equipped_1^;
!!VRi^js_roma_equipped_0^:S(FALSE);
!!VRi^js_roma_equipped_1^:S(FALSE);


!?FU(js_DoubleTentHeal)|i^js_roma_equipped_0^/i^js_roma_equipped_1^;
!!BG:Q?(sideTurn:y);

!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!if&i^js_roma_equipped_%i^/(sideTurn:y)=i;/(side:y)=i
    !!UN:Cx1/(STRUCT_HOOK_CONTEXT_EAX)/4/d<<1; = x2
  !!en;
!!en;


!?FU(js_GetExtraAttackToShooters);
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/-4/4/?(hero:y);
!!UN:C(ebp)/12/4/?(monPtr:y) C(monPtr)/84/4/?(monAtk:y);
!!UN:C(hero)/26/4/?(heroId:y) C(ebp)/8/4/?(monID:y);
!!MA:X(monID)/?f;
!!VRf:&(MON_FLAG_SHOOTER);

!!if&f;
  !!HE(heroId):A2/(ART_RING_OF_MACHINE_AFFINITY)/d/?(isEquipped:y);
  !!HE(heroId):A2/(ART_AMMO_CART)/d/?(isEquipped2:y);
  !!if&(isEquipped)/(isEquipped2);
    !!VR(monAtk): +4;
    !!UN:C(monPtr)/84/4/(monAtk);
  !!en;
!!en;


!?FU(js_GetExtraShotToCatapult)&i^js_AdditionalShots^;|i^js_roma_equipped_0^/i^js_roma_equipped_1^;
!!SN:W^js_AdditionalShots^/?(shotsNum:y);
!!SN:W^js_AdditionalShots^/0;
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp:y)/-20/4/d(shotsNum:y); 
!!UN:Cx1/(STRUCT_HOOK_CONTEXT_EBX)/4/d(shotsNum:y);


!?FU(OnBeforeBattleAction)|i^js_roma_equipped_0^/i^js_roma_equipped_1^;
!!SN:W^js_AdditionalShots^/0;
!!BG:A?(actionType:y) N?(stackNum:y) Q?(sideTurn:y);
!!BM(stackNum:y):T?(monType:y);
;!BM(stackNum:y):I?(side:y);

!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!if&i^js_roma_equipped_%i^/(sideTurn:y)=i;/(side:y)=i
    !!if&(monType:y)=(MON_BALLISTA)/(actionType:y)=(BATTLE_ACTION_SHOOT);
      !!SN:W^js_AdditionalShots^/d1;
    !!en;
    !!if&(monType:y)=(MON_CATAPULT)/(actionType:y)=(BATTLE_ACTION_CATAPULT);
      !!SN:W^js_AdditionalShots^/d1;
    !!en;
  !!en;
!!en;


!?FU(js_SetAdditionalShots)&i^js_AdditionalShots^;
!!SN:W^js_AdditionalShots^/?(shotsNum:y);
!!SN:W^js_AdditionalShots^/0;
!!SN:X?(edi:y)/1;                      
!!FU&(shotsNum:y)<1:E;
!!VR(esi:y):S(edi:y) +4;
!!UN:C(esi:y)/4/?(monStack:y);               [A_STACK] - like BM:Z
!!VR(ebx:y):S(edi:y) +16;
!!UN:C(ebx:y)/4/?y20;                        [D_STACK]
!!VR(monType:y):S(monStack:y) +52; 34
!!VR(monNum:y):S(monStack:y) +76; 4C
!!VR(blindSpell:y):S(monStack:y) +656; 290
!!VR(stoneGazeSpell:y):S(monStack:y) +688; 2B0
!!VR(paralizeSpell:y):S(monStack:y) +704; 2C0
!!VR(ammoLeft:y):S(monStack:y) +216; D8
!!VRy21:Sy20 +52; 34
!!VRy22:Sy20 +76; 4C

!!re i/0/(shotsNum:y)/1/-1;
  !!UN:C(monType:y)/4/?y31;               [BM:T]16#]
  !!UN:C(monNum:y)/4/?y32;                [BM:N]
  !!UN:C(blindSpell:y)/4/?y33;            [BM:G62]
  !!UN:C(stoneGazeSpell:y)/4/?y34;        [BM:G70]
  !!UN:C(paralizeSpell:y)/4/?y35;         [BM:G74]
  !!UN:C(ammoLeft:y)/4/?y36;              [BM:U3]
  !!UN:Cy21/4/?y41;                       [BM:T]
  !!UN:Cy22/4/?y42;                       [BM:N]
  !!FU|y31<0/y32<1/y41<0/y42<1/y33>0/y34>0/y35>0/y36<1:E;
  !!SN:E4453920/2/(monStack:y)/y20; 43F620
!!en;
