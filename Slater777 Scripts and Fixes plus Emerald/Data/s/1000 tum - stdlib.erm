ZVSE2
; Author:   Archer30
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework

; Standard library for Third Upgrade Mod
; Stripped by JackSlater

************************************************
**** Disable unused artifacts from the game ****
************************************************
; All the blank artifacts would be disabled if ACM is not presented
!?FU(js_DisableUnusedArtifacts);
!!re i/(ART_BLANK_HELMET)/(ART_BLANK_HORN);
  !!UN:Ai/1;
!!en;

!#FU(js_DisableUnusedArtifacts):P;


************************************************
**** Function to get the latest artifact ID ****
************************************************
; For Emerald 2 and Emerald 3
!?FU(GetMaxArtifactId);
!#VA(result:x);

!!re i/(ART_LAST_WOG)/999;
  ; get the first character of the name of the artifact
  !!SN:H^art^/i/0/?z1;
  !!UN:C9597928/1/?(firstChar:y);

  ; end the loop if the first character is #
  !!br&(firstChar)=35;
!!en;

!!VR(result):Si -1;


; Add, set or get hero power on the battlefield
!?FU(tum_ManageHeroPowerOfSide);
!#VA(side:x);
!#VA(power:x);                          [Can be add, get or set]

!!FU:S(@power)/?(powerSyntax:y);

!!UN:C(COMBAT_MANAGER)/(UNC_INT)/?(combatManager:y);
!!VR(offset:y):S(UNC_INT) *(side) +21460;

!!if&(powerSyntax)=(ARG_SYNTAX_ADD);
  !!UN:C(combatManager)/(offset)/(UNC_INT32)/d(power);
!!el&(powerSyntax)=(ARG_SYNTAX_GET);
  !!UN:C(combatManager)/(offset)/(UNC_INT32)/?(power);
!!el;
  !!UN:C(combatManager)/(offset)/(UNC_INT32)/(power);
!!en;


!?FU(tum_HOOK_BattleStackKilled);
!#VA(hook:x);

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_ESI)/4/?(stackStruct:y);
!!FU&(stackStruct)=(NULL):E;

!!UN:C(stackStruct)/244/4/?(side:y) C(stackStruct)/248/4/?(stackOfSide:y);
!!VR(stackId:y):S(side) *(BATTLE_STACKS_PER_SIDE) +(stackOfSide);
!!FU|(stackId)<=(NO_STACK)/(stackId)>(BATTLE_STACK_LAST):E;

!!FU(tum_OnBattleStackKilled):P(stackId)/(side);


// Get hero Id of the opponent for negative morale/luck artifacts
!?FU(tum_OnSetCreatureInfoDesc);
!#VA(hook:x);

!!VRi^tum_luckMoraleDesc_OppHeroIdPlusOne^:S0;

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y);
!!UN:C(ebp)/28/4/?(heroStruct:y);

!!if&(heroStruct);
  !!UN:C(heroStruct)/26/4/?(heroId:y);
  !!VRi^tum_luckMoraleDesc_OppHeroIdPlusOne^&(heroId)>(NO_HERO):S(heroId) +1;  
!!en;
