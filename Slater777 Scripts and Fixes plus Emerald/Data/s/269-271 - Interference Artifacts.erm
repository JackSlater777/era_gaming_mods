ZVSE2

********************************
**** Interference Artifacts ****
********************************
// Script by Archer30 and daemon_n

; Set up new real power depending on whether interference artifact is equipped
!?FU(OnSetupBattlefield);
!!VRi^tum_trueHeroPower_0^:S0;
!!VRi^tum_trueHeroPower_1^:S0;

!!if&i^battle_hero_vs_hero^;
  !!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
    !!HEi^battle_hero_%i^:A2/(ART_CHARM_OF_ECLIPSE)/?(backpacked:y)/?(equipped1:y);
    !!HEi^battle_hero_%i^:A2/(ART_SEAL_OF_SUNSET)/?(backpacked)/?(equipped2:y);
    !!HEi^battle_hero_%i^:A2/(ART_PLATE_OF_DYING_LIGHT)/?(backpacked)/?(equipped3:y);

    !!VR(enemyPowerReductionPercent:y):S0;
    !!VR(enemyPowerReductionPercent)&(equipped1):+10;
    !!VR(enemyPowerReductionPercent)&(equipped2):+10;
    !!VR(enemyPowerReductionPercent)&(equipped3):+25;
    !!VR(enemyPowerReductionPercent):F0/100;

    !!if&(enemyPowerReductionPercent)>0;
      !!VR(enemySide:y):Si X(TRUE);

      ; Get the new power value
      !!FU(tum_ManageHeroPowerOfSide):P(enemySide)/?(power:y);
      !!VR(newPower:y):S100 -(enemyPowerReductionPercent) *(power) :100 F1/(INT_MAX);

      ; Set the value up
      !!FU(tum_ManageHeroPowerOfSide):P(enemySide)/(newPower:y);
      !!VRi^tum_trueHeroPower_%(enemySide)^:S(newPower);
    !!en;
  !!en;
!!en;


; Set up text display according to the new power (if changed)
!?FU(tum_OnDlg_HeroPreview_SpellPowerWrite)|i^tum_trueHeroPower_0^>0/i^tum_trueHeroPower_1^>0;
!#VA(hook:x);

!!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EBP)/4/?(ebp:y) C(ebp)/8/4/?(heroStruct:y) C(heroStruct)/26/2/?(heroId:y);

!!re i/(BATTLE_LEFT)/(BATTLE_RIGHT);
  !!if&i^battle_hero_%i^=(heroId)/i^tum_trueHeroPower_%i^>0;
    !!VR(truePower:y):Si^tum_trueHeroPower_%i^;
    !!UN:C(hook)/(STRUCT_HOOK_CONTEXT_EAX)/4/?(txtPtr:y);
    !!SN:B(txtPtr)/d/?(power:z) B(txtPtr)/d/^%(power) (%(truePower))^;
  !!en;
!!en;
