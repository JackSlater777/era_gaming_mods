ZVSE2

** ENHANCED ARTIFACTS  -  ERM script 71


!?FU(OnAfterErmInstructions);
!!FU(js_RingOfMagiAddDesc):P;


!?FU(OnAfterLoadGame);
!!FU(js_RingOfMagiAddDesc):P;


!?FU(js_RingOfMagiAddDesc);
!!SN:H^art^/(ART_COLLAR_OF_CONJURING)/1/?(artDescr:z);
!!VR(artDescr:z): +^%T(artDesc.76)^;
!!SN:H^art^/(ART_COLLAR_OF_CONJURING)/1/(artDescr:z);

!!SN:H^art^/(ART_RING_OF_CONJURING)/1/?(artDescr:z);
!!VR(artDescr:z): +^%T(artDesc.77)^;
!!SN:H^art^/(ART_RING_OF_CONJURING)/1/(artDescr:z);

!!SN:H^art^/(ART_CAPE_OF_CONJURING)/1/?(artDescr:z);
!!VR(artDescr:z): +^%T(artDesc.78)^;
!!SN:H^art^/(ART_CAPE_OF_CONJURING)/1/(artDescr:z);

!!SN:H^art^/(ART_RING_OF_THE_MAGI)/1/?(artDescr:z);
!!VR(artDescr:z): +^%T(artDesc.139)^;
!!SN:H^art^/(ART_RING_OF_THE_MAGI)/1/(artDescr:z);


!?FU(OnBeforeBattleUniversal);
!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!VR(bonus:y):S(NULL);
  !!HEi^battle_hero_%i^:A2/(ART_RING_OF_THE_MAGI)/?y-4/?(isRingOfMagi:y);

  !!if&(isRingOfMagi:y);
    !!VR(bonus:y):S50;
  !!el;
    !!re j/0/2;
      !!VR(artId:y):Sj +(ART_COLLAR_OF_CONJURING);
      !!HEi^battle_hero_%i^:A2/(artId:y)/?y-4/?(isEqipped:y);

      !!if&(isEqipped);
        !!VR(bonusAdd:y):Sj*5 +5;
        !!VR(bonus):+(bonusAdd);
      !!en;
    !!en;
  !!en;

  !!VRi^wog_71_hero_%i_spellDamageBonus^:S(bonus);
!!en;


!?FU(OnMagicBasicResistance)&i^wog_71_hero_%i(battle_current_side)_spellDamageBonus^>0;
!!BG:A?(action:y);                      [get active stack #, action type]

!!if&(action)=(BATTLE_ACTION_HERO_CAST);
  !!MR:D?(basicDmg:y);                  [get spell damage]
  !!VR(newDmg:y):S(basicDmg) *i^wog_71_hero_%i(battle_current_side)_spellDamageBonus^ :100 +(basicDmg);
  !!MR&(newDmg)>(basicDmg):D(newDmg);
!!en;


!?FU(OnAfterBattleUniversal);
!!re i/(BATTLE_LEFT)/i^battle_hero_vs_hero^;
  !!VRi^wog_71_hero_%i_spellDamageBonus^:S(NULL);
!!en;
