ZVSE2


!?FU(OnAfterErmInstructions);
!!FU(js_IntAddDesc):P;


!?FU(OnAfterLoadGame);
!!FU(js_IntAddDesc):P;


!?FU(js_IntAddDesc);
!!SN:H^secskill^/24/1/?(intBaseDesc:z);
!!VR(intBaseDescTotal:z):S^%(intBaseDesc)%T(intelDesc.basic)^;
!!SN:H^secskill^/24/1/(intBaseDescTotal:z);

!!SN:H^secskill^/24/2/?(intAdvDesc:z);
!!VR(intAdvDescTotal:z):S^%(intAdvDesc)%T(intelDesc.advanced)^;
!!SN:H^secskill^/24/2/(intAdvDescTotal:z);

!!SN:H^secskill^/24/3/?(intExpDesc:z);
!!VR(intExpDescTotal:z):S^%(intExpDesc)%T(intelDesc.expert)^;
!!SN:H^secskill^/24/3/(intExpDescTotal:z);


!?HL-1;                                         [Level up trigger for intelligence]
!!HE-1:S24/?(intLvl:y);                              [get intelligence level]
!!if&(intLvl)>0;
  !!VR(percent:y):S(intLvl) *20;                [convert bonus to percentage - 20, 40, 60]
  !!VR(random:y):S0 T99;
  !!FU(js_IntBonus)&(random)<(percent):P;                      [handle intelligence bonus]
!!en;


!?FU(js_IntBonus);
!!VR(primSkill:y):S2 R1;                                   [random primary skill]
!!VR(skillPic:y):S31 +(primSkill:y);                       [picture of skill]
!!HE-1&(primSkill:y)=2:Fd/d/d1/d;                          [give appropriate bonus]
!!HE-1&(primSkill:y)=3:Fd/d/d/d1;

!!HE-1:O?(heroOwner:y);                         [get owner of this hero]

!!if&(heroOwner:y)<>-1;
  !!OW:I(heroOwner:y)/?(isAI:y);      [get AI (1) or human (0)]

  !!if&(isAI:y)=0;
    !!HE-1:B0/?(heroName:z);                                [get hero name]
    !!SN:T^intelDesc.lvlUpBonus^/?(msg:z)/^heroName^/(heroName:z);
    !!IF:Q2/(skillPic:y)/1/1/(msg:z);                        [If Bonus Show Picture]
  !!en;

!!en;
