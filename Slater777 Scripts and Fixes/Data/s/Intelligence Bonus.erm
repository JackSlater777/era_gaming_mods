ZVSE2


!?FU(OnAfterErmInstructions);
!!FU(js_IntelligenceAddDesc):P;


!?FU(OnAfterLoadGame);
!!FU(js_IntelligenceAddDesc):P;


!?FU(js_IntelligenceAddDesc);
!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_BASIC)/?(desc:z);
!!SN:T^intelligence.desc^/?(desc1:z)/^percent^/20;
!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_BASIC)/^%(desc)%(desc1)^;

!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_ADVANCED)/?(desc:z);
!!SN:T^intelligence.desc^/?(desc2:z)/^percent^/40;
!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_ADVANCED)/^%(desc)%(desc2)^;

!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_EXPERT)/?(desc:z);
!!SN:T^intelligence.desc^/?(desc3:z)/^percent^/60;
!!SN:H^secskill^/(SKILL_INTELLIGENCE)/(SKILL_EXPERT)/^%(desc)%(desc3)^;


!?HL-1;                                         [Level up trigger for intelligence]
!!HE-1:S24/?(intLvl:y);                              [get intelligence level]
!!if&(intLvl)>0;
  !!VR(percent:y):S(intLvl) *20;                [convert bonus to percentage - 20, 40, 60]
  !!VR(random:y):S0 T99;
  !!FU(js_IntBonus)&(random)<(percent):P;                      [handle intelligence bonus]
!!en;


!?FU(js_IntBonus);
!!VR(primSkill:y):S2 R1;                                   [random primary skill]
!!VR(skillPic:y):S31 +(primSkill:y);                       [picture of skill]
!!HE-1&(primSkill:y)=2:Fd/d/d1/d;                          [give appropriate bonus]
!!HE-1&(primSkill:y)=3:Fd/d/d/d1;

!!HE-1:O?(heroOwner:y);                         [get owner of this hero]

!!if&(heroOwner:y)<>-1;
  !!OW:I(heroOwner:y)/?(isAI:y);      [get AI (1) or human (0)]

  !!if&(isAI:y)=0;
    !!HE-1:B0/?(heroName:z);                                [get hero name]
    !!SN:T^intelligence.lvlUpBonus^/?(msg:z)/^heroName^/(heroName:z);
    !!IF:Q2/(skillPic:y)/1/1/(msg:z);                        [If Bonus Show Picture]
  !!en;

!!en;
