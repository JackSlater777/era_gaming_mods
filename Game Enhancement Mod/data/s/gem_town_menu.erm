ZVSE2
!#DC(GEM_DLG_TOWN_MENU) = 500;
*#SN:M(M_AUTO_ID)/12/(M_STR)/(M_TEMP)/?i^gem_def_menu_array^;
*?FU(OnGameEnter);
  !#SN:M(M_AUTO_ID)/0/(M_INT)/(M_TEMP)/?i^gem_left_click_array^;
  !#SN:M(M_AUTO_ID)/0/(M_STR)/(M_TEMP)/?i^gem_hint_array^;
  !#SN:M(M_AUTO_ID)/0/(M_STR)/(M_TEMP)/?i^gem_right_click_array^;


!?FU(OnKeyPressed_Town)&i^key^=(KEY_TILDE)/999;
  !!SN:F^GetButtonID^/^gem_town_menu^;
  !!if&v1>0;
    !!VR(soundPlay:z):S^button.wav^;
    !!SN:P(soundPlay);
    !!FU(gem_townDl_show):P;
  !!en;




!?FU(gem_townDl_show);
  !!FU(gem_townDL_creating):P;
  !!FU(gem_townDL_text_creating):P;
  !!FU(gem_DlgTown_ChangeCoords):P(GEM_DLG_TOWN_MENU)/1/2;
  !!DL(GEM_DLG_TOWN_MENU):S1;

!?FU(OnTownMouseClick)&999;
  !!CM:S?(clickSub:y) I?(clickedPlace:y);

  !!if&(clickSub)=(MOUSE_LMB_RELEASED)/(clickedPlace)=i^gem_town_menu_Button_ID^;
    !!FU(gem_townDl_show):P;
  !!en;

  !!if&(clickSub)=(MOUSE_RMB_PRESSED)/(clickedPlace)=i^gem_town_menu_Button_ID^;
    !!FU(gem_CheckGameLang):P;
    !!SN:T^%s(gem_t).menurmb^/?(rmbText:z);
    !!IF:M0/4/(rmbText);
    !!CM:R0;
  !!en;

!?FU(gem_townDL_creating);


  !!VRi^gem_town_menu_current_page^:S1; *i^gem_town_menu_current_page^;
  !!UN:C7505216/1/2;                    [; до инициализации диалога - убрать тень]
  !!DL(GEM_DLG_TOWN_MENU):N^gem_town_menu^;
  !!FU(gem_OnOpenButtonDlg):P;
  !!DL(GEM_DLG_TOWN_MENU):E21/0 A21/52/3000/1; E11/0;
  !!FU(gem_town_menu_elements_displaying):Pi^gem_total_elements_amount^/i^gem_town_menu_current_page^;/s^buton1^;
  *!IF:M^%i(gem_total_elements_amount)^;
  !!if&i^gem_total_elements_amount^<=6;
    !!DL(GEM_DLG_TOWN_MENU):E20/0 E21/0 A20/52/3000/0 A21/52/3000/0;
  !!en;

!?FU(gem_DlgTown_ChangeCoords);
  !#VA(dlgId:x) (x:x) (y:x);
  *#VA(localInt[5]:y);
  ; x1 - WoG dialog id
  ; x2 - coord X (-1 center)
  ; x3 - coord Y (-1 center)

  !!SN:E7510739/(CALLCONV_CDECL)/(dlgId);
  !!VR(ptrDlg:y):Sv1;
  !!if&(ptrDlg)<>(NULL):;
    !!UN:C(HD_X)/(UNC_INT16)/?(gameResolutionX:y);
    !!UN:C(HD_Y)/(UNC_INT16)/?(gameResolutionY:y);
    !!VR(xCoordinateShift:y):S(gameResolutionX) -800 :2;
    !!VR(yCoordinateShift:y):S(gameResolutionY) -600 :2 +222; +300; :2;
    !!UN:C(ptrDlg)/(UNC_UINT32)/?(strDlg:y);
    !!UN:C(strDlg)/24/(UNC_UINT32)/(xCoordinateShift);
    !!UN:C(strDlg)/28/(UNC_UINT32)/(yCoordinateShift); 
  !!en;

!?FU(gem_town_menu_elements_displaying);
  !#VA(elementsAmount:x) (currentPage:x);
  !!VR(firstButton:y):S(currentPage)*6 -6;
  !!VR(lastButton:y):S(currentPage)*6 -1;
  !!VR(maxPage:y):S0;
  !!if&(elementsAmount)=(FALSE);
    !!FU(gem_CheckGameLang):P;
    !!SN:T^%s(gem_t).emptyfield^/?(textBg:z);
    !!DL(GEM_DLG_TOWN_MENU):A12/3/(textBg)/1;
  !!en;

  !!re i/0/11;

    !!if&i>=(firstButton)/i<=(lastButton)/(elementsAmount)>0;
      !!DL(GEM_DLG_TOWN_MENU):Ei/1;
      !!VR(temp1:y):Si +1 %3;
      !!DL(GEM_DLG_TOWN_MENU)&(temp1)=1:Ai/52/12/1;
      !!DL(GEM_DLG_TOWN_MENU)&(temp1)=2:Ai/52/86/1; 
      !!DL(GEM_DLG_TOWN_MENU)&(temp1)=0:Ai/52/156/1;
      !!VR(counter:y):Si +1;
      *!VR(counter:y)&i>5:Si;
      !!SN:Vi^gem_hint_array^/i/?z(counter);
      !!VR(text:z):Sz(counter);

      !!DL(GEM_DLG_TOWN_MENU):Ai/9/s^gem_button_%i_def^/0;
      !!DL(GEM_DLG_TOWN_MENU):Hi/z(counter);   
      !!VR(elementsAmount):-1;
        *!IF:M^%z1 ^;

    !!el;
      !!DL(GEM_DLG_TOWN_MENU):Ai/9/^gem_trans.def^/1; Hi/zy4;   
      !!DL(GEM_DLG_TOWN_MENU):Ei/0; Ai/52/3000/1;
    !!en;

    !!VR(temp:y)&(elementsAmount)>0:Si %6;
    !!VR(maxPage)&(temp)=0/(elementsAmount)>0:+1;
  !!en;
  !!SN:Vi^gem_hint_array^/0/?z1;
  !!DL(GEM_DLG_TOWN_MENU):H0/z1;

  !!DL(GEM_DLG_TOWN_MENU)&(currentPage)=1:E20/1 E21/0 A20/52/189/1 A21/52/3000/1;
  !!DL(GEM_DLG_TOWN_MENU)&(currentPage)>1/(currentPage)<>(maxPage):E20/1 A20/52/189/1 E21/1 A21/52/12/1;
  !!DL(GEM_DLG_TOWN_MENU)&(currentPage)=(maxPage):E20/0 A20/52/3000/0 E21/1 A21/52/12/1;



!?FU(gem_AddTownButton);
  !#VA(defName:x) (textHint:x) (rmbMessage:x) (executeFunctionID:x) (funcArg1:x) (funcArg2:x) (funcArg3:x);
  !!re i/0/11;
    !!if&i^gem_button_%i_busy^=(FALSE);
      !!SN:Mi^gem_left_click_array^/d1; увеличить размер массива на 1
      !!SN:Mi^gem_hint_array^/d1; увеличить размер массива на 1
      !!SN:Mi^gem_right_click_array^/d1; увеличить размер массива на 1
      !!SN:Vi^gem_left_click_array^/i/(executeFunctionID);

      !!SN:Vi^gem_hint_array^/i/^%z(textHint)^;
      !!SN:Vi^gem_right_click_array^/i/^%z(rmbMessage)^;
      !!VRs^gem_button_%i_def^:S^%z(defName)^;
      !!VRs^gem_button_%i_hint^:S^%z(textHint)^;
      !!VRi^gem_button_%i_function_arg1^:S(funcArg1);
      !!VRi^gem_button_%i_function_arg2^:S(funcArg2);
      !!VRi^gem_button_%i_function_arg3^:S(funcArg3);
      !!VRi^gem_button_%i_busy^:S(TRUE);
      !!VRi^gem_total_elements_amount^:+1;
      !!br;
    !!en;
  !!en;

!?DL&v998=(GEM_DLG_TOWN_MENU)/v999>=0/v999<=11;
  !!if&i^gem_button_%v999_busy^<>(FALSE);
    !!if&v1000=(MOUSE_LMB_RELEASED);
      !!OW:C?(currentPlayer:y)/?(clickedPlayer:y);
      !!if&(clickedPlayer)=(currentPlayer);
        !!SN:Vi^gem_left_click_array^/v999/?(functionId:y);

        !!if|i^gem_button_%v999_function_arg1^<>(FALSE)/i^gem_button_%v999_function_arg2^<>(FALSE)/i^gem_button_%v999_function_arg3^<>(FALSE);
          !!VR(arg1:y):Si^gem_button_%v999_function_arg1^;
          !!VR(arg2:y):Si^gem_button_%v999_function_arg2^;
          !!VR(arg3:y):Si^gem_button_%v999_function_arg3^;
          !!VR(rightFunctionPart:z):S^%(arg1)/%(arg2)/%(arg3)^;
        !!el;
          !!VR(rightFunctionPart:z):S^^;
        !!en;

        !!VR(executeFunctionID:z):S^FU%(functionId):P%(rightFunctionPart);^;
        !!SN:F^ExecErmCmd^/(executeFunctionID);
      !!en; 
      !!FU(gem_town_menu_close_dlg):P;
    !!en;

    !!if&v1000=(MOUSE_RMB_PRESSED);
      !!SN:Vi^gem_right_click_array^/v999/?(rmbMessage:z);
      !!IF:M0/4/(rmbMessage);
    !!en;
  !!en;

!?DL&v998=(GEM_DLG_TOWN_MENU)/v999=30722/v1000=13;
!!FU(gem_town_menu_close_dlg):P;

!?DL&v998=(GEM_DLG_TOWN_MENU)/i^dlg_action^=(DLG_ACTION_OUTDLG_CLICK);
  !!FU(gem_town_menu_close_dlg):P;

!?FU(gem_town_menu_close_dlg);


  !!re i/0/i^gem_total_elements_amount^;
      !!VRi^gem_button_%i_function_arg1^:S(NULL);
      !!VRi^gem_button_%i_function_arg2^:S(NULL);
      !!VRi^gem_button_%i_function_arg3^:S(NULL);
    !!VRi^gem_button_%i_busy^:S(FALSE);
  !!en;
  !!VRi^gem_total_elements_amount^:S0;

    !!SN:Mi^gem_left_click_array^/0;    [очистить массив]
    !!SN:Mi^gem_hint_array^/0;          [очистить массив]
    !!SN:Mi^gem_right_click_array^/0;   [очистить массив]

    !!DL:C1;
    !!UN:C7505216/1/18;                 [; после закрытия диалога - вернуть параметр тени на место]
...

!?DL&v998=(GEM_DLG_TOWN_MENU)/v999>=20/v999<=21/v1000=13;
  !!if&v999=20;
    !!VRi^gem_town_menu_current_page^:+1;
  !!el;
    !!VRi^gem_town_menu_current_page^:-1;
  !!en;

  !!FU(gem_town_menu_elements_displaying):Pi^gem_total_elements_amount^/i^gem_town_menu_current_page^;