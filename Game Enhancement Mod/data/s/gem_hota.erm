ZVSE2
; Author:   Archer30 & Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.0+, Era Erm Framework

; Differentiate hero defs in the battle with genders.

!?FU(OnAfterErmInstructions);
!!FU(NewStrArray):P?i^hota_maleMightDefsArray^/(M_TEMP);
!!FU(NewStrArray):P?i^hota_maleMagicDefsArray^/(M_TEMP);
!!FU(NewStrArray):P?i^hota_femaleMightDefsArray^/(M_TEMP);
!!FU(NewStrArray):P?i^hota_femaleMagicDefsArray^/(M_TEMP);

!?FU(OnStartOrLoad);
!!SN:Mi^hota_maleMightDefsArray^/0 Mi^hota_maleMagicDefsArray^/0 Mi^hota_femaleMightDefsArray^/0 Mi^hota_femaleMagicDefsArray^/0;

!!FU(Array_Push):Pi^hota_maleMightDefsArray^/^CH00.def^/^CH02.def^/^CH04b.DEF^/^CH06.def^/^CH08.def^/^CH010.def^/^CH013.DEF^/^CH014.def^/^ch16.def^;
!!FU(Array_Push):Pi^hota_maleMagicDefsArray^/^CH01b.DEF^/^CH03b.DEF^/^CH05.def^/^CH07b.def^/^CH09b.DEF^/^CH011b.DEF^/^CH013b.def^/^CH015.def^/^CH17.def^;
!!FU(Array_Push):Pi^hota_femaleMightDefsArray^/^CH00b.DEF^/^CH02b.DEF^/^CH04.def^/^CH06b.DEF^/^CH08b.def^/^CH010b.def^/^CH012.def^/^CH014.def^/^CH16b.def^;
!!FU(Array_Push):Pi^hota_femaleMagicDefsArray^/^CH01.def^/^CH03.def^/^CH05b.DEF^/^CH07.def^/^CH09.def^/^CH11.def^/^CH012b.DEF^/^CH015.def^/^CH17b.DEF^;

*?FU(OnBeforeBattleBeforeDataSend);     [leave until next fix]
*!FU(hota_StartMainFunction):P;

*?FU(OnBeforeBattleAfterDataReceived);
*!FU(hota_StartMainFunction):P;

!?FU(OnSetupBattlefield)&1000;
!!FU&i^battle_humanOnly^:E;
!!FU(hota_StartMainFunction):P;

!?FU(hota_StartMainFunction);
!!BA:H0/?(attacker:y);
!!FU(hota_ChangeBattleHeroDefs):P(attacker)/?(value2:y);
!!SN:E(value2)/1/0/s^hota_battleHeroDef^;
!!BA:H1/?(defender:y);
!!SN:Mi^hota_maleMightDefsArray^/?(arraySize:y);

!!if&(defender)>(NO_HERO);
    !!FU(hota_ChangeBattleHeroDefs):P(defender)/?(value2);
    !!SN:E(value2)/1/1/s^hota_battleHeroDef^;
!!en;

!?FU(hota_ChangeBattleHeroDefs);
!#VA(hero:x) (value2:x);

!!IF:W(hero);
!!HE(hero):R2/?(gender:y) B2/?(class:y);

!!FU(hota_SetUpBattleHeroDefs):P(gender)/(class);

!!SN:L^EraPlugins\Hawaiing_hero_def.era^/?(value:y);
!!SN:A(value)/^HAWAIING_CHANGE_HERO_BATTLE_DEF^/?(value2);

!?FU(hota_SetUpBattleHeroDefs);
!#VA(gender:x) (class:x);

!!VR(type:y):S(class) %2;
!!VR(faction:y):S(class) :2;
!!SN&(gender)=0/(type)=0:Mi^hota_maleMightDefsArray^/(faction)/?s^hota_battleHeroDef^;
!!SN&(gender)=0/(type)=1:Mi^hota_maleMagicDefsArray^/(faction)/?s^hota_battleHeroDef^;
!!SN&(gender)=1/(type)=0:Mi^hota_femaleMightDefsArray^/(faction)/?s^hota_battleHeroDef^;
!!SN&(gender)=1/(type)=1:Mi^hota_femaleMagicDefsArray^/(faction)/?s^hota_battleHeroDef^;