ZVSE2
; Author:   Archer30 & Hawaiing
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Differentiate sprites of heroes in the battle with genders.


!?FU(OnAfterErmInstructions);
!!FU(NewStrArray):P18/?i^hota_defsList_0^/(M_TEMP);
!!FU(NewStrArray):P18/?i^hota_defsList_1^/(M_TEMP);

!?FU(OnStartOrLoad);
!!SN:Vi^hota_defsList_0^/0/^CH00.def^/^CH01b.DEF^/^CH02.def^/^CH03b.DEF^/^CH04b.DEF^/^CH05.def^/^CH06.def^/^CH07b.def^/^CH08.def^/^CH09b.DEF^/^CH010.def^/^CH011b.DEF^/^CH013.DEF^/^CH013b.def^/^CH014.def^/^CH015.def^/^ch16.def^/^CH17.def^;
!!SN:Vi^hota_defsList_1^/0/^CH00b.DEF^/^CH01.def^/^CH02b.DEF^/^CH03.def^/^CH04.def^/^CH05b.DEF^/^CH06b.DEF^/^CH07.def^/^CH08b.def^/^CH09.def^/^CH010b.def^/^CH11.def^/^CH012.def^/^CH012b.DEF^/^CH014.def^/^CH015.def^/^CH16b.def^/^CH17b.DEF^;

!?FU(OnSetupBattlefield)&i^battle_hasHuman^;
!!SN:L^Hawaiing_hero_def.era^/?(value:y);
!!FU&(value)=0:E;

!!BA:H0/?(attacker:y) H1/?(defender:y);
; Set up attacking hero
!!FU(hota_ChangeBattleHeroDefs):P(attacker)/?(result:y);
!!SN:E(result)/1/0/s^hota_battleHeroDef^;

; Set up defending hero is exists
!!if&(defender)>(NO_HERO);
    !!FU(hota_ChangeBattleHeroDefs):P(defender)/?(result);
    !!SN:E(result)/1/1/s^hota_battleHeroDef^;
!!en;

!?FU(hota_ChangeBattleHeroDefs);
!#VA(hero:x) (result:x);

!!HE(hero):R2/?(gender:y) B2/?(class:y);
!!SN:Mi^hota_defsList_%(gender)^/(class)/?s^hota_battleHeroDef^;

!!SN:L^Hawaiing_hero_def.era^/?(value:y);
!!SN:A(value)/^HAWAIING_CHANGE_HERO_BATTLE_DEF^/?(result);
