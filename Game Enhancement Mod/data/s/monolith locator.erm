ZVSE

; monolith locator.erm
; Версия 1.03
; Использованные переменные: нет
; Временные переменные: v2-v5
; Автор: wessonsm.



!?FU(OnGameEnter);
!!SN:L^HD_WOG.dll^/?y1;              [проверка включен ли HD мод]
!!VRy2:S0; !!VRy3:S0;
!!FU&y1=0:E;
!!UN:C4199496/2/?y2 C4199503/2/?y3;  [разрешение H, V]
!!VRy2:-736 :64; !!VRy3:-600 :64;    [смещение центра карты]
!!SN:W^MN.OffsetX^/y2;
!!SN:W^MN.OffsetY^/y3;

!?CM0;
!!CM:F?y1 I?y2;                      [тип клика, место клика]
!!FU|y1<>512/y2<>37:E;               [выход, если не ПКМ на карте приключений]

; реализуем нажатие Ctrl+ПКМ (спасибо igrik'у)
!!SN:L^user32.dll^/?y1 Ay1/^GetKeyState^/?y2 Ey2/1/17;
!!VRy3:Sv1&32768;
!!FU&y3<>32768:E;                     [выход, if not Ctrl]

!!CM:P?y1/?y2/?y3 ;                  [y1-y3 - координаты клика]
!!FU(MN.Visibility):Py1/y2/y3/?y4;   [проверка видимости]
!!FU&y4=0:E;                         [выход, если квадрат невидим]

!!OBy1/y2/y3:T?y4 U?y5;              [y4/y5 - тип/подтип объекта]
!!FU|y4<43/y4>45:E;                  [выход, если не монолит]

!!VRv5:S0;

!!if|y4=43/y4=44:;                   [монолит входа/выхода]
  !!FU(MN.CheckPortals):Py1/y2/y3/43/y5;
  !!FU(MN.CheckPortals):Py1/y2/y3/44/y5;
!!en:;
!!if&y4=45:;                         [двухсторонний монолит]
  !!FU(MN.CheckPortals):Py1/y2/y3/45/y5;
!!en:;
!!if&v5=2:;                          [если что-то показали]
  !!SN:W^MN.OffsetX^/?y6;
  !!SN:W^MN.OffsetY^/?y7;
  !!VRy1:-y6; !!VRy1&y1<0:S0;
  !!VRy2:-y7; !!VRy2&y2<0:S0;
  !!UN:Ly1/y2/y3/10;                 [переместить взгляд обратно]
  !!CM:R0;                           [отменить стандартное действие]
!!en:;

!?FU(MN.CheckPortals);     [параметры: x1..x3 - координаты, x4,x5 - тип, подтип]
!!UN:Ux4/x5/?y1;                     [считаем]
!!FU&y1<1:E;                         [выход, если только один]
!!VRy2:S1;                           [счетчик]
[:here]
!!UN:Ux4/x5/y2/2;                    [v2..v4 - координаты]
!!FU(MN.Visibility):Pv2/v3/v4/?y3;   [проверка видимости]
!!if&y3>0:;
  !!VRy4:Sx1 -v2; !!VRy5:Sx2 -v3;
  !!VRy4:*y4; !!VRy5:*y5;
  !!VRy4:+y5;                        [метрика]
  !!if|y4>2/x3<>v4:;                 [если координаты не совпадают с местом клика]
    !!SN:W^MN.OffsetX^/?y6;
    !!SN:W^MN.OffsetY^/?y7;
    !!VRv2:-y6; !!VRv2&v2<0:S0;
    !!VRv3:-y7; !!VRv3&v3<0:S0;
    !!UN:Lv2/v3/v4/1500;             [показать]
    !!VRv5:S2;                       [индикатор, что что-то показали]
  !!en:;
!!en;
!!VRy2:+1;                           [следующий монолит]
!!SN&y2<=y1:G[here];


!?FU(MN.Visibility);            [параметры: x1..x3 - координаты клетки, x4 - результат]
!!OW:C?y1;                      [y4 - текущий игрок]
!!VRy2&y1=0:S1;                 [y2 - битовая маска текущего игрока]
!!VRy2&y1=1:S2;
!!VRy2&y1=2:S4;
!!VRy2&y1=3:S8;
!!VRy2&y1=4:S16;
!!VRy2&y1=5:S32;
!!VRy2&y1=6:S64;
!!VRy2&y1=7:S128;
!!TRx1/x2/x3:V?x4;              [x4 - битовая маска видимости квадрата]
!!VRx4:&y2;                     [x4=0, если квадрат не видим для игрока]
