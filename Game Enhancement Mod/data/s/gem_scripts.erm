ZVSE2
;gem_scripts.erm
!#DC(GEM_BATTLE_TYPE_NO_AUTO) = 1;
!#DC(GEM_BATTLE_TYPE_AUTO_MANA) = 2;
!#DC(GEM_BATTLE_TYPE_AUTO_MANA_FREE) = 4;
!#DC(GEM_BATTLE_TYPE_ASK_BEFORE_START) = 8;





**************************************************
**** Game Enhancement Mod Part 1/2 - Archer30 ****
**************************************************


IMPORTANT!!!
This part of code needs to be executed SOONER than GEM. Please don't change the filename of the script without testing

**** Load from ini, default GEM Battle Mode depending on whether Quick Combat/Qucik Combat Battle Option is enabled

*?FU(OnEveryDay)&i^timerday^=1/i^timerIsAi^=(FALSE); [moved to gem options]
*!FU(gem_LoadBattleOptionsFromIni):Pi;
*!FU(gem_LoadGemSettingsFromIni):Pi;

!?FU(gem_LoadBattleOptionsFromIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!UN:C6916044/4/?(quickCombat:y);
!!FU(ReadIniInts):P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/1;[read from ini, default 1]
!!FU(ReadIniInts)&i^gem_Quick_Combat_Check^=(TRUE):P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/8;[if quick combat battle option is on]
!!FU(ReadIniInts)&i^gem_Quick_Combat_Check^=(FALSE)/(quickCombat)=(TRUE):P(filePath)/^main^/^gem_battleMode^/?i^gem_battleMode_ini^/2;[if quick combat battle option is off and quick combat system option is on]
!!VRi^Current_Battle_Type%i(gem_CurPlay)^:Si^gem_battleMode^;


**** Load GEM settings from ini

!?FU(gem_LoadGemSettingsFromIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!FU(ReadIniInts):P(filePath)/^main^/^gem_control_units^/?i^gem_option_1_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_newButtons^/?i^gem_option_2_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_allyHeroScreen^/?i^gem_option_3_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_enemyHeroScreen^/?i^gem_option_4_ini^/1; [read from ini, default 1]
!!FU(ReadIniInts):P(filePath)/^main^/^gem_spellResearch^/?i^gem_option_5_ini^/1;

**** Fix Quick Combat Syetem Option reverting changes at turn end

!?FU(OnEveryDay)&i^timerDay^>1/i^timerIsAi^=(FALSE);
!!UN:C6916044/4/?(quickCombat:y);
!!VR(battleType:y):Si^Current_Battle_Type%i(gem_CurPlay)^;
!!if&(battleType)=1/(quickCombat)=(TRUE);[change battle type if system option mismatches]
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S2;
!!el&(battleType)=2/(quickCombat)=(FALSE);
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S1;
!!el&(battleType)=4/(quickCombat)=(FALSE);
  !!VRi^Current_Battle_Type%i(gem_CurPlay)^:S1;
!!en;


**** End Game Enhancement Mod Part 1/2


____________________________________________________________________________________________________



!?FU(OnGameEnter);
!!TL:E1;

!?FU(OnGameLeave);
!!TL:E0;

; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
*                       [ scripts for the battles]
; --------------------------------------------------------------------------------------------------
; --------------------------------------------------------------------------------------------------
!?FU(OnKeyPressed_AdvMap)&x1=(KEY_W)/999;     
  !!OW:C?y4;                              
  !!SN:W^Current_Battle_Type%Y4^/?y1;     
    !!VRy2&y1<>8:Sy1*2;
    !!VRy2&y1=8:S1;
    !!SN:W^Current_Battle_Type%Y4^/y2;
    !!FU(gem_Button_Switch):P;                 
  !!FU(gem_Autobattle_Check_And_Sound):P1;

!?FU(gem_Autobattle_Check_And_Sound);
!#VA(playSound:x);
  !!OW:C?y4;
  !!SN:W^Current_Battle_Type%Y4^/?y2;
  !!SN:W^gem_CheckGameLang^/?y5;
  !!if|y2<2:;
    !!UN:C6916044/4/0;
    !!SN&y2=1/(playSound):T^%Y5_gem_map.battle1^/?z-1;
  !!el:;
    !!UN:C6916044/4/1;
    !!SN&y2=2/(playSound):T^%Y5_gem_map.battle2^/?z-1;
    !!SN&y2=4/(playSound):T^%Y5_gem_map.battle3^/?z-1;
    !!SN&y2=8/(playSound):T^%Y5_gem_map.battle4^/?z-1;
  !!en;
  !!IF&(playSound):L^%Z-1^;
  !!FU(gem_SaveBattleOptionsToIni)&x1=1:P;     [save options]


**  Disable autobattle vs human players (by daemon_n)
** Spell Points Delete in the autobattle, supports battle replay
!?FU(OnBeforeBattleForThisPcDefender);

  !!OW:Cd/?(currPlayer:y); I(currPlayer)/?(isAi:y); [check current player color]

  !!if&(currPlayer)<>i^timerOwner^/i^gem_several_PC^;
    !!VRi^gem_replay_counter^:S1;

    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;
!?FU(OnBeforeBattle)&i^battle_humanOnly^=(FALSE)/1000/i^battle_isNetwork^=(FALSE);     [before Human battle]
  !!OW:Cd/?(currPlayer:y); I(currPlayer)/?(isAi:y); [check current player color]
  *!if&(isAi)=(TRUE):;                  [if ai attacks]
    *!BA:Od/?(currPlayer);              [set choice of human from right side]
  *!en;
  *!IF:M^1^;
  !!if&(currPlayer)<>i^timerOwner^/i^gem_several_PC^;
    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;
  !!UN:C6916044/4/?(isAutoBattleState:y);[СѓР·РЅР°С‚СЊ Р°РІС‚Рѕ]

  !!if&(isAutoBattleState)=(FALSE)/i^Current_Battle_Type%(currPlayer)^>(GEM_BATTLE_TYPE_NO_AUTO)/i^Current_Battle_Type%(currPlayer)^<(GEM_BATTLE_TYPE_ASK_BEFORE_START):;
    !!VRi^Current_Battle_Type%(currPlayer)^:S(GEM_BATTLE_TYPE_NO_AUTO); [to know his choice]
    
    !!BA:Q0;
    !!FU:E;                             [and exit func]
  !!en;

  !!VRi^Current_Battle_Type%(currPlayer)^&(isAutoBattleState)=(TRUE)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO):S(GEM_BATTLE_TYPE_AUTO_MANA);
  !!VRi^gem_Was_Mana_Removed^:S(FALSE);                 [was not remove the SP]


  !!if&i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START):;                                 [if choising]
     !!VRi^Current_Battle_Type_Choice%(currPlayer)^&i^Current_Battle_Type_Choice%(currPlayer)^=(NULL):S(GEM_BATTLE_TYPE_NO_AUTO);
     !!FU(gem_CheckGameLang):P;

     !!SN:T^%s(gem_m).battlelist1^/?z-1;
     !!SN:T^%s(gem_m).battlelist2^/?z-2;
     !!SN:T^%s(gem_m).battlelist3^/?z-3;
     !!SN:T^%s(gem_m).battlelist4^/?z-4;
     !!IF:G1/1/i^Current_Battle_Type_Choice%(currPlayer)^/-1/-2/-3/-4;                  [show message]
     !!SN:W^Current_Battle_Type_Choice%(currPlayer)^/v1;  [store choice]
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_NO_AUTO):;                   [if choisen]
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S1;
    !!BA:Q0;                              [set no auto]
    !!FU:E;                               [and exit func]
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA):;                   [if choisen]
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S(NULL);

    !!BA:Q1;                              [set auto]
    !!FU:E;                               [end]
  !!en;

  !!if&i^Current_Battle_Type_Choice%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA_FREE)/i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_ASK_BEFORE_START)|i^Current_Battle_Type%(currPlayer)^=(GEM_BATTLE_TYPE_AUTO_MANA_FREE):;
    !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):S(NULL);

    !!FU(gem_Spellpoints_Removing):P;     [start remove mana func]
    !!VRi^gem_Was_Mana_Removed^:S(TRUE); [store it]
    !!BA:Q1;                              [set auto]
    !!FU:E;                               [end]
  !!en;                                   [final] 


//remove extra replay bttns
!?FU(Dlg_BattleResults_Proc)&i^gem_BeforeBattleResultClose^=(FALSE)/i^gem_OnlyOneBattleReplayIsAllowed^/i^gem_replay_counter^/i^battle_humanOnly^=(FALSE)/1000;
  !!VRi^gem_BeforeBattleResultClose^:S(TRUE);
  !!FU(gem_DlgShowItem):P0/0/0/0;/1;
  !!FU(gem_DlgShowItem):P30723/0/0/1;/1;

!?FU(OnBattleReplay)&i^battle_humanOnly^=(FALSE);
*!IF:L^%i(gem_replay_counter)^;
  !!if&i^gem_Was_Mana_Removed^;
    !!FU(gem_Spellpoints_Giving):P;             [give mana]
    !!VRi^gem_Was_Mana_Restored^:S(TRUE);
  !!en;

  !!VRi^gem_replay_counter^&i^gem_OnlyOneBattleReplayIsAllowed^=(TRUE):+1;


!?FU(OnAfterBattle)&i^battle_humanOnly^=(FALSE)/(ERM_FLAG_IS_HUMAN);

  !!if&i^battle_humanOnly^=(FALSE);
    !!if&i^gem_OnlyOneBattleReplayIsAllowed^;
      !!VRi^gem_replay_counter^:S0;
    !!en;

    !!FU(gem_Button_Switch):P;

    !!UN:P900/?y1;
    !!UN&y1=0:J11/1;


    !!if&i^gem_Was_Mana_Removed^/i^gem_Was_Mana_Restored^=(FALSE);
      !!FU(gem_Spellpoints_Giving):P;
    !!en;

      !!VRi^gem_Was_Mana_Removed^:S(FALSE);
      !!VRi^gem_Was_Mana_Restored^:S(FALSE);

    !!VRi^gem_BeforeBattleResultClose^:S(FALSE);

  !!en;

!?FU(gem_Spellpoints_Removing);                 [taking SP func]
  !!if&i^battle_human_0^;
      !!FU&904:E;                               [exit if human uses berserker's potion (61wog)]
      !!HE-10:I?i^gem_Auto_Spellpoints^/1 I0/1; [store SP Availabilityin y1 / take off SP]

  !!el&i^battle_hero_1^<>(NO_HERO);
      !!FU&905:E;                               [exit if human has no hero or uses berserker's potion (61wog)]
      !!HE-20:I?i^gem_Auto_Spellpoints^/1 I0/1; [store SP Availabilityin y1 / take off SP]

  !!en;


!?FU(gem_Spellpoints_Giving);                   [restore SP func]
  !!if&i^battle_human_0^;                       [if human hero is attacker]
      !!FU&904:E;                               [exit if human uses berserker's potion (61wog)]
      !!HE-10:Idi^gem_Auto_Spellpoints^/1;      [store SP Availabilityin y1 / take off SP]
      !!VRi^WOG_73_battle_hero_0_SpellPoints^&v847:Si^gem_Auto_Spellpoints^;
  !!el&i^battle_hero_1^<>(NO_HERO);             [if human defending]
      !!FU&905:E;                               [exit if human has no hero or uses berserker's potion (61wog)]
      !!HE-20:Idi^gem_Auto_Spellpoints^/1;      [restore SP]
      !!VRi^WOG_73_battle_hero_1_SpellPoints^&v847:Si^gem_Auto_Spellpoints^;
  !!en;

** enable sod style of creatures window in battle without troop experience script
!?FU(OnBeforeBattle)&1000/i^battle_humanOnly^=(FALSE);                            [before Human battle]
  !!UN:P900/?y1;                          [check troop experience script]
  !!if&y1=0:;                             [if disabled]
    !!UN:J11/1;                           [enable sod style of window]
  !!el:;                                  [else]
    !!UN:J11/0;                           [enable wog style of window]
  !!en;  

!?FU(OnAfterErmInstructions);           [once set "ask -before battle" - if option is enabled]
!!UN:P233/?(isEnabled:y) P233/(FALSE); =
!!SN:W^gem_Quick_Combat_Check^/(isEnabled);

!?FU(OnGameEnter)&999;
!!if&i^gem_Quick_Combat_Check^=(TRUE);
  !!re i/0/(PLAYER_LAST);
  !!OW:Ii/?(isAi:y)/?(isDead:y);

    !!if&(isAi)=(FALSE)/(isDead)=(FALSE);
    !!SN:W^Current_Battle_Type%i^/8;    [ set "ask -before battle"]
    !!en;
  !!en;
  !!SN:W^gem_Quick_Combat_Check^/(FALSE);
!!en;
!!FU(gem_Button_Switch):P;

!?FU(OnGameEnter);
  !!re i/(PLAYER_FIRST)/(PLAYER_LAST):;
    !!VRi^Current_Battle_Type%i^&i^Current_Battle_Type%i^=0:S(GEM_BATTLE_TYPE_NO_AUTO);
  !!en:;

  !!FU(gem_Autobattle_Check_And_Sound):P0;

!?FU(OnEveryDay)&1000;

!!FU(gem_Button_Switch):P;&i^timerDay^>0
!!FU(gem_Autobattle_Check_And_Sound):P0;

!?FU(OnAdventureMapLeftMouseClick)&999/i^mouse_action^=(MOUSE_LMB_RELEASED);

    !!if&i^manualf_Button_ID^=i^mouse_item^:;
      !!SN:W^Current_Battle_Type%i(gem_CurPlay)^/(GEM_BATTLE_TYPE_NO_AUTO);
      !!FU(gem_Button_Switch):P;
      !!FU(gem_Autobattle_Check_And_Sound):P1; !!FU:E;
    !!en;

    !!if&i^automf_Button_ID^=i^mouse_item^:;
      !!SN:W^Current_Battle_Type%i(gem_CurPlay)^/(GEM_BATTLE_TYPE_AUTO_MANA);
      !!FU(gem_Button_Switch):P;
      !!FU(gem_Autobattle_Check_And_Sound):P1;!!FU:E;
    !!en;

    !!if&i^autowm_Button_ID^=i^mouse_item^:;
      !!SN:W^Current_Battle_Type%i(gem_CurPlay)^/(GEM_BATTLE_TYPE_AUTO_MANA_FREE);
      !!FU(gem_Button_Switch):P;
      !!FU(gem_Autobattle_Check_And_Sound):P1;!!FU:E;
    !!en;

    !!if&i^choiceB_Button_ID^=i^mouse_item^:;
      !!SN:W^Current_Battle_Type%i(gem_CurPlay)^/(GEM_BATTLE_TYPE_ASK_BEFORE_START);
      !!FU(gem_Button_Switch):P;
      !!FU(gem_Autobattle_Check_And_Sound):P1;!!FU:E;
    !!en:;


!?FU(gem_Button_Switch)&999;
  !!UN:J8/1/^gem_interface.pac^; !!FU&-1:E;
  !!SN:F^GetButtonID^/^indicator^; !!FU&v1<1:E;

  !!VR(setFrame:y):Si^Current_Battle_Type%i(gem_CurPlay)^;
  !!VR(setFrame)&(setFrame)=(GEM_BATTLE_TYPE_ASK_BEFORE_START):-1;
  !!FU(H3Dlg_GetRootDlg):P?(rootDlg:y);
  !!FU(H3Dlg_GetCurrentDlg):P?(currentDlg:y);

  !!if&(rootDlg)=(currentDlg);
    !!FU(gem_DlgDefProc):Pv1/(setFrame);
    !!SN:D;  
  !!el;
    !!FU(gem_DlgDefProc):Pv1/(setFrame)/d/d/d/(rootDlg);
  !!en;




!?FU(OnAdventureMapRightMouseClick)&i^mouse_item^;

    !!if&i^manualf_Button_ID^=i^mouse_item^:;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button1hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
    !!en;

    !!if&i^automf_Button_ID^=i^mouse_item^:;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button2hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
    !!en;

    !!if&i^autowm_Button_ID^=i^mouse_item^:;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button3hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
    !!en;

    !!if&i^choiceB_Button_ID^=i^mouse_item^:;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button4hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
   !!en:;

**** right-click msg of Lord buttons

    !!if&i^mouse_item^=i^optionB_Button_ID^;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button5hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
    !!en;
    
    !!if&i^mouse_item^=i^mithrilB_Button_ID^;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button6hint^/?z-1;

      !!OW:Cd/?(clickedPlayer:y);
      !!OW:R(clickedPlayer)/(RES_MITHRIL)/?(resNumber:y);

      !!if&(resNumber)>0;
          !!SN:T^%s(gem_s).line7+^/?z1/^mithril^/(resNumber);
      !!el;
          !!SN:T^%s(gem_s).line7^/?z1/^mithril^/(resNumber);
      !!en;

      !!CM:R0;
      !!IF:M0/4/^%z1^; Q1^%Z-2^;      !FU(gem_Buttons_Disabling)&1:P;
    !!en;

    !!if&i^indicator_Button_ID^=i^mouse_item^:;
      !!SN:W^gem_CheckGameLang^/?y5 T^%Y5_gem_map.button7hint^/?z-1;
      !!CM:R0;
      !!IF:M0/4/^%Z-1^;
   !!en:;

____________________________________________________________________________________________________


!?FU(OnGameEnter);
  !!UN:C(GAME_MANAGER)/4/?(gmAdd:y);
  !!VR(mapTypeAdd:y):S(gmAdd)+128664;
  !!UN:C(mapTypeAdd)/4/?(thisMapType:y);
  !!UN&(thisMapType)<2:C(mapTypeAdd)/4/2;


**************************************************
**** Game Enhancement Mod Part 2/2 - Archer30 ****
**************************************************


IMPORTANT!!!
This part of code needs to be executed LATER than GEM. Please don't change the filename of the script without testing


**** Save GEM Battle Mode selection to ini

!?FU(gem_Autobattle_Check_And_Sound);
!!FU(gem_SaveBattleOptionsToIni)&x1=1:P;[inherit x1 from GEM]

!?FU(gem_SaveBattleOptionsToIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_battleMode^/?i^Current_Battle_Type%i(gem_CurPlay)^;
!!FU(SaveIni):P(filePath);


**** Save GEM Settings to ini

!?FU(OnCustomDialogEvent)&v998=505/v999>=30721/v999<=30722/v1000=13;
!!FU(gem_SaveGEMSettingsToIni):P;

!?FU(gem_SaveGEMSettingsToIni);
!!VR(filePath:z):S^Runtime/game_enhancement_mod.ini^;
*!SN:L^BattleSave.dll^/?(battleSavePlugin:y);
!!FU(WriteIniInts):P(filePath)/^main^/^gem_control_units^/?i^gem_option_1_%i(gem_CurPlay)^; [save battle save option only when plugin is disabled]
!!FU(WriteIniInts):P(filePath)/^main^/^gem_newButtons^/?i^gem_option_2_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_allyHeroScreen^/?i^gem_option_3_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_spellResearch^/?i^gem_option_5_%i(gem_CurPlay)^;
*!FU(WriteIniInts):P(filePath)/^main^/^gem_disableFreeJoin^/?i^gem_option_5_%i(gem_CurPlay)^;
!!FU(WriteIniInts):P(filePath)/^main^/^gem_enemyHeroScreen^/?i^gem_option_4_%i(gem_CurPlay)^;
!!FU(SaveIni):P(filePath);


**** End Game Enhancement Mod Part 2/2

