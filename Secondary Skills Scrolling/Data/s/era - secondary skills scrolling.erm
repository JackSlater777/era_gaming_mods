ZVSE2

** secondary skills Scrolling 
** Idea and original script - Berserker
** Reworked to support the mod "10 Secondary Skills" от igrik'а - Algor
** Rewritten in ERM 2 to support "Warfare" WoG-option - daemon_n
** fixed warfare blinks, fixed empty page display

!?FU(OnHeroScreenMouseClick)&i^mouse_action^=(MOUSE_LMB_RELEASED)/i^mouse_flags^=(NO_MOUSE_FLAGS);           // при клике в окне героя
  !!SN:F^GetButtonID^/^sss_next^;

  !!if&v1/i^mouse_item^=v1;
    !!VRi^sss_CurrentPage^:+1;
    !!FU(sss_SwithPage):Pi^sss_CurrentPage^;
    !!FU(sss_DoNotRefreshHeroScreenTwice):P;
  !!en;

  !!SN:F^GetButtonID^/^sss_pre^;

  !!if&v1/i^mouse_item^=v1;
    !!VRi^sss_CurrentPage^:-1;
    !!FU(sss_SwithPage):Pi^sss_CurrentPage^;
    !!FU(sss_DoNotRefreshHeroScreenTwice):P;
  !!en;


!?FU(OnOpenHeroScreen);
!!HEx1:O?(owner:y);
  *!FU(sss_StorePlayerHeroesData):P(owner);
  !!VRi^sss_CurrentPage^:S1;
  !!FU(sss_SwithPage):Pi^sss_CurrentPage^;

*?FU(sss_StorePlayerHeroesData);
  *#VA(playerID:x);
  *!OW:H(playerID)/1;
  *!re i/2/v1/1/1;
      *!HEvi:S?i^sss_hero_%vi^;
  *!en;

!?FU(OnPreHeroScreen)&i^sss_do_not_update_screen^=(FALSE);
!!FU(sss_SwithPage):Pi^sss_CurrentPage^;

!?FU(OnPostHeroScreen)&i^sss_do_not_update_screen^=(FALSE);   
!!FU(sss_SwithPage):Pi^sss_CurrentPage^;

!?FU(OnCloseHeroScreen);
  !!HEx1:O?(owner:y);
  *!FU(sss_RestorePlayerHeroesData):P(owner);
  !!VRi^sss_CurrentPage^:S0;

*?FU(sss_RestorePlayerHeroesData);
  *#VA(playerID:x);
  *!OW:H(playerID)/1;

  *!re i/2/v1/1/1;
      *!HEvi:Si^sss_hero_%vi^;
      *!SN:W^sss_hero_%vi^;
  *!en;


!?FU(OnKeyPressed_HeroScreen)&i^key^=(KEY_PRIOR)|i^key^=(KEY_NEXT);
  !!VRi^sss_CurrentPage^&i^key^=(KEY_PRIOR):-1;
  !!VRi^sss_CurrentPage^&i^key^=(KEY_NEXT):+1;
  !!FU(sss_SwithPage):Pi^sss_CurrentPage^;

  !!FU(sss_DoNotRefreshHeroScreenTwice):P;

!?FU(sss_SwithPage);                // отображение x1 страницы втор.навыков в экране героя
!#VA(page:x);
  *!HE-1:Z?(hero:y);
  *!UN:C(hero)/257/4/?(storedSkills:y);
  *!VR(heroSkillsToShowPtr:y):S(hero) +229;

  !!FU(NewIntArray):P?(skillsList:y);
  !!UN:P193/?(warfare:y);
  !!VR(warfarePlaced:y):S(FALSE);
  !!re i/(SEC_SKILL_FIRST)/(SEC_SKILL_LAST):;
    !!HE-1:Si/?(learned:y);

    !!if&(learned);
      !!if|i=(SKILL_BALLISTICS)/i=(SKILL_ARTILLERY)/i=(SKILL_FIRST_AID);
        !!if&(warfare);
          !!if&(warfarePlaced:y)=(FALSE);
            !!VR(warfarePlaced:y):S(TRUE);
            !!IF:W-1;
            !!FU(Array_Push):P(skillsList)/w48;
          !!en;
          !!co;
        !!en;
      !!en;

      !!FU(Array_Push):P(skillsList)/i;
    !!en;
  !!en;
  !!SN:M(skillsList)/?(skills:y);
  !!FU&(skills)=0:E;

  !!UN:C7620583/1/?(maxToShow:y);                    // y6 - максимальное количество втор.навыков отображаемых в окне героя

  !!VR(totalPages:y):S(skills) :(maxToShow); F1/(maxToShow);
  !!VR(remainder:y):S(skills) %(maxToShow);
  !!if&(remainder);
    !!VR(totalPages):+1;
  !!en;
  !!if&(page)>(totalPages);
    !!VRi^sss_CurrentPage^:S1;
  !!el&(page)<1;
    !!VRi^sss_CurrentPage^:S(totalPages);
  !!en;

  !!VR(startSkill:y):Si^sss_CurrentPage^ -1 *(maxToShow);
  !!VR(lastSkill:y):Si^sss_CurrentPage^ *(maxToShow);

  !!VR(lastSkill)&(lastSkill)>(skills):S(skills) %(maxToShow) +(startSkill);
  !!VR(skillsToShow:y):S(lastSkill) -(startSkill);

  !!HE-1:S(skillsToShow);

  !!re i/(startSkill)/(lastSkill)/1/-1;
    !!SN:M(skillsList)/i/?(skill:y);
    !!VR(slot:y):Si -(startSkill) +1;
    !!HE-1:S(slot)/(skill)/1;
  !!en;
  *!UN:C(hero)/257/4/(storedSkills);

!?FU(sss_DoNotRefreshHeroScreenTwice);
!!VRi^sss_do_not_update_screen^:S(TRUE);
!!UN:R3/-1;                             // обновление окна текущего героя
!!VRi^sss_do_not_update_screen^:S(FALSE);
