ZVSE2
; Author:   Archer30
; Special thanks to Perry R
; Engine:   ERM 2.0+
; Requires: ERA 3.3+, Era Erm Framework

; Config DL for Enhanced Henchmen


!?FU(OnCustomDialogEvent)&i^dlg_id^=44/i^mouse_action^=(MOUSE_LMB_PRESSED); [checkboxes switch between check and cancel for Enhanced Henchmen]
!!if|i^mouse_item^=74/i^mouse_item^=82;
  !!SN:P^Button.wav^;
  !!UN:P49/?(wogOption:y);

  !!if&(wogOption)<>(TRUE);
    !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_Off)^;
  !!el&i^enhanced_henchmen_on^<>(TRUE);
    !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Missing_Mod)^;
  !!en;

  !!FU(diff_ConfigureEnhancedHench)&(wogOption)/i^enhanced_henchmen_on^:P;
!!en;

!?FU(diff_ConfigureEnhancedHench);      [open the Neutrals difficulty settings and set all names of options]
; Set Up the name of Dialogue
!!DL168:N^Hen_Set.txt^;

!!DL168:A13/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Dlg_Title)^;

; Set up experience options
!!DL168:A50/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_0)^;
!!DL168:A51/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_1)^;
!!DL168:A52/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_2)^;
!!DL168:A53/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_3)^;
!!DL168:A54/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_4)^;

; Set Up booleans
!!DL168:A30/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_5)^;
!!DL168:A31/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_6)^;
!!DL168:A32/(DLG_CMD_SET_TEXT)/^%T(Diff_Mod.Hench_Option_7)^;

; Set Up Confirm and Cancel button hints - doesn't work for some reason WIP
!!DL168:H30722/^%T(Diff_Mod.confirm_button_hint_0)^;
!!DL168:H30721/^%T(Diff_Mod.confirm_button_hint)^;

!!FU(diff_RefreshEnhancedHenchDlg):P;
!!FU(diff_SetUpHenchDlgRandomMons):P;

; Show complete Dialogue number 168
!!DL168:S1;

!?FU(diff_RefreshEnhancedHenchDlg);
!!FU(DecorateInt):Pi^hench_battleExpStatic^/?(staticStr:z);
!!FU(DecorateInt):Pi^hench_battleExpRandom^/?(randomStr:z);
!!FU(DecorateInt):Pi^hench_battleHeroExpCoef^/?(coefStr:z);
!!FU(DecorateInt):Pi^hench_bonusHeroLvUp^/?(heroLvStr:z);
!!FU(DecorateInt):Pi^hench_bonusDaily^/?(dailyStr:z);

!!DL168:A70/(DLG_CMD_SET_TEXT)/^%(staticStr)^;
!!DL168:A71/(DLG_CMD_SET_TEXT)/^%(randomStr)^;
!!DL168:A72/(DLG_CMD_SET_TEXT)/^%(coefStr)%^;
!!DL168:A73/(DLG_CMD_SET_TEXT)/^%(heroLvStr)^;
!!DL168:A74/(DLG_CMD_SET_TEXT)/^%(dailyStr)^;

!!DL168:A10/(DLG_CMD_SET_DEF_FRAME)/i^hench_stackExpEnabled^;
!!DL168:A11/(DLG_CMD_SET_DEF_FRAME)/i^hench_bannerFree^;
!!DL168:A12/(DLG_CMD_SET_DEF_FRAME)/i^hench_reviveAnywhere^;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_action^=(MOUSE_LMB_PRESSED);  [checkboxes switch between check and cancel]
!!if&i^mouse_item^=70;
  !!SN:T^Diff_Mod.Hench_Option_0_Title^/?z2/^exp^/i^hench_battleExpStatic^;
  !!SN:T^Diff_Mod.Hench_Option_0_Legend^/?z3;
  !!IF:D29/2/3;
  !!IF:E1/29;
  !!VRz1:H1;

  !!VRi^hench_battleExpStatic^&1/v1>-1:Vz1 F0/i^hench_battleExpMax^;
!!el&i^mouse_item^=71;
  !!SN:T^Diff_Mod.Hench_Option_1_Title^/?z2/^exp^/i^hench_battleExpRandom^;
  !!SN:T^Diff_Mod.Hench_Option_1_Legend^/?z3;
  !!IF:D29/2/3;
  !!IF:E1/29;
  !!VRz1:H1;

  !!VRi^hench_battleExpRandom^&1/v1>-1:Vz1 F0/i^hench_battleExpMax^;
!!el&i^mouse_item^=72;
  !!SN:T^Diff_Mod.Hench_Option_2_Title^/?z2/^exp^/i^hench_battleHeroExpCoef^;
  !!SN:T^Diff_Mod.Hench_Option_2_Legend^/?z3;
  !!IF:D29/2/3;
  !!IF:E1/29;
  !!VRz1:H1;

  !!VRi^hench_battleHeroExpCoef^&1/v1>-1:Vz1 F0/100;
!!el&i^mouse_item^=73;
  !!SN:T^Diff_Mod.Hench_Option_3_Title^/?z2/^exp^/i^hench_bonusHeroLvUp^;
  !!SN:T^Diff_Mod.Hench_Option_3_Legend^/?z3;
  !!IF:D29/2/3;
  !!IF:E1/29;
  !!VRz1:H1;

  !!VRi^hench_bonusHeroLvUp^&1/v1>-1:Vz1 F0/1000;
!!el&i^mouse_item^=74;
  !!SN:T^Diff_Mod.Hench_Option_4_Title^/?z2/^exp^/i^hench_bonusDaily^;
  !!SN:T^Diff_Mod.Hench_Option_4_Legend^/?z3;
  !!IF:D29/2/3;
  !!IF:E1/29;
  !!VRz1:H1;

  !!VRi^hench_bonusDaily^&1/v1>-1:Vz1 F0/1000;
!!el&i^mouse_item^=10;
  !!if&i^hench_stackExpEnabled^;
    !!VRi^hench_stackExpEnabled^:S(FALSE);
    !!VRi^hench_bannerFree^:S(FALSE);   [Disable free banners if stack exp is disabled]
  !!el;
    !!VRi^hench_stackExpEnabled^:S(TRUE);
  !!en;
!!el&i^mouse_item^=11;
  !!if&i^hench_bannerFree^;
    !!VRi^hench_bannerFree^:S(FALSE);
  !!el&i^hench_stackExpEnabled^;        [Enable free banners only when stack exp is enabled]
    !!VRi^hench_bannerFree^:S(TRUE);
  !!en;
!!el&i^mouse_item^=12;
  !!if&i^hench_reviveAnywhere^;
    !!VRi^hench_reviveAnywhere^:S(FALSE);
  !!el;
    !!VRi^hench_reviveAnywhere^:S(TRUE);
  !!en;
!!en;

!!VR(flag:y):S(FALSE);
!!VR(flag)&i^mouse_item^>=70/i^mouse_item^<=74:S(TRUE);
!!VR(flag)&i^mouse_item^>=10/i^mouse_item^<=12:S(TRUE);

!!SN&i^mouse_item^>=10/i^mouse_item^<=12:P^button.wav^;
!!FU(diff_RefreshEnhancedHenchDlg)&(flag)>(FALSE):P;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_action^=(MOUSE_RMB_PRESSED);                 [Show right click information all options]
!!if|i^mouse_item^=70/i^mouse_item^=50;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_0_Info)^;
!!el|i^mouse_item^=71/i^mouse_item^=51;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_1_Info)^;
!!el|i^mouse_item^=72/i^mouse_item^=52;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_2_Info)^;
!!el|i^mouse_item^=73/i^mouse_item^=53;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_3_Info)^;
!!el|i^mouse_item^=74/i^mouse_item^=54;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_4_Info)^;
!!el|i^mouse_item^=10/i^mouse_item^=30;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_5_Info)^;
!!el|i^mouse_item^=11/i^mouse_item^=31;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_6_Info)^;
!!el|i^mouse_item^=12/i^mouse_item^=32;
  !!IF:M0/(MSG_TYPE_POPUP)/^%T(Diff_Mod.Hench_Option_7_Info)^;
!!en;

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_item^=30721/i^mouse_action^=(MOUSE_LMB_RELEASED);      [When clicking on the close Button in Neutral Dif Exit and reset all options]
!!DL:C1;
!!FU(hench_LoadGlobalConfig):P;           [Load Default settings from json again]

!?FU(OnCustomDialogEvent)&i^dlg_id^=168/i^mouse_item^=30722/i^mouse_action^=(MOUSE_LMB_RELEASED);      [Close Dialogue with Confirm Button Save all Settings]
!!DL:C1;

// Cosmetics
!?FU(diff_SetUpHenchDlgRandomMons);
!!VR(phoenixCadre:y):S(MON_PHOENIX) +2;
!!VR(arcticCadre:y):S(MON_ARCTIC_SHARPSHOOTER) +2;
!!VR(hellCadre:y):S(MON_HELL_BARON) +2;

!!DL168:A82/(DLG_CMD_SET_DEF_FRAME)/(phoenixCadre);
!!DL168:A83/(DLG_CMD_SET_DEF_FRAME)/(arcticCadre);
!!DL168:A84/(DLG_CMD_SET_DEF_FRAME)/(hellCadre);

!!DL168:A80/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A81/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A85/(DLG_CMD_SET_DEF_FRAME)/0;
!!DL168:A86/(DLG_CMD_SET_DEF_FRAME)/0;

!!FU(GetMaxMonsterId):P?(lastMon:y);

!!if&(lastMon)>(MON_LAST_WOG);
  !!FU(diff_SetHenchDlgRandomMonCadre):P(lastMon)/80;
  !!FU(diff_SetHenchDlgRandomMonCadre):P(lastMon)/81;
  !!FU(diff_SetHenchDlgRandomMonCadre):P(lastMon)/85;
  !!FU(diff_SetHenchDlgRandomMonCadre):P(lastMon)/86;
!!en;

!?FU(diff_SetHenchDlgRandomMonCadre);
!#VA(lastMon:x) (item:x);

!!VR(randomMon:y):T0/197/(lastMon);
!!VR(randomCadre:y):S(randomMon) +2;
!!DL168:A(item)/(DLG_CMD_SET_DEF_FRAME)/(randomCadre);
